# -*- mode: qore; indent-tabs-mode: nil -*-
# @file DatasourceProvider.qm Qore user module providing an API for returning datasource connection strings from identifiers

/*  DatasourceProvider.qm Copyright 2016 - 2017 Qore Technologies, s.r.o.

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

# this module requires Qore 0.8.13 or better
%requires qore >= 0.8.13

%require-types
%enable-all-warnings
%new-style
%strict-args

module DatasourceProvider {
    version = "1.0";
    desc    = "API for providing datasource connection strings";
    author  = "David Nichols <david.nichols@qoretechnologies.com>";
    url     = "http://qore.org";
    license = "MIT";
}

/** @mainpage DatasourceProvider Module

    @tableofcontents

    @section datasourceproviderintro DatasourceProvider Module Introduction

    The %DatasourceProvider module provides an API for providing @ref Qore::SQL::Datasource "Datasource"
    connection strings.

    To use this module, use \c "%requires DatasourceProvider" in your code.

    All the public symbols in the module are defined in the DatasourceProvider namespace

    Functions:
    - @ref DatasourceProvider::get_ds_string() "get_ds_string()"

    @subsection datasourceprovider_modules Datasource Provider Modules

    This module uses the \c "QORE_DATASOURCE_PROVIDERS" environment variable to load datasource provider
    modules.  Each datasource provider module must provide a single public function called
    \c get_mod_ds_string() with the following signature:
    @code{.py}
public string sub get_mod_ds_string(string str) {}
    @endcode

    If the string is not a valid identifier for a datasource, the function should throw an
    exception (any exception; the exception is not displayed by default).

    Datasource provider modules declared in the \c "QORE_DATASOURCE_PROVIDERS" environment variable must
    be separated by the platform-specific @ref Qore::PathSep "PathSep" character as in the following
    examples:

    @par Unix Example
    @verbatim
export QORE_DATASOURCE_PROVIDERS=MyDatasourceProvider:OtherDatasourceProvider
    @endverbatim

    @par Windows CMD.EXE Example
    @verbatim
set QORE_DATASOURCE_PROVIDERS=MyDatasourceProvider;OtherDatasourceProvider
    @endverbatim

    @par Windows PowerShell Example
    @verbatim
$env:QORE_DATASOURCE_PROVIDERS="MyDatasourceProvider;OtherDatasourceProvider"
    @endverbatim

    @section datasourceproviderrelnotes Release Notes

    @subsection datasourceprovider_1_0 DatasourceProvider 1.0
    - the initial version of the DatasourceProvider module
*/

#! the DatasourceProvider namespace contains all the objects in the DatasourceProvider module
public namespace DatasourceProvider {
    #! returns a datasource connection string
    /** @par Example:
        @code{.py}
string connstr = get_ds_string(str);
        @endcode

        @param str the identifier or datasource connection string
        @param verbose display datasource provider module errors on stdout (otherwise errors are ignored)

        @return the datasource connection string

        @throw DATASOURCE-ERROR the connection string cannot be matched as an external datasource identifier by a datasource provider module and there is no driver specification in the string (ex: \c "driver:...")

        @note
        - datasource provider modules are processed in the order they are declared with the \c QORE_DATASOURCE_PROVIDERS environment variable; datasource provider modules must be separated with @ref Qore::PathSep "PathSep" characters
        - no database connection is attempted with this function call

        @see @ref datasourceprovider_modules
     */
    public string sub get_ds_string(string str, *bool verbose) {
        # check for a driver
        bool driver = (str =~ /^.+:/);

        # process datasource provider modules in declaration order
        if (ENV.QORE_DATASOURCE_PROVIDERS) {
            list pl = ENV.QORE_DATASOURCE_PROVIDERS.split(PathSep);
            foreach string mod in (pl) {
                try {
                    return try_get_ds_string(mod, str);
                }
                catch (hash<ExceptionInfo> ex) {
                    if (verbose)
                        printf("%s: %s\n", ex.err, ex.desc);
                    # otherwise ignore exceptions
                }
            }
            if (!driver)
                throw "DATASOURCE-ERROR", sprintf("datasource identifier %y could not be matched to a known datasource; tried the following datasource provider modules: %y", str, pl);
        }

        if (!driver)
            throw "DATASOURCE-ERROR", sprintf("datasource identifier %y could not be matched to a known datasource; there is no driver specification in the string (ex: \"driver:...\") and no datasource provider modules are set in the QORE_DATASOURCE_PROVIDERS environment variable", str);
        return str;
    }
}

# private, non-exported namespace
namespace Priv {
    string sub try_get_ds_string(string mod, string str) {
        Program p();
        p.loadModule(mod);
        return p.callFunction("get_mod_ds_string", str);
    }
}
