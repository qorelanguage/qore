# -*- mode: qore; indent-tabs-mode: nil -*-
# @file SqlUtil.qm Qore user module for working with SQL data

/*  SqlUtil.qm Copyright (C) 2013 - 2017 Qore Technologies, s.r.o.

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

# this module requires Qore 0.8.12 or better
%requires qore >= 0.8.12

# requires the Util module
%requires Util >= 1.0

# don't use "$" signs for variables and class members, assume local variable scope
%new-style

# require type definitions everywhere
%require-types

# enable all warnings
%enable-all-warnings

module SqlUtil {
    version = "1.3.2";
    desc = "user module for working with SQL data";
    author = "David Nichols <david@qore.org>";
    url = "http://qore.org";
    license = "MIT";
}

# version history is included below in the docs

/** @mainpage SqlUtil Module

    @tableofcontents

    @section sqlutilintro Introduction to the SqlUtil Module

    The %SqlUtil module provides a high level database-independent API for working with database objects and SQL.

    To use this module, use \c "%requires SqlUtil" in your code.

    All the public symbols in the module are defined in the SqlUtil namespace

    Major sections of this documentation:
    - @subpage sql_operations "SQL Operations": working with database data (finding, updating, inserting, deleting, merging data, etc)
    - @subpage schema_management "Schema Management": working schema definitions (creating, modifying, aligning tables, functions, types, triggers, etc)
    - @subpage dba_management "DBA Management": special database administrator tools

    The %SqlUtil module provides generic functionality and a framework for SQL operations and schema management, and in order
    to use the %SqlUtil module with a particular database, a driver-specific module has to be available as well.

    Currently the following driver-specific modules are available:
    - <a href="../../FreetdsSqlUtil/html/index.html">FreetdsSqlUtil</a>: for working with MS SQL Server and Sybase databases through the <a href="https://github.com/qorelanguage/module-sybase">freetds DBI driver</a>
    - <a href="../../MysqlSqlUtil/html/index.html">MysqlSqlUtil</a>: for working with MySQL databases
    - <a href="../../OracleSqlUtil/html/index.html">OracleSqlUtil</a>: for working with Oracle databases
    - <a href="../../PgsqlSqlUtil/html/index.html">PgsqlSqlUtil</a>: for working with PostgreSQL databases

    The underlying driver-specific module is automatically loaded and used when required; the classes provided in
    the %SqlUtil module provide a generic API that uses the driver-specific implementations for the underlying driver-specific
    implementation.

    @section sqlutil_overview Overview of Functionality

    SqlUtil provides API support for the following:
    - @ref sql_operations
      - @ref retrieving_data
      - @ref inserting_data
      - @ref updating_data
      - @ref deleting_data
      - @ref sql_upsert
    - @ref schema_management
      - @ref creating_new_objects
      - @ref retrieving_objects
      - @ref listing_objects
      - @ref interdependent_tables
      - @ref aligning_schemas
      - @ref table_management
    - @ref dba_management
      - @ref dba_indexes
      - @ref dba_statistics
      - @ref dba_space_management

    @section sqlutil_relnotes Release Notes for the SqlUtil Module

    @subsection sqlutilv1_3_2 SqlUtil v1.3.2
    - fixed schema alignment skips column with name "driver" (<a href="https://github.com/qorelanguage/qore/issues/1684">issue 1684</a>)
    - fixed sqlutil schema management: functional indexes are rejected without () in name (<a href="https://github.com/qorelanguage/qore/issues/1610">issue 1610</a>)

    @subsection sqlutilv1_3_1 SqlUtil v1.3.1
    - implemented the \a force option (i.e. cascade) for dropping code objects (<a href="https://github.com/qorelanguage/qore/issues/1314">issue 1314</a>)
    - fixed the ignored character_semantics column option in schema alignmed (<a href="https://github.com/qorelanguage/qore/issues/1379">issue 1379</a>)
    - implemented the @ref SqlUtil::cop_length() column function (<a href="https://github.com/qorelanguage/qore/issues/1395">issue 1395</a>)

    @subsection sqlutilv1_3 SqlUtil v1.3
    - implemented insert option support (see @ref AbstractTable::InsertOptions) with the \c "returning" clause in supported drivers
    - implemented support for late table resolution in joins to support executing SQL from serialized parameters
    - improved error messages for common errors such as join errors
    - implemented support for DBA actions (see @ref dba_management)
    - implemented support for driver-dependent pseudocolumns
    - implemented per-column support for the \c "desc" keyword in orderby expressions
    - implemented support for \c "or" logic in @ref where_clauses
    - implemented AbstractTable::getBulkUpsertClosure() to better support bulk SQL merge operations
    - implemented @ref SqlUtil::wop_or() to allow for generating complex SQL strings with \a "or" and \a "and" expressions
    - implemented the @ref SqlUtil::cop_cast() operator for converting [column] value into another datatype
    - implemented the @ref SqlUtil::cop_sum() aggregate operator for returning sum of column values
    - implemented update operators @ref SqlUtil::uop_plus(), @ref SqlUtil::uop_minus(), @ref SqlUtil::uop_multiply(), @ref SqlUtil::uop_divide()
    - implemented support for numeric column arguments for the @ref select_option_orderby "orderby" and @ref select_option_groupby "groupby" options
    - implemented the @ref SqlUtil::cop_coalesce() column operator functions for the \c "COALESCE" operator returning the first non-NULL value in a list of columns (<a href="https://github.com/qorelanguage/qore/issues/671">issue 671</a>)
    - column aliases (defined with @ref SqlUtil::cop_as()) can now be used in the @ref where_clauses "where hash argument" and in join criteria
    - removed all APIs that handle implicit transactions; APIs must commit transactions explicitly (<a href="https://github.com/qorelanguage/qore/issues/533">issue 533</a>)
    - @ref sql_cop_funcs "column operator functions" can be used in @ref where_clauses "where hashes" (<a href="https://github.com/qorelanguage/qore/issues/529">issue 529</a>)
    - implemented @ref SqlUtil::AbstractTable::bindEmptyStringsAsNull() (<a href="https://github.com/qorelanguage/qore/issues/776">issue 776</a>)
    - implemented the \c "omit_update" @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" for asymmetrical upserts (updates only update a subset of the columns inserted) (<a href="https://github.com/qorelanguage/qore/issues/791">issue 791</a>)
    - implemented the @ref SqlUtil::AbstractTable::UpsertUpdateOnly upsert option (<a href="https://github.com/qorelanguage/qore/issues/793">issue 793</a>)
    - implemented @ref SqlUtil::cop_substr() and @ref SqlUtil::uop_substr() operators (<a href="https://github.com/qorelanguage/qore/issues/801">issue 801</a>)
    - implemented @ref SqlUtil::op_substr() where operator (<a href="https://github.com/qorelanguage/qore/issues/883">issue 883</a>)
    - fixed a bug with queries using a \a desc argument with the @ref select_option_orderby "orderby" query option with multiple sort columns; the \c "desc" string was added only to the last column but should have been added to all columns
    - fixed a bug where foreign key constraints with supporting indexes were not tracked and therefore schema alignment on DBs that automatically create indexes for foreign key constraints would fail
    - fixed a bug where driver-specific objects were not included when dropping a schema
    - fixed a bug in subquery handling where bind-by-value arguments from the subquery were lost
    - fixed a bug in the partition by/over operator where column names as given in the query argument hash were not properly recognized
    - fixed a bug in schema alignment; when aligning a schema and an index supporting a PK constraint is introduced in the new schema, the alignment would fail when a constraint is attempted to be disabled that doesn't exist
    - fixed a bug generating select statements for tables accessed through a synonym when used with join clauses; previously inconsistent schema prefixes could be used which could cause errors parsing the SQL statements generated
    - fixed a bug where the @ref SqlUtil::AbstractTable "AbstractTable" lock was held while executing SQL to determine the upsert strategy to use with @ref SqlUtil::AbstractTable::UpsertAuto "UpsertAuto" (<a href="https://github.com/qorelanguage/qore/issues/408">issue 409</a>)
    - fixed a bug where complex bind values as hashes (such as used by the pgsql and oracle drivers) were rejected by SqlUtil (<a href="https://github.com/qorelanguage/qore/issues/494">issue 494</a>) when updating
    - fixed a bug where wildcard columns in join tables were not working (<a href="https://github.com/qorelanguage/qore/issues/499">issue 499</a>)
    - fixed a bug in @ref SqlUtil::op_in() where invalid SQL was generated with an argument of 0 (<a href="https://github.com/qorelanguage/qore/issues/500">issue 500</a>)
    - fixed a bug in @ref SqlUtil::cop_value() where an exception was thrown with an argument of 0 (<a href="https://github.com/qorelanguage/qore/issues/511">issue 511</a>)
    - fixed @ref SqlUtil::cop_count() operator to allow other operators as its argument (<a href="https://github.com/qorelanguage/qore/issues/517">issue 517</a>)
    - fixed bugs in @ref SqlUtil::cop_seq() and @ref SqlUtil::cop_seq_currval() (<a href="https://github.com/qorelanguage/qore/issues/624">issue 624</a>)
    - fixed a bug in @ref SqlUtil::join_inner() where the \a cond argument was ignored (<a href="https://github.com/qorelanguage/qore/issues/645">issue 645</a>)
    - fixed @ref SqlUtil::uop_lower() and @ref SqlUtil::uop_upper() operators to allow nesting (<a href="https://github.com/qorelanguage/qore/issues/657">issue 657</a>)
    - fixed a bug where SqlUtil was generating invalid SQL for some DBs where a wilcard was used with explicit column names (<a href="https://github.com/qorelanguage/qore/issues/708">issue 708</a>)
    - fixed a bug where updating an index without any source constraints caused an invalid exception to be raised (<a href="https://github.com/qorelanguage/qore/issues/768">issue 768</a>)
    - fixed a bug in @ref SqlUtil::AbstractTable::update() "AbstractTable::update()" with sequence operators (<a href="https://github.com/qorelanguage/qore/issues/942">issue 942</a>)

    @subsection sqlutilv1_2 SqlUtil v1.2
    - added insert operator support; for example, for inserting with values from sequences
    - added new upsert constant maps: @ref SqlUtil::AbstractTable::UpsertStrategyMap "UpsertStrategyMap" and @ref SqlUtil::AbstractTable::UpsertStrategyDescriptionMap "UpsertStrategyDescriptionMap"
    - added static SqlUtil::AbstractSqlUtilBase::getDatasourceDesc() method
    - added new AbstractTable::insertFromSelect*() variants taking Table arguments
    - added SqlUtil::AbstractTable::checkExistence() method
    - added support for the \c forupdate @ref select_option_hash "select option"
    - fixed a bug in schema management where excessively verbose column aliases were used that caused errors when automatically updating columns with existing rows and new default values and non-null contraints with PostgreSQL databases
    - fixed a bug where select and row iterator operations could fail with certain select hash arguments without a \c "columns" entry but where column names were otherwise required

    @subsection sqlutilv1_1 SqlUtil v1.1
    - implemented new upsert strategy @ref SqlUtil::AbstractTable::UpsertInsertOnly
    - adding a default value to a column with a not null constraint and existing data populates existing null columns with the new default value automatically in schema alignment
    - fixed an infinitely recursive call in SqlUtil::AbstractTable::del()
    - fixed bugs generating "create table" and "align table" SQL with DBs where unique indexes automatically create unique constraints (ex: MySQL)

    @subsection sqlutilv1_0 SqlUtil v1.0
    - initial release of the SqlUtil modules for schema management and SQL operations
*/
/** @page sql_operations SQL Operations

    @tableofcontents

    @section sql_operations_intro Introduction to SQL Operations

    The @ref SqlUtil::AbstractTable "AbstractTable" class provides methods for SQL operations. Instances of this class are returned from most of the API calls working with tables. You need to use a helper class @ref SqlUtil::Table "Table" if you need to create table object in your own.

    @ref SqlUtil::AbstractTable "AbstractTable" uses knowledge about the internal structure of database tables to create and execute SQL on the database.

    SQL strings are constructed programmatically for the following reasons:
    - to present an abstract, database-independent interface to the programmer
    - to allow for database-specific optimizations to be used without requiring programming expertise for the specific database being used
    - to minimize dynamic SQL and therefore use the database server's SQL statement cache more efficiently (if applicable)
    - to prevent SQL injection attacks

    Also for the above reasons, values are bound by value in all possible cases rather than inserted into SQL strings directly.

    @section retrieving_data Retrieving Data from the Database

    There are many methods in the @ref SqlUtil::AbstractTable "AbstractTable" class for retrieving data from the database; here is an overview:
    - @ref SqlUtil::AbstractTable::find(any): finds a row corresponding to the given single value of the primary key; if not present @ref nothing is returned
    - @ref SqlUtil::AbstractTable::find(list): finds all rows corresponding to the given list of values of the primary key; if none are present @ref nothing is returned
    - @ref SqlUtil::AbstractTable::find(hash): find a row corresponding to the given hash of column name/value pairs, which must at least reference all columns of the table's primary key; if no row matches, @ref nothing is returned
    - @ref SqlUtil::AbstractTable::findSingle(): finds the first (of possibly many) rows that match the @ref where_clauses "where clause hash argument" or returns @ref nothing if no row matches
    - @ref SqlUtil::AbstractTable::findAll(): returns all rows that match the @ref where_clauses "where clause hash argument" or returns @ref nothing if no row matches
    - @ref SqlUtil::AbstractTable::getRowIterator(): returns an @ref Qore::SQL::SQLStatement "SQLStatement" object iterating rows that match the @ref select_option_hash "select option hash" argument
    - @ref SqlUtil::AbstractTable::getSelectSql(): returns an SQL string and bind arguments for the @ref select_option_hash "select option hash" argument
    - @ref SqlUtil::AbstractTable::select(): returns a hash keyed by column name of lists (row values) corresponding to the @ref select_option_hash "select option hash" argument
    - @ref SqlUtil::AbstractTable::selectRow(): return a hash repersenting a single row corresponding to the @ref select_option_hash "select option hash" argument; if the query returns more than one row an exception will be raised
    - @ref SqlUtil::AbstractTable::selectRows(): returns lists of row values corresponding to the @ref select_option_hash "select option hash" argument

    @subsection find_single_row Retrieving a Single Row

    @par From the Primary Key
    To retrieve a single row based on a single primary key value, use @ref SqlUtil::AbstractTable::find(any):
    @code{.py}
*hash row = table.find(id);
    @endcode
    If no primary key value matches the given argument, then @ref nothing is returned. A %Qore SQL statement like the following is generated from the above:
    @code{.py}
*hash row = ds.selectRow("select * from schema.table where id = %v", id);
    @endcode
    \n
    To retrieve a single row based on a complex primary key value, or a single primary key value and extra criteria, use @ref SqlUtil::AbstractTable::find(hash):
    @code{.py}
*hash row = table.find(("account_type": type, "name": name));
    @endcode
    If no row value matches the given argument hash, then @ref nothing is returned. A %Qore SQL statement like the following is generated from the above:
    @code{.py}
*hash row = ds.selectRow("select * from schema.table where account_type = %v and name = %v", type, name);
    @endcode

    @par At Most One Matching Row From the Table
    To retrieve at most one matching row from the table, use @ref SqlUtil::AbstractTable::findSingle():
    @code{.py}
*hash row = table.findSingle(("permission_type": op_between("US", "UX")));
    @endcode
    This method can be used to efficiently check the table if at least one record matches the given \c where criteria; even if more rows match,
    only a single row is returned.  If no rows match, then @ref nothing is returned. A %Qore SQL statement like the following is generated from the above:
    @code{.py}
*hash row = ds.selectRow("select * from schema.table where permission_type between %v and %v limit %v", "US", "UX", 1);
    @endcode

    @note handling the \c "limit" option depends on the underlying database; see @ref sql_paging for more information

    @subsection find_multiple_rows Retrieving Multiple Rows

    @par From the Primary Key
    To retrieve multiple rows from a list of single primary key values, use @ref SqlUtil::AbstractTable::find(list)
    @code{.py}
*list rows = table.find(list);
    @endcode
    All rows matching the single primary key values in the argument list are returned; if no rows match, @ref nothing is returned.

    @par From Simple Where Criteria
    To find all rows matching simple where criteria, use @ref SqlUtil::AbstractTable::findAll():
    @code{.py}
*list rows = table.findAll(("id": op_gt(100), "created": op_gt(2013-03-01)));
    @endcode
    See @ref where_clauses for a description of the hash argument; if no rows match, @ref nothing is returned.

    @par From Complex Select Criteria
    To select a hash keyed by column name of lists (row values) based on complex select criteria, use @ref SqlUtil::AbstractTable::select():
    @code{.py}
hash sh = (
    "columns": ("id", "name", "q.*"),
    "where": ("type": "user"),
    "limit": 100,
    "offset": 200,
    "join": join_inner(queues, "q"),
);
*hash h = table.select(sh);
    @endcode
    See @ref select_option_hash for a description of the hash argument; if no rows match, then a hash with column
    names assigned to empty lists is returned.\n\n
    To select a list of row hashes based on complex select criteria, use @ref SqlUtil::AbstractTable::selectRows():
    @code{.py}
*list rows = table.selectRows(sh);
    @endcode
    If no rows match, then @ref nothing is returned.

    @subsection get_row_iterator Acquiring a Row Iterator for Query Results
    @par From Complex Select Criteria
    To get an iterator for the row values corresponding to a select statement, use @ref SqlUtil::AbstractTable::getRowIterator():
    @code{.py}
Table t(ds, "table_name");
SQLStatement i = table.getRowIterator(sh);
on_exit table.commit();
map printf("row: %y\n", $1), i;
    @endcode
    See @ref select_option_hash for a description of the hash argument; if no argument is passed to the method,
    then an iterator for all rows in the table is returned.

    @subsection get_rows_in_batches Retrieving Multiple Rows in Batches
    @par From Complex Select Criteria
    Multiple rows can be retrieved from a single call to an @ref Qore::SQL::SQLStatement object as returned by @ref SqlUtil::AbstractTable::getRowIterator():
    @code{.py}
Table t(ds, "table_name");
SQLStatement i = table.getRowIterator(sh);
on_exit table.commit();
while (True) {
    list l = i.fetchRows(1000);
    if (!l)
        break;
    printf("rows read: %d\n", l.size());
}
    @endcode
    See @ref select_option_hash for a description of the hash argument; if no argument is passed to the method,
    then an iterator for all rows in the table is returned.

    @subsection column_spec Specifying Columns in Query Output
    @par From Complex Select Criteria
    @ref SqlUtil::AbstractTable "AbstractTable" methods taking a @ref select_option_hash "select option hash argument" support specifying output columns including column operators by assigning a column / column operator list to the @ref select_option_columns "columns" key.
    @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", "started", cop_as("warnings", "warning_count"), cop_as("errors", "error_count")), "where": ("type": "user")));
    @endcode
    @see @ref select_option_columns "select option columns" for details about the \c "columns" option of the @ref select_option_hash "select option hash"

    @subsection select_option_hash Complex Select Criteria

    Selecting data is performed by passing a select option hash argument to a suitable method that will build and execute a DB-specific SQL command based on this value; the following methods take a select option hash argument:
    - @ref SqlUtil::AbstractTable::getRowIterator()
    - @ref SqlUtil::AbstractTable::getSelectSql()
    - @ref SqlUtil::AbstractTable::insertFromSelect()
    - @ref SqlUtil::AbstractTable::insertFromSelectCommit()
    - @ref SqlUtil::AbstractTable::select()
    - @ref SqlUtil::AbstractTable::selectRow()
    - @ref SqlUtil::AbstractTable::selectRows()
    - @ref SqlUtil::AbstractTable::upsertFromSelect()
    - @ref SqlUtil::AbstractTable::upsertFromSelectCommit()

    SQL strings for select statements are constructed programmatically based on a select option hash described below.

    @par Select Option Hash Example:
    @code{.py}
hash soh = (
    "columns": ("id", "name", "q.*"),
    "where": ("type": "user"),
    "limit": 100,
    "offset": 200,
    "join": join_inner(queues, "q"),
);
    @endcode

    The select option hash argument has the following keys; all of which are optional:
    - @ref select_option_comment "comment": an optional string with with comment used in select statement
    - @ref select_option_hint "hint": an optional string with with hint used in select statement (platform dependent)
    - @ref select_option_columns "columns": describes the output columns and any output column operations; if not present, then all columns are returned directly in the output
    - @ref select_option_where "where": (@ref hash_type "hash") describes how any \c "where" clause will be built; if not present, then there is no \c where clause
    - @ref select_option_orderby "orderby": (@ref list_type "list of strings") describes the ordering of the results; if not present (and no other output ordering is required, such as implied by the @ref select_option_offset "offset" option), then results are returned in the order returned by the database server
    - @ref select_option_desc "desc": (@ref bool_type "bool") specifies descending order for results; if not present or not @ref Qore::True "True" and results are ordered, then the results are returned in ascending order
    - @ref select_option_limit "limit": (@ref int_type "int") specifies the maximum number of results to be returned; if not present, then all results are returned
    - @ref select_option_offset "offset": (@ref int_type "int") specifies the starting offset of the first record to be returned (starting with 0)
    - @ref select_option_join "join": (@ref hash_type "hash") specifies any SQL join operations to return results from multiple tables
    - @ref select_option_groupby "groupby": (@ref list_type "list of strings") specifies grouping for aggregate column functions
    - @ref select_option_having "having": (@ref hash_type "hash") specifies filtering for results with aggregate column functions
    - @ref select_option_superquery "superquery": (@ref hash_type "hash") specifies that the rest of the argument belong to a subquery and the hash arguments under this key will select from the subquery
    - @ref select_option_forupdate "forupdate": (@ref bool_type "bool") adds a \c "for update" clause to a select query

    @anchor select_option_comment
    @par Select Option "comment"
    The comment does not have any impact to data returned from the statement but it can help with statement finding and optimizations.

    <b>Comment Example:</b>
    @code{.py}
table.selectRows( ("comment" : "foo bar") );
    @endcode
    will produce select statement like this:
    @code{.py}
select /* foo bar *<!--%%-->/ ...
    @endcode
    @warning Oracle: using different comments in the same SQL can lead to new optimizer statement hard parsing.

    @anchor select_option_hint
    @par Select Option "hint"
    In database query operations, various SQL implementations use hints as additions to the SQL standard that instruct the database engine on how to execute the query. For example, a hint may tell the engine to use as little memory as possible (even if the query will run slowly), or to use or not to use an index (even if the query optimizer would decide otherwise).

    <b>Hint Example:</b>
    @code{.py}
table.selectRows( ("hint" : "full(t1)") );
    @endcode
    will produce select statement like this:
    @code{.py}
select /*+ full(a) *<!--%%-->/ ...
    @endcode
    The string is taken as is and it's up to user to handle correct aliases in join functions etc.
    @note Hints are platform dependent. Curently only Oracle and some versions of PostgreSQL hints are supported in Sqlutil module.
    @warning Use hints only when you know what you are doing.

    @anchor select_option_columns
    @par Select Option "columns"
    <b>Columns Example:</b>
    @code{.py}
list columns = ("id", "name", "started", cop_as("warnings", "warning_count"), cop_as("errors", "error_count"));
*list rows = table.selectRows(("columns": columns, "where": ("type": "user")));
    @endcode
    By default, all columns are returned from a query; to limit the columns returned, or to perform column operations on the columns returned, use the \c "columns" option of the @ref select_option_hash "select option hash". \n\n
    This option takes a list, each element of the list can be one of the following.\n\n
    <b>A Simple String Giving a Column Name</b>\n
    ex: \c "name"
    @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", "started")));
    @endcode \n
    <b>A String in Dot Notation</b>\n
    This format is for use with @ref select_option_join "joins"; ex: \c "q.name"
    @code{.py}
*list rows = table.selectRows(("columns": ("table.id", "t2.customer_name"), "join": join_inner(table2, "t2", ("id": "altid"))));
    @endcode \n
    <b>A Column Operation Specified by a Column Operator Function</b>\n
    ex: <tt>cop_as("column_name", "column_alias")</tt> \n
    See @ref sql_cop_funcs "column operator function" for more information on column operator functions
    @code{.py}
*list rows = table.selectRows(("columns": ("id", cop_as("warnings", "warning_count"), cop_as("errors", "error_count"))));
    @endcode
    For @ref sql_cop_funcs "column operator functions" taking a column name, either a string name or a name in dot notation is acceptable\n\n
    <b>The Value \c "*", Meaning All Columns</b>\n
    ex: \c "*"
    @code{.py}
*list rows = table.selectRows(("columns": "*"));
    @endcode
    This is the default if no \c "columns" key is included in the @ref select_option_hash "select option hash" \n\n
    <b>An \c "*" in Dot Notation</b>\n
    ex: \c "q.*"
    @code{.py}
*list rows = table.selectRows(("columns": ("table.id", "t2.*"), "join": join_inner(table2, "t2", ("id": "altid"))));
    @endcode

    @anchor select_option_where
    @par Select Option "where"
    <b>Where Example:</b>
    @code{.py}
*list rows = table.selectRows(("where": ("type": "user"), "limit": 100, "offset": 200));
    @endcode
    The hash value assigned to this key describes how the \c "where" clause in the query is built.  Because the \c "where" clause logic is common to many SQL methods, this topic is covered in a separate section.  See @ref where_clauses for a detailed description of the value of this key.

    @anchor select_option_orderby
    @par Select Option "orderby"
    <b>Orderby Example:</b>
    @code{.py}
*list rows = table.selectRows(("where": ("account_type": "CUSTOMER"), "orderby": "created_date"));
    @endcode
    This option is a list of the following values:
    - a simple string giving a column name; ex: \c "name"
    - a simple string with a column name preceded by a \c "-" sign; ex: \c "-name", meaning that that column should be sorted in descending order
    - a string giving a table or table alias and a column name in dot notation (for use with @ref select_option_join "joins"); ex: \c "q.name"
    - a positive integer giving the column number for the ordering
    @note
    - By using the @ref select_option_offset "offset option" the results will be automatically ordered according to the primary key of the table

    @anchor select_option_desc
    @par Select Option "desc"
    <b>Desc Example:</b>
    @code{.py}
*list rows = table.selectRows(("where": ("account_type": "CUSTOMER"), "orderby": "created_date", "desc": True));
    @endcode
    If the value of this key is @ref Qore::True "True" and results are ordered (either due to the @ref select_option_orderby "orderby option" or due to implicit ordering by the primary key due to the use of the @ref select_option_offset "offset option"), then results will be sorted in descending order.\n\n
    Otherwise, ordered results are returned in ascending order by default.

    @note per-column descending options can be given by prepending a \c "-" character to the column name in the @ref select_option_orderby "orderby option list"

    @anchor select_option_limit
    @par Select Option "limit"
    <b>Limit Example:</b>
    @code{.py}
*list rows = table.selectRows(("where": ("type": "user"), "limit": 100, "offset": 200));
    @endcode
    This option will limit the number of rows returned.
    @note
    - This option is required if the @ref select_option_offset "offset option" is non-zero
    - If this option is present and an @ref select_option_orderby "orderby option" is also present, then at least a subset of the @ref select_option_orderby "orderby" columns must correspond to a unique key of the table or an exception is raised

    @anchor select_option_offset
    @par Select Option "offset"
    <b>Offset Example:</b>
    @code{.py}
*list rows = table.selectRows(("where": ("type": "user"), "limit": 100, "offset": 200));
    @endcode
    This option specifies the row number offset for the rows returned where the first row is at offset zero.
    @note
    - If this option is present, then either an @ref select_option_orderby "orderby option" must be present of which at least a subset of the @ref select_option_orderby "orderby" columns must correspond to a unique key of the table, or, if no @ref select_option_orderby "orderby option" is used, then the table must have a primary key which is used for the ordering instead.
    - Additionally, this option requires the presence of the @ref select_option_limit "limit option", or an exception will be thrown.
    @see @ref sql_paging

    @anchor select_option_join
    @par Select Option "join"
    <b>Join Example:</b>
    @code{.py}
*list rows = table.selectRows(("columns": ("name", "version", "id", cop_as("st.value", "source"), cop_as("st.value", "offset")),
                                    "join": join_left(function_instance_tags, "st", NOTHING, ("st.tag": "_source"))
                                            + join_left(function_instance_tags, "lt", NOTHING, ("st.tag": "_offset"))));
    @endcode
    To join multiple tables in a single query, use the \c "join" option.  The \c "join" hash key should be assigned to a join description hash as returned by one of the @ref sql_jop_funcs or combined join description hash created by concatenating such values (see an example of this above).
    @note the join columns do not need to be specified in the case that a foreign key in one table exists to the primary key of the other table; in this case this information is assumed for the join automatically

    @see @ref joins for more examples

    @anchor select_option_groupby
    @par Select Option "groupby"
    <b>Groupby Example:</b>
    @code{.py}
*list rows = table.selectRows(("columns": (cop_as(cop_max("service_type"), "type"), cop_count()), "groupby": "service_type"));
    @endcode
    The \c "groupby" option allows for aggregate SQL column operator functions to be used (ex: @ref cop_max(), cop_min()) in select statements.
    The \c "groupby" hash key should be assigned to a list of column specifiers or a single column specifier.  Column specifiers for the \c "groupby"
    key are strings giving column names, optionally in dot notation or positive column numbers.

    @anchor select_option_having
    @par Select Option "having"
    <b>Having Example:</b>
    @code{.py}
*list rows = table.selectRows(("columns": (cop_as(cop_max("service_type"), "type"), cop_count()), "groupby": "service_type", "having": ("service_type": (COP_COUNT, op_ge(100)))));
    @endcode
    The \c "having" option allows for query results with aggregate SQL column operator functions to be filtered by user-defined criteria.
    The \c "having" hash key should be assigned to a hash where each key is a column specifier (optionally in dot notation) and the values are lists with two elements; the first element must be a @ref sql_cops "column operator code", and the second element is a column operator description normally provided by using a @ref sql_cop_funcs "column operator function" as in the above example.

    @anchor select_option_superquery
    @par Select Option "superquery"
    <b>Superquery Example:</b>
    @code{.py}
*list rows = table.selectRows("columns": ("serviceid", "service_methodid", cop_as(cop_over(cop_max("service_methodid"), "serviceid"), "max_methodid")), "superquery": ("columns": ("serviceid", "service_methodid"), "where": ("max_methodid": op_ceq("service_methodid"))));
    @endcode
    The \c "superquery" option allows for the rest of the query arguments to define a subquery where as the hash arguments assigned to the \c "superquery" key define the select made from the subquery.  In the example above, the \c "OVER" sql windowing function is used and then rows matching the \c "max_methodid)" result value are selected.\n\n
    The above example results in an SQL command equivalent to the following:
    @code{.py}
*list rows = table.vselectRows("select serviceid,service_methodid from (select serviceid,service_methodid,max(service_methodid) over (partition by serviceid) as max_methodid from schema.service_methods) subquery where max_methodid = service_methodid");
    @endcode
    @note that MySQL does not support SQL windowing functions so the above example would fail on MySQL.

    @anchor select_option_forupdate
    @par Select Option "forupdate"
    <b>For Update Example:</b>
    @code{.py}
on_success ds.commit();
on_error ds.rollback();

*list rows = table.selectRows("columns": ("serviceid", "service_methodid"), "forupdate": True);
    @endcode
    \n The \c "forupdate" option allows for the rows selected to be locked for updating; to release the locks, call commit() or rollback() on the underlying datasource object.
  The above example results in an SQL commit equivalent to the following:
    @code{.py}
*list rows = table.vselectRows("select serviceid,service_methodid from schema.service_methods for update");
    @endcode

    @subsection sql_paging Select With Paging

    There is support for paging query results in the following methods:
    - @ref SqlUtil::AbstractTable::getRowIterator()
    - @ref SqlUtil::AbstractTable::getSelectSql()
    - @ref SqlUtil::AbstractTable::select()
    - @ref SqlUtil::AbstractTable::selectRows()

    @note the above list also applies to the corresponding @ref SqlUtil::AbstractTable methods

    Each of these methods takes a @ref select_option_hash "select option hash argument" that allows the \c "limit" and \c "offset" options to be specified to specify the data window for the results.

    If the \c "offset" options is used, then an \c "orderby" option is required which must match some unique constraint or unique index on the table to guarantee the order of results, unless the table has a primary key, in which case the primary key will be used by default if no \c "orderby" option is supplied.

    @par Example:
    Select 100 rows starting at row 200 (the table's primary key will be used for the \c "orderby" option by default): \n
    @code{.py}
*list rows = table.selectRows(("where": ("type": "user"), "limit": 100, "offset": 200));
    @endcode
    As an illustration of the different SQL that is generated for different database types; for the above query, here is the SQL generated for Oracle:
    @code{.py}
ds.vselectRows("select * from (select /*+ first_rows(100) *<!--%%-->/ a.*, rownum rnum from (select * from schema.table where type = %v order by type) a where rownum <= %v) where rnum > %v", ("user", 300, 200));
    @endcode
    And for PostgreSQL:
    @code{.py}
ds.vselectRows("select * from public.table where type = %v order by type limit %v offset %v", ("user", 100, 200));
    @endcode

    @subsection check_matching_rows Check For At Least One Matching Row

    Use the @ref SqlUtil::AbstractTable::findSingle() method to find at least one matching row:
    @code{.py}
*hash h = table.findSingle(("account_type": "CUSTOMER"));
if (h)
    printf("found 1 customer row: %y\n", l[0]);
    @endcode

    Also it's possible to use the \c "limit" option to make an efficient check for at least one matching row as in the following example (which is functionally equivalent to the previous example):
    @code{.py}
*hash h = table.selectRow(("where": ("account_type": "CUSTOMER"), "limit": 1));
if (h)
    printf("found 1 customer row: %y\n", l[0]);
    @endcode

    @section inserting_data Inserting Data into the Database

    The following methods can be used to insert data into the database:
    - @ref SqlUtil::AbstractTable::insert(): inserts a single row into a table without committing the transaction
    - @ref SqlUtil::AbstractTable::insertCommit(): inserts a single row into a table and commits the transaction
    - @ref SqlUtil::AbstractTable::insertFromSelect(): inserts data in a table based on a select statement created from the @ref select_option_hash "select option hash" argument and without committing the transaction
    - @ref SqlUtil::AbstractTable::insertFromSelectCommit(): inserts data in a table based on a select statement created from the @ref select_option_hash "select option hash" argument and commits the transaction

    @see @ref sql_upsert for information about upserting or merging data

    @subsection inserting_data_explicitly Inserting Data Explicitly

    @par Example:
    @code{.py}
table.insert(("id": id, "name": name, "created": now_us()));
    @endcode

    Data can be explicitly inserted into the database with immediate values with @ref SqlUtil::AbstractTable::insert() and @ref SqlUtil::AbstractTable::insertCommit() as in the above example.

    Additionally, instead of giving a literal value to be inserted, @ref sql_iop_funcs can be used to insert values based on SQL operations used directly in the insert statement.

    @subsection inserting_data_from_select Inserting Data From a Select Statement

    @par Example:
    @code{.py}
int rows = table.insertFromSelect(("id", "name", "created"), source_table, (("columns": ("cid", "fullname", "created"), "where": ("type": "CUSTOMER"))));
    @endcode

    Data can be inserted into the database based on the results of a select statement with @ref SqlUtil::AbstractTable::insertFromSelect() and @ref SqlUtil::AbstractTable::insertFromSelectCommit() as in the above example.

    The example above would generate a %Qore SQL command like the following:
    @code{.py}
return ds.vexec("insert into schema.table (id,name,created) select cid,fullname,created from schema.source_table where type = %v", ("CUSTOMER"));
    @endcode

    The return value of these methods is the number of rows inserted.  See @ref select_option_hash "select option hash" for more information about how to form the select criteria in these methods.

    @subsection inserting_data_from_iterator Inserting Data from an Iterator Source

    To insert data from an iterator source (such as an @ref Qore::SQL::SQLStatement object), call @ref SqlUtil::AbstractTable::insertFromIterator() or @ref SqlUtil::AbstractTable::insertFromIteratorCommit() as in the following example:

    @par Example:
    @code{.py}
# get the rows to be inserted
list l = get_table_rows();
# insert the data and commit after every 5000 rows
table.insertFromIterator(l.iterator(), ("commit_block": 5000));
    @endcode

    The iterator given to the @ref SqlUtil::AbstractTable::insertFromIterator() or @ref SqlUtil::AbstractTable::insertFromIteratorCommit() methods can be any iterator whose @ref Qore::AbstractIterator::getValue() "getValue()" method returns a @ref hash_type "hash".

    @note the @ref SqlUtil::AbstractTable::InsertFromIteratorOptions "insert option" \c "commit_block" can be used to insert a large amount of data in pieces in order to avoid overwhelming the database server's rollback cache

    @section updating_data Updating Data

    The following methods can be used to update data:
    - @ref SqlUtil::AbstractTable::update(): updates a single row and does not commit the transaction
    - @ref SqlUtil::AbstractTable::updateCommit(): updates a single row and commits the transaction

    @par Example:
    @code{.py}
int rows_updated = t.update(("permission_type": uop_append("-migrated", uop_lower())));
    @endcode

    The example above generates a %Qore SQL command like the following on Oracle and PostgreSQL for example:
    @code{.py}
return ds.vexec("update schema.table set permission_type = lower(permission_type) || '-migrated');
    @endcode
    And the following on MySQL:
    @code{.py}
return ds.vexec("update schema.table set permission_type = concat(lower(permission_type), '-migrated'));
    @endcode

    @section deleting_data Deleting Data

    The following methods can be used to dekete data:
    - @ref SqlUtil::AbstractTable::del(): updates the table based on a @ref where_clauses "where clause" and does not commit the transaction
    - @ref SqlUtil::AbstractTable::delCommit(): updates the table based on a @ref where_clauses "where clause" and commits the transaction
    - @ref SqlUtil::AbstractTable::truncate(): truncates the table and does not commit the transaction
    - @ref SqlUtil::AbstractTable::truncateCommit(): truncates the table and commits the transaction releasing the transaction lock on the underlying datasource object

    @par Example:
    @code{.py}
int dcnt = table.del(("record_type": "OLD-CUSTOMER"));
    @endcode

    The above example would generate a %Qore SQL command like the following:
    @code{.py}
return ds.vexec("delete from schema.table where record_type = %v", ("OLD-CUSTOMER"));
    @endcode

    The @ref SqlUtil::AbstractTable::del() and @ref SqlUtil::AbstractTable::delCommit() methods can be used to delete data from the database.

    See @ref where_clauses for information about specifying the criteria for the rows to be deleted.

    @section joins Joining Tables

    Joining tables is made by providing a join specification to the @ref select_option_join "join select option" in
    a @ref select_option_hash "select option hash" as in the following example:
    @code{.py}
*list rows = table.selectRows(("columns": ("table.id", "t2.customer_name"), "join": join_inner(table2, "t2", ("id": "altid"))));
    @endcode
    In the above example, \a table is joined with \a table2 on <tt>table.id = table2.altid</tt>.

    Joins on multiple tables are performed by combining the results of @ref sql_op_funcs "join functions" with the @ref plus_operator "+ operator"
    as follows:
    @code{.py}
*list rows = table.selectRows(("join": join_inner(table2, "t2", ("id": "altid")) + join_inner(table3, "t3")));
    @endcode
    In the above example, \a table is joined with \a table2 on <tt>table.id = table2.altid</tt> and with \a table3 on an
    automatically detected primary key to foreign key relationship between the two tables.

    Joins are by default made with the primary table; to join with another join table, then give the alias for the table as the first
    argument to the @ref sql_op_funcs "join function" as in the following example:
    @code{.py}
*list rows = table.selectRows(("join": join_inner(table2, "t2", ("id": "altid")) + join_inner("t2", table3, "t3")));
    @endcode
    In the above example, \a table is joined with \a table2 on <tt>table.id = table2.altid</tt> and \a table2 (aliased as \c t2) is joined
    with \a table3 (aliased as \c t3) on an automatically detected primary key to foreign key relationship between the two tables.

    @see @ref select_option_join "join select option"

    @section where_clauses Where Clauses

    Several methods accept a hash of conditions to build a \c "where" clause to restrict the rows that are operated on or returned; for example:
    - @ref SqlUtil::AbstractTable::del()
    - @ref SqlUtil::AbstractTable::delCommit()
    - @ref SqlUtil::AbstractTable::findAll()
    - @ref SqlUtil::AbstractTable::findSingle()
    - @ref SqlUtil::AbstractTable::getRowIterator()
    - @ref SqlUtil::AbstractTable::getSelectSql()
    - @ref SqlUtil::AbstractTable::insertFromSelect()
    - @ref SqlUtil::AbstractTable::insertFromSelectCommit()
    - @ref SqlUtil::AbstractTable::select()
    - @ref SqlUtil::AbstractTable::selectRow()
    - @ref SqlUtil::AbstractTable::selectRows()
    - @ref SqlUtil::AbstractTable::update()
    - @ref SqlUtil::AbstractTable::updateCommit()
    - @ref SqlUtil::AbstractTable::upsertFromSelect()
    - @ref SqlUtil::AbstractTable::upsertFromSelectCommit()

    @note the above list also applies to the corresponding @ref SqlUtil::AbstractTable methods

    The where clause or condition hash is made of keys signifying the column names, and either a direct value meaning that the column value has to match exactly, or SQL operators can be given by using the appropriate operator function as the key value.  Each member of the where hash translates to an expression that is combined with \c "AND" in the SQL query; to combine expressions with \c "OR", there are two options:
    - use the @ref SqlUtil::wop_or() function to combine where expressions with the \c "or" operator
    - use a list of @ref select_option_hash "select option hashes", which will combine each @ref select_option_hash "select option hash" with \c "OR" as in @ref where_list "this example".

    The where condition hash has the following format:
    - each key gives a column name or a table/alias with column name in dot notation
    - the values are either direct values, meaning that the equality operator (\c "=") is used, or a @ref sql_op_funcs "SQL operator function" for operators in the where clause

    @note To reference a column more than once in a where clause, prefix the column specification with a unique number and a colon as in the following example: @code{.py} hash w = ("0:created": op_ge(mindate), "1:created": op_lt(maxdate)); @endcode

    See @ref sql_op_funcs for a list of operator functions.

    @par Where Hash Example:
    @code{.py}
hash w = (
    "name": "Smith",
    "account_type": op_like("%CUSTOMER%"),
    "id": op_ge(500),
);
    @endcode \n
    The preceding example results in a where clause equivalent to: \c "name = 'Smith' and type like '%CUSTOMER%' and id >= 500", except
    that bind by value is used, so, if used in a context like the following:
    @code{.py}
Table t(ds, "table");
*hash qh = t.select(("where": w));
    @endcode \n
    the complete query would look instead as follows:
    @code{.py}
ds.vselect("select * from table where name = %v and account_type like %v and id >= %v", ("Smith", "%CUSTOMER%", 500));
    @endcode

    @anchor where_list
    @par Where List Example:
    @code{.py}
hash w1 = (
    "name": "Smith",
    "account_type": op_like("%CUSTOMER%"),
    "id": op_ge(500),
);
hash w2 = (
    "name": "Jones",
    "account_type": op_like("%VENDOR%"),
    "id": op_ge(2500),
);
Table t(ds, "table");
*hash qh = t.select(("where": (w1, w2)));
    @endcode \n
    the complete query would look instead as follows:
    @code{.py}
ds.vselect("select * from table where (name = %v and account_type like %v and id >= %v) or (name = %v and account_type like %v and id >= %v)", ("Smith", "%CUSTOMER%", 500, "Jones", "%VENDOR%", 2500));
    @endcode

    @par Code Examples:
    Find a single row in the table where the \c "permission_type" column is a value between \c "US" and \c "UX":\n
    @code{.py}
*hash row = table.findSingle(("permission_type": op_between("US", "UX")));
    @endcode
    resulting in an internal SQL command that looks as follows (depending on the database):
    @code{.py}
*hash row = ds.vselectRow("select * from table where permission_type between %v and %v limit %v", ("US", "UX", 1));
    @endcode \n
    Delete all rows in the table where the \c "name" column is like \c "%Smith%":\n
    @code{.py}
int row_count = table.del(("name": op_like("%Smith%")));
    @endcode
    resulting in an internal SQL command that looks as follows:
    @code{.py}
ds.vexec("delete from table where name like %v", ("%Smith%"));
    @endcode \n
    Find all rows where \c "id" is greater than \c 100 and \c "created" is after \c 2013-03-01:\n
    @code{.py}
*list rows = table.findAll(("id": op_gt(100), "created": op_gt(2013-03-01)));
    @endcode
    resulting in an internal SQL command that looks as follows:
    @code{.py}
ds.vexec("select * from table where id > %v and created > %v", (100, 2013-03-01));
    @endcode \n

    @section sql_upsert Upserting or Merging Data

    This module offers a high-level api for "upserting" or merging data from one table into another table through the following methods:
    - @ref SqlUtil::AbstractTable::upsert()
    - @ref SqlUtil::AbstractTable::upsertCommit()
    - @ref SqlUtil::AbstractTable::getUpsertClosure()
    - @ref SqlUtil::AbstractTable::getUpsertClosureWithValidation()
    - @ref SqlUtil::AbstractTable::upsertFromIterator()
    - @ref SqlUtil::AbstractTable::upsertFromIteratorCommit()
    - @ref SqlUtil::AbstractTable::upsertFromSelect()
    - @ref SqlUtil::AbstractTable::upsertFromSelectCommit()

    @subsection sql_upsert_single Upsert a Single Row

    @par Example:
    @code{.py}
table.upsert(("id": id, "name": name, "account_type": account_type));
    @endcode

    To upsert or merge a single row in the database, call @ref SqlUtil::AbstractTable::upsert() or @ref SqlUtil::AbstractTable::upsertCommit() with the
    single row to be upserted or merged as a hash as in the preceding example.

    @subsection sql_upsert_many Upserting Many Rows Using An Upsert Closure

    To upsert or merge many rows by using an upsert closure, call @ref SqlUtil::AbstractTable::getUpsertClosure() or @ref SqlUtil::AbstractTable::getUpsertClosureWithValidation() and provide an example row as an argument to acquire a closure that will be executed on the rest of the rows as in the following example.

    @par Simple Example:
    @code{.py}
# get the rows to be inserted
list l = get_table_rows();

if (l) {
    code upsert = table.getUpsertClosure(l[0]);

    on_success ds.commit();
    on_error ds.rollback();

    # loop through the reference data rows
    map upsert($1), l;
}
    @endcode

    @par Complex Example With Callbacks:
    @code{.py}
# set the upsert strategy depending on the use case
int upsert_strategy = verbose ? AbstractTable::UpsertSelectFirst : AbstractTable::UpsertAuto;

# hash summarizing changes
hash sh;

# get the rows to be inserted
list l = get_table_rows();

if (l) {
    # get the upsert closure to use based on the first row to be inserted
    code upsert = table.getUpsertClosure(l[0], upsert_strategy);

    on_success ds.commit();
    on_error ds.rollback();

    # loop through the reference data rows
    foreach hash h in (l) {
        int code = upsert(h);
        if (code == AbstractTable::UR_Unchanged)
            continue;

        string change = AbstractTable::UpsertResultMap{code};
        ++sh{change};

        if (!verbose) {
            printf(".");
            flush();
        }
        else if (verbose > 1)
            printf("*** reference data %s: %y: %s\n", table.getName(), h, change);
    }

    # show table summary
    if (sh)
        printf("*** reference data %s: %s\n", table.getName(), (foldl $1 + ", " + $2, (map sprintf("%s: %d", $1.key, $1.value), sh.pairIterator())));
    else
        printf("*** reference data %s: OK\n", table.getName());
}
    @endcode

    @subsection sql_upsert_from_iterator Upserting Many Rows from an Iterator Source

    To upsert or merge many rows from an iterator source (such as an @ref Qore::SQL::SQLStatement object), call @ref SqlUtil::AbstractTable::upsertFromIterator() or @ref SqlUtil::AbstractTable::upsertFromIteratorCommit() as in the following example:

    @par Simple Example:
    @code{.py}
# get the rows to be inserted
list l = get_table_rows();
table.upsertFromIterator(l.iterator());
    @endcode

    @par Complex Example With Callbacks:
    @code{.py}
# set the upsert strategy depending on the use case
int upsert_strategy = verbose ? AbstractTable::UpsertSelectFirst : AbstractTable::UpsertAuto;

# get the rows to be inserted
list l = get_table_rows();

code callback = sub (string table_name, hash row, int result) {
    if (result == AbstractTable::UR_Unchanged)
        return;
    string change = AbstractTable::UpsertResultMap{result};
    if (verbose)
        printf("*** reference data %s: %y: %s\n", table_name, row, change);
};

hash sh = table.upsertFromIterator(l.iterator(), upsert_strategy, False, ("info_callback": callback, "commit_block": 5000));
if (sh)
    printf("*** reference data %s: %s\n", table.getName(), (foldl $1 + ", " + $2, (map sprintf("%s: %d", $1.key, $1.value), sh.pairIterator())));
else
    printf("*** reference data %s: OK\n", table.getName());
    @endcode

    The iterator given to the @ref SqlUtil::AbstractTable::upsertFromIterator() or @ref SqlUtil::AbstractTable::upsertFromIteratorCommit() methods can be any iterator whose @ref Qore::AbstractIterator::getValue() "getValue()" method returns a @ref hash_type "hash".

    @note the @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c "commit_block" can be used to insert a large amount of data in pieces in order to avoid overwhelming the database server's rollback cache

    @subsection sql_upsert_from_select Upserting Many Rows from a Select Statement

    To upsert or merge many rows from a select statement, use @ref SqlUtil::AbstractTable::upsertFromSelect() or @ref SqlUtil::AbstractTable::upsertFromSelectCommit() as in the following example:

    @par Simple Example:
    @code{.py}
table.upsertFromSelect(table2, ("where": ("account_type": "CUSTOMER")));
    @endcode

    @par Complex Example With Callbacks:
    @code{.py}
# set the upsert strategy depending on the use case
int upsert_strategy = verbose ? AbstractTable::UpsertSelectFirst : AbstractTable::UpsertAuto;

code callback = sub (string table_name, hash row, int result) {
    if (result == AbstractTable::UR_Unchanged)
        return;
    string change = AbstractTable::UpsertResultMap{result};
    if (verbose)
        printf("*** reference data %s: %y: %s\n", table_name, row, change);
};

hash sh = table.upsertFromSelect(table2, ("where": ("account_type": "CUSTOMER")), upsert_strategy, False, ("info_callback": callback, "commit_block": 5000));
if (sh)
    printf("*** reference data %s: %s\n", table.getName(), (foldl $1 + ", " + $2, (map sprintf("%s: %d", $1.key, $1.value), sh.pairIterator())));
else
    printf("*** reference data %s: OK\n", table.getName());
    @endcode

    The source table does not have to be in the same database or even of the same database type (ie you can upsert to and from any database type supported by SqlUtil).

    @note the @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c "commit_block" can be used to insert a large amount of data in pieces in order to avoid overwhelming the database server's rollback cache

    @subsection sql_upsert_with_delete Upserting Many Rows and Deleting Unwanted Rows

    Call any of the batch upsert methods with @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c delete_others set to @ref Qore::True "True" to perform a batch upsert / merge operation on a table, and then scan the table and delete any unwanted rows.  If there are no rows to be deleted, these calls have very similar performance to the batch upsert method calls without any deletions, however, if there are rows to be deleted, then the entire source table must be iterated to compare each row to valid data to delete the rows that do not belong.  Therefore for large tables this can be an expensive operation.

    The only difference in the following examples and the preceding ones is that @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c delete_others is @ref Qore::True "True" in these examples.

    @par Simple Example:
    @code{.py}
# get the rows to be inserted
list l = get_table_rows();
table.upsertFromSelect(table2, ("where": ("account_type": "CUSTOMER")), AbstractTable::UpsertAuto, ("delete_others": True, "commit_block": 5000));
    @endcode

    @par Complex Example With Callbacks:
    @code{.py}
# set the upsert strategy depending on the use case
int upsert_strategy = verbose ? AbstractTable::UpsertSelectFirst : AbstractTable::UpsertAuto;

# get the rows to be inserted
list l = get_table_rows();

code callback = sub (string table_name, hash row, int result) {
    if (result == AbstractTable::UR_Unchanged)
        return;
    string change = AbstractTable::UpsertResultMap{result};
    if (verbose)
        printf("*** reference data %s: %y: %s\n", table_name, row, change);
};

hash sh = table.upsertFromSelect(table2, ("where": ("account_type": "CUSTOMER")), upsert_strategy, ("delete_others": True, "info_callback": callback, "commit_block": 5000));
if (sh)
    printf("*** reference data %s: %s\n", table.getName(), (foldl $1 + ", " + $2, (map sprintf("%s: %d", $1.key, $1.value), sh.pairIterator())));
else
    printf("*** reference data %s: OK\n", table.getName());
    @endcode

    @note the @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c "commit_block" can be used to insert a large amount of data in pieces in order to avoid overwhelming the database server's rollback cache

    @subsection sql_upsert_strategies Upsert Strategies
    The approach used is based on one of the following strategies (see @ref upsert_options):
    - @ref SqlUtil::AbstractTable::UpsertAuto "AbstractTable::UpsertAuto": if the target table is empty, then @ref SqlUtil::AbstractTable::UpsertInsertFirst is used, otherwise @ref SqlUtil::AbstractTable::UpsertUpdateFirst is used; note that if a driver-specific optimized version of the upsert operation is implemented, this strategy will normally result in the best performance
    - @ref SqlUtil::AbstractTable::UpsertInsertFirst "AbstractTable::UpsertInsertFirst": first an insert will be attempted, if it fails due to a duplicate key, then an update will be made; this strategy should be used if more inserts will be made than updates
    - @ref SqlUtil::AbstractTable::UpsertUpdateFirst "AbstractTable::UpsertUpdateFirst": first an update will be attempted, if it fails due to missing data, then an insert is performed; this strategy should be used if more updates will be made then inserts
    - @ref SqlUtil::AbstractTable::UpsertSelectFirst "AbstractTable::UpsertSelectFirst": first a select is made on the unique key, if the data to be updated is equal, nothing is done and @ref upsert_results "upsert result" @ref SqlUtil::AbstractTable::UR_Unchanged is returned
    - @ref SqlUtil::AbstractTable::UpsertInsertOnly "AbstractTable::UpsertInsertOnly": insert if the row doesn't exist, otherwise do nothing and @ref upsert_results "upsert result" @ref SqlUtil::AbstractTable::UR_Unchanged is returned
    - @ref SqlUtil::AbstractTable::UpsertUpdateOnly "AbstractTable::UpsertUpdateOnly": update if the row exists, otherwise do nothing and @ref upsert_results "upsert result" @ref SqlUtil::AbstractTable::UR_Unchanged is returned

    @note @ref SqlUtil::AbstractTable::UpsertSelectFirst "AbstractTable::UpsertSelectFirst" is the only upsert strategy that can return @ref SqlUtil::AbstractTable::UR_Updated; the @ref SqlUtil::AbstractTable::UpsertSelectFirst "AbstractTable::UpsertSelectFirst" strategy should be used when verbose reporting is required, particularly if it's necessary to report the actual number of changed rows.
*/
/** @page schema_management Schema Management

    @tableofcontents

    @section schema_management_intro Introduction to Schema Management

    Most schema management is performed by using the @ref SqlUtil::Database "Database" class (which is a wrapper for the @ref SqlUtil::AbstractDatabase "AbstractDatabase" class).

    The most complex object is the @ref SqlUtil::AbstractTable "AbstarctTable", which has its own @ref table_management "section".

    One of the primary goals of schema management in %SqlUtil is to facilitate automatic schema management from a database-independent schema
    description. The idea is that a schema can be described in a generic way and then %SqlUtil can use the generic schema description to align
    the database with the description.  Aligning in %SqlUtil is performed as follows:
    - if the object does not exist, then it is created
    - if the object exists, but does not match the description, it is either:
      - dropped and recreated, or
      - modified in place
    - if the object exists and matches, then nothing is done

    Unfortuntely, it's not currently possible to describe all database objects in a generic way (and it probably never will be); in particular,
    functions, triggers, and other objects based on procedural code or database-specific functionality have to be defined in their
    database-specific formats.

    The following is a table describing the alignment support for various objects supported by %SqlUtil; for database-specific objects, see the
    database-specific %SqlUtil module:
    - <a href="../../MysqlSqlUtil/html/index.html">MysqlSqlUtil</a>
    - <a href="../../OracleSqlUtil/html/index.html">OracleSqlUtil</a>
    - <a href="../../PgsqlSqlUtil/html/index.html">PgsqlSqlUtil</a>

    |!Database Object|!Alignment Type
    |@ref SqlUtil::AbstractColumn "Columns"|aligned in place
    |@ref SqlUtil::AbstractConstraint "Constraints"|dropped and recreated
    |@ref SqlUtil::AbstractFunction "Functions" and @ref SqlUtil::AbstractFunction "Procedures"|dropped and recreated
    |@ref SqlUtil::AbstractIndex "Indexes"|dropped and recreated
    |@ref SqlUtil::AbstractSequence "Sequences"|dropped and recreated
    |@ref SqlUtil::AbstractTrigger "Triggers"|dropped and recreated
    |@ref SqlUtil::AbstractTable "AbstractTables"|aligned in place

    @note
    - For objects that are dropped and recreated, renaming the object is generally supported if the underlying database server supports
    renaming.
    - Schemas (and other objects) can also be dropped based on a generic description (or descriptions derived from the database); see
    @ref SqlUtil::Database::getDropSchemaSql()

    @section creating_new_objects Creating New Objects

    New objects are generally creating using appropriate methods from the @ref SqlUtil::Database "Database" class, such as the following:
    - @ref SqlUtil::Database::makeFunction() "Database::makeFunction()"
    - @ref SqlUtil::Database::makeProcedure() "Database::makeProcedure()"
    - @ref SqlUtil::Database::makeSequence() "Database::makeSequence()"
    - @ref SqlUtil::Database::makeTable() "Database::makeTable()"

    Tables can also be created by instantiating a @ref SqlUtil::Table "Table" object (see @ref SqlUtil::AbstractTable::constructor()).

    Each database driver may provide additional objects (such as types or materialized views, etc); see driver-specific documentation for more
    information.

    @section retrieving_objects Retrieving Objects from the Database

    Existing objects can be retrieved from the database using appropriate methods from the @ref SqlUtil::Database "Database" class,
    such as the following:
    - @ref SqlUtil::Database::getFunction() "Database::getFunction()"
    - @ref SqlUtil::Database::getProcedure() "Database::getProcedure()"
    - @ref SqlUtil::Database::getSequence() "Database::getSequence()"
    - @ref SqlUtil::Database::getTable() "Database::getTable()"

    Each database driver may provide additional objects (such as types or materialized views, etc); see driver-specific documentation for more
    information.

    @section listing_objects Listing Objects

    Listing database objects can be performed using appropriate methods from the @ref SqlUtil::Database "Database" class, such as the following:
    - @ref SqlUtil::Database::listFunctions() "Database::listFunctions()"
    - @ref SqlUtil::Database::listProcedures() "Database::listProcedures()"
    - @ref SqlUtil::Database::listSequences() "Database::listSequences()"
    - @ref SqlUtil::Database::listTables() "Database::listTables()"
    - @ref SqlUtil::Database::listViews() "Database::listViews()"

    Alternatively, iterators for lists of objects can be acquired with the following methods:
    - @ref SqlUtil::Database::functionIterator() "Database::functionIterator()"
    - @ref SqlUtil::Database::procedureIterator() "Database::procedureIterator()"
    - @ref SqlUtil::Database::sequenceIterator() "Database::sequenceIterator()"
    - @ref SqlUtil::Database::tableIterator() "Database::tableIterator()"
    - @ref SqlUtil::Database::viewIterator() "Database::viewIterator()"

    @section interdependent_tables Working With Interdependent Tables

    The @ref SqlUtil::Tables class manages a group of tables and the foreign key dependencies between the tables.
    This class is useful when managing an entire schema, particularly in the context of manual changes to
    a schema, such as a schema upgrade or downgrade for complex scenarios that are beyond the capabilities of
    the automatic schema and table alignment code.

    Below you can find some examples of what the @ref SqlUtil::Tables "Tables" class can do.

    @par Cache All Tables from a Schema
    To load all tables in a particular schema into an object of class @ref SqlUtil::Tables "Tables":
    @code{.py}
Tables tables(ds);
    @endcode

    @par Drop All Foreign Constraints to a Table
    The following is an example of how all foreign constraints to a particular table can be dropped:
    @code{.py}
*list l = tables.getDropAllForeignConstraintsOnTableSql("table_name", ("sql_callback_executed": True));
{
    on_success ds.commit();
    on_error ds.rollback();
    map ds.execRaw($1), l;
}
    @endcode
    This can be useful when performing a schema change and a table will be dropped, for example.

    @note by setting the @ref sql_callback_executed "sql_callback_executed option" to @ref Qore::True "True", the affected constraints and linked unique constraints are also updated in the cached table objects, see @ref sql_callback_executed for more information

    @par Drop a Constraint If It Exists
    The following code provides an example of how to unconditionally drop a constraint from a table and update the internal links between unique constraints and foreign constraints (if a foreign constraint is dropped):
    @code{.py}
*string sql = tables.getDropConstraintIfExistsSql("table_name", "fk_table_name_other_table", ("sql_callback_executed": True));
if (sql) {
    on_success ds.commit();
    on_error ds.rollback();
    ds.execRaw(sql);
}
    @endcode

    @note by setting the @ref sql_callback_executed "sql_callback_executed option" to @ref Qore::True "True", the affected constraints and linked unique constraints are also updated in the cached table objects, see @ref sql_callback_executed for more information

    @par Rename a Table
    The following code gives an example of renaming a table and updating all foreign constraint and unique key links in impacted tables if the source table exists and the target name does not exist:
    @code{.py}
*string sql = tables.getRenameTableIfExistsSql("old_table_name", "new_table_name", ("sql_callback_executed": True));
if (sql) {
    on_success ds.commit();
    on_error ds.rollback();
    ds.execRaw(sql);
}
    @endcode

    @note by setting the @ref sql_callback_executed "sql_callback_executed option" to @ref Qore::True "True", the affected constraints and linked unique constraints are also updated in the cached table objects, see @ref sql_callback_executed for more information

    @section aligning_schemas Aligning Schemas With a Template

    A schema template can be created any way required (ie by hand, or by retrieving the description from another database), but typically they will be created from a @ref schema_desc_hash "schema description hash".

    The following code provides an example of creating a schema template and using it to align the schema in the database with the template using callbacks:
    @code{.py}
int change_count;
code info_callback = sub (string str, int ac, string type, string name, *string table, *string new_name, *string info) {
    if (ac != AbstractDatabase::AC_NotFound && ac != AbstractDatabase::AC_Unchanged)
        ++change_count;
    if (verbose)
        printf("*** %s\n", str);
    else {
        print(AbstractDatabase::ActionLetterMap{ac});
        flush();
    }
};

code sql_callback = sub (string str) {
    if (verbose > 1)
        printf("%s\n", str);
    ds.execRaw(str);
};

hash callback_opts = ("info_callback": info_callback, "sql_callback": sql_callback, "sql_callback_executed": True);

Database db(ds);
Tables table_cache();
db.getAlignSql(schema_template, callback_opts, table_cache);
    @endcode

    @subsection schema_desc_hash Schema Description Hash

    The schema description hash has the following structure:
    - \c tables: a hash of @ref table_desc_hash "table description hashes" keyed by table name; each entry describes a table
    - \c sequences: a hash of sequence description hashes keyed by sequence name; each entry describes a sequence
    - \c functions: a hash of function description hashes keyed by function name; each entry describes a function
    - \c procedures: a hash of procedure description hashes keyed by procedure name; each entry describes a procedure
    - \c driver: this key can optionally contain a hash keyed by driver name which contains a hash of values that will be added to the schema description hash before processing; this way a schema description hash can contain all the information required for the schema including driver-specific options; any driver-specific options will overwrite values in the top level of the hash if there are duplicate hash keys

    Each database driver may provide additional objects (such as types or materialized views, packages, etc); see
    driver-specific documentation for more information.

    The following is an example of a schema description hash:
    @code{.py}
const Options = (
    "driver": (
        "oracle": (
            "compute_statistics": True,
            "character_semantics": True,
        ),
    ),
    );

const T_Customers = (
    "columns": (
        "id": (
            "qore_type": Type::Number,
            "size": 14,
            "notnull": True,
            # this column is normally populated from a sequence by a trigger, but mysql
            # enforces not null constraints before "before insert" triggers are fired, so
            # we can't use our emulated sequences on mysql with a not null constraint on this
            # column, also since this column is a part of the primary key for this table,
            # we can't leave it nullable, so we use auto_increment
            "driver": ("mysql": ("native_type": "bigint", "unsigned": True, "auto_increment": True, "size": NOTHING)),
        ),
        "family_name": (
            "qore_type": Type::String,
            "size": 120,
            "notnull": True,
        ),
        "first_names": (
            "qore_type": Type::String,
            "size": 240,
            "notnull": True,
        ),
        "created": (
            "qore_type": Type::Date,
            "notnull": True,
            # this column is populated by a trigger, but mysql enforces not null
            # constraints before "before insert" triggers are fired, so for mysql only
            # this column must be nullable
            "driver": ("mysql": ("notnull": False)),
        ),
        "modified": (
            "qore_type": Type::Date,
        ),
    ),
    "primary_key": ("name": "pk_customers", "columns": "id"),
    "indexes": (
        "sk_customers_name": ("columns": "family_name"),
    ),
    "driver": (
        "pgsql": (
            "functions": (
                "trig_customers()": "returns trigger language plpgsql as function$ begin if (tg_op = 'INSERT') then if new.created is null then select current_timestamp into new.created;
    end if; end if; if new.modified is null then select current_timestamp into new.modified; end if; return new; end; function$", #",
            ),
        ),
    ),
    "triggers": (
        "driver": (
            "oracle": (
                "trig_customers": "BEFORE INSERT OR UPDATE ON customers REFERENCING NEW AS NEW OLD AS OLD FOR EACH ROW begin if inserting then if :new.id is null then select seq_customers.nextval into :new.id from dual; end if; if :new.created is null then :new.created := sysdate; end if; end if; if :new.modified is null or :new.modified = :old.modified then :new.modified := sysdate; end if; end;",
            ),
            "pgsql": (
                "trig_customers": "before insert or update on customers for each row execute procedure trig_customers()",
            ),
            "mysql": (
                "trig_customers_insert": "before insert on customers for each row begin if new.created is null then set new.created = now(); end if; if new.modified is null then set new.modified = now(); end if; end",
                "trig_customers_update": "before update on customers for each row begin if new.modified is null or new.modified = old.modified then set new.modified = now(); end if; end",
            ),
        ),
    ),
);

const SequenceList = (
    "seq_customers": {},
    );

const Sequences = (
    "driver": (
        "oracle": SequenceList,
        "pgsql": SequenceList,
    ),
    );

const Tables = (
    "customers": T_Customers,
    );

const Schema = (
    "sequences": Sequences,
    "tables": Tables,
    );
    @endcode

    @section table_management Table Management

    The main class to be used for table management is @ref SqlUtil::Table "Table" (which is a wrapper for @ref SqlUtil::AbstractTable "AbstractTable").  This class can be used both for building a table from scratch or for retrieving the structure of a table from the database.

    Real table related work is done in @ref SqlUtil::AbstractTable "AbstractTable". AbstractTable is also instance returned from most of API calls used.

    @subsection creating_new_tables Creating New Tables

    Tables can be created manually, by creating a @ref SqlUtil::Table "Table object" and then populating the table with columns, indexes, etc, and then calling @ref SqlUtil::AbstractTable::create() for example.
    The following methods can be used for adding new attributes to a table:
    - @ref SqlUtil::AbstractTable::addColumn()
    - @ref SqlUtil::AbstractTable::addPrimaryKey()
    - @ref SqlUtil::AbstractTable::addUniqueConstraint()
    - @ref SqlUtil::AbstractTable::addIndex()
    - @ref SqlUtil::AbstractTable::addForeignConstraint()
    - @ref SqlUtil::AbstractTable::addCheckConstraint()
    - @ref SqlUtil::AbstractTable::addTrigger()

    Note that if the table is known to be in the database already (for example, if the original table info was read from the database), then calling one of the above methods will also effect the change in the database immediately.

    Alternatively, the following methods can be used to retrieve the SQL that can be executed for the particular database driver that the @ref SqlUtil::Table object is based on:
    - @ref SqlUtil::AbstractTable::getAddColumnSql()
    - @ref SqlUtil::AbstractTable::getAddPrimaryKeySql()
    - @ref SqlUtil::AbstractTable::getAddUniqueConstraintSql()
    - @ref SqlUtil::AbstractTable::getAddIndexSql()
    - @ref SqlUtil::AbstractTable::getAddForeignConstraintSql()
    - @ref SqlUtil::AbstractTable::getAddCheckConstraintSql()
    - @ref SqlUtil::AbstractTable::getAddTriggerSql()

    Otherwise a table can be built from a @ref table_desc_hash "table description hash" with one of the following methods:
    - @ref SqlUtil::AbstractTable::constructor()
    - @ref SqlUtil::AbstractTable::setupTable()

    Table description hashes are also used when aligning tables to a template, which can be used to automate schema upgrades; see @ref SqlUtil::AbstractTable::getAlignSql(), @ref SqlUtil::Database::getAlignSql(), and @ref table_desc_hash "table description hash" for more information.

    @subsection retrieving_table_info Retrieving Table Information

    The following methods retrieve table information from the database:
    - @ref SqlUtil::AbstractTable::cache() "Table::cache()": this method reads in the entire table configuration immediately
    - @ref SqlUtil::AbstractTable::describe() "Table::describe()": this method returns an object of class @ref SqlUtil::Columns describing the table's columns
    - @ref SqlUtil::AbstractTable::getPrimaryKey() "Table::getPrimaryKey()": this method returns an object of class @ref SqlUtil::AbstractPrimaryKey describing the table's primary key if it has one
    - @ref SqlUtil::AbstractTable::getIndexes() "Table::getIndexes()": this method returns an object of class @ref SqlUtil::Indexes describing the table's indexes
    - @ref SqlUtil::AbstractTable::getForeignConstraints() "Table::getForeignConstraints()": this method returns an object of class @ref SqlUtil::ForeignConstraints describing the table's foreign constraints
    - @ref SqlUtil::AbstractTable::getConstraints() "Table::getConstraints()": this method returns an object of class @ref SqlUtil::Constraints describing the table's non-foreign constraints
    - @ref SqlUtil::AbstractTable::getTriggers() "Table::getTriggers()": this method returns an object of class @ref SqlUtil::Triggers describing the table's triggers

    The @ref SqlUtil::AbstractTable::cache() method can be used to retrieve all table information from the database immediately as in the following example:
    @code{.py}
Table table(ds, "table_name");
table.cache();
string sql = table.getCreateSqlString();
    @endcode

    Use the following code to test if a table exists:
    @code{.py}
Table table(ds, "table_name");
if (table.checkExistence())
    printf("table exists\n");
    @endcode

    Or the following:
    @code{.py}
Database db(ds);
*AbstractTable table = db.getTable("table_name");
    @endcode

    @subsection aligning_tables Aligning Tables With a Template

    Template tables can be created any way required (ie by hand, or by retrieving the description from another database), but typically they will be created from a @ref table_desc_hash "table description hash".

    The following code provides an example of creating a template table and using it to align a table in the database with the template using callbacks:
    @code{.py}
int change_count;
code info_callback = sub (string str, int ac, string type, string name, *string table, *string new_name, *string info) {
    if (ac != AbstractDatabase::AC_NotFound && ac != AbstractDatabase::AC_Unchanged)
        ++change_count;
    if (verbose)
        printf("*** %s\n", str);
    else {
        print(AbstractDatabase::ActionLetterMap{ac});
        flush();
    }
};

code sql_callback = sub (string str) {
    if (verbose > 1)
        printf("%s\n", str);
    ds.execRaw(str);
};

hash callback_opts = ("info_callback": info_callback, "sql_callback": sql_callback, "sql_callback_executed": True);

Table template_table(ds, table_desc_hash, "table_name");
Table db_table(ds, "table_name");
db_table.getAlignSql(template_table, callback_opts);
    @endcode

    @subsection table_desc_hash Table Description Hash

    Table description hashes have the following structure:
    - \c columns: (required) a hash of column information keyed by column name; the values are @ref column_desc_hash "column description hashes"
    - \c primary_key: (optional) a @ref pk_desc_hash "primary key description hash" describing the primary key for the table
    - \c indexes: (optional) a hash of index information keyed by index name; the values are @ref index_desc_hash "index description hashes"
    - \c triggers: (optional) a hash of trigger information keyed by trigger name; the values are the trigger source code; since triggers are driver-dependent, a driver-independent table description would include trigger hashes under the \c drivers key and the driver key name under that; see below for an example
    - \c foreign_constraints: (optional) a hash of foreign constraint information keyed by constraint name; the values are @ref fk_desc_hash "foreign constraint hashes"
    - \c unique_constraints: (optional) a hash of unique constraint information keyed by constraint name; the values are @ref uk_desc_hash "unique constraint hashes"
    - \c driver: this key can optionally contain a hash keyed by driver name which contains a hash of values that will be added to the table description hash before processing; this way a table description hash can contain all the information required for the table including driver-specific options; any driver-specific options will overwrite values in the top level of the hash if there are duplicate hash keys, see below for an example

    Here is an example of a table description hash:
    @code{.py}
hash table_desc = (
    "columns": (
        "domain": (
            "qore_type": Type::String,
            "size": 240,
            "notnull": True,
        ),
        "keyname": (
            "qore_type": Type::String,
            "size": 240,
            "notnull": True,
        ),
        "value": (
            "qore_type": Type::String,
            "size": 4000,
        ),
        "created": (
            "qore_type": Type::Date,
            "notnull": True,
            "driver": (
                "oracle": (
                    "native_type": "date",
                ),
            ),
        ),
        "modified": (
            "qore_type": Type::Date,
            "driver": (
                "oracle": (
                    "native_type": "date",
                ),
            ),
        ),
    ),
    "primary_key": ("name": "pk_system_properties", "columns": ("domain", "keyname")),
    "indexes": (
        "sk_system_properties_domain": ("columns": "domain"),
        "driver": (
            "oracle": (
                "pk_system_properties": ("columns": ("domain", "keyname"), "unique": True),
            ),
        ),
    ),
    "driver": (
        "pgsql": (
            "functions": (
                "trig_system_properties()": "returns trigger language plpgsql as function$ begin if (tg_op = 'INSERT') then if new.created is null then select current_timestamp into new.created; end if; end if; if new.modified is null then select current_timestamp into new.modified; end if; return new; end; function$",
            ),
        ),
    ),
    "triggers": (
        "driver": (
            "oracle": (
                "trig_system_properties": "BEFORE INSERT OR UPDATE ON SYSTEM_PROPERTIES REFERENCING NEW AS NEW OLD AS OLD FOR EACH ROW begin if inserting then if :new.created is null then :new.created := sysdate; end if; end if; if :new.modified is null or :new.modified = :old.modified then :new.modified := sysdate; end if; end;",
            ),
            "pgsql": (
                    "trig_system_properties": "before insert or update on system_properties for each row execute procedure trig_system_properties()",
            ),
            "mysql": (
            ),
        ),
    ),
);
    @endcode

    @note
    - some databases will automatically create indexes for you when you create primary keys and unique constraints; some do not (ex: oracle).  In the above example, an index is created explicitly for the primary key constraint for oracle only.
    - the above example includes an additional driver-specific key for the \c "pgsql" driver: \c "functions" which is used by the PgsqlSqlUtil module to create trigger functions used by the table's trigger.

    @subsection column_desc_hash Column Description Hash

    Column description hashes have the following structure:
    - \c qore_type: a qore type string that will be converted to a native DB type with some default conversion
    - \c native_type: the native database column type; if both \c native_type and \c qore_type are given then \c native_type is used
    - \c size: for data types requiring a size component, the size; for numeric columns this represents the precision for example
    - \c scale: for numeric data types, this value gives the scale
    - \c default_value: the default value for the column
    - \c default_value_native: a boolean flag to say if a \c default_value should be validated against table column type (False) or used as it is (True) to allow to use DBMS native functions or features. Defaults to False. It is strongly recommended to use \c default_value_native for \c default_value in \c driver specific sub-hash to avoid non-portable schema hashes
    - \c comment: an optional comment for the column
    - \c notnull: if the column should have a "not null" constraint on it; if missing the default value is @ref Qore::False "False"
    - \c driver: this key can optionally contain a hash keyed by driver name which contains a hash of values that will be added to the column description hash before processing; this way a column description hash can contain all the information required for the column including driver-specific options; any driver-specific options will overwrite values in the top level of the hash if there are duplicate hash keys, see below for an example

    Note that the above structure is an extension of the fields in @ref SqlUtil::AbstractTable::ColumnDescOptions, adding \c notnull and \c driver keys for additional information to create the column in the table.

    Here is an example of a column description hash:
    @code{.py}
hash date_col_desc = (
    "qore_type": Type::Date,
    "notnull": True,
    "driver": (
        "oracle": (
            "native_type": "date",
        ),
    ),
);
    @endcode

    The above hash describes a column that will be have \c TIMESTAMP type on most databases, but \c DATE on Oracle.

    @subsection pk_desc_hash Primary Key Description Hash

    The primary key description hash has the following keys:
    - \c name: (required) the name of the primary key
    - \c columns: (required) a single column name or a list of column names making up the primary key

    Here is an example of a primary key description hash:
    @code{.py}
hash pk_desc = (
    "name": "pk_queue",
    "columns": "queueid",
);
    @endcode

    @subsection fk_desc_hash Foreign Constraint Description Hash

    The foreign constraint description hash has the following keys:
    - \c columns: (required) a single column name or a list of column names in the current table making up the foreign constraint
    - \c table: (required) a string giving the name of the table the foreign constraint is on
    - \c target_columns: (optional) this key is only necessary if the columns in the foreign table have different names than in the current table; if so, it must be assigned to a single column name or a list of column names in the foreign table; if this key is present then the same number of columns must appear in each key

    The following is an example of a foreign key description hash:
    @code{.py}
hash fk_desc = (
    "columns": "queueid",
    "tables": "queues",
);
    @endcode

    @subsection uk_desc_hash Unique Constraint Description Hash

    The unique constraint description hash has the following key:
    - \c columns: (required) a single column name or a list of column names in the current table making up the unique constraint

    The following is an example of a unique constraint description hash:
    @code{.py}
hash uk_desc = (
    "columns": ("type", "name"),
);
    @endcode

    @subsection index_desc_hash Index Description Hash

    Index description hashes have the following structure:
    - \c columns: (required) a single column or a list of column names making up the index
    - \c unique: a boolean value indicating if the index is unique or not, if missing, then the index is assumed to e not unique

    The following is an example of an index description hash:
    @code{.py}
hash ix_desc = (
    "columns": ("type", "name"),
    "unique": True,
);
    @endcode

    @section callbacks Callbacks

    Most of the SqlUtil methods that return SQL strings also accept an option hash where callbacks an be set.  Callbacks can be used
    to display detailed information about long-running operations, such as schema or data alignment for complex schemas or large
    data sets, or as a generic framework for executing and logging SQL operations.

    @subsection info_callback SQL Info CallBack Closure or Call Ceference

    The \c "info_callback" @ref closure "closure" or @ref call_reference "call reference" is called with information about the current SQL
    operation and can be specified as in the following example:
    @code{.py}
int change_count;
code info_callback = sub (string str, int ac, string type, string name, *string table, *string new_name, *string info) {
    if (ac != AbstractDatabase::AC_NotFound && ac != AbstractDatabase::AC_Unchanged)
        ++change_count;
    if (verbose)
        printf("*** %s\n", str);
    else {
        print(AbstractDatabase::ActionLetterMap{ac});
        flush();
    }
};

hash callback_opts = ("info_callback": info_callback);
db.getAlignSql(schema, callback_opts);
    @endcode

    @subsection sql_callback SQL Raw/DDL CallBack Closure or Call Ceference

    The \c "sql_callback" @ref closure "closure" or @ref call_reference "call reference" is called with an SQL string suitable for raw execution for each SQL command that is returned
    or generated for a particular operation and can be specified as in the following example:
    @code{.py}
code sql_callback = sub (string str) {
    ds.execRaw(str);
    if (verbose > 1)
        printf("%s\n", str);
};

hash callback_opts = ("sql_callback": sql_callback, "sql_callback_executed": True);
db.getAlignSql(schema, callback_opts);
    @endcode

    @note this callback is used for DDL execution and does not use bound arguments; for SQL data operations, the @ref sqlarg_callback is used instead

    @subsection sql_callback_executed SQL CallBack Executed Flag

    If the \c "sql_callback_executed" flag is set to @ref Qore::True "True" then the called method knows that the changes are made in the database and the internal representation of the affected object(s) is also updated accordingly.  Here is an example:
    @code{.py}
code sql_callback = sub (string str) {
    if (verbose > 1)
        printf("%s\n", str);
    ds.execRaw(str);
};

hash callback_opts = ("sql_callback": sql_callback, "sql_callback_executed": True);
db.getAlignSql(schema, callback_opts);
    @endcode

    @subsection sqlarg_callback SQL Operation Callback Closure or Call Reference

    The \c "sqlarg_callback" @ref closure "closure" or @ref call_reference "call reference" is called with an SQL string and arguments for each SQL command that is executed
    during SQL data operations and can be specified as in the following example:
    @code{.py}
code sqlarg_callback = sub (string str, *list args) {
    if (verbose > 1)
        printf("SQL: %s\nargs: %y", str, args);
};

table.insertFromIterator(i, sqlarg_callback);
    @endcode

    @note this callback is used for data operations and is called with bound arguments; for DLL operations, see @ref sql_callback

    @subsection upsert_info_callback Upsert Info Callback

    The \c "info_callback" upsert option key can be assigned to a @ref closure "closure" or @ref call_reference "call reference" that
    is called whenever an row result is available; this callback takes the following arguments:
    - @ref string_type "string" table_name: the name of the table
    - @ref hash_type "hash" row: the row affected
    - @ref int_type "int" result: the upsert result code (see @ref upsert_results) for possible values

    Here is an example of an upsert callback:
    @code{.py}
code upsert_callback = sub (string table_name, hash row, int result) {
    # verbosity threshold
    int t = 0;
    if (result != AbstractTable::UR_Unchanged)
        ++change_count;
    else
        t = 1;

    if (opt.verbose > t) {
        if (dot_count) {
            print("\n");
            dot_count = 0;
        }
        printf("%s reference data %s: %y: %s\n", t ? "+++" : "***", table_name, row, AbstractTable::UpsertResultMap{result});
    }
    else {
        ++dot_count;
        print(AbstractTable::UpsertResultLetterMap{result});
        flush();
    }
};

hash sh = table.upsertFromIterator(i, upsert_strategy, False, ("info_callback": upsert_callback));
    @endcode

    @subsection insert_info_callback Insert Info Callback

    This option is identical to @ref upsert_info_callback, except the \a result argument is always @ref SqlUtil::AbstractTable::UR_Inserted.
*/
/** @page dba_management DBA Management

    @tableofcontents

    @section dba_management_intro Introduction to DB Administration

    This part of SqlUtil contain some additional tools to handle subset of database administration.
    Common application developer usually does not need to handle anything described in this section on his own.

    @warning Database administration is complex task and it requires knowledge in various areas of IT. Always ask your DBA if and when you can use these tools.

    DBA tools are part of the AbstractDatabase (or Database) classes. Methods described below are wrappers around database specific commands.

    @section dba_indexes Index Management

    - @ref SqlUtil::AbstractDatabase::rebuildIndex()

    Index rebuilding is quite controversial task and it is highly variable for each database server. And even worse - it's varying between database server versions too. Modern database servers usually do not need to rebuild indexes in regular operations, but there can be situations when it's required still. Like: testing DB features, index corruption as a result of HW failure, storage parameters changing etc.

    The easiest criteria which can be quickly and easily given to you is it not to rebuild or coalesce any index unless you have a specific, proven reason for doing so.

    rebuildIndex() methods always takes a index name or an SqlUtil::AbstractIndex instance as first argument. The second argument is an optional hash with rebuild index options. These options are specific for each database server.
    See driver-specific modules for more info.

    @code{.py}
new Database(ds).rebuildIndex("ix_foo_bar");
    @endcode

    @subsection dba_indexes_oracle Oracle Options

    Oracle backend implementation uses \c ALTER \c INDEX \c REBUILD statement internally.

    |!Option|!Type|!Description
    |\c parallel|Boolean|Use parallel keyword in the statement if it's set to True
    |\c logging|Boolean|Use \c logging keyword in the statement if it's set to True. By default \c nologging keyword is used.
    |\c statistics|Boolean|Use \c compute \c statistics keyword in the stetement if it's set to True
    |\c tablespace|String|Rebuild index in given tablespace
    |\c cond_rebuild|Boolean|Rebuild index only when it meets \c INDEX_STATS criteria
    |\c cond_maxheight|Integer|maximum index height before rebuilding index
    |\c cond_maxleafpct|Integer|maximum percentage of deleted leaf nodes before rebuilding index

    @subsection dba_indexes_pgsql PostgreSQL Options

    PostgreSQL uses \c REINDEX \c INDEX statement internally.

    There are no special options for PostgreSQL.

    @subsection dba_indexes_mysql MySQL Options

    MySQL does not offer any command to rebuild an index. Exception \c MYSQL-REBUILD-INDEX-ERROR is thrown if is the rebuildIndex method called for MySQL.

    Use reclaimSpace method for MySQL.

    @section dba_statistics Database Statistics

    - @ref SqlUtil::AbstractDatabase::computeStatistics()

    Database statistics are critical metadata used by SQL optimizers to build as good execution plans as possible.
    Some DB environments refresh statistics on regular basis, some not. All is usually highly configured by DBA.

    The optional argument holds options for statistics. These options are specific for each database server.
    See driver-specific modules for more info.

    @subsection dba_statistics_oracle Oracle Options

    Oracle uses \c DBMS_STATS package internally. With \c GATHER_SCHEMA_STATS or \c GATHER_TABLE_STATS depending on options set below.

    Statistics are calculated in currently logged user's schema.

    Please consult Oracle documentation for exact option meanings.

    |!Option|!Type|!Description
    |\c tables|list of strings|If it's set, the table by table statistics are computed with \c GATHER_TABLE_STATS. Otherwise the full schema with \c GATHER_SCHEMA_STATS is used.
    |\c estimate_percent|Integer|A percent as integer given. \c DBMS_STATS.AUTO_SAMPLE_SIZE is used as a default
    |\c block_sample|Boolean|Whether use random block sampling
    |\c method_opt|String|an optional size clause. By default "for all columns size auto" is used.
    |\c degree|Integer|a degree of parallelism. Default is NULL.
    |\c granularity|String|granularity of statistics to collect. Default is "ALL"
    |\c cascade|Boolean|True = gather statistics on indexes as well, False = do not make statistics for indexes. By default "dbms_stats.auto_cascade" is used.
    |\c stattab|String|user statistics table identifier describing where to save the current statistics
    |\c statid|String|identifier to associate with these statistics within \c stattab
    |\c options|String|further specification of which objects to gather statistics. By default "gather auto"
    |\c statown|String|schema containing \c stattab (if different than \c ownname)

    <b>Example:</b>
    Compute statistics for enumerated tables only.
    @code{.py}
hash opts = (
        "tables" : ( "workflows", "workflow_instance"),
    );
db.computeStatistics(opts);
    @endcode

    @subsection dba_statistics_pgsql PostgreSQL Options

    PostgreSQL uses \c ANALYZE \c TABLE internally.

    |!Option|!Type|!Description
    |\c tables|list of strings|If it's set, the table by table statistics are computed. Otherwise all tables in the user DB are used.

    @subsection dba_statistics_mysql MySQL Options

    MySQL uses \c ANALYZE \c TABLE internally.

    Please consult MySQL documentation for exact option meanings.

    |!Option|!Type|!Description
    |\c tables|list of strings|If it's set, the table by table statistics are computed. Otherwise all tables in the user DB are used.
    |\c no-binlog|Boolean|If True, the "no_write_to_binlog" is used. If False (default), the "local" keyword is used.

    @section dba_space_management Space Management

    - @ref SqlUtil::AbstractDatabase::reclaimSpace()

    Space reclaiming can be done if there was a huge deletion from some tables. As usual causes are DB-dependent.

    The optional argument holds options for space management. These options are specific for each database server.
    See driver-specific modules for more info.

    @subsection dba_space_management_oracle Oracle Options

    Oracle uses \c ALTER \c TABLE \c SHRINK \c SPACE \c CASCADE statement internally. So it requires enabled row movement.

    Please consult Oracle documentation for more information.

    |!Option|!Type|!Description
    |\c tables|list of strings|If it's set, the table by table statistics are computed. Otherwise all tables in the user DB are used.

    @subsection dba_space_management_pgsql PostgreSQL Options

    PostgreSQL uses \c VACUUM internally.

    Please consult PostgreSQL documentation for exact option meanings.

    |!Option|!Type|!Description
    |\c tables|list of strings|If it's set, the table by table statistics are computed. Otherwise all tables in the user DB are used.
    |\c full|Boolean|True if \c full keyword should be used
    |\c analyze|Boolean|True if \c analyze keyword should be used

    @subsection dba_space_management_mysql MySQL Options

    MySQL uses \c OPTIMIZE \c TABLE internally.

    Please consult MySQL documentation for exact option meanings.

    |!Option|!Type|!Description
    |\c tables|list of strings|If it's set, the table by table statistics are computed. Otherwise all tables in the user DB are used.
    |\c no-binlog|Boolean|If True, the "no_write_to_binlog" is used. If False (default), the "local" keyword is used.
*/

#! the SqlUtil namespace contains all the objects in the SqlUtil module
public namespace SqlUtil {
    /* @defgroup DBFeaturesConstants DB Features Constants
        These constants can be used as a lookup values in AbstractDatabase::features() method.
    */
    #@{
    #! Features constants

    #! Feature: functions
    public const DB_FUNCTIONS = "functions";
    #! Feature: materialized views / snapshots
    public const DB_MVIEWS = "materialized views";
    #! Feature: packages
    public const DB_PACKAGES = "packages";
    #! Feature: procedures
    public const DB_PROCEDURES = "procedures";
    #! Feature: sequences
    public const DB_SEQUENCES = "sequences";
    #! Feature: tables
    public const DB_TABLES = "tables";
    #! Feature: named types
    public const DB_TYPES = "named types";
    #! Feature: views
    public const DB_VIEWS = "views";
    #! Feature: synonyms
    public const DB_SYNONYMS = "synonyms";
    #@}

    /* @defgroup SqlTypeConstants SQL Type Constants
        These constants can be used for the \c "qore_type" values when creating columns to specify additional SQL column types
    */
    #@{
    #! specifies a \c VARCHAR column (equivalent to @ref Qore::Type::String)
    public const VARCHAR = "string";

    #! specifies a numeric column (equivalent to @ref Qore::Type::Number)
    public const NUMERIC = "number";

    #! specifies a \c CHAR column
    public const CHAR = "char";

    #! specifies a large variable-length binary column (ie \c BLOB or \c BYTEA, etc)
    public const BLOB = "blob";

    #! specifies a large variable-length character column (ie \c CLOB or \c TEXT, etc)
    public const CLOB = "clob";
    #@}

    /** @defgroup ColumnSizeOptions Column Size Options
        These constants represent the possible values for column size options
     */
    #@{
    #! the data type does not take a size parameter
    public const SZ_NONE = 0;

    #! the data type takes a mandatory size parameter
    public const SZ_MAND = 1;

    #! the data type takes an optional size parameter
    public const SZ_OPT = 2;

    #! the data type is numeric so takes an optional precision and scale
    public const SZ_NUM = 3;
    #@}

    /** @defgroup sql_cops SQL Column Operators
        These are the operators that can be used in the \c "columns" argument for select statements
     */
    #@{
    #! to rename a column on output
    /** @see cop_as()
    */
    public const COP_AS = "as";

    #! to convert column value into another datatype
    /** @see cop_cast()
    */
    public const COP_CAST = "cast";

    #! to prepend a string to a column on output
    /** @see cop_prepend()
    */
    public const COP_PREPEND = "prepend";

    #! to append a string to a column on output
    /** @see cop_append()
    */
    public const COP_APPEND = "append";

    #! to append a constant value (SQL Literal) to use as an output column value
    /** @see cop_value()
    */
    public const COP_VALUE = "value";

    #! to return column value in upper case
    /** @see cop_upper()
    */
    public const COP_UPPER = "upper";

    #! to return column value in lower case
    /** @see cop_lower()
    */
    public const COP_LOWER = "lower";

    #! to return distinct values
    /** @see cop_distinct
    */
    public const COP_DISTINCT = "distinct";

    #! to return the minimum value
    /** @see cop_min
    */
    public const COP_MIN = "min";

    #! to return the maximum value
    /** @see cop_max
    */
    public const COP_MAX = "max";

    #! to return the average value
    /** @see cop_avg
    */
    public const COP_AVG = "avg";

    #! to return the sum value
    /** @see cop_sum
    */
    public const COP_SUM = "sum";

    #! to return the row count
    /** @see cop_count
    */
    public const COP_COUNT = "count";

    #! the SQL \c "over" clause
    /** @see cop_over()
    */
    public const COP_OVER = "over";

    #! the SQL \c "minus" operator
    /** @see cop_minus()
    */
    public const COP_MINUS = "minus";

    #! the SQL \c "plus" operator
    /** @see cop_plus()
    */
    public const COP_PLUS = "plus";

    #! the SQL \c "divide" operator
    /** @see cop_divide()
    */
    public const COP_DIVIDE = "divide";

    #! the SQL \c "multiply" operator
    /** @see cop_multiply()
    */
    public const COP_MULTIPLY = "multiply";

    #! to return a date value with year information only
    /** @see cop_year()
    */
    public const COP_YEAR = "year";

    #! to return a date value with year to month information
    /** @see cop_year_month()
    */
    public const COP_YEAR_MONTH = "year_month";

    #! to return a date value with year to day information
    /** @see cop_year_day()
    */
    public const COP_YEAR_DAY = "year_day";

    #! to return a date value with year to hour information
    /** @see cop_year_hour()
    */
    public const COP_YEAR_HOUR = "year_hour";

    #! to return the next value of a sequence
    /** @see cop_seq()
    */
    public const COP_SEQ = "seq";

    #! to return the last value of a sequence issued in the same session
    /** @see cop_seq_currval()
    */
    public const COP_SEQ_CURRVAL = "seq_currval";

    #! to return the first non-null argument in the list
    /** @see cop_coalesce()
    */
    public const COP_COALESCE = "coalesce";

    #! to extract a substring from a column
    /** @see cop_substr()
    */
    public const COP_SUBSTR = "substr";

    #! to get the length of a text field
    /** @see cop_length()

        @since %SqlUtil 1.3.1
    */
    public const COP_LENGTH = "length";

    #! a hash of default column operator descriptions
    public const DefaultCopMap = (
        COP_AS: (
            "arg": Type::String,
            "withalias": True,
            "code": string sub (string cve, string arg, reference psch) {
                psch{arg} = cve;
                return sprintf("%s as %s", cve, arg);
            },
        ),
        COP_PREPEND: (
            "arg": Type::String,
            "sqlvalue": True,
            "code": string sub (string cve, string arg) {
                return sprintf("%s || %s", arg, cve);
            },
        ),
        COP_APPEND: (
            "arg": Type::String,
            "sqlvalue": True,
            "code": string sub (string cve, string arg) {
                return sprintf("%s || %s", cve, arg);
            },
        ),
        COP_VALUE: (
            "sqlvalue": True,
            "nocolumn": True,
            "code": string sub (*string cve, any arg) {
                return arg;
            },
        ),
        COP_UPPER: (
            "code": string sub (string cve, any arg) {
                return sprintf("upper(%s)", cve);
            },
        ),
        COP_LOWER: (
            "code": string sub (string cve, any arg) {
                return sprintf("lower(%s)", cve);
            },
        ),
        COP_DISTINCT: (
            "code": string sub (string cve, any arg) {
                return sprintf("distinct %s", cve);
            },
        ),
        COP_MIN: (
            "code": string sub (string cve, any arg) {
                return sprintf("min(%s)", cve);
            },
            "group": True,
        ),
        COP_MAX: (
            "code": string sub (string cve, any arg) {
                return sprintf("max(%s)", cve);
            },
            "group": True,
        ),
        COP_AVG: (
            "code": string sub (string cve, any arg) {
                return sprintf("avg(%s)", cve);
            },
            "group": True,
        ),
        COP_SUM: (
            "code": string sub (string cve, any arg) {
                return sprintf("sum(%s)", cve);
            },
            "group": True,
        ),
        COP_COUNT: (
            "nocolumn": True,
            "code": string sub (*string cve, any arg) {
                return sprintf("count(%s)", cve ? cve : "*");
            },
        ),
        COP_MINUS: (
            "argcolumn": True,
            "code": string sub (string arg1, string arg2) {
                return sprintf("%s - %s", arg1, arg2);
            },
        ),
        COP_PLUS: (
            "argcolumn": True,
            "code": string sub (string arg1, string arg2) {
                return sprintf("%s + %s", arg1, arg2);
            },
        ),
        COP_DIVIDE: (
            "argcolumn": True,
            "code": string sub (string arg1, string arg2) {
                return sprintf("%s / %s", arg1, arg2);
            },
        ),
        COP_MULTIPLY: (
            "argcolumn": True,
            "code": string sub (string arg1, string arg2) {
                return sprintf("%s * %s", arg1, arg2);
            },
        ),
        COP_SEQ: (
            "nocolumn": True,
            "code": string sub (*string cve, hash arg) {
                throw "SEQUENCE-ERROR", sprintf("cannot select sequence %y because this database does not support sequences", arg.seq);
            }
        ),
        COP_SEQ_CURRVAL: (
            "nocolumn": True,
            "code": string sub (*string cve, hash arg) {
                throw "SEQUENCE-ERROR", sprintf("cannot select the current value of sequence %y because this database does not support sequences", arg.seq);
            }
        ),
        COP_COALESCE: (
            "columnargs": True,
            "code": string sub (*string cve, hash arg) {
                return sprintf("coalesce(%s)", (foldl $1 + "," + $2, arg.args));
            }
        ),
        COP_SUBSTR: (
            "code": string sub (string cve, list args) {
                if (!exists args[1])
                    return sprintf("substring(%s from %d)", cve, args[0]);
                return sprintf("substring(%s from %d for %d)", cve, args[0], args[1]);
            },
        ),
        COP_LENGTH: (
            "code": string sub (string cve, any arg) {
                return sprintf("length(%s)", cve);
            },
        ),
        );
    #@}

    /** @defgroup sql_cop_funcs SQL Column Operator Functions
        These are functions that can be used in the \c "columns" argument for select statements:
        - @ref cop_append(): append a string to the output of a column
        - @ref cop_as(): rename columns
        - @ref cop_avg(): return the averge value for the given column when grouping
        - @ref cop_cast(): convert column value into another datatype
        - @ref cop_coalesce(): returns the first non-NULL value in the given columns
        - @ref cop_distinct(): add \c DISTINCT to the column name
        - @ref cop_length(): return the length of the given text column
        - @ref cop_lower(): return a column in lower case
        - @ref cop_max(): return the maximum value for the given column when grouping
        - @ref cop_min(): return the minimum value for the given column when grouping
        - @ref cop_over(): return a hash for the \c "over" operator for windowing functions
        - @ref cop_prepend(): prepend a string to the output of a column
        - @ref cop_seq(): increments the sequence and returns the next value
        - @ref cop_seq_currval(): returns the current value of the given sequence without changing the sequence
        - @ref cop_substr(): returns a substring of a text column
        - @ref cop_sum(): return the sum of all values for the given column when grouping
        - @ref cop_upper(): return a column in upper case
        - @ref cop_value(): return a constant value (SQL literal) in a column
        - @ref cop_year(): return the year of the given date column
        - @ref cop_year_day(): return the year to day value of the given date column
        - @ref cop_year_hour(): return the year to hour value of the given date column
        - @ref cop_year_month(): return the year and month value of the given date column

        Column operator functions can be nested as in the following example:
        @par Example:
        @code{.py}
*hash rows = t.selectRows(("columns": cop_as(cop_lower("permission_type"), "perm"), "where": ("permission_type": "USER"), "limit": 100, "offset": 200));
        @endcode
     */
    #@{
    #! returns a hash with \c cop, \c column, and \c arg keys
    /** @param cop the column operator (one of @ref sql_cops)
        @param column the column name
        @param arg the argument to the operator

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"

        @note Normally this function is not called directly, but rather by the other column operator functions
    */
    public hash sub make_cop(string cop, any column, any arg) {
        switch (column.typeCode()) {
            case NT_HASH:
            case NT_STRING: return ("cop": cop, "column": column, "arg": arg);
            default: throw "MAKE-COLUMN-OPERATOR-ERROR", sprintf("invalid column value passed to make_cop(); got type %y, expecting \"string\" or \"hash\"", column.type());
        }
    }

    #! returns a hash for the \c "as" operator with the given argument
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", cop_as("errors", "error_count")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins) or any other column "cop_..." function
        @param arg the new name of the output column

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"

        @see SqlUtil::cop_value for SQL literals handling
    */
    public hash sub cop_as(any column, string arg) {
        return make_cop(COP_AS, column, arg);
    }

    #! returns a hash for the \c "cast" operator with the given argument(s)
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", cop_cast("id", "string")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins) or any other column "cop_..." function
        @param arg the new datatype to cast the column value(s) to
        @param arg1 optional, type dependent, specification (e.g. size or precision)
        @param arg2 optional, type dependent, specification (e.g. scale)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"

        @see SqlUtil::cop_value for SQL literals handling
    */
    public hash sub cop_cast(any column, string arg, *any arg1, *any arg2) {
        return make_cop(COP_CAST, column, (arg, arg1, arg2));
    }

    #! returns a hash for the \c "prepend" operator with the given argument
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", cop_prepend("name", "migrated-")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)
        @param arg the text to prepend to the row values in the output column

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_prepend(any column, string arg) {
        return make_cop(COP_PREPEND, column, arg);
    }

    #! returns a hash for the \c "append" operator with the given argument
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", cop_append("name", "-migrated")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)
        @param arg the text to append (ie concatenate) to the row values in the output column

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_append(any column, string arg) {
        return make_cop(COP_APPEND, column, arg);
    }

    #! returns a hash for the \c "value" (literal) operator with the given argument
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", cop_value(100)), "where": ("type": "user")));
        @endcode

        @param arg the value to be returned in the column

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"

        SQL literals can be useful in some cases - as dummy values for select
        statements where there is exact columns required, unions, expected values
        for \c arc.insertFromIterator(src.getRowIterator(sh))
        "insert as select", etc.

        The term literal refers to a fixed data value. For example, 123, 'foobar' etc.

        Mapping of Qore values to literals:

        |!Qore Type|!SQL Type|!Qore Example|!SQL interpretation
        |Integer|NUMBER as it is|\c 123|\c 123
        |Float|NUMBER as it is|\c 12.3|\c 12.3
        |Numeric|NUMBER as it is|\c 1.2n|\c 1.2
        |Date|String representation of the date using DB native implementation like TO_TIMESTAMP for Oracle.|\c now()|\c to_timestamp('20150421104825000000', 'YYYYMMDDHH24MISSFF6')
        |Bool|Internal representation of the bool value using DB native implementation|\c True|\c 1
        |String|Standard and escaped string literal. No additional literal methods like Oracle's <tt>nq{foobar}</tt> are supported now|\c "foo bar"|\c 'foo bar'
        |\c NULL|Direct null literal|\c NULL|\c null
        |\c NOTHING|Direct null literal|\c NOTHING|\c null

        @note Passing an existing SQL function name as a value to the op_value
              does not result in function call. The string value is escaped as it is.
              Example: sysdate becomes 'sysdate'. See example below.

        The most useful SqlUtil::cop_value usage is with cooperation of SqlUtil::cop_as which allows
        nice and human readable column name aliases.

        @warning Using SqlUtil::cop_value without SqlUtil::cop_as can result in errors
                 depending on the DB backend. For example Oracle's use of <tt>cop_value(1), cop_value(True)</tt>
                 ends with <tt>ORA-00918: column ambiguously defined</tt>
                 because both values are interpreted as 1 in the resulting SQL.

    <b>Example</b>
    @code{.py}
%new-style
%requires SqlUtil

DatasourcePool ds("oracle:pvanek_omq/omq@xbox(al32utf8)");
Database db(ds);
Table t(ds, "dual");

hash sh = (
    "columns" : (
        cop_as(cop_value(1), "as_int"),
        cop_as(cop_value(1.2), "as_float"),
        cop_as(cop_value(3.2n), "as_numeric"),
        cop_as(cop_value(now()), "as_date"),
        cop_as(cop_value("foo bar"), "as_string"),
        cop_as(cop_value("sysdate"), "as_function_call"),
        cop_as(cop_value(True), "as_bool"),
        cop_as(cop_value(NULL), "as_null"),
        cop_as(cop_value(NOTHING), "as_nothing"),
    ),
);
string sql;
on_exit { printf("sql: %s\n", sql); }
printf("%N\n", t.selectRow(sh, \sql));

# results in:
hash: (9 members)
  as_int : 1
  as_float : 1.2
  as_numeric : 3.2
  as_date : 2015-04-21 10:56:57.000000 Tue +02:00 (CEST)
  as_string : "foo bar"
  as_function_call : "sysdate"
  as_bool : 1
  as_null : <NULL>
  as_nothing : <NULL>
sql: select 1 as as_int,1.2 as as_float,3.2 as as_numeric,to_timestamp('20150421105657000000', 'YYYYMMDDHH24MISSFF6') as as_date,'foo bar' as as_string,'sysdate' as as_function_call,1 as as_bool,null as as_null,null as as_nothing from pvanek_omq.workflows fetch next %v rows only
    @endcode
    */
    public hash sub cop_value(any arg) {
        return make_cop(COP_VALUE, "", arg);
    }

    #! returns a hash for the \c "upper" operator with the given argument; returns a column value in upper case
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", cop_upper("name")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_upper(any column) {
        return make_cop(COP_UPPER, column);
    }

    #! returns a hash for the \c "lower" operator with the given argument; returns a column value in lower case
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", cop_lower("name")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_lower(any column) {
        return make_cop(COP_LOWER, column);
    }

    #! returns a hash for the \c "distinct" operator with the given argument; returns distinct column values
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", cop_distinct("name")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_distinct(any column) {
        return make_cop(COP_DISTINCT, column);
    }

    #! returns a hash for the \c "min" operator; returns minimum column values
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": (cop_min("id")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_min(any column) {
        return make_cop(COP_MIN, column);
    }

    #! returns a hash for the \c "max" operator; returns maximum column values
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": (cop_max("id")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_max(any column) {
        return make_cop(COP_MAX, column);
    }

    #! returns a hash for the \c "avg" operator; returns average column values
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": (cop_avg("quantity")), "where": ("order_type": "wholesale")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_avg(any column) {
        return make_cop(COP_AVG, column);
    }

    #! returns a hash for the \c "sum" operator; returns the total sum of a numeric column.
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": (cop_sum("quantity")), "where": ("order_type": "wholesale")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_sum(any column) {
        return make_cop(COP_SUM, column);
    }

    #! returns a hash for the \c "count" operator; returns row counts
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("account_type", cop_count()), "where": ("type": "user"), "groupby": "account_type"));
        @endcode

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_count(any column = "") {
        return make_cop(COP_COUNT, column);
    }

    #! returns a hash for the \c "over" clause
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("account_type", cop_count(), cop_as(cop_over(cop_max("qty"), "account_id"), "max_qty_per_account")), "where": ("type": "user"), "groupby": "account_type"));
        @endcode

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_over(any column, string arg) {
        return make_cop(COP_OVER, column, arg);
    }

    #! returns a hash for the \c "-" operator with the given arguments
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", cop_minus("complete_count", "error_count")), "where": ("type": "user")));
        @endcode

        @param column1 the column specification for the first argument (string name or dot notation for use in joins)
        @param column2 the column specification for the second argument (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_minus(any column1, any column2) {
        return make_cop(COP_MINUS, column1, column2);
    }

    #! returns a hash for the \c "+" operator with the given arguments
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", cop_plus("complete_count", "error_count")), "where": ("type": "user")));
        @endcode

        @param column1 the column specification for the first argument (string name or dot notation for use in joins)
        @param column2 the column specification for the second argument (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_plus(any column1, any column2) {
        return make_cop(COP_PLUS, column1, column2);
    }

    #! returns a hash for the \c "/" operator with the given arguments
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", cop_divide("complete_count", "error_count")), "where": ("type": "user")));
        @endcode

        @param column1 the column specification for the first argument (string name or dot notation for use in joins)
        @param column2 the column specification for the second argument (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_divide(any column1, any column2) {
        return make_cop(COP_DIVIDE, column1, column2);
    }

    #! returns a hash for the \c "*" operator with the given arguments
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", cop_multiply("complete_count", "error_count")), "where": ("type": "user")));
        @endcode

        @param column1 the column specification for the first argument (string name or dot notation for use in joins)
        @param column2 the column specification for the second argument (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_multiply(any column1, any column2) {
        return make_cop(COP_MULTIPLY, column1, column2);
    }

    #! returns a hash for the \c "year" operator with the given argument
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", cop_year("error_time")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_year(any column) {
        return make_cop(COP_YEAR, column);
    }

    #! returns a hash for the \c "year_month" operator with the given argument
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", cop_year_month("error_time")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_year_month(any column) {
        return make_cop(COP_YEAR_MONTH, column);
    }

    #! returns a hash for the \c "year_day" operator with the given argument
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", cop_year_day("error_time")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_year_day(any column) {
        return make_cop(COP_YEAR_DAY, column);
    }

    #! returns a hash for the \c "year_hour" operator with the given argument
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", cop_year_hour("error_time")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_year_hour(any column) {
        return make_cop(COP_YEAR_HOUR, column);
    }

    #! returns a hash for the \c "seq" operator with the given argument giving the sequence name whose value should be returned
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", cop_seq("xid", "xis")), "where": ("type": "user")));
        @endcode

        @param seq the name of the sequence whose value should be returned
        @param as an optional column name that should be returned for the sequence value (so that @ref SqlUtil::cop_as() need not be used)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_seq(string seq, *string as) {
        return make_cop(COP_SEQ, "", ("seq": seq, "as": as));
    }

    #! returns a hash for the \c "seq_currval" operator with the given argument giving the sequence name whose current value should be returned
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", "name", cop_seq("xid", "xid_val"), cop_seq_currval("xid", "xid_current")), "where": ("type": "user")));
        @endcode

        @param seq the name of the sequence whose current value should be returned
        @param as an optional column name that should be returned for the sequence value (so that @ref SqlUtil::cop_as() need not be used)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_seq_currval(string seq, *string as) {
        return make_cop(COP_SEQ_CURRVAL, "", ("seq": seq, "as": as));
    }

    #! returns a hash for the \c "coalesce" operator with the given column arguments; the first non-NULL column value will be returned
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("first_name", "last_name", cop_colesce("first_name", "last_name")), "where": ("type": "user")));
        @endcode

        @param col1 the name or column operator hash for the first value
        @param col2 the name or column operator hash for the second value, additional values should follow this argument

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"

        @throw COALESCE-ERROR the arguments must be either string column designators or column operator hashes
    */
    public hash sub cop_coalesce(any col1, any col2) {
        code check = sub (any c) {
            if (c.typeCode() != NT_STRING && c.typeCode() != NT_HASH)
                throw "COALESCE-ERROR", sprintf("cop_coalesce() accepts string column designators or column operator hashes as arguments; got: %y", c);
        };
        # ensure at least two arguments
        check(col1);
        check(col2);
        # validate any remaining args
        map check($1), argv;

        list args = (col1, col2);
        if (argv)
            args += argv;

        return make_cop(COP_COALESCE, "", ("args": args));
    }

    #! returns a hash for the \c "substr" operator with the given arguments; returns a substring of a column value
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", cop_substr("name", 1, 1)), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)
        @param start position where the substring starts
        @param count length of the substring in characters

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub cop_substr(any column, int start, *int count) {
        return make_cop(COP_SUBSTR, column, (start, count));
    }

    #! returns a hash for the \c "len" operator with the given argument; returns the length of the given text field
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("columns": ("id", cop_length("product_code")), "where": ("type": "user")));
        @endcode

        @param column the column specification for the column (string name or dot notation for use in joins)

        @return a column operator description hash corresponding to the arguments for use in the @ref select_option_columns "columns" argument of a @ref select_option_hash "select option hash"

        @since %SqlUtil 1.3.1
    */
    public hash sub cop_length(any column) {
        return make_cop(COP_LENGTH, column);
    }
    #@}

    /** @defgroup sql_uops SQL Update Operators
        These are the operators that can be used in update statements to process column values automatically
     */
    #@{
    #! a hash of valid update operators
    public const DefaultUopMap = (
        COP_PREPEND: True,
        COP_APPEND: True,
        COP_UPPER: True,
        COP_LOWER: True,
        COP_MINUS: (
            "sqlvalue": True,
            "code": string sub (string cve, any arg) {
                return sprintf("%s - %s", cve, arg);
            },
        ),
        COP_PLUS: (
            "sqlvalue": True,
            "code": string sub (string cve, any arg) {
                return sprintf("%s + %s", cve, arg);
            },
        ),
        COP_DIVIDE: (
            "sqlvalue": True,
            "code": string sub (string cve, any arg) {
                return sprintf("%s / %s", cve, arg);
            },
        ),
        COP_MULTIPLY: (
            "sqlvalue": True,
            "code": string sub (string cve, any arg) {
                return sprintf("%s * %s", cve, arg);
            },
        ),
        COP_SUBSTR: True,
        COP_SEQ: (
            "nocolumn": True,
            "code": string sub (*string cve, string arg) {
                throw "SEQUENCE-ERROR", sprintf("cannot select sequence %y because this database does not support sequences", arg);
            }
        ),
        COP_SEQ_CURRVAL: (
            "nocolumn": True,
            "code": string sub (*string cve, string arg) {
                throw "SEQUENCE-ERROR", sprintf("cannot select the current value of sequence %y because this database does not support sequences", arg);
            }
        ),
        COP_COALESCE: (
            "columnargs": True,
            "code": string sub (*string cve, softlist args) {
                return sprintf("coalesce(%s)", (foldl $1 + "," + $2, args));
            }
        ),
        );
    #@}

    /** @defgroup sql_uop_funcs SQL Update Operator Functions
        These are functions that can be used as the values of update keys in select statements:
        - @ref uop_prepend(): prepend a string to the output of a column
        - @ref uop_append(): append a string to the output of a column
        - @ref uop_upper(): return a column in upper case
        - @ref uop_lower(): return a column in lower case
        - @ref uop_substr(): extract a substring from a column
        - @ref uop_plus(): add a numeric value to a column
        - @ref uop_minus(): subtract a numeric value from a column
        - @ref uop_multiply(): multiply a column by a numeric value
        - @ref uop_divide(): divide a column by a numeric value
        - @ref uop_seq(): returns the value of the given sequence
        - @ref uop_seq_currval(): returns the last value issued in the current session for the given sequence

        Update operator functions can be nested as in the following example:
        @par Example:
        @code{.py}
int rows_updated = t.update(("permission_type": uop_append("-migrated", uop_lower())));
        @endcode
     */
    #@{
    #! returns a hash with \c uop, \c arg, and \c nest keys
    /** @param uop the update operator (one of @ref sql_uops)
        @param arg the argument to the operator
        @param nest any nested operation to the operator

        @return an update operator description hash corresponding to the arguments for use in update statements

        @note Normally this function is not called directly, but rather by the other update operator functions
    */
    public hash sub make_uop(string uop, any arg, *hash nest) {
        return ("uop": uop, "arg": arg, "nest": nest);
    }

    #! returns a hash for the \c "prepend" operator with the given argument
    /** @par Example:
        @code{.py}
int rows_updated = t.update(("permission_type": uop_prepend("migrated-", uop_lower())));
        @endcode

        @param arg the text to prepend to the row values in the output column
        @param nest any nested operation to the operator

        @return an update operator description hash corresponding to the arguments for use in update statements
    */
    public hash sub uop_prepend(string arg, *hash nest) {
        return make_uop(COP_PREPEND, arg, nest);
    }

    #! returns a hash for the \c "append" or concatenate operator with the given argument
    /** @par Example:
        @code{.py}
int rows_updated = t.update(("permission_type": uop_append("-migrated", uop_lower())));
        @endcode

        @param arg the text to prepend to the row values in the output column
        @param nest any nested operation to the operator

        @return an update operator description hash corresponding to the arguments for use in update statements
    */
    public hash sub uop_append(string arg, *hash nest) {
        return make_uop(COP_APPEND, arg, nest);
    }

    #! returns a hash for the \c "upper" operator with the given argument; returns a column value in upper case
    /** @par Example:
        @code{.py}
int rows_updated = t.update(("permission_type": uop_upper()));
        @endcode

        @param nest any nested operation to the operator

        @return an update operator description hash corresponding to the arguments for use in update statements
    */
    public hash sub uop_upper(*hash nest) {
        return make_uop(COP_UPPER, NOTHING, nest);
    }

    #! returns a hash for the \c "lower" operator with the given argument; returns a column value in lower case
    /** @par Example:
        @code{.py}
int rows_updated = t.update(("permission_type": uop_lower()));
        @endcode

        @param nest any nested operation to the operator

        @return an update operator description hash corresponding to the arguments for use in update statements
    */
    public hash sub uop_lower(*hash nest) {
        return make_uop(COP_LOWER, NOTHING, nest);
    }

    #! returns a hash for the \c "substr" operator with the given arguments; returns a substring of a column value
    /** @par Example:
        @code{.py}
int rows_updated = t.update(("first_name": uop_substr(1, 1)));
        @endcode

        @param start position where the substring starts
        @param count length of the substring in characters
        @param nest any nested operation to the operator

        @return an update operator description hash corresponding to the arguments for use in update statements
    */
    public hash sub uop_substr(int start, *int count, *hash nest) {
        return make_uop(COP_SUBSTR, (start, count), nest);
    }

    #! returns a hash for the \c "+" operator with the given arguments
    /** @par Example:
        @code{.py}
int rows_updated = t.update(("counter": uop_plus(1)));
        @endcode

        @param arg numeric value to add to the column
        @param nest any nested operation to the operator

        @return an update operator description hash corresponding to the arguments for use in update statements
    */
    public hash sub uop_plus (any arg, *hash nest) {
        return make_uop (COP_PLUS, arg, nest);
    }

    #! returns a hash for the \c "-" operator with the given arguments
    /** @par Example:
        @code{.py}
int rows_updated = t.update(("counter": uop_minus(1)));
        @endcode

        @param arg numeric value to subtract from the column
        @param nest any nested operation to the operator

        @return an update operator description hash corresponding to the arguments for use in update statements
    */
    public hash sub uop_minus (any arg, *hash nest) {
        return make_uop (COP_MINUS, arg, nest);
    }

    #! returns a hash for the \c "*" operator with the given arguments
    /** @par Example:
        @code{.py}
int rows_updated = t.update(("counter": uop_multiply(2)));
        @endcode

        @param arg numeric value to multiply the column by
        @param nest any nested operation to the operator

        @return an update operator description hash corresponding to the arguments for use in update statements
    */
    public hash sub uop_multiply (any arg, *hash nest) {
        return make_uop (COP_MULTIPLY, arg, nest);
    }

    #! returns a hash for the \c "/" operator with the given arguments
    /** @par Example:
        @code{.py}
int rows_updated = t.update(("counter": uop_divide(2)));
        @endcode

        @param arg numeric value to divide the column by
        @param nest any nested operation to the operator

        @return an update operator description hash corresponding to the arguments for use in update statements
    */
    public hash sub uop_divide (any arg, *hash nest) {
        return make_uop (COP_DIVIDE, arg, nest);
    }

    #! returns a hash for the \c "seq" operator with the given argument giving the sequence name whose value should be returned
    /** @par Example:
        @code{.py}
*list rows = t.update(("id": uop_seq("xid")), ("type": "user"));
        @endcode

        @param seq the name of the sequence whose value should be returned

        @return an update operator description hash corresponding to the arguments for use in update statements
    */
    public hash sub uop_seq(string seq) {
        return make_uop(COP_SEQ, seq);
    }

    #! returns a hash for the \c "seq" operator with the given argument giving the sequence name whose current value should be returned
    /** @par Example:
        @code{.py}
*list rows = t.update(("id": uop_seq("xid")), ("type": "user"));
        @endcode

        @param seq the name of the sequence whose current value should be returned

        @return an update operator description hash corresponding to the arguments for use in update statements
    */
    public hash sub uop_seq_currval(string seq) {
        return make_uop(COP_SEQ_CURRVAL, seq);
    }
    #@}

    /** @defgroup sql_jops SQL Join Operators
        These are the operators that can be used in the @ref select_option_join "join" argument in a @ref select_option_hash "select option hash"

        @note these operators are normally not used directly, but rather are used by the @ref sql_jop_funcs
    */
    #@{
    #! for standard inner joins
    /** @see join_inner()
    */
    public const JOP_INNER = "inner";

    #! for left outer joins
    /** @see join_left()
    */
    public const JOP_LEFT = "left";

    #! for right outer joins
    /** @see join_right()
    */
    public const JOP_RIGHT = "right";

    #! a hash of valid join operators
    public const JopMap = (
        JOP_INNER: "inner",
        JOP_LEFT: "left outer",
        JOP_RIGHT: "right outer",
        );
    #@}

    /** @defgroup sql_jop_funcs SQL Join Operator Functions
        These are functions that can be used in the @ref select_option_join "join" argument in a @ref select_option_hash "select option hash":
        - join_inner(): specifies an inner join
        - join_left(): specifies a left outer join
        - join_right(): specifies a right outer join

        @note for join functions, explicit join columns do not need to be specified in the case that a foreign key in one table exists to the primary key of the other table; in this case this information is assumed for the join automatically
     */
    #@{
    #! returns a hash keyed with the table name assigned to a hash with \c jop, \c table, \c jcols, \c cond, \c alias, and \c ta keys
    /**
        @note this function is normally not used directly, but rather is called by the other @ref sql_jop_funcs
    */
    public hash sub make_jop(string jop, AbstractTable table, *string alias, *hash jcols, *hash cond, *string ta, *hash opt) {
        string hk = alias ? alias : table.getSqlName();
        hash h{hk} = ("jop": jop, "table": table, "jcols": jcols, "cond": cond, "alias": alias, "ta": ta, "opt": opt);
        return h;
    }

    #! returns a hash keyed with the table name assigned to a hash with \c jop, \c table, \c jcols, \c cond, \c alias, and \c ta keys
    /** This version is used for deferred table resolution at join time

        @note this function is normally not used directly, but rather is called by the other @ref sql_jop_funcs
    */
    public hash sub make_jop(string jop, string table_name, *string alias, *hash jcols, *hash cond, *string ta, *hash opt) {
        string hk = alias ? alias : table_name;
        hash h{hk} = ("jop": jop, "table": table_name, "jcols": jcols, "cond": cond, "alias": alias, "ta": ta, "opt": opt);
        return h;
    }

    #! returns a hash for standard inner joins with the given arguments
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_inner(table2, "t2", ("id": "altid"))));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_inner(table2)));
        @endcode

        @param table the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub join_inner(AbstractTable table, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_INNER, table, alias, jcols, cond, NOTHING, opt);
    }

    #! returns a hash for standard inner joins with the given arguments
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_inner(table2, "t2", ("id": "altid"))));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_inner(table2)));
        @endcode

        @param table the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub join_inner(Table table, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_INNER, table.getTable(), alias, jcols, cond, NOTHING, opt);
    }

    #! returns a hash for standard inner joins with the given arguments
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_inner(table2, "t2", ("id": "altid"))));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_inner(table2)));
        @endcode

        @param table_name the table to join with; table will be subject to deferred resolution
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub join_inner(string table_name, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_INNER, table_name, alias, jcols, cond, NOTHING, opt);
    }

    #! returns a hash for standard inner joins with the given arguments for use when joining with a table other than the primary table
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_inner(table2, "t2", ("id": "altid")) + join_inner("t2", table3)));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_inner(table2)));
        @endcode

        @param ta the table name or alias of the other table to join with when not joining with the primary table
        @param table the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"

        @since %SqlUtil 1.3
    */
    public hash sub join_inner(string ta, AbstractTable table, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_INNER, table, alias, jcols, cond, ta, opt);
    }

    #! returns a hash for standard inner joins with the given arguments for use when joining with a table other than the primary table
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_inner(table2, "t2", ("id": "altid")) + join_inner("t2", table3)));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_inner(table2)));
        @endcode

        @param ta the table name or alias of the other table to join with when not joining with the primary table
        @param table the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub join_inner(string ta, Table table, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_INNER, table.getTable(), alias, jcols, cond, ta, opt);
    }

    #! returns a hash for standard inner joins with the given arguments for use when joining with a table other than the primary table
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_inner(table2, "t2", ("id": "altid")) + join_inner("t2", table3)));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_inner(table2)));
        @endcode

        @param ta the table name or alias of the other table to join with when not joining with the primary table
        @param table_name the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"

        @since %SqlUtil 1.3
    */
    public hash sub join_inner_alias(string ta, string table_name, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_INNER, table_name, alias, jcols, cond, ta, opt);
    }

    #! returns a hash for left outer joins with the given arguments
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_left(table2, "t2", ("id": "altid"), ("tag", op_like("offset%")))));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_left(table2, "t2", NOTHING, ("tag", op_like("offset%")))));
        @endcode

        @param table the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub join_left(AbstractTable table, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_LEFT, table, alias, jcols, cond, NOTHING, opt);
    }

    #! returns a hash for left outer joins with the given arguments
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_left(table2, "t2", ("id": "altid"), ("tag", op_like("offset%")))));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_left(table2, "t2", NOTHING, ("tag", op_like("offset%")))));
        @endcode

        @param table the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub join_left(Table table, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_LEFT, table.getTable(), alias, jcols, cond, NOTHING, opt);
    }

    #! returns a hash for left outer joins with the given arguments
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_left(table2, "t2", ("id": "altid"), ("tag", op_like("offset%")))));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_left(table2, "t2", NOTHING, ("tag", op_like("offset%")))));
        @endcode

        @param table_name the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"

        @since %SqlUtil 1.3
    */
    public hash sub join_left(string table_name, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_LEFT, table_name, alias, jcols, cond, NOTHING, opt);
    }

    #! returns a hash for left outer joins with the given arguments for use when joining with a table other than the primary table
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_left(table2, "t2", ("id": "altid"), ("tag", op_like("offset%"))) + join_left("t2", table3, "t3")));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_left(table2, "t2", NOTHING, ("tag", op_like("offset%"))) + join_left("t2", table3, "t3")));
        @endcode

        @param ta the table name or alias of the other table to join with when not joining with the primary table
        @param table the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub join_left(string ta, AbstractTable table, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_LEFT, table, alias, jcols, cond, ta, opt);
    }

    #! returns a hash for left outer joins with the given arguments for use when joining with a table other than the primary table
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_left(table2, "t2", ("id": "altid"), ("tag", op_like("offset%"))) + join_left("t2", table3, "t3")));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_left(table2, "t2", NOTHING, ("tag", op_like("offset%"))) + join_left("t2", table3, "t3")));
        @endcode

        @param ta the table name or alias of the other table to join with when not joining with the primary table
        @param table the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub join_left(string ta, Table table, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_LEFT, table.getTable(), alias, jcols, cond, ta, opt);
    }

    #! returns a hash for left outer joins with the given arguments for use when joining with a table other than the primary table
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_left(table2, "t2", ("id": "altid"), ("tag", op_like("offset%"))) + join_left("t2", table3, "t3")));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_left(table2, "t2", NOTHING, ("tag", op_like("offset%"))) + join_left("t2", table3, "t3")));
        @endcode

        @param ta the table name or alias of the other table to join with when not joining with the primary table
        @param table_name the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"

        @since %SqlUtil 1.3
    */
    public hash sub join_left_alias(string ta, string table_name, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_LEFT, table_name, alias, jcols, cond, ta, opt);
    }

    #! returns a hash for right outer joins with the given arguments
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_right(table2, "t2", ("id": "altid"), ("tag", op_like("offset%")))));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_right(table2, "t2", NOTHING, ("tag", op_like("offset%")))));
        @endcode

        @param table the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub join_right(AbstractTable table, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_RIGHT, table, alias, jcols, cond, NOTHING, opt);
    }

    #! returns a hash for right outer joins with the given arguments
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_right(table2, "t2", ("id": "altid"), ("tag", op_like("offset%")))));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_right(table2, "t2", NOTHING, ("tag", op_like("offset%")))));
        @endcode

        @param table the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub join_right(Table table, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_RIGHT, table.getTable(), alias, jcols, cond, NOTHING, opt);
    }

    #! returns a hash for right outer joins with the given arguments
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_right(table2, "t2", ("id": "altid"), ("tag", op_like("offset%")))));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_right(table2, "t2", NOTHING, ("tag", op_like("offset%")))));
        @endcode

        @param table_name the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"

        @since %SqlUtil 1.3
    */
    public hash sub join_right(string table_name, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_RIGHT, table_name, alias, jcols, cond, NOTHING, opt);
    }

    #! returns a hash for right outer joins with the given arguments for use when joining with a table other than the primary table
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_right(table2, "t2", ("id": "altid"), ("tag", op_like("offset%"))) + join_right("t2", table3, "t3")));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_right(table2, "t2", NOTHING, ("tag", op_like("offset%"))) + join_right("t2", table3, "t3")));
        @endcode

        @param ta the table name or alias of the other table to join with when not joining with the primary table
        @param table the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub join_right(string ta, AbstractTable table, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_RIGHT, table, alias, jcols, cond, ta, opt);
    }

    #! returns a hash for right outer joins with the given arguments for use when joining with a table other than the primary table
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_right(table2, "t2", ("id": "altid"), ("tag", op_like("offset%"))) + join_right("t2", table3, "t3")));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_right(table2, "t2", NOTHING, ("tag", op_like("offset%"))) + join_right("t2", table3, "t3")));
        @endcode

        @param ta the table name or alias of the other table to join with when not joining with the primary table
        @param table the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"
    */
    public hash sub join_right(string ta, Table table, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_RIGHT, table.getTable(), alias, jcols, cond, ta, opt);
    }

    #! returns a hash for right outer joins with the given arguments for use when joining with a table other than the primary table
    /** @par Example With Explicit Join Columns:
        @code{.py}
*list rows = table.selectRows(("join": join_right(table2, "t2", ("id": "altid"), ("tag", op_like("offset%"))) + join_right("t2", table3, "t3")));
        @endcode

        @par Example With Implicit Join Columns:
        (using an automatically detected foreign key to primary key constraint)
        @code{.py}
*list rows = table.selectRows(("join": join_right(table2, "t2", NOTHING, ("tag", op_like("offset%"))) + join_right("t2", table3, "t3")));
        @endcode

        @param ta the table name or alias of the other table to join with when not joining with the primary table
        @param table_name the table to join with
        @param alias the alias for \a table in the query
        @param jcols the columns to use for the join, the keys will be columns in the source table and the values are columns in the \a table argument; if this argument is @ref nothing, then if there is any foreign key in one table pointing to the primary key of the other table, then this information is automatically assumed as the join condition; a foreign constraint in the current table to the primary key in \a table will be used first if it exists
        @param cond additional conditions for the join clause for the \a table argument; see @ref where_clauses for more information
        @param opt optional join options (for example, to specify a partition for the join if supported)

        @return a join description hash corresponding to the arguments for use in the @ref select_option_join "join" argument of a @ref select_option_hash "select option hash"

        @since %SqlUtil 1.3
    */
    public hash sub join_right_alias(string ta, string table_name, *string alias, *hash jcols, *hash cond, *hash opt) {
        return make_jop(JOP_RIGHT, table_name, alias, jcols, cond, ta, opt);
    }
    #@}

    /** @defgroup sql_ops SQL Operators
        These are the operators that can be used in where clauses; see @ref where_clauses for more information
     */
    #@{
    #! the SQL "like" operator for use in @ref where_clauses
    /** @see op_like()
    */
    public const OP_LIKE = "like";

    #! the SQL less than (<) operator for use in @ref where_clauses
    /** @see op_lt()
    */
    public const OP_LT = "<";

    #! the SQL less than or equals (<=) operator for use in @ref where_clauses
    /** @see op_le()
    */
    public const OP_LE = "<=";

    #! the SQL greater than operator (>) for use in @ref where_clauses
    /** @see op_gt()
    */
    public const OP_GT = ">";

    #! the SQL greater than or equals operator (>=) for use in @ref where_clauses
    /** @see op_ge()
    */
    public const OP_GE = ">=";

    #! the SQL not equals operator (!= or <>) for use in @ref where_clauses
    /** @see op_ne()
    */
    public const OP_NE = "!=";

    #! the SQL equals operator (=) for use in @ref where_clauses
    /** @see op_eq()
    */
    public const OP_EQ = "=";

    #! the SQL less than (<) operator for use in @ref where_clauses when comparing two columns
    /** @see op_clt()
    */
    public const OP_CLT = "C<";

    #! the SQL less than or equals (<=) operator for use in @ref where_clauses
    /** @see op_cle()
    */
    public const OP_CLE = "C<=";

    #! the SQL greater than operator (>) for use in @ref where_clauses
    /** @see op_cgt()
    */
    public const OP_CGT = "C>";

    #! the SQL greater than or equals operator (>=) for use in @ref where_clauses
    /** @see op_cge()
    */
    public const OP_CGE = "C>=";

    #! the SQL not equals operator (!= or <>) for use in @ref where_clauses
    /** @see op_cne()
    */
    public const OP_CNE = "C!=";

    #! the SQL equals operator (=) for use in @ref where_clauses
    /** @see op_ceq()
    */
    public const OP_CEQ = "C=";

    #! the SQL \c "between" operator for use in @ref where_clauses
    /** @see op_between()
    */
    public const OP_BETWEEN = "between";

    #! the SQL \c "in" operator for use in @ref where_clauses
    /** @see op_in()
    */
    public const OP_IN = "in";

    #! the SQL \c "not" operator for use in @ref where_clauses
    /** @see op_not()
    */
    public const OP_NOT = "not";

    #! the SQL \c "substr" operator for use in @ref where_clauses
    /** @see op_substr()
    */
    public const OP_SUBSTR = "substr";

    #! to combine SQL expressions with \c "or" for use in @ref where_clauses
    public const OP_OR = "or";

    #! a hash of valid operators for use in @ref where_clauses
    public const DefaultOpMap = (
        OP_LIKE: (
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                args += arg;
                return sprintf("%s like %v", cn);
            },
        ),
        OP_LT: (
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                args += arg;
                return sprintf("%s < %v", cn);
            },
        ),
        OP_LE: (
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                args += arg;
                return sprintf("%s <= %v", cn);
            },
        ),
        OP_GT: (
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                args += arg;
                return sprintf("%s > %v", cn);
            },
        ),
        OP_GE: (
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                args += arg;
                return sprintf("%s >= %v", cn);
            },
        ),
        OP_NE: (
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                if (arg === NULL || !exists arg)
                    return sprintf("%s is not null", cn);
                args += arg;
                return sprintf("(%s != %v or %s is null)", cn, cn);
            },
        ),
        OP_EQ: (
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                if (arg === NULL || !exists arg)
                    return sprintf("%s is null", cn);
                args += arg;
                return sprintf("%s = %v", cn);
            },
        ),
        OP_BETWEEN: (
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                args += arg[0];
                args += arg[1];
                return sprintf("%s between %v and %v", cn);
            },
        ),
        OP_IN: (
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                *string ins = (foldl $1 + "," + $2, (map t.getSqlValue($1), arg));
                return exists ins ? sprintf("%s in (%s)", cn, ins) : "1 != 1";
            },
        ),
        OP_NOT: (
            "recursive": True,
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                return sprintf("not (%s)", cn);
            },
        ),
        OP_CLT: (
            "argcolumn": True,
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                return sprintf("%s < %s", cn, arg);
            },
        ),
        OP_CLE: (
            "argcolumn": True,
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                return sprintf("%s <= %s", cn, arg);
            },
        ),
        OP_CGT: (
            "argcolumn": True,
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                return sprintf("%s > %s", cn, arg);
            },
        ),
        OP_CGE: (
            "argcolumn": True,
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                return sprintf("%s >= %s", cn, arg);
            },
        ),
        OP_CNE: (
            "argcolumn": True,
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                return sprintf("%s != %s", cn, arg);
            },
        ),
        OP_CEQ: (
            "argcolumn": True,
            "code": string sub (object t, string cn, string arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                return sprintf("%s = %s", cn, arg);
            },
        ),
        OP_SUBSTR: (
            "code": string sub (object t, string cn, any arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                args += arg[0]; # start
                if (!exists arg[1]) {
                    args += arg[2]; # text
                    return sprintf("substring(%s from %v) = %v", cn);
                }
                args += arg[1]; # count
                args += arg[2]; # text
                return sprintf("substring(%s from %v for %v) = %v", cn);
            },
        ),
        OP_OR: (
            "code": string sub (object t, string cn, list arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
                return t.getOrClause(arg, \args, jch, join, ch, psch);
            },
        ),
        );
    #@}

    /** @defgroup sql_op_funcs SQL Operator Functions
        These are the operators that can be used in where clauses; see @ref where_clauses for more information:
        - op_between(): for the \c "between" operator
        - op_in(): for the \c "in" operator
        - op_like(): for the \c "like" operator
        - op_substr(): for the \c "substr" operator
        - op_ge(): for the \c ">=" operator when comparing column values to immediate values
        - op_gt(): for the \c ">" operator when comparing column values to immediate values
        - op_le(): for the \c "<=" operator when comparing column values to immediate values
        - op_lt(): for the \c "<" operator when comparing column values to immediate values
        - op_ne(): for the \c "!=" or \c "<>" operator when comparing column values to immediate values
        - op_eq(): for the \c "=" operator when comparing column values to immediate values
        - op_not(): for negating other operators
        - op_cge(): for the \c ">=" operator when comparing the values of two columns
        - op_cgt(): for the \c ">" operator when comparing the values of two columns
        - op_cle(): for the \c "<=" operator when comparing the values of two columns
        - op_clt(): for the \c "<" operator when comparingthe values of two columns
        - op_cne(): for the \c "!=" or \c "<>" operator when comparing the values of two columns
        - op_ceq(): for the \c "=" operator when comparing the values of two columns
        - wop_or(): for combining SQL expressions with \c "or"
     */
    #@{
    #! returns a hash with \c op and \c arg keys
    public hash sub make_op(string op, any arg) {
        return ("op": op, "arg": arg);
    }

    #! returns a hash for the \c "like" operator with the given argument for use in @ref where_clauses "where clauses"
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_like("%smith%"))));
        @endcode

        @param str the argument for the operator

        @return a where operation description hash for use in @ref where_clauses "where clauses"
    */
    public hash sub op_like(string str) {
        return make_op(OP_LIKE, str);
    }

    #! returns a hash for the \c "<" operator with the given argument for use in @ref where_clauses "where clauses" when comparing column values to immediate values
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_lt("Zebra"))));
        @endcode

        @param arg the argument for the operator

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @see op_clt() for a function to be used when comparing two column values
    */
    public hash sub op_lt(any arg) {
        return make_op(OP_LT, arg);
    }

    #! returns a hash for the \c "<=" operator with the given argument for use in @ref where_clauses "where clauses" when comparing column values to immediate values
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_le("Zebra"))));
        @endcode

        @param arg the argument for the operator

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @see op_cle() for a function to be used when comparing two column values
    */
    public hash sub op_le(any arg) {
        return make_op(OP_LE, arg);
    }

    #! returns a hash for the \c ">" operator with the given argument for use in @ref where_clauses "where clauses" when comparing column values to immediate values
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_gt("Apple"))));
        @endcode

        @param arg the argument for the operator

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @see op_cgt() for a function to be used when comparing two column values
    */
    public hash sub op_gt(any arg) {
        return make_op(OP_GT, arg);
    }

    #! returns a hash for the \c ">=" operator with the given argument for use in @ref where_clauses "where clauses" when comparing column values to immediate values
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_ge("Apple"))));
        @endcode

        @param arg the argument for the operator

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @see op_cge() for a function to be used when comparing two column values
    */
    public hash sub op_ge(any arg) {
        return make_op(OP_GE, arg);
    }

    #! returns a hash for the \c "!=" or \c "<>" operator with the given argument for use in @ref where_clauses "where clauses" when comparing column values to immediate values
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_ne("Smith"))));
        @endcode

        @param arg the argument for the operator

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @see op_cne() for a function to be used when comparing two column values

        This operator also handles Qore::NULL as SQL NULL values expanding them into <tt>IS NOT NULL</tt> clause.
    */
    public hash sub op_ne(any arg) {
        return make_op(OP_NE, arg);
    }

    #! returns a hash for the \c "=" operator with the given argument for use in @ref where_clauses "where clauses" when comparing column values to immediate values
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_eq("Smith"))));
        @endcode

        @param arg the argument for the operator

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @see op_ceq() for a function to be used when comparing two column values

        This operator also handles Qore::NULL as SQL NULL values expanding them into <tt>IS NULL</tt> clause.
    */
    public hash sub op_eq(any arg) {
        return make_op(OP_EQ, arg);
    }

    #! returns a hash for the \c "between" operator with the given arguments, neither of which can be @ref null or @ref nothing; for use in @ref where_clauses "where clauses"
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_between("US", "UX"))));
        @endcode

        @param l the lower bound for the \c "between" operator
        @param r the upper bound for the \c "between" operator

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @throw BETWEEN-ERROR one or both of the required arguments are @ref null or @ref nothing
     */
    public hash sub op_between(any l, any r) {
        if (!exists l || l === NULL)
            throw "BETWEEN-ERROR", sprintf("the first argument is %y; both arguments to op_between() must be values that can be used with the BETWEEN operator (ie not NULL or NOTHING)", l);
        if (!exists r || r === NULL)
            throw "BETWEEN-ERROR", sprintf("the second argument is %y; both arguments to op_between() must be values that can be used with the BETWEEN operator (ie not NULL or NOTHING)", r);
        return make_op(OP_BETWEEN, (l, r));
    }

    #! returns a hash for the \c "in" operator with all arguments passed to the function; for use in @ref where_clauses "where clauses"
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_in(200, 300, 500, 9))));
        @endcode

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @note The argument list size may be constrained depending on the database server / driver used; passing a large number of arguments to this function may be a sign of an improper application or query design; consider using a join with a temporary table instead of passing a large number of arguments to this function
    */
    public hash sub op_in() {
        return make_op(OP_IN, argv);
    }

    #! returns a hash for the \c "in" operator with the given argument list as the first argument; for use in @ref where_clauses "where clauses"
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_in(idlist))));
        @endcode

        @param args a list of values for the \c "in" operator

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @note The argument list size may be constrained depending on the database server / driver used; passing a large number of arguments to this function may be a sign of an improper application or query design; consider using a join with a temporary table instead of passing a large number of arguments to this function
    */
    public hash sub op_in(list args) {
        return make_op(OP_IN, args);
    }

    #! returns a hash for the \c "not" operator; for use in @ref where_clauses "where clauses"
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": opt_not(op_in(200, 300, 500, 9))))_;
        @endcode

        @return a where operation description hash for use in @ref where_clauses "where clauses"
    */
    public hash sub op_not(hash arg) {
        return make_op(OP_NOT, arg);
    }

    #! returns a hash for the \c "<" operator with the given argument for use in @ref where_clauses "where clauses" when comparing the values of two columns
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_clt("other_name"))));
        @endcode

        @param arg the name of the column to compare against

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @see op_lt() for a function to be used when comparing a column against an immediate value
    */
    public hash sub op_clt(string arg) {
        return make_op(OP_CLT, arg);
    }

    #! returns a hash for the \c "<=" operator with the given argument for use in @ref where_clauses "where clauses" when comparing the values of two columns
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_cle("other_name"))));
        @endcode

        @param arg the name of the column to compare against

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @see op_le() for a function to be used when comparing a column against an immediate value
    */
    public hash sub op_cle(string arg) {
        return make_op(OP_CLE, arg);
    }

    #! returns a hash for the \c ">" operator with the given argument for use in @ref where_clauses "where clauses" when comparing the values of two columns
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_cgt("other_name"))));
        @endcode

        @param arg the name of the column to compare against

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @see op_gt() for a function to be used when comparing a column against an immediate value
    */
    public hash sub op_cgt(string arg) {
        return make_op(OP_CGT, arg);
    }

    #! returns a hash for the \c ">=" operator with the given argument for use in @ref where_clauses "where clauses" when comparing the values of two columns
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_cge("other_name"))));
        @endcode

        @param arg the name of the column to compare against

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @see op_ge() for a function to be used when comparing a column against an immediate value
    */
    public hash sub op_cge(string arg) {
        return make_op(OP_CGE, arg);
    }

    #! returns a hash for the \c "!=" or \c "<>" operator with the given argument for use in @ref where_clauses "where clauses" when comparing the values of two columns
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_cne("other_name"))));
        @endcode

        @param arg the name of the column to compare against

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @see op_ne() for a function to be used when comparing a column against an immediate value
    */
    public hash sub op_cne(string arg) {
        return make_op(OP_CNE, arg);
    }

    #! returns a hash for the \c "=" operator with the given argument for use in @ref where_clauses "where clauses" when comparing the values of two columns
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_ceq("other_name"))));
        @endcode

        @param arg the name of the column to compare against

        @return a where operation description hash for use in @ref where_clauses "where clauses"

        @see op_eq() for a function to be used when comparing a column against an immediate value
    */
    public hash sub op_ceq(string arg) {
        return make_op(OP_CEQ, arg);
    }

    #! returns a hash for the \c "substr" operator with the given arguments; for use in @ref where_clauses "where clauses"
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_substr(1, 6, "Martin"))));
        @endcode

        @param start position where the substring starts
        @param count length of the substring in characters
        @param text what substring should match

        @return a where operation description hash for use in @ref where_clauses "where clauses"
     */
    public hash sub op_substr(int start, *int count, string text) {
        return make_op(OP_SUBSTR, (start, count, text));
    }

    #! returns a hash for the \c "substr" operator with the given arguments; for use in @ref where_clauses "where clauses"
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("hex": op_substr(5, "BEEF"))));
        @endcode

        @param start position where the substring starts
        @param text what substring should match

        @return a where operation description hash for use in @ref where_clauses "where clauses"
     */
    public hash sub op_substr(int start, string text) {
        return make_op(OP_SUBSTR, (start, NOTHING, text));
    }

    #! returns a hash with a fake \c "_OR_" column name; the list of arguments to the function is combined such that each hash in the list generates SQL expressions combined with \a "and" logic, and each of those clauses is combined with \a "or" logic; this is for use in @ref where_clauses "where clauses"
    /** @par Example:
        @code{.py}
*list rows = table.selectRows(("where": ("name": op_ceq("other_name")) + wop_or(("type": "C", "validation_flag": op_ne(NULL)), ("expiration_date": op_ge(date)))));
        @endcode
        Generates:
        @verbatim
select * from table where name != other_name and ((type != 'C' and validation flag is not null) or (expiration_date >= %v))
        @endverbatim

        @param h1 the hash of expressions to combine with \c "or" logic
        @param ... remaining expressions to combine with \c "or" logic

        @return a hash with a fake column name (\c "_OR_" with a numeric prefix for uniqueness) for use in a where operation description hash for use in @ref where_clauses "where clauses"

        @note a random prefix is used so that multiple uses of the function can be used to generate a single @ref where_clauses "where clause hash"
    */
    public hash sub wop_or(hash h1, hash h2) {
        softlist l = h1;
        l += h2;
        if (argv)
            l += argv;
        return (sprintf("%d:_OR_", rand() % 10000000): make_op(OP_OR, l));
    }
    #@}

    /** @defgroup sql_iops SQL Insert Operators
        These are operators that can be used for special value functions as the values of inserted columns.

        @note these operators are normally not used directly, but rather are used by the @ref sql_iop_funcs
    */
    #@{
    #! for using the value of a sequence
    /** @see iop_seq()
    */
    public const IOP_SEQ = "seq";

    #! for using the last value of a sequence issued in the current session
    /** @see iop_seq_currval()
    */
    public const IOP_SEQ_CURRVAL = "seq_currval";

    #! a hash of default insert operator descriptions (currently empty, all operators are driver-dependent)
    public const DefaultIopMap = {};
    #@}

    /** @defgroup sql_iop_funcs SQL Insert Operator Functions
        These are functions that can be used to give special values in insert queries
        - @ref iop_seq(): insert the value of a sequence into the column
    */
    #@{
    #! returns a hash with \c _iop and \c arg keys
    /** @param iop the value operator (one of @ref sql_iops)
        @param arg the argument to the operator

        @return an insert operator description hash corresponding to the arguments for use as values in insert queries

        @note Normally this function is not called directly, but rather by the other insert operator functions
    */
    public hash sub make_iop(string iop, any arg) {
        return ("_iop": iop, "arg": arg);
    }

    #! returns a hash for retrieving the value of the given sequence in insert queries
    /** @par Example:
        @code{.py}
table.insert(("id": id, "ref": iop_seq("xid")));
        @endcode

        @param arg the name of the sequence whose value should be inserted into the column

        @return a value operator description hash corresponding to the arguments for use in insert queries
    */
    public hash sub iop_seq(string arg) {
        return make_iop(IOP_SEQ, arg);
    }

    #! returns a hash for retrieving the current value of the given sequence in insert queries
    /** @par Example:
        @code{.py}
table.insert(("id": id, "ref": iop_seq("xid"), "ref2": iop_seq_currval("xid")));
        @endcode

        @param arg the name of the sequence whose current value should be inserted into the column

        @return a value operator description hash corresponding to the arguments for use in insert queries
    */
    public hash sub iop_seq_currval(string arg) {
        return make_iop(IOP_SEQ_CURRVAL, arg);
    }
    #@}

    #! known drivers
    public const SqlUtilDrivers = (
        "oracle",
        "pgsql",
        "mysql",
        "freetds",
        "sybase",
        );

    #! abstract container class that throws an exception if an unknown key is accessed
    public class AbstractHashContainer {
        public {}

        private {
            *hash h;
        }

        #! creates the object with the hash argument passed
        constructor(*hash nh) {
            h = nh;
        }

        #! creates a copy of the object
        constructor(AbstractHashContainer old) {
            h = old.h;
        }

        #! creates a "deep copy" of the object
        copy(AbstractHashContainer old) {
            map h.$1 = old.h.$1.copy(), old.h.keyIterator();
        }

        #! returns the value of the given key in the contained hash if it exists, otherwise throws a \c KEY-ERROR exception
        /** @par Example:
            @code{.py}
any v = c.name;
            @endcode

            @param k the name of the key to access

            @return the value of the given key in the contained hash if it exists

            @throw KEY-ERROR the given key does not exist in the contained hash

            @note this method is called automatically when an unknown or inaccessible member name is accessed from outside the class

            @see @ref memberGate_methods
        */
        any memberGate(string k) {
            if (!h.hasKey(k))
                throw "KEY-ERROR", sprintf("cannot access unknown %s %y; expecting one of: %y", getElementName(), k, h.keys());
            return h{k};
        }

        #! purges the contained data
        /** @par Example:
            @code{.py}
c.clear();
            @endcode

        */
        clear() {
            delete h;
        }

        #! removes the given key from the contained hash and returns the value
        abstract any take(string k);

        #! renames the given key; maintains the key order
        renameKey(string old_name, string new_name) {
            h = map {$1.key == old_name ? new_name : $1.key: $1.value}, h.pairIterator();
        }

        #! returns the hash contained by this object
        *hash getHash() {
            return h;
        }

        #! returns @ref Qore::True "True" if the hash argument has the same keys (in any order), @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
bool b = c.matchKeys(h);
            @endcode

            @param h1 the hash to compare

            @return @ref Qore::True "True" if the hash argument has the same keys (in any order), @ref Qore::False "False" if not
        */
        bool matchKeys(hash h1) {
            if (h.size() != h1.size())
                return False;

            foreach string k in (h.keyIterator()) {
                if (!h1.hasKey(k))
                    return False;
            }

            return True;
        }

        #! returns @ref Qore::True "True" if the list argument has the same list of key strings as the keys in the object (in any order), @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
bool b = c.matchKeys(l);
            @endcode

            @param l the hash to compare

            @return @ref Qore::True "True" if the list argument has the same list of key strings as the keys in the object (in any order), @ref Qore::False "False" if not
        */
        bool matchKeys(list l) {
            if (h.size() != l.size())
                return False;

            foreach string k in (l.iterator()) {
                if (!h.hasKey(k))
                    return False;
            }

            return True;
        }

        #! returns @ref Qore::True "True" if the container argument has the same keys (in any order), @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
bool b = c.matchKeys(c1);
            @endcode

            @param c the container to compare

            @return @ref Qore::True "True" if the container argument has the same keys (in any order), @ref Qore::False "False" if not
        */
        bool matchKeys(AbstractHashContainer c) {
            return matchKeys(c.h);
        }

        #! returns @ref Qore::True "True" if the hash argument has at least the same keys (in any order, can have more keys), @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
bool b = c.partialMatchKeys(h);
            @endcode

            @param h1 the hash to compare

            @return @ref Qore::True "True" if the hash argument has at least the same keys (in any order, can have more keys), @ref Qore::False "False" if not
        */
        bool partialMatchKeys(hash h1) {
            if (h.size() > h1.size())
                return False;

            foreach string k in (h.keyIterator()) {
                if (!h1.hasKey(k))
                    return False;
            }

            return True;
        }

        #! returns @ref Qore::True "True" if the list argument has at least the same keys (in any order, can have more keys), @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
bool b = c.partialMatchKeys(l);
            @endcode

            @param l the list of strings to compare

            @return @ref Qore::True "True" if the list argument has at least the same keys (in any order, can have more keys), @ref Qore::False "False" if not
        */
        bool partialMatchKeys(list l) {
            if (h.size() > l.size())
                return False;

            # make a hash of the list for quick searching
            hash h1;
            map h1.$1 = True, l;

            foreach string k in (h.keyIterator()) {
                if (!h1.hasKey(k))
                    return False;
            }

            return True;
        }

        #! returns @ref Qore::True "True" if the container argument has at least the same keys (in any order, can have more keys), @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
bool b = c.partialMatchKeys(c1);
            @endcode

            @param c the container to compare

            @return @ref Qore::True "True" if the container argument has at least the same keys (in any order, can have more keys), @ref Qore::False "False" if not
        */
        bool partialMatchKeys(AbstractHashContainer c) {
            return partialMatchKeys(c.h);
        }

        #! Returns @ref Qore::False "False" if the contained hash has no keys, @ref Qore::True "True" if it does
        /** @par Example:
            @code{.py}
bool b = h.val();
            @endcode

            The opposite of empty()

            @return @ref Qore::False "False" if the contained hash has no keys, @ref Qore::True "True" if it does
        */
        bool val() {
            return h.val();
        }

        #! Returns a list of key names of the contained hash
        /** @par Example:
            @code{.py}
list l = h.keys();
            @endcode

            @return a list of key names of the contained hash
        */
        list keys() {
            return h.keys();
        }

        #! Returns a list of values of the contained hash
        /** @par Example:
            @code{.py}
list l = h.values();
            @endcode

            @return a list of values of the contained hash
        */
        list values() {
            return h.values();
        }

        #! Returns a @ref Qore::HashIterator "HashIterator" object for the contained hash
        /** @par Example:
            @code{.py}
map printf("+ %y\n", $1.value), h.iterator();
            @endcode

            @return a @ref Qore::HashIterator "HashIterator" object for the contained hash
        */
        Qore::AbstractIterator iterator() {
            return h.iterator();
        }

        #! Returns a @ref Qore::HashKeyIterator "HashKeyIterator" object for the contained hash
        /** @par Example:
            @code{.py}
map printf("+ %s\n", $1), h.keyIterator();
            @endcode

            @return a @ref Qore::HashKeyIterator "HashKeyIterator" object for the contained hash
        */
        Qore::AbstractIterator keyIterator() {
            return h.keyIterator();
        }

        #! Returns a @ref Qore::HashPairIterator "HashPairIterator" object for the contained hash
        /** @par Example:
            @code{.py}
map printf("+ %s: %y\n", $1.key, $1.value), h.pairIterator();
            @endcode

            @return a @ref Qore::HashPairIterator "HashPairIterator" object for the contained hash
        */
        Qore::AbstractIterator pairIterator() {
            return h.pairIterator();
        }

        #! returns @ref Qore::True "True" if the container is empty, @ref Qore::False "False" if not
        bool empty() {
            return h.empty();
        }

        #! Returns the number of keys in the contained hash
        /** @par Example:
            @code{.py}
int num = h.size();
            @endcode

            @return the number of keys in the contained hash
        */
        int size() {
            return h.size();
        }

        #! Returns @ref Qore::True "True" if the key exists in the contained hash (may or may not be assigned a value), @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
bool b = h.hasKey(key);
            @endcode

            @param k the key name to check

            @return @ref Qore::True "True" if the key exists in the contained hash (may or may not be assigned a value), @ref Qore::False "False" if not
        */
        bool hasKey(string k) {
            return h.hasKey(k);
        }

        #! Returns @ref Qore::True "True" if the key exists in the contained hash and is assigned a value, @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
bool b = h.hasKeyValue(key);
            @endcode

            @param k the key name to check

            @return @ref Qore::True "True" if the key exists in the contained hash and is assigned a value, @ref Qore::False "False" if not
        */
        bool hasKeyValue(string k) {
            return h.hasKeyValue(k);
        }

        #! Returns the first key name in the contained hash or @ref nothing if the contained hash has no keys
        /** @par Example:
            @code{.py}
*string n = h.firstKey();
            @endcode

            @return the first key name in the contained hash or @ref nothing if the contained hash has no keys

            @see lastKey()
        */
        *string firstKey() {
            return h.firstKey();
        }

        #! Returns the last key name in the contained hash or @ref nothing if the contained hash has no keys
        /** @par Example:
            @code{.py}
*string n = h.lastKey();
            @endcode

            @return the last key name in the contained hash or @ref nothing if the contained hash has no keys

            @see firstKey()
        */
        *string lastKey() {
            return h.lastKey();
        }

        #! must return the name of the contained element
        abstract string getElementName();
    }

    #! abstract container class that throws an exception if an unknown key is accessed
    public class AbstractListContainer {
        public {}

        private {
            softlist l;
        }

        #! creates the object with the list argument passed
        constructor(softlist nl) {
            l = nl;
        }

        #! returns the value of the given element in the contained list if it exists, otherwise throws an \c ELEMENT-ERROR exception
        /** @par Example:
            @code{.py}
any v = c.get(2);
            @endcode

            @param i the index of the element to access

            @return the value of the given index in the contained list if it exists

            @throw ELEMENT-ERROR the given element does not exist in the contained list
        */
        abstract any get(softint i);

        #! adds the given value to the list
        add(any val) {
            l += val;
        }

        #! removes the given element from the contained list and returns the value
        any take(int i) {
            checkIndex(i);
            return extract l, i, 1;
        }

        #! returns the list contained by this object
        list getList() {
            return l;
        }

        #! Returns @ref Qore::False "False" if the contained list is empty, @ref Qore::True "True" if not
        /** @par Example:
            @code{.py}
bool b = l.val();
            @endcode

            The opposite of empty()

            @return @ref Qore::False "False" if the contained list is empty, @ref Qore::True "True" if not
        */
        bool val() {
            return l.val();
        }

        #! Returns a @ref Qore::ListIterator "ListIterator" object for the contained list
        /** @par Example:
            @code{.py}
map printf("+ %s: %y\n", $1), h.iterator();
            @endcode

            @return a @ref Qore::HashIterator "HashIterator" object for the contained hash
        */
        Qore::ListIterator iterator() {
            return l.iterator();
        }

        #! returns @ref Qore::True "True" if the container is empty, @ref Qore::False "False" if not
        bool empty() {
            return l.empty();
        }

        #! Returns the number of elements in the contained list
        /** @par Example:
            @code{.py}
int num = l.size();
            @endcode

            @return the number of elements in the contained list
        */
        int size() {
            return l.size();
        }

        #! must return the name of the contained element
        abstract string getElementName();

        private checkIndex(int i) {
            if (i < 0 || i >= l.size())
                throw "ELEMENT-ERROR", sprintf("cannot access %s element %d; total elements: %d", getElementName(), i, l.size());
        }
    }

    #! the table container class stores a collection of tables in a schema
    public class Tables inherits AbstractHashContainer {
        #! creates an empty object
        constructor() {
        }

        #! creates and populates the object from a hash description
        constructor(AbstractDatasource ds, hash tables, *hash opt) {
            populate(ds, tables, opt);
        }

        #! creates and populates the object from tables in the database
        constructor(AbstractDatasource ds) {
            populate(ds);
        }

        #! adds the given value to the hash with the given key name
        add(string k, Table val) {
            h{k} = val.getTable();
        }

        #! adds the given value to the hash with the given key name
        add(string k, AbstractTable val) {
            h{k} = val;
        }

        #! adds the given value to the hash with the given key name
        add(Table val) {
            h{val.getName()} = val.getTable();
        }

        #! adds the given value to the hash with the given key name
        add(AbstractTable val) {
            h{val.getName()} = val;
        }

        #! removes the given key from the contained hash and returns the value
        AbstractTable take(string k) {
            return remove h{k};
        }

        #! populates the object from a hash description
        populate(AbstractDatasource ds, hash tables, *hash opt) {
            delete h;
            AbstractDatabase::checkDriverOptions(\tables, ds.getDriverName());
            foreach string name in (tables.keyIterator()) {
                if (h.hasKey(name))
                    continue;
                hash th = tables{name} - "rename";
                # create table template
                Table t(ds, th, name, opt + ("table_cache": self));
                # store in table_cache
                h{name} = t.getTable();
            }
        }

        #! populates the object from tables in the database
        populate(AbstractDatasource ds) {
            delete h;
            Database db(ds);
            foreach string name in (db.tableIterator()) {
                if (h{name}) {
                    h{name}.cache();
                    continue;
                }
                Table t(ds, name);
                t.cache(("table_cache": self));
                h{name} = t.getTable();
            }
        }

        #! returns a list of SQL strings that can be used to drop all the foreign constraints on a particular table
        /** @par Example:
            @code{.py}
*list l = tables.getDropAllForeignConstraintsOnTableSql(name);
            @endcode

            Make sure that all relevant tables are cached before calling this method; if the table is not cached, then @ref nothing is returned

            @param name the name of the table to find all the foreigjn constraints for
            @param opt a hash of options for the SQL strings; see @ref AbstractDatabase::DropSchemaOptions for common options; each driver can support additional driver-specific options

            @return a list of SQL strings that can be used to drop all the foreign constraints on a particular table

            @throw OPTION-ERROR invalid or unsupported option passed
        */
        *list getDropAllForeignConstraintsOnTableSql(string name, *hash opt) {
            if (!h{name})
                return;

            AbstractTable t = h{name};

            list l = ();
            foreach AbstractUniqueConstraint uk in (t.getUniqueConstraintIterator()) {
                foreach hash sch in (uk.getSourceConstraintIterator()) {
                    l += AbstractDatabase::doCallback(opt, sch.fk.getDropSql(sch.table), AbstractDatabase::AC_Drop, "foreign constraint", sch.fk.getName(), sch.table);
                    string tn = sch.table;
                    tn =~ s/.*\.//;
                    if (opt.sql_callback_executed && h{tn})
                        h{tn}.removeForeignConstraint(sch.fk.getName());
                }
            }

            return l;
        }

        #! returns the AbstractTable object corresponding to the key given or throws a KEY-ERROR exception
        /** @par Example:
            @code{.py}
AbstractTable t = tables.table_name;
            @endcode

            @param k the name of the key to access

            @return the value of the given key in the contained hash if it exists

            @throw KEY-ERROR the given key does not exist in the contained hash

            @note this method is called automatically when an unknown or inaccessible member name is accessed from outside the class

            @see @ref memberGate_methods
        */
        AbstractTable memberGate(string k) {
            return AbstractHashContainer::memberGate(k);
        }

        #! returns \c "table" since this object stores AbstractTable objects
        string getElementName() {
            return "table";
        }

        #! gets a table from the database or from the cache if already cached; if the table does not exist, then @ref nothing is returned
        *AbstractTable getIfExists(AbstractDatasource ds, string name) {
            if (h{name})
                return h{name};

            Table t(ds, name);
            if (!t.checkExistence())
                return;
            t.cache(("table_cache": self));
            return h{name} = t.getTable();
        }

        #! gets a table from the database or from the cache if already cached
        AbstractTable get(AbstractDatasource ds, string name) {
            if (h{name})
                return h{name};

            Table t(ds, name);
            t.cache(("table_cache": self));
            return h{name} = t.getTable();
        }

        #! returns an SQL string that can be used to rename the given table if it exists and the target does not exist, otherwise returns @ref nothing
        /** @par Example:
            @code{.py}
*string sql = tables.getRenameTableIfExistsSql(old_name, new_name);
            @endcode

            If the @ref sql_callback_executed "sql_callback_executed option key" is @ref Qore::True "True", this
            method also takes care of renaming internal foreign constraint references and source constraint references
            from unique keys for the renamed table (see @ref sql_callback_executed for more information).

            @param old_name the current name of the table
            @param new_name the new name of the table
            @param opts optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @return an SQL string that can be used to rename the given table if it exists and the target does not exist, otherwise returns @ref nothing
         */
        *string getRenameTableIfExistsSql(string old_name, string new_name, *hash opts) {
            if (!h{old_name} || h{new_name})
                return;

            AbstractTable t = h{old_name};
            string oldsn = t.getSqlName();
            *string sql = t.getRenameSql(new_name, opts);
            if (opts.sql_callback_executed)
                tableRenamedIntern(old_name, new_name, oldsn);

            return sql;
        }

        #! updates table names and internal references for renamed tables
        /** @par Example:
            @code{.py}
tables.tableRenamed(old, newv);
            @endcode

            @param old_name the current name of the table
            @param new_name the new name of the table
            @param old_sql_name the old full SQL name of the table (ie the value returned by @ref SqlUtil::AbstractTable::getSqlName())

            @return @ref Qore::True "True" if the table was renamed internally, @ref Qore::False "False" if not
        */
        bool tableRenamed(string old_name, string new_name, string old_sql_name) {
            if (!h{old_name} || h{new_name})
                return False;
            tableRenamedIntern(old_name, new_name, old_sql_name);
            return True;
        }

        private tableRenamedIntern(string old_name, string new_name, string oldsn) {
            renameKey(old_name, new_name);
            AbstractTable t = h{new_name};

            # search through all unique keys and rename foreign keys in source tables
            foreach AbstractUniqueConstraint uk in (t.getUniqueConstraintIterator()) {
                foreach hash sch in (uk.getSourceConstraintIterator()) {
                    sch.fk.target.table = t.getName();
                }
            }

            if (!t.getForeignConstraints().empty()) {
                string newsn = t.getSqlName();

                # search through all foreign constraints and try to rename source constraints
                foreach AbstractForeignConstraint fk in (t.getForeignConstraints().iterator()) {
                    if (h{fk.target.table}) {
                        AbstractTable tt = h{fk.target.table};
                        foreach AbstractUniqueConstraint uk in (tt.getUniqueConstraintIterator())
                            uk.renameSourceConstraintTable(oldsn, newsn);
                    }
                }
            }
        }

        #! returns an SQL string that can be used to drop an existing constraint on a table, if the table is not already cached or the constraint does not exist, then @ref nothing is returned
        /** @par Example:
            @code{.py}
*string sql = tables.getDropConstraintIfExistsSql("table", "pk_table");
            @endcode

            If the @ref sql_callback_executed "sql_callback_executed option key" is @ref Qore::True "True" and
            if the constraint to be dropped is a foreign constraint, then if any unique constraint is linked
            to this foreign constraint, the link is removed (see @ref sql_callback_executed for more information).

            @param tname the name of the table
            @param cname the name of the constraint to drop if it exists
            @param opts optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @return an SQL string that can be used to drop an existing constraint on a table, if the table is not already cached or the constraint does not exist, then @ref nothing is returned
        */
        *string getDropConstraintIfExistsSql(string tname, string cname, *hash opts) {
            if (!h{tname})
                return;

            AbstractTable t = h{tname};
            AbstractConstraint c;
            *string sql = t.getDropConstraintIfExistsSql(cname, opts, \c);
            if (!sql)
                return;
            if (opts.sql_callback_executed && c instanceof AbstractForeignConstraint) {
                AbstractForeignConstraint fk = c;
                if (h{fk.target.table}) {
                    AbstractTable tt = h{fk.target.table};
                    list cols = fk.target.columns.keys();
                    foreach AbstractUniqueConstraint uk in (tt.getUniqueConstraintIterator())
                        uk.removeSourceConstraint(t.getSqlName(), cols);
                }
            }
            return sql;
        }

        list getCreateList() {
            # foreign dependency hash: table -> hash: source table (ie table with foreign constraint on key)
            hash tdh;
            # source dependency hash: table -> list: foreign table (key has foreign constraint on value)
            hash sdh;
            getDependencies(\tdh, \sdh);

            hash th = h;

            #printf("TDH: %N\nSDH: %N\nNT: %y", tdh, sdh, nt);exit(1);

            # make a list of tables in dependency order
            list l = ();
            while (True) {
                bool found = False;
                foreach AbstractTable t in (th.iterator()) {
                    if (!sdh{t.getName()}) {
                        l += t;
                        found = True;
                        remove th{t.getName()};
                        map remove sdh.$1{t.getName()}, tdh{t.getName()}.keyIterator();
                        remove tdh{t.getName()};
                    }
                }
                if (!found)
                    break;
            }

            return l;
        }

        Qore::AbstractIterator createIterator() {
            return getCreateList().iterator();
        }

        #! returns a list of cached table names in the order that can be used to drop the tables, taking into account foreign constraint dependencies
        /** @par Example:
            @code{.py}
list l = tables.getDropList();
map $1.drop(), l;
            @endcode

            @return a list of cached table names in the order that can be used to drop the tables, taking into account foreign constraint dependencies
        */
        list getDropList() {
            if (!h)
                return ();

            # foreign dependency hash: table -> hash: source table (ie table with foreign constraint on key)
            hash tdh;
            # source dependency hash: table -> list: foreign table (key has foreign constraint on value)
            hash sdh;
            # table hash
            hash th = h;
            getDependencies(\tdh, \sdh, \th);

            #printf("TDH: %N\nSDH: %N\nNT: %y", tdh, sdh, nt);exit(1);

            # make a list of tables in dependency order
            list l = ();
            while (True) {
                bool found = False;
                foreach AbstractTable t in (th.iterator()) {
                    if (!tdh{t.getName()}) {
                        if (t.checkExistence())
                            l += t;
                        found = True;
                        remove th{t.getName()};
                        map remove tdh.$1{t.getName()}, sdh{t.getName()}.keyIterator();
                        remove sdh{t.getName()};
                    }
                }
                if (!found)
                    break;
            }

            return l;
        }

        #! returns an iterator for a list of cached table names in the order that can be used to drop the tables, taking into account foreign constraint dependencies
        /** @par Example:
            @code{.py}
map $1.drop(), tables.getDropIterator();
            @endcode

            @return an iterator for a list of cached table names in the order that can be used to drop the tables, taking into account foreign constraint dependencies
        */
        Qore::AbstractIterator dropIterator() {
            return getDropList().iterator();
        }

        private getDependencies(reference tdh, reference sdh, *reference th) {
            # list of tables not in DB
            list nt = ();
            foreach AbstractTable t in (h.iterator()) {
                if (th && !t.checkExistence()) {
                    nt += t.getName();
                    continue;
                }
                foreach AbstractForeignConstraint fk in (t.getForeignConstraints().iterator()) {
                    # skip constraints on the same table
                    if (fk.target.table == t.getName())
                        continue;
                    # make dependency map entries
                    sdh{t.getName()}{fk.target.table} = True;
                    tdh{fk.target.table}{t.getName()} = True;
                }
            }

            # process tables not in DB
            foreach string name in (nt) {
                remove th{name};
                map remove tdh.$1{name}, sdh{name}.keyIterator();
                remove sdh{name};
            }
        }
    }

    #! column container class that throws an exception if an unknown column is accessed
    public class Columns inherits AbstractHashContainer {
        #! creates the object from the argument
        constructor(*hash c) : AbstractHashContainer(c) {
        }

        #! creates a copy of the object
        constructor(Columns old) : AbstractHashContainer(old) {
        }

        #! adds the given value to the hash with the given key name
        add(string k, AbstractColumn val) {
            h{k} = val;
        }

        #! removes the given key from the contained hash and returns the value
        AbstractColumn take(string k) {
            return remove h{k};
        }

        #! returns the AbstractColumn object corresponding to the key given or throws a KEY-ERROR exception
        /** @par Example:
            @code{.py}
AbstractColumn c = cols.id;
            @endcode

            @param k the name of the key to access

            @return the value of the given key in the contained hash if it exists

            @throw KEY-ERROR the given key does not exist in the contained hash

            @note this method is called automatically when an unknown or inaccessible member name is accessed from outside the class

            @see @ref memberGate_methods
        */
        AbstractColumn memberGate(string k) {
            return AbstractHashContainer::memberGate(k);
        }

        #! returns a subset of the current columns according to the list argument
        Columns subset(softlist l) {
            hash nh = h{l};
            if (nh.size() != l.size())
                throw "SUBSET-ERROR", sprintf("subset requested: %y is not possible, can only provide: %y", l, nh.keys());
            return new Columns(nh);
        }

        #! returns \c "column" since this object stores column objects
        string getElementName() {
            return "column";
        }

        #! returns @ref Qore::True "True" if the argument has the same columns in the same order as the current object, @ref Qore::False "False" if not
        bool equal(Columns cols) {
            AbstractIterator i = cols.iterator();
            foreach AbstractColumn c in (iterator()) {
                if (!i.next())
                    return False;
                if (!c.equal(i.getValue()))
                    return False;
            }
            return True;
        }
    }

    #! the base class for column information
    public class AbstractColumn {
        public {
            #! the name of the column
            string name;

            #! the native type name of the column
            string native_type;

            #! the equivalent qore type name of the column if known
            *string qore_type;

            #! the size of the column
            int size;

            #! @ref Qore::True "True" if the column can hold a NULL value, @ref Qore::False "False" if not
            bool nullable;

            #! default value for column
            *string def_val;

            #! comment on the column
            *string comment;
        }

        constructor(string n, string nt, *string qt, int sz, bool nul, *string dv, *string c) {
            name = n;
            native_type = nt;
            qore_type = qt;
            size = sz;
            nullable = nul;
            def_val = dv;
            comment = c;
        }

        #! returns the string describing the native type that can be used in SQL (for example to add the colunn to a table or when creating the table)
        string getNativeTypeString() {
            string sql = native_type;
            if (size)
                sql += sprintf("(%d)", size);
            return sql;
        }

        #! returns an sql string that can be used to add the column to a table
        string getCreateSql(AbstractTable t) {
            string sql = sprintf("%s %s", name, getNativeTypeString());
            if (exists def_val)
                sql += " default " + def_val + "";
            if (!nullable)
                sql += " not null";
            return sql;
        }

        #! returns a list of sql strings that can be used to add the column to an existing table
        /** @par Example:
            @code{.py}
list l = col.getAddColumnSql(t);
            @endcode

            @param t the AbstractTable object to modify

            @return a list of sql strings that can be used to add the column to an existing table
         */
        abstract list getAddColumnSql(AbstractTable t);

        #! returns a string that can be used to drop the column from the table
        string getDropSql(string table_name) {
            return sprintf("alter table %s drop column %s", table_name, name);
        }

        #! returns a list of sql strings that can be used to modify the column to the new definition; if the column definitions are identical then an empty list is returned
        /** @par Example:
            @code{.py}
list l = col.getModifySql(t, newcol);
            @endcode

            The column names are assumed to be equal.

            @param t the AbstractTable object to modify
            @param c the new column definition

            @return a list of sql strings that can be used to modify the column to the new definition; if the column definitions are identical then an empty list is returned
         */
        list getModifySql(AbstractTable t, AbstractColumn c, *hash opt) {
            list l = getModifySqlImpl(t, c, opt);

            # update existing rows with the new default column value if:
            /* - a default value is being assigned and there was no default value before
               - a not null constraint is being added whereas there was none before
               - the table is not empty
            */
            if (def_val != c.def_val
                && (exists c.def_val && c.def_val != NULL)
                && (!exists def_val || def_val == NULL)
                && !c.nullable && nullable && !t.emptyData()) {
                string sql = sprintf("update %s set %s = %s where %s is null", t.getSqlName(), name, c.def_val, name);
                # insert in the front of the list
                unshift l, sql;
            }

            return l;
        }

        #! returns a string that can be used to rename the column
        /** @par Example:
            @code{.py}
string str = col.getRenameSql(t, "new_name");
            @endcode

            @param t the AbstractTable object to modify
            @param new_name the new name for the column

            @return a string that can be used to rename the column
        */
        abstract string getRenameSql(AbstractTable t, string new_name);

        #! returns @ref Qore::True "True" if the argument is equal to the current object, @ref Qore::False "False" if not
        bool equal(AbstractColumn c) {
            if (self.className() != c.className() || name != c.name || native_type != c.native_type || size != c.size || nullable != c.nullable || def_val != c.def_val || comment != c.comment) {
                #printf("L: %N\nXXXXXXXXXXXXXXX\nR: %N\n\n", self, c);
                return False;
            }

            return equalImpl(c);
        }

        #! returns @ref Qore::True "True" if the argument is equal to the current object, @ref Qore::False "False" if not
        private abstract bool equalImpl(AbstractColumn c);

        #! returns a list of sql strings that can be used to modify the column to the new definition; if the column definitions are identical then an empty list is returned
        /** @par Example:
            @code{.py}
list l = col.getModifySql(t, newcol);
            @endcode

            The column names are assumed to be equal.

            @param t the AbstractTable object to modify
            @param c the new column definition

            @return a list of sql strings that can be used to modify the column to the new definition; if the column definitions are identical then an empty list is returned
         */
        private abstract list getModifySqlImpl(AbstractTable t, AbstractColumn c, *hash opt);
    }

    #! the base class to use to extend AbstractColumn to implement numeric columns
    public class NumericColumnInfo {
        public {
            #! the scale for numeric columns
            int scale;
        }

        constructor(softint n_scale = 0) {
            scale = n_scale;
        }

        #! returns the string describing the native type that can be used in SQL (for example to add the column to a table or when creating the table)
        string getNativeTypeString(string native_type, int precision) {
            string sql = native_type;
            if (precision) {
                sql += sprintf("(%d", precision);
                if (scale)
                    sql += sprintf(",%d", scale);
                sql += ")";
            }
            return sql;
        }
   }

    #! index container class that throws an exception if an unknown index is accessed
    public class Indexes inherits AbstractHashContainer {
        constructor(*hash c) : AbstractHashContainer(c) {
        }

        #! adds the given value to the hash with the given key name
        add(string k, AbstractIndex val) {
            h{k} = val;
        }

        #! find an index with columns equal to the index passed
        *AbstractIndex findEqual(AbstractIndex ix) {
            foreach AbstractIndex ix2 in (h.iterator()) {
                if (ix2.equalExceptName(ix))
                    return ix2;
            }
        }

        #! removes the given key from the contained hash and returns the value
        AbstractIndex take(string k) {
            return remove h{k};
        }

        #! tries to remove the given key from the contained hash and returns the value if it exists
        *AbstractIndex tryTake(string k) {
            return remove h{k};
        }

        #! returns the AbstractIndex object corresponding to the key given or throws a KEY-ERROR exception
        /** @par Example:
            @code{.py}
AbstractIndex ix = indexes.pk_jobs;
            @endcode

            @param k the name of the key to access

            @return the value of the given key in the contained hash if it exists

            @throw KEY-ERROR the given key does not exist in the contained hash

            @note this method is called automatically when an unknown or inaccessible member name is accessed from outside the class

            @see @ref memberGate_methods
        */
        AbstractIndex memberGate(string k) {
            return AbstractHashContainer::memberGate(k);
        }

        string getElementName() {
            return "index";
        }
    }

    #! the abstract base class for index information
    public class AbstractIndex {
        public {
            #! the name of the index
            string name;

            #! @ref Qore::True "True" if the index is a unique index, @ref Qore::False "False" if not
            bool unique;

            #! an object of class Columns representing the columns in the index
            Columns columns;
        }

        private {
            #! the AbstractColumnSupportingConstraint that this index supports, if any
            *AbstractColumnSupportingConstraint constraint;
        }

        #! creates the object from the name, a unique flag, and a hash of column information
        constructor(string n, bool u, hash c) {
            name = n;
            unique = u;
            columns = new Columns(c);
        }

        #! returns the index name
        string getName() {
            return name;
        }

        #! returns @ref Qore::True "True" if the constraint references the named column
        bool hasColumn(string cname) {
            return columns.hasKey(cname);
        }

        #! returns a string that can be used to create the index in the database
        abstract string getCreateSql(string table_name, *hash opt);

        #! returns a string that can be used to drop the index from the database
        string getDropSql(string table_name) {
            return sprintf("drop index %s", name);
        }

        #! returns @ref Qore::True "True" if the argument is equal to the current index, @ref Qore::False "False" if not
        bool equal(AbstractIndex ix) {
            if (ix.className() != self.className())
                return False;

            if (name != ix.name || unique != ix.unique || (columns.keys() != ix.columns.keys()))
                return False;

            return equalImpl(ix);
        }

        #! returns @ref Qore::True "True" if the argument is equal to the current index with the exception of the name, @ref Qore::False "False" if not
        bool equalExceptName(AbstractIndex ix) {
            if (ix.className() != self.className())
                return False;

            if (unique != ix.unique || (columns.keys() != ix.columns.keys()))
                return False;

            return equalImpl(ix);
        }

        #! returns @ref Qore::True "True" if the argument is equal to the current index, @ref Qore::False "False" if not
        abstract bool equalImpl(AbstractIndex ix);

        #! returns a string that can be used to rename the index in the database
        abstract string getRenameSql(string table_name, string new_name);

        #! tags the index with a column supporting constraint (unique or fk constraint, etc) that the index supports
        setSupportingConstraint(AbstractColumnSupportingConstraint c) {
            constraint = c;
        }

        #! clears the supporting constraint
        setSupportingConstraint() {
            remove constraint;
        }

        #! returns the supporting constraint, if any
        *AbstractColumnSupportingConstraint getSupportingConstraint() {
            return constraint;
        }

        #! returns a list of strings to drop and recreate the current index; if there are dependent constraints, the list contains commands to disable the constraints before dropping the index and also contains commands to re-enable the contraints after re-creating the index
        list getRecreateSql(AbstractDatasource ds, string table_name, *hash opt) {
            list l = ();
            list enl;
            # get drop SQL and disable any dependent constraints plus a list of strings to re-enable the constraints
            if (constraint) {
                hash drh = constraint.getDisableReenableSql(ds, table_name, opt);
                l += drh.disable;
                enl = drh.enable;
            }

            ## drop current index
            l += getDropSql(table_name);

            # add new index to table
            l += getCreateSql(table_name, opt);

            # re-enable any constraints disabled when dropping the index
            if (enl)
                l += enl;

            return l;
        }
    }

    #! constraint container class that throws an exception if an unknown constraint is accessed
    public class Constraints inherits AbstractHashContainer {
        constructor(*hash c) : AbstractHashContainer(c) {
        }

        #! adds the given value to the hash with the given key name
        add(string k, AbstractConstraint val) {
            h{k} = val;
        }

        #! removes the given key from the contained hash and returns the value
        AbstractConstraint take(string k) {
            return remove h{k};
        }

        #! finds a unique constraint with the same columns as the unique constraint passed
        *AbstractUniqueConstraint findEqualUniqueConstraint(AbstractUniqueConstraint uk) {
            foreach AbstractConstraint c in (h.iterator()) {
                if (!(c instanceof AbstractUniqueConstraint))
                    continue;
                AbstractUniqueConstraint uk2 = c;
                if (uk2.matchKeys(uk))
                    return uk2;
            }
        }

        #! returns the AbstractConstraint object corresponding to the key given or throws a KEY-ERROR exception
        /** @par Example:
            @code{.py}
AbstractConstraint ix = constraints.uk_jobs;
            @endcode

            @param k the name of the key to access

            @return the value of the given key in the contained hash if it exists

            @throw KEY-ERROR the given key does not exist in the contained hash

            @note this method is called automatically when an unknown or inaccessible member name is accessed from outside the class

            @see @ref memberGate_methods
        */
        AbstractConstraint memberGate(string k) {
            return AbstractHashContainer::memberGate(k);
        }

        string getElementName() {
            return "constraint";
        }
    }

    #! abstract base class for constraints
    public class AbstractConstraint {
        public {}

        private {
            #! the name of the constraint
            string name;
        }

        #! creates the object and sets its name
        constructor(string n) {
            name = n;
        }

        #! returns the constraint name
        string getName() {
            return name;
        }

        #! renames the constraint
        rename(string n) {
            name = n;
        }

        #! returns a string that can be used to create the constraint in the database
        abstract string getCreateSql(string table_name, *hash opt);

        #! returns a string that can be used to drop the constraint from the database
        string getDropSql(string table_name) {
            return sprintf("alter table %s drop constraint %s", table_name, name);
        }

        #! returns a list of SQL strings that can be used to rename the constraint in the database
        abstract list getRenameSql(string table_name, string new_name);

        #! returns a string that can be used to temporarily disable the constraint from the database; if disabling constraints is not supported, then the constraint will be dropped
        string getDisableSql(string table_name) {
            return getDropSql(table_name);
        }

        #! returns a string that can be used to enable the constraint in the database; if disabling constraints is not supported, then the constraint will be dropped
        string getEnableSql(string table_name, *hash opt) {
            return getCreateSql(table_name, opt);
        }

        #! returns @ref Qore::True "True" if the argument is equal to the current object, @ref Qore::False "False" if not
        bool equal(AbstractConstraint c) {
            if (c.className() != self.className() || name != c.name)
                return False;

            return equalImpl(c);
        }

        #! returns @ref Qore::True "True" if the argument is equal to the current object, @ref Qore::False "False" if not
        private abstract bool equalImpl(AbstractConstraint c);

        #! returns @ref Qore::True "True" if the object supports an index property and is set, @ref Qore::False "False" if not
        abstract bool setIndexBase(string ix);

        #! clears any index base for the constraint
        abstract clearIndex();

        #! returns @ref Qore::True "True" if the constraint references the named column
        bool hasColumn(string cname) {
            return False;
        }
    }

    #! abstract class for check constraints
    public class AbstractCheckConstraint inherits AbstractConstraint {
        public {
            #! the source of the check clause
            string src;
        }

        #! creates the object and sets its name and the check clause source
        constructor(string n, string n_src) : AbstractConstraint(n) {
            name = n;
            src = n_src;
        }

        #! returns @ref Qore::True "True" if the argument is equal to the current object, @ref Qore::False "False" if not
        private bool equalImpl(AbstractConstraint c) {
            return src != cast<AbstractCheckConstraint>(c).src ? False : True;
        }

        #! returns @ref Qore::True "True" if the object supports an index property and is set, @ref Qore::False "False" if not
        bool setIndexBase(string ix) {
            return False;
        }

        #! clears any index base for the constraint
        clearIndex() {
        }
    }

    #! the API for a constraint with columns
    public class AbstractColumnSupportingConstraint inherits AbstractConstraint, Columns {
        private {
            #! a hash of ForeignConstraintSources, keyed by table name, the value is a hash of foreign constraints keyed by constraint name
            *hash sourceConstraints;

            #! the index supporting the constraint
            *string index;
        }

        #! creates the object from the name and a hash of column information
        constructor(string n, *hash c, *string n_index) : AbstractConstraint(n), Columns(c) {
            index = n_index;
        }

        #! creates the object from the name and a Columns object
        constructor(string n, Columns c, *string n_index) : AbstractConstraint(n), Columns(c) {
            index = n_index;
        }

        #! returns an iterator through all known source foreign constraints on the current table
        /** each element is a hash with the following keys:
            - \c fk: the AbstractForeignConstraint object
            - \c table: the table name
        */
        Qore::AbstractIterator getSourceConstraintIterator() {
            list l = ();
            if (sourceConstraints) {
                HashIterator i(sourceConstraints);
                while (i.next())
                    map l += ("fk": $1, "table": i.getKey()), i.getValue().iterator();
            }

            return l.iterator();
        }

        #! returns lists of SQL strings to disable this constraint plus any dependent constraints and another list of SQL strings to reenable the same constraints; used when updating indexes that depend on constraints
        hash getDisableReenableSql(AbstractDatasource ds, string table_name, *hash opts) {
            hash ch = (
                "disable": (),
                "enable": (),
                );

            HashIterator i(sourceConstraints);
            while (i.next()) {
                # make sure table actually exists before disabling constraints
                string key = i.getKey();
                Table t(ds, key);
                if (!t.checkExistence())
                    continue;
                foreach AbstractForeignConstraint fk in (i.getValue().iterator()) {
                    ch.disable += fk.getDisableSql(key);
                    unshift ch.enable, fk.getEnableSql(key, opts);
                }
            }

            ch.disable += getDisableSql(table_name);
            unshift ch.enable, getEnableSql(table_name, opts);

            return ch;
        }

        #! find an index that matches the constraint and marks both objects as related
        findMatchingIndex(*Indexes indexes) {
            if (index) {
                if (indexes.hasKey(index))
                    indexes{index}.setSupportingConstraint(self);
                return;
            }

            # see if the primary key corresponds to a unique index with the same name & columns
            if (indexes.hasKey(name)) {
                AbstractIndex ix = indexes{name};
                if (ix.columns.matchKeys(h) && setIndexBase(ix.name))
                    ix.setSupportingConstraint(self);
            }
            else {
                # search for an index with matching columns
                foreach AbstractIndex ix in (indexes.iterator()) {
                    if (ix.columns.matchKeys(h)) {
                        if (setIndexBase(ix.name))
                            ix.setSupportingConstraint(self);
                        break;
                    }
                }
            }
        }

        #! adds a foreign constraint source to the unique constraint
        /** @par Example:
            @code{.py}
uk.addSourceConstraint(table_name, constraint_name);
            @endcode

            @param tname the name of the source table
            @param fk the source constraint

            @throw SOURCE-CONSTRAINT-ERROR a different source constraint with the given name already exists
        */
        addSourceConstraint(string tname, AbstractForeignConstraint fk) {
            if (sourceConstraints{tname}{fk.getName()})
                throw "SOURCE-CONSTRAINT-ERROR", sprintf("unique constraint %y: already has a source constraint from table %y from foreign constraint %y", name, tname, fk.getName());
            sourceConstraints{tname}{fk.getName()} = fk;
            #printf("added source constraint to %y: %s.%s\n", name, tname, fk.getName());
        }

        #! removes a source constraint
        removeSourceConstraint(string tname, list cols) {
            if (matchKeys(cols))
                remove sourceConstraints{tname};
        }

        #! renames a table in a source constraint
        renameSourceConstraintTable(string old_name, string new_name) {
            if (sourceConstraints{old_name})
                sourceConstraints{new_name} = remove sourceConstraints{old_name};
        }

        #! returns @ref Qore::True "True" if the constraint references the named column
        bool hasColumn(string cname) {
            return h.hasKey(cname);
        }

        #! returns the name of the associated index, if any
        *string getIndex() {
            return index;
        }

        #! returns @ref Qore::True "True" if the argument is equal to the current object, @ref Qore::False "False" if not
        private bool equalImpl(AbstractConstraint c) {
            return (self.keys() != cast<AbstractColumnSupportingConstraint>(c).keys()) ? False : True;
        }

        #! returns @ref Qore::True "True" if the object supports an index property and is set, @ref Qore::False "False" if not
        bool setIndexBase(string ix) {
            if (index && index != ix)
                return False;
            index = ix;
            return True;
        }

        #! clears any index base for the constraint
        clearIndex() {
            delete index;
        }

        #! returns a string that can be used to create the constraint in the database
        abstract string getCreateSql(string table_name, *hash opts);
    }

    #! represents a unique column constraint
    public class AbstractUniqueConstraint inherits AbstractColumnSupportingConstraint {
        #! creates the object from the name an a hash of column information
        constructor(string n, *hash c, *string n_index) : AbstractColumnSupportingConstraint(n, c, n_index) {
        }
    }

    #! represents a primary key
    public class AbstractPrimaryKey inherits AbstractUniqueConstraint {
        constructor() : AbstractUniqueConstraint("") {
        }

        constructor(string n, *hash c) : AbstractUniqueConstraint(n, c) {
        }
    }

    #! foreign constraint container class that throws an exception if an unknown constraint is accessed
    public class ForeignConstraints inherits AbstractHashContainer {
        constructor(*hash c) : AbstractHashContainer(c) {
        }

        #! adds the given value to the hash with the given key name
        add(string k, AbstractForeignConstraint val) {
            h{k} = val;
        }

        #! removes the given key from the contained hash and returns the value
        AbstractForeignConstraint take(string k) {
            return remove h{k};
        }

        #! find an index with columns equal to the index passed
        *AbstractForeignConstraint findEqual(AbstractForeignConstraint fk) {
            foreach AbstractForeignConstraint fk2 in (h.iterator()) {
                if (fk2.matchKeys(fk) && fk2.target.table == fk.target.table && fk2.target.columns.matchKeys(fk.target.columns))
                    return fk2;
            }
        }

        #! returns the AbstractForeignConstraint object corresponding to the key given or throws a KEY-ERROR exception
        /** @par Example:
            @code{.py}
AbstractForeignConstraint ix = fcs.fk_jobs_job_defs;
            @endcode

            @param k the name of the key to access

            @return the value of the given key in the contained hash if it exists

            @throw KEY-ERROR the given key does not exist in the contained hash

            @note this method is called automatically when an unknown or inaccessible member name is accessed from outside the class

            @see @ref memberGate_methods
        */
        AbstractForeignConstraint memberGate(string k) {
            return AbstractHashContainer::memberGate(k);
        }

        #! returns either a hash of AbstractColumn information or @ref nothing if no foreign constraint can be found that matches the arguments
        *hash findConstraintOn(string table, softlist cols) {
            foreach AbstractForeignConstraint fk in (h.iterator()) {
                if (fk.target.table == table && fk.target.columns.matchKeys(cols))
                    return fk.getHash();
            }
        }

        #! returns \c "foreign constraint" for the type of object encapsulated
        string getElementName() {
            return "foreign constraint";
        }
    }

    #! a class describing a foreign constraint target
    public class ForeignConstraintTarget {
        public {
            #! the name of the target table
            string table;

            #! columns in the target table
            Columns columns;
        }

        #! creates the object and sets the target table name and the target columns
        constructor(string t, Columns c) {
            table = t;
            columns = c;
        }

        #! returns @ref Qore::True "True" if the argument is equal to the current object, @ref Qore::False "False" if not
        bool equal(ForeignConstraintTarget targ) {
            return table != targ.table || (columns.keys() != targ.columns.keys()) ? False : True;
        }
    }

    #! the base class for foreign key constraint information
    public class AbstractForeignConstraint inherits AbstractColumnSupportingConstraint {
        public {
            #! a ForeignConstraintTarget object to describe the target table and columns
            ForeignConstraintTarget target;
        }

        constructor(string n, Columns c, ForeignConstraintTarget t) : AbstractColumnSupportingConstraint(n, c) {
            target = t;
        }

        #! returns @ref Qore::True "True" if the argument is equal to the current object, @ref Qore::False "False" if not
        private bool equalImpl(AbstractConstraint con) {
            AbstractForeignConstraint c = cast<AbstractForeignConstraint>(con);
            return (h.keys() != c.h.keys())
                || !target.equal(c.target)
                ? False : True;
        }
    }

    #! base class for sequences
    public class AbstractSequence {
        public {
            #! the name of the sequence
            string name;

            #! the starting number
            number start;

            #! the increment
            number increment;

            #! the ending number
            *number max;
        }

        #! creates the object from the arguments
        constructor(string n_name, number n_start = 1, number n_increment = 1, *softnumber n_max) {
            name = n_name;
            start = n_start;
            increment = n_increment;
            max = n_max;
            if (!increment)
                throw "SEQUENCE-ERROR", sprintf("sequence %y: increment cannot be zero", name);
            if (max) {
                if (increment > 0 && max < start)
                    throw "SEQUENCE-ERROR", sprintf("ascending sequence %y increment %d: max value %d is less than the starting value: %d", name, increment, max, start);
                if (increment < 0 && max > start)
                    throw "SEQUENCE-ERROR", sprintf("descending sequence %y increment %d: max value %d is less than the starting value: %d", name, increment, max, start);
            }
        }

        #! returns a string that can be used to create the sequence in the database
        abstract string getCreateSql(*hash opt);

        #! returns a string that can be used to drop the sequence from the database
        /** @param opt drop options (if supported by the driver and object); in this generic base class method, this argument is ignored
         */
        string getDropSql(*hash opt) {
            return sprintf("drop sequence %s", name);
        }

        #! returns a list of strings that can be used to rename the sequence in the database
        /** @param new_name the new name of the object
            @param opt drop options (if supported by the driver and object)
         */
        abstract softlist getRenameSql(string new_name, *hash opt);
    }

    #! base class for views
    public class AbstractView {
        public {
            # ! potential object schema
            *string schema;

            #! the name of the sequence
            string name;

            #! the source code
            string src;

            #! Flag showing if is the view updatable with DML commands.
            bool updatable;
        }

        #! creates the object from the arguments
        constructor(string n_name, string n_src) {
            name = n_name;
            src = n_src;
        }

        #! returns a string that can be used to create the view in the database
        abstract string getCreateSql(*hash opt);

        #! returns a string that can be used to drop the view from the database
        /** @param opt drop options (if supported by the driver and object); in this generic base class method, this argument is ignored
         */
        string getDropSql(*hash opt) {
            return sprintf("drop view %s", name);
        }

        #! returns a list with command(s) that can be used to rename the view in the database
        /** @param new_name the new name of the object
            @param opt drop options (if supported by the driver and object)
         */
        abstract softlist getRenameSql(string new_name, *hash opt);
    }

    #! base class for function or objects with code
    public class AbstractFunctionBase {
        public {
            #! the name of the object
            string name;

            #! the type of object
            string type;

            #! the source of the object
            string src;
        }

        #! creates the object from the arguments passed
        /** @param n the name of the object
            @param n_type the type of object
            @param n_src the source of the object
         */
        constructor(string n, string n_type, string n_src) {
            name = n;
            type = n_type;
            src = n_src;
        }

        #! returns the type of object
        string getType() {
            return type;
        }

        #! returns a string that can be used to drop the function from the database
        /** @param opt drop options (if supported by the driver and object); in this generic base class method, this argument is ignored
         */
        string getDropSql(*hash opt) {
            return sprintf("drop %s %s", type, name);
        }

        #! returns @ref Qore::True "True" if the argument is equal to the current object, @ref Qore::False "False" if not
        bool equal(AbstractFunctionBase t) {
            if (self.className() != t.className() || name != t.name || type != t.type) {
                return False;
            }

            string src1 = src;
            src1 =~ s/\n/ /g;
            string src2 = t.src;
            src2 =~ s/\n/ /g;

            if (src1 != src2) {
                #printf("%s\nXXXXXXXXXXXXXXX\n%s\n", src1, src2);
                return False;
            }
            #printf("%s\nXXXXXX EQUAL XXXXXX\n%s\n", src1, src2);

            return equalImpl(t);
        }

        #! returns @ref Qore::True "True" if the argument is equal to the current object, @ref Qore::False "False" if not
        private abstract bool equalImpl(AbstractFunctionBase t);
    }

    #! base class for functions
    public class AbstractFunction inherits AbstractFunctionBase {
        #! creates the object from the arguments passed
        /** @param n the name of the object
            @param n_type the type of object
            @param n_src the source of the object
         */
        constructor(string n, string n_type, string n_src) : AbstractFunctionBase(n, n_type, n_src) {
        }

        #! returns a list of SQL strings that can be used to create the function in the database
        abstract list getCreateSql(*hash opt);

        #! returns a list of strings that can be used to rename the function in the database
        /** @param new_name the new name of the object
            @param opt drop options (if supported by the driver and object)
         */
        abstract softlist getRenameSql(string new_name, *hash opt);

        #! sets the new name of the object
        setName(string new_name) {
            name = new_name;
        }
    }

    #! function container class that throws an exception if an unknown function is accessed
    public class Functions inherits AbstractHashContainer {
        constructor(*hash c) : AbstractHashContainer(c) {
        }

        #! adds the given value to the hash with the given key name
        add(string k, AbstractFunction val) {
            h{k} = val;
        }

        #! removes the given key from the contained hash and returns the value
        AbstractFunction take(string k) {
            return remove h{k};
        }

        #! returns the AbstractFunction object corresponding to the key given or throws a KEY-ERROR exception
        /** @par Example:
            @code{.py}
AbstractFunction f = funcs.func1;
            @endcode

            @param k the name of the key to access

            @return the value of the given key in the contained hash if it exists

            @throw KEY-ERROR the given key does not exist in the contained hash

            @note this method is called automatically when an unknown or inaccessible member name is accessed from outside the class

            @see @ref memberGate_methods
        */
        AbstractFunction memberGate(string k) {
            return AbstractHashContainer::memberGate(k);
        }

        string getElementName() {
            return "function";
        }
    }

    #! the base class for triggers
    public class AbstractTrigger inherits AbstractFunctionBase {
        #! creates the object and sets its name and the trigger source
        constructor(string n, string n_src) : AbstractFunctionBase(n, "trigger", n_src) {
        }

        #! returns a string that can be used to create the trigger in the database
        abstract list getCreateSql(string table_name, *hash opt);

        #! returns a string that can be used to rename the trigger in the database
        abstract softlist getRenameSql(string table_name, string new_name);

        #! returns a string that can be used to drop the trigger in the database
        abstract list getDropSql(string table_name);
    }

    #! trigger container class that throws an exception if an unknown trigger is accessed
    public class Triggers inherits AbstractHashContainer {
        constructor(*hash c) : AbstractHashContainer(c) {
        }

        #! adds the given value to the hash with the given key name
        add(string k, AbstractTrigger val) {
            h{k} = val;
        }

        #! removes the given key from the contained hash and returns the value
        AbstractTrigger take(string k) {
            return remove h{k};
        }

        #! returns the AbstractTrigger object corresponding to the key given or throws a KEY-ERROR exception
        /** @par Example:
            @code{.py}
AbstractTrigger trig = trigs.job_trig;
            @endcode

            @param k the name of the key to access

            @return the value of the given key in the contained hash if it exists

            @throw KEY-ERROR the given key does not exist in the contained hash

            @note this method is called automatically when an unknown or inaccessible member name is accessed from outside the class

            @see @ref memberGate_methods
        */
        AbstractTrigger memberGate(string k) {
            return AbstractHashContainer::memberGate(k);
        }

        string getElementName() {
            return "trigger";
        }
    }

    #! represents a database; this class embeds an AbstractDatabase object that is created automatically in the constructor based on the database driver for the AbstractDatasource object providing the database connection
    /** Driver-specific modules that provide the AbstractDatabase implementation embedded in this class are loaded on demand based on the driver's name.
        The driver-specific module's name is generated based on the db-driver's name with the first letter capitalized then with \c "SqlUtil" appended.

        For example:
        - \c "oracle": <a href="../../OracleSqlUtil/html/index.html">OracleSqlUtil</a>
        - \c "pgsql": <a href="../../PgsqlSqlUtil/html/index.html">PgsqlSqlUtil</a>
        - \c "mysql": <a href="../../MysqlSqlUtil/html/index.html">MysqlSqlUtil</a>

        etc.
    */
    public class Database {
        private {
            #! the embedded AbstractDatabase object that actually provides the functionality for this class
            AbstractDatabase db;
        }

        #! creates the Database object
        /** @par Example:
            @code{.py}
Database db(ds);
            @endcode

            @param ds the AbstractDatasource object used to provide the DB connection
            @param opts a hash of options for the function creation string; see @ref AbstractDatabase::DatabaseOptions for common options; each driver can support additional driver-specific options

            @throw DATABASE-DRIVER-ERROR no database-specific module can be loaded
            @throw OPTION-ERROR invalid or unsupported option passed
        */
        constructor(AbstractDatasource ds, *hash opts) {
            db = AbstractDatabase::getDatabase(ds, opts);
        }

        #! creates the AbstractDatabase object
        /** @par Example:
            @code{.py}
AbstractDatabase db("pgsql:user/pass@db%host");
            @endcode

            @param ds a datasource description string in the format that can be parsed by parse_datasource()
            @param opts a hash of options for the function creation string; see @ref AbstractDatabase::DatabaseOptions for common options; each driver can support additional driver-specific options

            @throw OPTION-ERROR invalid or unsupported option passed
        */
        constructor(string ds, *hash opts) {
            db = AbstractDatabase::getDatabase(ds, opts);
        }

        #! creates the AbstractDatabase object
        /** @par Example:
            @code{.py}
AbstractDatabase db("pgsql:user/pass@db%host");
            @endcode

            @param ds a hash giving parameters for the new datasource with the following possible keys (the \c "type" key is mandatory, also usable with the output of the parse_datasource() function):
            - \c type: (@ref string_or_nothing_type "*string") The name of the database driver to use; this key is mandatory; if not present, an exception will be raised. See @ref sql_constants for builtin constants for DBI drivers shipped with Qore, or see the DBI driver documentation to use an add-on driver (this string should be the name of the driver to be loaded)
            - \c user: (@ref string_or_nothing_type "*string") The user name for the new connection
            - \c pass: (@ref string_or_nothing_type "*string") The password for the new connection
            - \c db: (@ref string_or_nothing_type "*string") The database name for the new connection
            - \c charset: (@ref string_or_nothing_type "*string") The database-specific name of the character encoding to use for the new connection. Also see Qore::SQL::Datasource::setDBCharset() for a method that allows this parameter to be set after the constructor. If no value is passed for this parameter, then the database character encoding corresponding to the default character encoding for the %Qore process will be used instead.
            - \c host: (@ref string_or_nothing_type "*string") The host name for the new connection
            - \c port: (@ref softint_type "softint") The port number for the new connection
            - \c options: (@ref hash_or_nothing_type "*hash") Any options for the new connection
            @param opts a hash of options for the table creation string; see @ref AbstractDatabase::DatabaseOptions for common options; each driver can support additional driver-specific options

            @throw OPTION-ERROR invalid or unsupported option passed
        */
        constructor(hash ds, *hash opts) {
            db = AbstractDatabase::getDatabase(ds, opts);
        }

        #! gets the underlying AbstractDatabase
        SqlUtil::AbstractDatabase getDatabase() {
            return db;
        }

        #! executes a method on the contained AbstractDatabase object
        any methodGate(string meth) {
            return call_object_method_args(db, meth, argv);
        }
    }

    #! base class for abstract SqlUtil classes
    public class AbstractSqlUtilBase {
        private {
            #! the connection to the database server
            AbstractDatasource ds;
            #! datasource description
            string dsdesc;
            #! mutex for atomic actions
            Mutex l();
            #! option hash
            *hash opts;
        }

        #! creates the object; private constructor
        /** @param nds the AbstractDatasource for the connection to the database
            @param nopts a hash of options for the table creation string; see @ref SqlUtil::AbstractTable::TableOptions for common options; each driver can support additional driver-specific options

            @throw OPTION-ERROR invalid or unsupported option passed
         */
        private constructor(AbstractDatasource nds, *hash nopts) {
            opts = nopts;
            ds = nds;

            # ensure datasource is open
            ds.getServerVersion();
            dsdesc = AbstractSqlUtilBase::makeDatasourceDesc(ds);
        }

        static string makeDatasourceDesc(AbstractDatasource ds) {
            string dsdesc = sprintf("%s:%s@%s", ds.getDriverName(), ds.getUserName(), ds.getDBName());
            if (ds.getHostName())
                dsdesc += "%" + ds.getHostName();
            if (ds.getPort())
                dsdesc += sprintf(":%d", ds.getPort());
            return dsdesc;
        }

        private validateOptionsIntern(string err, hash ropt, reference opt, string tag) {
            # make sure driver-specific options take precedence over generic options
            string dn = getDriverName();
            AbstractDatabase::checkDriverOptions(\opt, dn);

            # check valid options and option value types
            foreach string k in (opt.keyIterator()) {
                if (!exists opt{k}) {
                    delete opt{k};
                    continue;
                }

                if (!ropt.hasKey(k))
                    throw err, sprintf("%s: unknown key %y; valid keys for %y: %y", tag, k, self.className(), ropt.keys());

                AbstractSqlUtilBase::validateOptionIntern(err, ropt{k}, \opt{k}, k, tag);
            }
        }

        private static validateOptionIntern(string err, string type, reference opt, string k, string tag) {
            switch (type) {
                case Type::Int: {
                    if (opt.type() != type) {
                        if (!opt.intp())
                            throw err, sprintf("%s: key %y has type %y, expecting \"int\" (value: %y)", tag, k, opt.type(), opt);
                        opt = opt.toInt();
                    }
                    break;
                }
                case Type::Number: {
                    if (opt.type() != type) {
                        if (!opt.intp())
                            throw err, sprintf("%s: key %y has type %y, expecting \"number\" (value: %y)", tag, k, opt.type(), opt);
                        opt = number(opt);
                    }
                    break;
                }
                case Type::Boolean: {
                    if (opt.type() != type) {
                        if (!opt.intp())
                            throw err, sprintf("%s: key %y has type %y, expecting \"boolean\" (value: %y)", tag, k, opt.type(), opt);
                        opt = opt.toBool();
                    }
                    break;
                }
                case Type::String: {
                    if (opt.type() != type) {
                        if (!opt.strp())
                            throw err, sprintf("%s: key %y has type %y, expecting \"string\" (value: %y)", tag, k, opt.type(), opt);
                        opt = opt.toString();
                    }
                    break;
                }
                case "softstringlist": {
                    softlist l = opt;
                    foreach any v in (l) {
                        if (v.typeCode() != NT_STRING)
                            throw err, sprintf("%s: key %y element %d/%d (first is 0) has type %y, expecting \"string\" (value: %y)", tag, k, $#, l.size(), v.type(), opt);
                    }

                    opt = l;
                    break;
                }
                case "stringhashlist": {
                    softlist l = opt;
                    foreach any v in (l) {
                        switch (v.typeCode()) {
                            case NT_STRING:
                            case NT_HASH:
                                break;
                            default:
                                throw err, sprintf("%s: key %y element %d/%d (first is 0) has type %y, expecting \"string\" or \"hash\" (value: %y)", tag, k, $#, l.size(), v.type(), opt);
                        }
                    }

                    opt = l;
                    break;
                }
                case "softstringinthashlist": {
                    softlist l = opt;
                    foreach any v in (l) {
                        int t = v.typeCode();
                        bool ok;
                        if (t == NT_STRING) {
                            if (v.toInt() == v)
                                v = v.toInt();
                            else
                                ok = True;
                        }
                        if (!ok) {
                            if (t == NT_INT) {
                                if (v < 1)
                                    throw err, sprintf("%s: key %y element %d/%d (first is 0) is %d, column numbers must be positive integers", tag, k, $#, l.size(), v);
                            }
                            else if (v.typeCode() != NT_HASH)
                                throw err, sprintf("%s: key %y element %d/%d (first is 0) has type %y, expecting \"string\" or \"hash\" (value: %y)", tag, k, $#, l.size(), v.type(), opt);
                        }
                    }

                    opt = l;
                    break;
                }
                case "hash/list": {
                    if (opt.typeCode() == NT_LIST) {
                        foreach any v in (opt) {
                            if (v.typeCode() != NT_HASH)
                                throw err, sprintf("%s: key %y element %d/%d (first is 0) has type %y, expecting \"hash\" (value: %y)", tag, k, $#, opt.size(), v.type(), opt);
                        }
                    }
                    else if (opt.typeCode() != NT_HASH)
                        throw err, sprintf("key %y requires type \"list\" or \"hash\" but type %y was passed (%y)", k, opt.type(), opt);
                    break;
                }
                case "Tables": {
                    if (!(opt instanceof Tables))
                        throw err, sprintf("%s: key %y has type %s, expecting class \"Tables\"", tag, k, opt.typeCode() == NT_OBJECT ? sprintf("class \"%s\"", opt.className()) : sprintf("\"%s\"", opt.type()));
                    break;
                }

                # no validation for NOTHING
                case Type::NothingType: break;

                case "code": if (opt.typeCode() == NT_CLOSURE || opt.typeCode() == NT_CALLREF)
                    break;

                # throw an exception if the type is not supported
                default:
                    if (opt.type() != type)
                        throw err, sprintf("key %y requires type %y but type %y was passed (%y)", k, type, opt.type(), opt);
            }
        }

        #! Check input node for all hash keys - if it contains a key with whitespace in the beginning or at the end -> error
        private validateHashKeysForWhitespaces(any node) {
            switch (node.typeCode()) {
                case NT_HASH: {
                        HashIterator it(node);
                        while (it.next()) {
                            if (trim(it.getKey()) != it.getKey()) {
                                throw "VALIDATE-HASH-ERROR", sprintf("Key '%s' contains whitespaces at the end or in the beginning.", it.getKey());
                            }
                            validateHashKeysForWhitespaces(it.getValue());
                        }
                    }
                    break;
                case NT_LIST:{
                        ListIterator it(node);
                        while (it.next()) {
                            validateHashKeysForWhitespaces(it.getValue());
                        }
                    }
                    break;
            } # switch
        }

        #! gets the underlying AbstractDatasource
        Qore::SQL::AbstractDatasource getDatasource() {
            return ds;
        }

        #! returns the database driver name
        string getDriverName() {
            return ds.getDriverName();
        }

        #! returns a descriptive string for the datasource
        string getDatasourceDesc() {
            return dsdesc;
        }
    }

    #! the base abstract class for the database implementation
    public class AbstractDatabase inherits AbstractSqlUtilBase {
        public {
            #! database options
            /** The following keys can be set for this option:
                - \c native_case: (@ref bool_type) if @ref Qore::True "True" then objects will be returned in the case the database server returns them in even if the database server uses case-insensitive names; normally in this case %SqlUtil will convert the names to lower-case
            */
            const DatabaseOptions = (
                "native_case": Type::Boolean,
                );

            #! generic cache options
            /** The following keys can be set for this option:
                - \c table_cache: (@ref SqlUtil::Tables "Tables") an optional table cache for maintaining cached tables and foreign key relationships between tables
            */
            const CacheOptions = (
                "table_cache": "Tables",
                );

            #! generic callback options
            /** The following keys can be set for this option:
                - \c info_callback: see @ref info_callback
                - \c sql_callback: see @ref sql_callback
                - \c sql_callback_executed: see @ref sql_callback_executed
            */
            const CallbackOptions = (
                "info_callback": "code",
                "sql_callback": "code",
                "sql_callback_executed": Type::Boolean,
                );

            /** @defgroup info_actions Info Callback Action Codes
                These are the action codes used with the @ref info_callback in schema and table alignment
                @see
                - @ref ActionMap
                - @ref ActionDescMap
                - @ref ActionLetterMap
             */
            #@{
            #! used when an existing object matches the template and no changes are made
            const AC_Unchanged = 0;

            #! used when a new object is created
            const AC_Create = 1;

            #! used when an object is dropped
            const AC_Drop = 2;

            #! used when an object is renamed
            const AC_Rename = 3;

            #! used when an object is modified in place
            const AC_Modify = 4;

            #! used when a table is truncated
            const AC_Truncate = 5;

            #! used when an element is added to an existing object
            const AC_Add = 6;

            #! used when an object is recreated (usually dropped and recreated in place)
            const AC_Recreate = 7;

            #! used when data is inserted in a table
            const AC_Insert = 8;

            #! used when data is updated in a table
            const AC_Update = 9;

            #! used when data is deleted in a table
            const AC_Delete = 10;

            #! used when dropping object but the object is not present
            const AC_NotFound = 11;
            #@}

            #! maps from action codes to action descriptions
            const ActionMap = (
                AC_Unchanged: "unchanged",
                AC_Create: "create",
                AC_Drop: "drop",
                AC_Rename: "rename",
                AC_Modify: "modify",
                AC_Truncate: "truncate",
                AC_Add: "add",
                AC_Recreate: "recreate",
                AC_Insert: "insert",
                AC_Update: "update",
                AC_Delete: "delete",
                AC_NotFound: "not found",
            );

            #! maps from action descriptions to action codes
            const ActionDescMap = (
                "unchanged": AC_Unchanged,
                "create": AC_Create,
                "drop": AC_Drop,
                "rename": AC_Rename,
                "modify": AC_Modify,
                "truncate": AC_Truncate,
                "add": AC_Add,
                "recreate": AC_Recreate,
                "insert": AC_Insert,
                "update": AC_Update,
                "delete": AC_Delete,
                "not found": AC_NotFound,
            );

            #! maps from action codes to action letter codes
            const ActionLetterMap = (
                AC_Unchanged: ".",
                AC_Create: "C",
                AC_Drop: "D",
                AC_Rename: "N",
                AC_Modify: "M",
                AC_Truncate: "T",
                AC_Add: "A",
                AC_Recreate: "R",
                AC_Insert: "I",
                AC_Update: "U",
                AC_Delete: "X",
                AC_NotFound: ".",
            );

            #! default generic creation options
            /** This option is comprised of @ref AbstractDatabase::CallbackOptions plus the following keys:
                - \c replace: (@ref bool_type) if @ref Qore::True "True" and supported by the underlying db driver, \c "create or replace" text is used when creating objects
                - \c table_cache: (@ref SqlUtil::Tables "Tables") an optional table cache for maintaining cached tables and foreign key relationships between tables
                - \c data_tablespace: (@ref string_type) a string giving the data tablespace to use for tables
                - \c index_tablespace: (@ref string_type) a string giving the index tablespace to use for indexes
            */
            const CreationOptions = CallbackOptions + (
                "replace": Type::Boolean,
                "table_cache": "Tables",
                "data_tablespace": Type::String,
                "index_tablespace": Type::String,
                );

            #! default generic schema description / alignment options
            /** Currently this option is identical to @ref AbstractDatabase::CreationOptions plus the following keys:
                - \c force: (@ref bool_Type) is @ref Qore::True "True" and supported by the underlying db driver, \c FORCE or \c CASCADE options are used when dropping or aligning objects
            */
            const AlignSchemaOptions = CreationOptions + (
                "force": Type::Boolean,
                );

            #! default generic drop schema options
            /** Currently this option is identical to @ref AbstractDatabase::CallbackOptions plus the following keys:
                - \c force: (@ref bool_Type) is @ref Qore::True "True" and supported by the underlying db driver, \c FORCE or \c CASCADE options are used when dropping objects
            */
            const DropSchemaOptions = CallbackOptions + (
                "force": Type::Boolean,
                );

            #! default generic schema description keys
            /** The following keys can be set in @ref schema_desc_hash "schema description hashes":
                - \c tables: a hash keyed by table name set to @ref table_desc_hash "table description hash" values
                - \c table_map: a hash for automatically renaming tables; if the source name (key) exists and the target name (value) does not exist, then the source table is automatically renamed
                - \c sequences: a hash keyed by sequence name set to @ref AbstractDatabase::SequenceDescriptionOptions "sequence hash description" values
                - \c sequence_map: a hash for automatically renaming sequences; if the source name (key) exists and the target name (value) does not exist, then the source sequence is automatically renamed
                - \c functions: a hash keyed by function name set to function source string values
                - \c function_map: a hash for automatically renaming functions; if the source name (key) exists and the target name (value) does not exist, then the source function is automatically renamed
                - \c procedures: a hash keyed by procedure name set to procedure source string values
                - \c procedure_map: a hash for automatically renaming procedures; if the source name (key) exists and the target name (value) does not exist, then the source procedure is automatically renamed

                @see @ref schema_desc_hash for more information
            */
            const SchemaDescriptionOptions = (
                "tables": Type::Hash,
                "table_map": Type::Hash,

                "sequences": Type::Hash,
                "sequence_map": Type::Hash,

                "functions": Type::Hash,
                "function_map": Type::Hash,

                "procedures": Type::Hash,
                "procedure_map": Type::Hash,

                #"views": Type::Hash,
                #"view_map": Type::Hash,
                );

            #! default generic sequence description keys
            /** The following keys can be set in sequence description hashes:
                - \c start: (@ref int_type) the starting value for the sequence
                - \c increment: (@ref int_type) the increment value for the sequence
                - \c end: (@ref int_type) the ending value for the sequence
            */
            const SequenceDescriptionOptions = (
                "start": Type::Int,
                "increment": Type::Int,
                "end": Type::Int,
                );

            #! Options for computeStatistics()
            const ComputeStatisticsOptions = (
                "tables"            : "softstringlist",
                );

            #! Options for reclaimSpace()
            const ReclaimSpaceOptions = (
                "tables"            : "softstringlist",
                );
        }

        private {
            #! native case option
            bool native_case = False;
        }

        #! creates the object; private constructor
        /** @param nds the AbstractDatasource for the connection to the database
            @param nopts a hash of options for the function creation string; see @ref AbstractDatabase::DatabaseOptions for common options; each driver can support additional driver-specific options

            @throw OPTION-ERROR invalid or unsupported option passed
         */
        private constructor(AbstractDatasource nds, *hash nopts) : AbstractSqlUtilBase(nds, nopts) {
            validateOptionsIntern("OPTION-ERROR", getDatabaseOptions(), \nopts);
            if (nopts.native_case)
                native_case = True;
        }

        #! See @ref DBFeaturesConstants
        list features() {
            return featuresImpl();
        }

        static doOkCallback(*hash opt, int ac, string type, string name, *string table, *string info) {
            if (opt.info_callback) {
                bool c = (type == "column");
                if (c)
                    name = sprintf("%s.%s", table, name);

                string str = sprintf("%s %s", type, name);
                if (table && !c)
                    str += sprintf(" on %s", table);

                if (ac == AC_Unchanged)
                    str += " does not need updating";
                else if (ac == AC_NotFound)
                    str += " does not exist or has already been dropped";

                if (info)
                    str += sprintf(" (%s)", info);

                opt.info_callback(str, ac, type, name, table, NOTHING, info);
            }
        }

        static *string doCallback(*hash opt, *string sql, int ac, string type, string name, *string table, *string new_name, *string info) {
            if (!sql)
                return;
            if (opt.info_callback) {
                bool c = (type == "column");
                if (c)
                    name = sprintf("%s.%s", table, name);
                string str = sprintf("%s %s %s", ActionMap{ac}, type, name);
                if (table && !c)
                    str += sprintf(" %s %s", ac == AC_Drop ? "from" : "on", table);
                if (new_name)
                    str += sprintf(" to %s", new_name);
                if (info)
                    str += sprintf(" (%s)", info);
                opt.info_callback(str, ac, type, name, table, new_name, info);
            }
            if (opt.sql_callback)
                opt.sql_callback(sql);
            return sql;
        }

        static list doCallback(*hash opt, list sql, int ac, string type, string name, *string table, *string new_name, *string info) {
            if (!sql)
                return sql;
            if (opt.info_callback) {
                bool c = (type == "column");
                if (c)
                    name = sprintf("%s.%s", table, name);
                string str = sprintf("%s %s %s", ActionMap{ac}, type, name);
                if (table && !c)
                    str += sprintf(" %s %s", ac == AC_Drop ? "from" : "on", table);
                if (new_name)
                    str += sprintf(" to %s", new_name);
                if (info)
                    str += sprintf(" (%s)", info);
                opt.info_callback(str, ac, type, name, table, new_name, info);
            }
            if (opt.sql_callback)
                map opt.sql_callback($1), sql;
            return sql;
        }

/*
        static *string doCallback(*hash opt, *string sql, string fmt) {
            if (!sql)
                return;
            if (opt.info_callback)
                opt.info_callback(vsprintf(fmt, argv));
            if (opt.sql_callback)
                opt.sql_callback(sql);
            return sql;
        }

        static list doCallback(*hash opt, list sql, string fmt) {
            if (sql) {
                if (opt.info_callback)
                    opt.info_callback(vsprintf(fmt, argv));
                if (opt.sql_callback)
                    map opt.sql_callback($1), sql;
            }
            return sql;
        }
*/

        #! executes some SQL with optional arguments so that if an error occurs the current transaction state is not lost
        /** @par Example:
            @code{.py}
t.tryExec("drop table tmp_table");
            @endcode

            Include any arguments in the parameter list after the \a sql argument

            @param sql the SQL to execute

            @return any return value from the SQL command executed
         */
        any tryExec(string sql) {
            return tryExecArgsImpl(sql, argv);
        }

        #! executes some SQL with optional arguments so that if an error occurs the current transaction state is not lost
        /** @par Example:
            @code{.py}
t.tryExec("delete from tmp_table where id = %v and name = %v", arglist);
            @endcode

            @param sql the SQL to execute
            @param args the bind / placeholder or other arguments corresponding to the SQL string

            @return any return value from the SQL command executed
         */
        any tryExecArgs(string sql, *softlist args) {
            return tryExecArgsImpl(sql, args);
        }

        #! executes some SQL so that if an error occurs the current transaction state is not lost
        /** @par Example:
            @code{.py}
t.tryExecRaw("drop table tmp_table");
            @endcode

            Include any arguments in the parameter list after the \a sql argument

            @param sql the SQL to execute

            @return any return value from the SQL command executed
         */
        any tryExecRaw(string sql) {
            return tryExecRawImpl(sql);
        }

        #! accepts a hash argument describing a database schema and returns a list of SQL strings that can be used to align the structure and configuration of the current schema with that of the argument; if the objects described are identical then an empty list is returned
        /** @par Example:
            @code{.py}
list l = db.getAlignSql(schema_hash);
            @endcode

            @param schema_hash the @ref schema_desc_hash "schema description hash" that decribes the target state of the schema
            @param opt a hash of options for the SQL creation strings; see @ref AbstractDatabase::AlignSchemaOptions for common options; each driver can support additional driver-specific options
            @param table_cache an optional object of class Tables to return the cache of AbstractTable objects keyed by table name for the schema template (ie target table definitions)

            @return a list of SQL strings that can be used to align the structure and configuration of the current schema with that of the argument; if the objects described are identical then an empty list is returned

            @throw SCHEMA-DESCRIPTION-ERROR the @ref schema_desc_hash "schema description hash" has an error or a required object does not exist
            @throw OPTION-ERROR invalid or unsupported option passed
        */
        list getAlignSql(hash schema_hash, *hash opt, *Tables table_cache) {
            validateOptionsIntern("SCHEMA-DESCRIPTION-ERROR", getSchemaDescriptionOptions(), \schema_hash);
            validateOptionsIntern("OPTION-ERROR", getAlignSchemaOptions(), \opt);
            validateHashKeysForWhitespaces(schema_hash);

            # get creation options
            *hash copt = opt{getAlignSchemaOptions().keys()};

            if (!table_cache)
                table_cache = new Tables();

            # setup db table cache
            opt.db_table_cache = new Tables();

            list l = ();

            string drv = getDriverName();
            AbstractDatabase::checkDriverOptions(\schema_hash, drv);
            AbstractDatabase::checkDriverOptions(\schema_hash.sequence_map, drv);
            AbstractDatabase::checkDriverOptions(\schema_hash.sequences, drv);

            # call driver-specific align schema sql function (start)
            list tl = getAlignSqlImpl(schema_hash, opt + ("start": True));
            if (tl) l += tl;

            # rename any sequences
            HashIterator i(schema_hash.sequence_map);
            while (i.next()) {
                string key = i.getKey();
                any value = i.getValue();
                if (value.typeCode() != NT_STRING)
                    throw "SCHEMA-DESCRIPTION-ERROR", sprintf("sequence_map %y value is not a string in the schema description hash; got type %y instead (%y)", key, value.type(), value);

                *AbstractSequence seq = getSequence(key);
                if (seq) {
                    l += AbstractDatabase::doCallback(opt, seq.getRenameSql(value), AC_Rename, "sequence", key, NOTHING, value);
                    # remove sequence from list since we know it exists
                    remove schema_hash.sequences{value};
                }
            }

            # create any missing sequences
            i = new HashIterator(schema_hash.sequences);
            while (i.next()) {
                string name = i.getKey();
                hash sh = i.getValue();
                validateOptionsIntern("SEQUENCE-DESCRIPTION-ERROR", getSequenceDescriptionOptions(), \sh);

                # see if sequence exists
                if (getSequence(name))
                    continue;

                AbstractSequence seq = makeSequence(name, sh.start, sh.increment, sh.end);
                l += AbstractDatabase::doCallback(opt, seq.getCreateSql(), AC_Create, "sequence", name);
            }

            # create template tables
            table_cache.populate(ds, schema_hash.tables, copt);

            # rename any tables
            AbstractDatabase::checkDriverOptions(\schema_hash.table_map, drv);
            foreach hash th in (schema_hash.table_map) {
                if (th.value.typeCode() != NT_STRING)
                    throw "SCHEMA-DESCRIPTION-ERROR", sprintf("table_map %y value is not a string in the schema description hash; got type %y instead (%y)", th.key, th.value.type(), th.value);

                Table t(ds, th.key);
                if (t.checkExistence()) {
                    l += AbstractDatabase::doCallback(opt, t.getRenameSql(th.value), AC_Rename, "table", th.key, NOTHING, th.value);
                    opt.db_table_cache.add(th.value, t);
                }
            }

            # now process DB tables
            AbstractDatabase::checkDriverOptions(\schema_hash.tables, drv);
            foreach string name in (schema_hash.tables.keyIterator()) {
                #printf("processing table %y\n", name);
                AbstractTable t;
                if (opt.db_table_cache.hasKey(name))
                    t = opt.db_table_cache{name};
                else {
                    t = (new Table(ds, name)).getTable();
                    # store in db_table_cache
                    opt.db_table_cache.add(name, t);
                }

                tl = t.getAlignSql(table_cache{name}, opt + schema_hash.tables{name}.rename + ("table_cache": table_cache));
                if (tl)
                    l += tl;
            }

            tl = alignCodeUnlocked("function", schema_hash, \getFunction(), \makeFunction(), opt, Type::String);
            if (tl) l += tl;

            tl = alignCodeUnlocked("procedure", schema_hash, \getProcedure(), \makeProcedure(), opt, Type::String);
            if (tl) l += tl;

            # call driver-specific align schema sql function (end)
            tl = getAlignSqlImpl(schema_hash, opt);
            if (tl) l += tl;

            return l;
        }

        #! accepts a hash argument describing a database schema and returns a list of SQL strings that can be used to drop any existing objects described in the schema; if none of the described objects exist, then an empty list is returned
        /** @par Example:
            @code{.py}
list l = db.getDropSchemaSql(schema_hash);
            @endcode

            @param schema_hash the @ref schema_desc_hash "schema description hash" that decribes the state of the schema
            @param opt a hash of options for the SQL creation strings; see @ref AbstractDatabase::DropSchemaOptions for common options; each driver can support additional driver-specific options

            @return a list of SQL strings that can be used to drop any existing objects in the database; if none of the described objects exist, then an empty list is returned

            @throw SCHEMA-DESCRIPTION-ERROR the @ref schema_desc_hash "schema description hash" has an error
            @throw OPTION-ERROR invalid or unsupported option passed
        */
        list getDropSchemaSql(hash schema_hash, *hash opt) {
            validateOptionsIntern("SCHEMA-DESCRIPTION-ERROR", getSchemaDescriptionOptions(), \schema_hash);
            validateOptionsIntern("OPTION-ERROR", getDropSchemaOptions(), \opt);

            list l = ();

            string drv = getDriverName();
            AbstractDatabase::checkDriverOptions(\schema_hash, drv);

            # call driver-specific align schema sql function (start)
            list tl = getDropSchemaSqlImpl(schema_hash, opt + ("start": True));
            if (tl) l += tl;

            # now process DB procedures
            AbstractDatabase::checkDriverOptions(\schema_hash.procedures, drv);

            # drop procedures
            tl = dropSqlUnlocked("procedure", schema_hash, \getProcedure(), \makeProcedure(), opt, Type::String);
            if (tl) l += tl;

            # now process DB functions
            AbstractDatabase::checkDriverOptions(\schema_hash.functions, drv);

            # drop functions
            tl = dropSqlUnlocked("function", schema_hash, \getFunction(), \makeFunction(), opt, Type::String);
            if (tl) l += tl;

            # now process DB tables
            AbstractDatabase::checkDriverOptions(\schema_hash.tables, drv);

            # setup tables with names from description but contents from DB
            Tables tables();
            map tables.getIfExists(ds, $1), schema_hash.tables.keyIterator();
            foreach AbstractTable t in (tables.dropIterator()) {
                l += AbstractDatabase::doCallback(opt, t.getDropSql(), AC_Drop, "table", t.getName());
            }

            # now process DB sequences
            AbstractDatabase::checkDriverOptions(\schema_hash.sequences, drv);

            # drop sequences
            tl = dropSqlUnlocked("sequence", schema_hash, \getSequence(), \makeSequenceFromDescription(), opt, Type::Hash);
            if (tl) l += tl;

            # call driver-specific align schema sql function (end)
            # Option "force" is here to drop all objects (where required)
            # without dependencies checking (Oracle NTY for now)
            tl = getDropSchemaSqlImpl(schema_hash, opt + ("force": True) );
            if (tl) l += tl;

            return l;
        }

        private list dropSqlUnlocked(string type, hash schema_hash, code get, code make, *hash opt, string make_arg_type) {
            list l = ();
            hash copt = getCreationOptions();
            string drv = getDriverName();

            # drop objects
            reference fmap = \schema_hash{type + "s"};
            AbstractDatabase::checkDriverOptions(\fmap, drv);
            foreach string name in (fmap.keyIterator()) {
                AbstractSqlUtilBase::validateOptionIntern("SCHEMA-DESCRIPTION-ERROR", make_arg_type, \fmap{name}, name, type + "s");

                # make template object
                object tmpl = make(name, fmap{name}, opt.(copt.keys()));

                # see if object exists
                *object f = get(name);
                if (f)
                    l += AbstractDatabase::doCallback(opt, tmpl.getDropSql(opt), AC_Drop, type, name);
                else
                    AbstractDatabase::doOkCallback(opt, AC_NotFound, type, name);
            }

            return l;
        }

        private list alignCodeUnlocked(string type, hash schema_hash, code get, code make, *hash opt, string make_arg_type) {
            list l = ();
            hash copt = getCreationOptions();
            string drv = getDriverName();

            hash code_cache;

            # rename objects
            reference fmap = \schema_hash{type + "_map"};
            AbstractDatabase::checkDriverOptions(\fmap, drv);
            HashIterator i(fmap);
            while (i.next()) {
                string key = i.getKey();
                any value = i.getValue();
                if (value.typeCode() != NT_STRING)
                    throw "SCHEMA-DESCRIPTION-ERROR", sprintf("%s %y value is not a string in the schema description hash; got type %y instead (%y)", type + "_map", key, value.type(), value);

                *AbstractFunction f = get(key);
                if (f) {
                    list tl = AbstractDatabase::doCallback(opt, f.getRenameSql(value), AC_Rename, type, key, NOTHING, value);
                    if (tl)
                        l += tl;

                    # add to function cache
                    f.setName(value);
                    code_cache{value} = f;
                }
            }

            # create any missing functions; replace updated functions
            reference fh = \schema_hash{type + "s"};
            AbstractDatabase::checkDriverOptions(\fh, drv);
            foreach string name in (fh.keyIterator()) {
                AbstractSqlUtilBase::validateOptionIntern("SCHEMA-DESCRIPTION-ERROR", make_arg_type, \fh{name}, name, type + "s");

                # make template function
                AbstractFunction templateFunc = make(name, fh{name}, opt.(copt.keys()));

                # see if function exists
                *AbstractFunction f;
                if (code_cache{name})
                    f = code_cache{name};
                else {
                    f = get(name);
                    if (!f) {
                        l += AbstractDatabase::doCallback(opt, templateFunc.getCreateSql(opt), AC_Create, type, name);
                        continue;
                    }
                }

                if (!templateFunc.equal(f)) {
                    l += AbstractDatabase::doCallback(opt, f.getDropSql(opt), AC_Drop, type, name);
                    l += AbstractDatabase::doCallback(opt, templateFunc.getCreateSql(opt), AC_Create, type, name);
                }
                else
                    AbstractDatabase::doOkCallback(opt, AC_Unchanged, type, name);
            }

            return l;
        }

        #! creates a database-specific AbstractSequence object corresponding to the arguments
        /** @par Example:
            @code{.py}
AbstractSequence seq = db.makeSequence("seq_queues");
            @endcode

            @param name the name of the sequence
            @param start the starting value of the sequence
            @param increment the increment value of the sequence
            @param end the endinf value of the sequence, omit for no ending value
            @param opts an optional hash of options for the creation of the sequence; see @ref AbstractDatabase::CreationOptions for common options; each driver can support additional driver-specific options

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw SEQUENCE-ERROR end not compatible with start and increment, increment is zero
        */
        AbstractSequence makeSequence(string name, number start = 1, number increment = 1, *softnumber end, *hash opts) {
            validateOptionsIntern("OPTION-ERROR", getCreationOptions(), \opts);

            return makeSequenceImpl(name, start, increment, end, opts);
        }

        AbstractSequence makeSequenceFromDescription(string name, *hash sh, *hash opts) {
            validateOptionsIntern("SEQUENCE-DESCRIPTION-ERROR", getSequenceDescriptionOptions(), \sh);

            return makeSequence(name, sh.start, sh.increment, sh.end);
        }

        #! creates a database-specific AbstractTable object corresponding to the arguments
        /** @par Example:
            @code{.py}
AbstractTable table = db.makeTable("table", th);
            @endcode

            @param name the name of the table
            @param desc a @ref table_desc_hash "table description hash" describing the table
            @param opts an optional hash of options for the table creation string; see @ref SqlUtil::AbstractTable::TableOptions for common options; each driver can support additional driver-specific options

            @throw OPTION-ERROR invalid or unsupported option passed

            @see SqlUtil::AbstractTable::constructor()
        */
        AbstractTable makeTable(string name, hash desc, *hash opts) {
            AbstractTable t = AbstractTable::getTable(ds, name, opts);
            t.setupTable(desc, opts);
            return t;
        }

        #! creates a database-specific AbstractFunction object corresponding to the arguments
        /** @par Example:
            @code{.py}
AbstractFunction f = db.makeFunction("update_queue", src);
            @endcode

            @param name the name of the function
            @param src the database-specific source code of the function (see the database-specific documentation for information about the format of this parameter; generally it's simply the normal source code for the function required by the database)
            @param opts an optional hash of options for the creation of the function; see @ref AbstractDatabase::CreationOptions for common options; each driver can support additional driver-specific options

            @throw OPTION-ERROR invalid or unsupported option passed
        */
        AbstractFunction makeFunction(string name, string src, *hash opts) {
            validateOptionsIntern("OPTION-ERROR", getCreationOptions(), \opts);

            return makeFunctionImpl(name, src, opts);
        }

        #! creates a database-specific AbstractFunction object for a stored procedure corresponding to the arguments
        /** @par Example:
            @code{.py}
AbstractFunction f = db.makeProcedure("get_queue_info", src);
            @endcode

            @param name the name of the stored procedure
            @param src the database-specific source code of the stored procedure (see the database-specific documentation for information about the format of this parameter; generally it's simply the normal source code for the stored procedure required by the database)
            @param opt an optional hash of options for the creation of the stored procedure; see @ref AbstractDatabase::CreationOptions for common options; each driver can support additional driver-specific options

            @throw OPTION-ERROR invalid or unsupported option passed
        */
        AbstractFunction makeProcedure(string name, string src, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getCreationOptions(), \opt);

            return makeProcedureImpl(name, src, opt);
        }

        #! drops the given function if it exists; returns @ref Qore::True "True" if the function was dropped, @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
db.dropFunctionIfExists(name);
            @endcode

            @param name the name of the function
            @param opt optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @return @ref Qore::True "True" if the function was dropped, @ref Qore::False "False" if not

            @throw OPTION-ERROR invalid or unknown callback option
        */
        bool dropFunctionIfExists(string name, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getDropSchemaOptions(), \opt);
            *AbstractFunction f = getFunction(name);
            if (!f)
                return False;
            return AbstractDatabase::doDrop(f.getDropSql(opt), "function", name, opt);
        }

        #! drops the given procedure if it exists; returns @ref Qore::True "True" if the procedure was dropped, @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
db.dropProcedureIfExists(name);
            @endcode

            @param name the name of the procedure
            @param opt optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @return @ref Qore::True "True" if the procedure was dropped, @ref Qore::False "False" if not

            @throw OPTION-ERROR invalid or unknown callback option
        */
        bool dropProcedureIfExists(string name, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getDropSchemaOptions(), \opt);
            *AbstractFunction f = getProcedure(name);
            if (!f)
                return False;
            return AbstractDatabase::doDrop(f.getDropSql(opt), "procedure", name, opt);
        }

        #! drops the given sequence if it exists; returns @ref Qore::True "True" if the sequence was dropped, @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
db.dropSequenceIfExists(name);
            @endcode

            @param name the name of the sequence
            @param opt optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @return @ref Qore::True "True" if the sequence was dropped, @ref Qore::False "False" if not

            @throw OPTION-ERROR invalid or unknown callback option
        */
        bool dropSequenceIfExists(string name, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getDropSchemaOptions(), \opt);
            *AbstractSequence seq = getSequence(name);
            if (!seq)
                return False;
            return AbstractDatabase::doDrop(seq.getDropSql(opt), "sequence", name, opt);
        }

        #! drops the given view if it exists; returns @ref Qore::True "True" if the view was dropped, @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
db.dropViewIfExists(name);
            @endcode

            @param name the name of the view
            @param opt optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @return @ref Qore::True "True" if the view was dropped, @ref Qore::False "False" if not

            @throw OPTION-ERROR invalid or unknown callback option
        */
        bool dropViewIfExists(string name, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getDropSchemaOptions(), \opt);
            *AbstractView obj = getView(name);
            if (!obj)
                return False;
            return AbstractDatabase::doDrop(obj.getDropSql(opt), "view", name, opt);
        }

        #! drops the given table if it exists; returns @ref Qore::True "True" if the table was dropped, @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
db.dropTableIfExists(name);
            @endcode

            @param name the name of the table
            @param opt optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @return @ref Qore::True "True" if the table was dropped, @ref Qore::False "False" if not

            @throw OPTION-ERROR invalid or unknown callback option
        */
        bool dropTableIfExists(string name, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getDropSchemaOptions(), \opt);
            *AbstractTable obj = getTable(name);
            if (!obj)
                return False;
            obj.drop(opt);
            return True;
        }

        #! returns the SQL require to drop the given function if it exists or @ref nothing if the named function does not exist
        /** @par Example:
            @code{.py}
string sql = db.getDropFunctionSqlIfExists(name);
            @endcode

            @param name the name of the function
            @param opt optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @return the SQL require to drop the given function if it exists or @ref nothing if the named function does not exist

            @throw OPTION-ERROR invalid or unknown callback option
        */
        *string getDropFunctionSqlIfExists(string name, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getDropSchemaOptions(), \opt);
            *AbstractFunction f = getFunction(name);
            if (!f)
                return;
            return AbstractDatabase::doDropSql(f.getDropSql(opt), "function", name, opt);
        }

        #! returns the SQL require to drop the given procedure if it exists or @ref nothing if the named procedure does not exist
        /** @par Example:
            @code{.py}
string sql = db.getDropProcedureSqlIfExists(name);
            @endcode

            @param name the name of the procedure
            @param opt optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @return the SQL require to drop the given procedure if it exists or @ref nothing if the named procedure does not exist

            @throw OPTION-ERROR invalid or unknown callback option
        */
        *string getDropProcedureSqlIfExists(string name, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getDropSchemaOptions(), \opt);
            *AbstractFunction f = getProcedure(name);
            if (!f)
                return;
            return AbstractDatabase::doDropSql(f.getDropSql(opt), "procedure", name, opt);
        }

        #! returns the SQL require to drop the given sequence if it exists or @ref nothing if the named sequence does not exist
        /** @par Example:
            @code{.py}
string sql = db.getDropSequenceSqlIfExists(name);
            @endcode

            @param name the name of the sequence
            @param opt optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @return the SQL require to drop the given sequence if it exists or @ref nothing if the named sequence does not exist

            @throw OPTION-ERROR invalid or unknown callback option
        */
        *string getDropSequenceSqlIfExists(string name, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getDropSchemaOptions(), \opt);
            *AbstractSequence obj = getSequence(name);
            if (!obj)
                return;
            return AbstractDatabase::doDropSql(obj.getDropSql(opt), "sequence", name, opt);
        }

        #! returns the SQL require to drop the given table if it exists or @ref nothing if the named table does not exist
        /** @par Example:
            @code{.py}
string sql = db.getDropTableSqlIfExists(name);
            @endcode

            @param name the name of the table
            @param opt optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @return the SQL require to drop the given table if it exists or @ref nothing if the named table does not exist

            @throw OPTION-ERROR invalid or unknown callback option
        */
        *list getDropTableSqlIfExists(string name, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getDropSchemaOptions(), \opt);
            *AbstractTable t = getTable(name);
            if (!t)
                return;
            return t.getDropSql(opt);
        }

        doDropSql(*softlist l, string type, string name, *hash opt) {
            if (l)
                AbstractDatabase::doCallback(opt, l, AC_Drop, type, name);
        }

        bool doDrop(*softlist l, string type, string name, *hash opt) {
            if (l) {
                AbstractDatabase::doCallback(opt, l, AC_Drop, type, name);
                on_success ds.commit();
                on_error ds.rollback();
                map ds.execRaw($1), l;
                return True;
            }
            return False;
        }

        #! returns a list of SQL strings that can be used to update a function in the database to the function definition passed as an argument
        /** @par Example:
            @code{.py}
list l = db.getAlignFunctionSql(f);
            @endcode

            @param f the template or target version of the function to compare to the database version
            @param opt an optional hash of options for the generation of the SQL strings; see @ref AbstractDatabase::CreationOptions for common options; each driver can support additional driver-specific options

            @return a list of SQL strings that can be used to update a function in the database to the function definition passed as an argument

            @throw OPTION-ERROR invalid or unsupported option passed
         */
        list getAlignFunctionSql(AbstractFunction f, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignSchemaOptions(), \opt);

            *AbstractFunction of = getFunctionImpl(f.name);

            list l = ();

            if (!of) {
                l += AbstractDatabase::doCallback(opt, f.getCreateSql(opt), AC_Create, "function", f.name);
            }
            else if (!of.equal(f)) {
                l += AbstractDatabase::doCallback(opt, of.getDropSql(opt), AC_Drop, "function", f.name);
                l += AbstractDatabase::doCallback(opt, of.getCreateSql(opt), AC_Create, "function", f.name);
            }
            else
                AbstractDatabase::doOkCallback(opt, AC_Unchanged, "function", f.name);

            return l;
        }

        #! returns a list of SQL strings that can be used to update a stored procedure in the database to the stored procedure definition passed as an argument
        /** @par Example:
            @code{.py}
list l = db.getAlignProcedureSql(f);
            @endcode

            @param f the template or target version of the stored procedure to compare to the database version
            @param opt an optional hash of options for the generation of the SQL strings; see @ref AbstractDatabase::CreationOptions for common options; each driver can support additional driver-specific options

            @return a list of SQL strings that can be used to update a stored procedure in the database to the stored procedure definition passed as an argument

            @throw OPTION-ERROR invalid or unsupported option passed
         */
        list getAlignProcedureSql(AbstractFunction f, *hash opt) {
            *AbstractFunction of = getProcedureImpl(f.name);

            list l = ();

            if (!of)
                l += f.getCreateSql(opt);
            else if (!of.equal(f)) {
                l += of.getDropSql(opt);
                l += f.getCreateSql(opt);
            }

            return l;
        }

        #! returns an AbstractTable argument for the given table name or @ref nothing if the table cannot be found
        /** @par Example:
            @code{.py}
*AbstractTable table = db.getTable("queues");
            @endcode

            @param name the uniquely-identifying name of the table to retrieve

            @return an AbstractTable argument for the given table name or @ref nothing if the table cannot be found
         */
        *AbstractTable getTable(string name) {
            AbstractTable t = AbstractTable::getTable(ds, name);
            return t.checkExistence() ? t : NOTHING;
        }

        #! returns an AbstractSequence argument for the given sequence name or @ref nothing if the sequence cannot be found
        /** @par Example:
            @code{.py}
*AbstractSequence seq = db.getSequence("seq_queues");
            @endcode

            @param name the uniquely-identifying name of the sequence to retrieve

            @return an AbstractSequence argument for the given sequence name or @ref nothing if the sequence cannot be found
         */
        *AbstractSequence getSequence(string name) {
            return getSequenceImpl(name);
        }

        #! returns an AbstractFunction argument for the given function name or @ref nothing if the function cannot be found
        /** @par Example:
            @code{.py}
*AbstractFunction f = db.getFunction("update_queue");
            @endcode

            @param name the uniquely-identifying name of the function to retrieve

            @return an AbstractFunction argument for the given function name or @ref nothing if the function cannot be found

            @note for databases that support overloaded functions, the arguments must be passed in parentheses after the name to match overloaded functions properly
         */
        *AbstractFunction getFunction(string name) {
            return getFunctionImpl(name);
        }

        #! returns an AbstractFunction argument for the given stored procedure name or @ref nothing if the stored procedure cannot be found
        /** @par Example:
            @code{.py}
*AbstractFunction f = db.getProcedure("update_queue");
            @endcode

            @param name the uniquely-identifying name of the stored procedure to retrieve

            @return an AbstractFunction argument for the given stored procedure name or @ref nothing if the stored procedure cannot be found

            @note for databases that support overloaded stored procedures, the arguments must be passed in parentheses after the name to match overloaded stored procedures properly
         */
        *AbstractFunction getProcedure(string name) {
            return getProcedureImpl(name);
        }

        #! returns an AbstractView argument for the given view name or @ref nothing if the view cannot be found
        /** @par Example:
            @code{.py}
*AbstractFunction f = db.getView("my_view");
            @endcode

            @param name the uniquely-identifying name of the view to retrieve

            @return an AbstractView argument for the given view name or @ref nothing if the sview cannot be found
         */
        *AbstractView getView(string name) {
            return getViewImpl(name);
        }

        #! returns the next value in the given sequence
        /** @par Example:
            @code{.py}
number n = db.getNextSequenceValue("seq_customers");
            @endcode

            @param name the name of the sequence

            @return the next value in the given sequence
        */
        int getNextSequenceValue(string name) {
            return getNextSequenceValueImpl(name);
        }

        #! returns the last value issued for the given sequence in the current session
        /** @par Example:
            @code{.py}
number n = db.getCurrentSequenceValue("seq_customers");
            @endcode

            @param name the name of the sequence

            @return the last value issued for the given sequence in the current session
        */
        int getCurrentSequenceValue(string name) {
            return getCurrentSequenceValueImpl(name);
        }

        #! returns an SQL string corresponding to the list of commands in the argument
        /** @par Example:
            @code{.py}
string sql = t.getSqlFromList(list);
            @endcode

            @param l a list of SQL commands

            @return an SQL string corresponding to the list of commands in the argument
        */
        string getSqlFromList(list l) {
            return getCreateSqlImpl(l);
        }

        #! returns @ref Qore::True "True" if the database supports sequences
        bool supportsSequences() {
            return supportsSequencesImpl();
        }

        #! returns @ref Qore::True "True" if the database supports named types
        bool supportsTypes() {
            return supportsTypesImpl();
        }

        #! returns @ref Qore::True "True" if the database supports packages
        bool supportsPackages() {
            return supportsPackagesImpl();
        }

        #! returns a list of string table names in the database
        list listTables() {
            return listTablesImpl();
        }

        #! returns an iterator listing the string table names in the database
        Qore::ListIterator tableIterator() {
            return listTablesImpl().iterator();
        }

        #! returns a list of string function names in the database
        list listFunctions() {
            return listFunctionsImpl();
        }

        #! returns an iterator listing the string function names in the database
        Qore::ListIterator functionIterator() {
            return listFunctionsImpl().iterator();
        }

        #! returns a list of string procedure names in the database
        list listProcedures() {
            return listProceduresImpl();
        }

        #! returns an iterator listing the string procedure names in the database
        Qore::ListIterator procedureIterator() {
            return listProceduresImpl().iterator();
        }

        #! returns a list of string sequence names in the database
        list listSequences() {
            return listSequencesImpl();
        }

        #! returns an iterator listing the string sequence names in the database
        Qore::ListIterator sequenceIterator() {
            return listSequencesImpl().iterator();
        }

        #! returns a list of string view names in the database
        list listViews() {
            return listViewsImpl();
        }

        #! returns an iterator listing the string view names in the database
        Qore::ListIterator viewIterator() {
            return listViewsImpl().iterator();
        }

        #! Rebuild an index in the DB
        /** See @ref dba_indexes
            @param name an index name
            @param options an optional database-specific option hash

            @return bool True if the index was rebuild, False it it was left untouched

            @throw REBUILD-INDEX-ERROR in case of MySQL or in case of wrong options given

            @since %SqlUtil 1.2
         */
        bool rebuildIndex(string name, *hash options) {
            validateOptionsIntern("REBUILD-INDEX-ERROR", getRebuildIndexOptions(), \options);
            return rebuildIndexImpl(name, options);
        }

        #! Rebuild an index in the DB
        /** See @ref dba_indexes
            @param index an AbstractIndex object
            @param options an optional database-specific option hash

            @throw REBUILD-INDEX-ERROR in case of MySQL or in case of wrong options given

            @since %SqlUtil 1.2
         */
        bool rebuildIndex(AbstractIndex index, *hash options) {
            validateOptionsIntern("REBUILD-INDEX-ERROR", getRebuildIndexOptions(), \options);
            return rebuildIndexImpl(index.name, options);
        }

        #! Compute database statistics
        /** See @ref dba_statistics
            @param options an optional database-specific option hash

            @throw COMPUTE-STATISTICS-ERROR in case of wrong options given

            @since %SqlUtil 1.2
         */
        computeStatistics(*hash options) {
            validateOptionsIntern("COMPUTE-STATISTICS-ERROR", getComputeStatisticsOptions(), \options);
            return computeStatisticsImpl(options);
        }

        #! Reclaim taken but unused space in the DB
        /** See @ref dba_space_management
            @param options an optional database-specific option hash

            @throw RECLAIM-SPACE-ERROR in case of wrong options given

            @since %SqlUtil 1.2
         */
        reclaimSpace(*hash options) {
            validateOptionsIntern("RECLAIM-SPACE-ERROR", getReclaimSpaceOptions(), \options);
            return reclaimSpaceImpl(options);
        }

        private validateOptionsIntern(string err, hash ropt, reference opt) {
            AbstractSqlUtilBase::validateOptionsIntern(err, ropt, \opt, ds.getDBName());
        }

        private validateOptionsIntern(string err, hash ropt, reference opt, string tag) {
            AbstractSqlUtilBase::validateOptionsIntern(err, ropt, \opt, tag);
        }

        static AbstractDatabase getDatabase(AbstractDatasource nds, *hash opts) {
            string drv = nds.getDriverName();

            if (opts)
                AbstractDatabase::checkDriverOptions(\opts, drv);

            # generate module and module namespace name
            string mn = drv[0].upr() + drv.substr(1) + "SqlUtil";

            # try to load module
            try {
                load_module(mn);
            }
            catch (hash ex) {
                throw "DATABASE-DRIVER-ERROR", sprintf("%s: no support for driver %y yet: %s: %s", get_ex_pos(ex), drv, ex.err, ex.desc);
            }

            # try to get AbstractDatabase object
            try {
                return call_function(mn + "::" + "get_database", nds, opts);
            }
            catch (hash ex) {
                throw "DATABASE-DRIVER-ERROR", sprintf("%s: error retrieving AbstractDatabase object for driver %y: %s: %s", get_ex_pos(ex), drv, ex.err, ex.desc);
            }
        }

        static AbstractDatabase getDatabase(string dsstr, *hash opts) {
            Datasource nds(dsstr);
            return AbstractDatabase::getDatabase(nds, opts);
        }

        static AbstractDatabase getDatabase(hash dsh, *hash opts) {
            Datasource nds(dsh);
            return AbstractDatabase::getDatabase(nds, opts);
        }

        static checkDriverOptions(reference h, string drv) {
            if (h.driver) {
                if (h.driver{drv})
                    h += h.driver{drv};
                delete h.driver;
            }
        }

        #! override in subclasses to return driver-specific options
        private hash getDatabaseOptions() {
            return DatabaseOptions;
        }

        #! override in subclasses to return driver-specific options
        private hash getCallbackOptions() {
            return CallbackOptions;
        }

        #! override in subclasses to return driver-specific options
        private hash getCreationOptions() {
            return CreationOptions;
        }

        #! override in subclasses to return driver-specific options
        private hash getCacheOptions() {
            return CacheOptions;
        }

        #! override in subclasses to return driver-specific options
        private hash getAlignSchemaOptions() {
            return AlignSchemaOptions;
        }

        #! override in subclasses to return driver-specific options
        private hash getDropSchemaOptions() {
            return DropSchemaOptions;
        }

        #! override in subclasses to return driver-specific options
        private hash getSchemaDescriptionOptions() {
            return SchemaDescriptionOptions;
        }

        #! override in subclasses to return driver-specific options
        private hash getSequenceDescriptionOptions() {
            return SequenceDescriptionOptions;
        }

        #! override in subclasses to return driver-specific options
        private hash getRebuildIndexOptions() {
            return hash();
        }

        #! override in subclasses to return driver-specific options
        private hash getComputeStatisticsOptions() {
            return ComputeStatisticsOptions;
        }

        #! override in subclasses to return driver-specific options
        private hash getReclaimSpaceOptions() {
            return ReclaimSpaceOptions;
        }

        #! tries to execute a command so that if an error occurs the current transaction status is not lost
        private any tryExecArgsImpl(string sql, *softlist args) {
            return ds.vexec(sql, args);
        }

        #! tries to execute a command so that if an error occurs the current transaction status is not lost
        private any tryExecRawImpl(string sql) {
            return ds.execRaw(sql);
        }

        private abstract string getCreateSqlImpl(list l);
        private abstract list getAlignSqlImpl(hash schema_hash, *hash opt);
        private abstract list getDropSchemaSqlImpl(hash schema_hash, *hash opt);

        private abstract *AbstractSequence getSequenceImpl(string name);
        private abstract *AbstractFunction getFunctionImpl(string name);
        private abstract *AbstractFunction getProcedureImpl(string name);
        private abstract *AbstractView getViewImpl(string name);

        private abstract AbstractSequence makeSequenceImpl(string name, number start = 1, number increment = 1, *softnumber end, *hash opts);
        private abstract AbstractFunction makeFunctionImpl(string name, string src, *hash opts);
        private abstract AbstractFunction makeProcedureImpl(string name, string src, *hash opts);

        private abstract list featuresImpl();
        private abstract list listTablesImpl();
        private abstract list listFunctionsImpl();
        private abstract list listProceduresImpl();
        private abstract list listSequencesImpl();
        private abstract list listViewsImpl();

        #! returns the next value in the given sequence
        private abstract int getNextSequenceValueImpl(string name);
        #! returns the last value issued for the given sequence in the current session
        private abstract int getCurrentSequenceValueImpl(string name);

        #! returns @ref Qore::True "True" if the database supports sequences
        private abstract bool supportsSequencesImpl();
        private abstract bool supportsPackagesImpl();
        private abstract bool supportsTypesImpl();

        private abstract bool rebuildIndexImpl(string name, *hash options);
        private abstract computeStatisticsImpl(*hash options);
        private abstract reclaimSpaceImpl(*hash options);
    }

    #! represents a database table; this class embeds an AbstractTable object that is created automatically in the constructor based on the database driver for the AbstractDatasource object providing the database connection
    /** Driver-specific modules that provide the AbstractTable implementation embedded in this class are loaded on demand based on the driver's name.
        The driver-specific module's name is generated based on the db-driver's name with the first letter capitalized then with \c "SqlUtil" appended.

        @note SqlUtil::Table does not provide any functionality except it is able to create
              real low level AbstractTable inherited object. All functionality is provided
              by embedded AbstractTable object.

        For example:
        - \c "oracle": <a href="../../OracleSqlUtil/html/index.html">OracleSqlUtil</a>
        - \c "pgsql": <a href="../../PgsqlSqlUtil/html/index.html">PgsqlSqlUtil</a>
        - \c "mysql": <a href="../../MysqlSqlUtil/html/index.html">MysqlSqlUtil</a>

        etc.

        @note Objects of Table class can hold AbstractTable pointing to a view in Oracle.
    */
    public class Table {
        private {
            #! the embedded AbstractTable object that actually provides the functionality for this class
            AbstractTable t;
        }

        #! creates the Table object
        /** @par Example:
            @code{.py}
Table table(ds, "table");
            @endcode

            @param ds the AbstractDatasource object used to provide the DB connection
            @param name the name of the table
            @param opts a hash of options for the table creation string; see @ref SqlUtil::AbstractTable::TableOptions for common options; each driver can support additional driver-specific options

            @throw TABLE-DRIVER-ERROR no database-specific module can be loaded
            @throw OPTION-ERROR invalid or unsupported option passed
        */
        constructor(AbstractDatasource ds, string name, *hash opts) {
            t = AbstractTable::getTable(ds, name, opts);
        }

        #! creates the Table object
        /** @par Example:
            @code{.py}
Table table("pgsql:user/pass@db%host", "table");
            @endcode

            @param ds a datasource description string in the format that can be parsed by parse_datasource()
            @param name the name of the table
            @param opts a hash of options for the table creation string; see @ref SqlUtil::AbstractTable::TableOptions for common options; each driver can support additional driver-specific options

            @throw TABLE-DRIVER-ERROR no database-specific module can be loaded
            @throw OPTION-ERROR invalid or unsupported option passed
        */
        constructor(string ds, string name, *hash opts) {
            t = AbstractTable::getTable(ds, name, opts);
        }

        #! creates the Table object
        /** @par Example:
            @code{.py}
Table table("pgsql:user/pass@db%host", "table");
            @endcode

            @param ds a hash giving parameters for the new datasource with the following possible keys (the \c "type" key is mandatory, also usable with the output of the parse_datasource() function):
            - \c type: (@ref string_or_nothing_type "*string") The name of the database driver to use; this key is mandatory; if not present, an exception will be raised. See @ref sql_constants for builtin constants for DBI drivers shipped with Qore, or see the DBI driver documentation to use an add-on driver (this string should be the name of the driver to be loaded)
            - \c user: (@ref string_or_nothing_type "*string") The user name for the new connection
            - \c pass: (@ref string_or_nothing_type "*string") The password for the new connection
            - \c db: (@ref string_or_nothing_type "*string") The database name for the new connection
            - \c charset: (@ref string_or_nothing_type "*string") The database-specific name of the character encoding to use for the new connection. Also see Qore::SQL::Datasource::setDBCharset() for a method that allows this parameter to be set after the constructor. If no value is passed for this parameter, then the database character encoding corresponding to the default character encoding for the %Qore process will be used instead.
            - \c host: (@ref string_or_nothing_type "*string") The host name for the new connection
            - \c port: (@ref softint_type "softint") The port number for the new connection
            - \c options: (@ref hash_or_nothing_type "*hash") Any options for the new connection
            @param name the name of the table
            @param opts a hash of options for the table creation string; see @ref SqlUtil::AbstractTable::TableOptions for common options; each driver can support additional driver-specific options

            @throw TABLE-DRIVER-ERROR no database-specific module can be loaded
            @throw OPTION-ERROR invalid or unsupported option passed
        */
        constructor(hash ds, string name, *hash opts) {
            t = AbstractTable::getTable(ds, name, opts);
        }

        #! creates the object from a table description hash
        /** @param ds the AbstractDatasource for the connection to the database
            @param desc a @ref table_desc_hash "table description hash" describing the table
            @param name the name of the table
            @param opts an optional hash of options for the table creation string; see @ref SqlUtil::AbstractTable::TableOptions for common options; each driver can support additional driver-specific options

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw DESCRIPTION-ERROR invalid or unsupported description hash value passed
        */
        constructor(AbstractDatasource ds, hash desc, string name, *hash opts) {
            t = AbstractTable::getTable(ds, name);
            t.setupTable(desc, opts);
        }

        #! returns the AbstractTable object contained by this object
        AbstractTable getTable() {
            return t;
        }

        #! executes a method on the contained AbstractTable object
        /** See @ref SqlUtil::AbstractTable API for reference
         */
        any methodGate(string meth) {
            return call_object_method_args(t, meth, argv);
        }

    } # class Table

    #! the base abstract class for the table implementation
    /** Use @ref SqlUtil::Table if you need to construct table object directly
     */
    public class AbstractTable inherits AbstractSqlUtilBase {
        public {
            #! table options
            /** The following keys can be set for this option:
                - \c native_case: (@ref bool_type) if @ref Qore::True "True" then objects will be returned in the case the database server returns them in even if the database server uses case-insensitive names; normally in this case %SqlUtil will convert the names to lower-case
                - \c table_cache: (@ref SqlUtil::Tables "Tables") an optional table cache for maintaining cached tables and foreign key relationships between tables
            */
            const TableOptions = (
                "native_case": Type::Boolean,
                "table_cache": "Tables",
                );

            #! default index options
            /**
                - \c index_tablespace: (@ref string_type) a string giving the index tablespace to use for indexes
                - \c replace: (@ref bool_type) if @ref Qore::True "True" and supported by the underlying db driver "create or replace" text is used when creating objects
            */
            const IndexOptions = (
                "index_tablespace": Type::String,
                "replace": Type::Boolean,
                );

            #! default constraint options
            /** currently this option is identical to @ref SqlUtil::AbstractTable::IndexOptions
            */
            const ConstraintOptions = IndexOptions;

            #! default cache options
            const CacheOptions = (
                "table_cache": "Tables",
                );

            #! default foreign constraint options
            /** The following keys can be set for this option:
                - \c table_cache: (@ref SqlUtil::Tables "Tables") an optional table cache for maintaining cached tables and foreign key relationships between tables
            */
            const ForeignConstraintOptions = ConstraintOptions + (
                "table_cache": "Tables",
                );

            #! default trigger options
            /** currently this option is identical to @ref AbstractDatabase::CreationOptions
            */
            const TriggerOptions = AbstractDatabase::CreationOptions;

            #! default select options
            /** the possible keys for select options are as follows (all keys are optional):
                - @ref select_option_columns "columns": describes the output columns and any output column operations
                - @ref select_option_where "where": describes how any \c "where" clause will be built
                - @ref select_option_orderby "orderby": describes the ordering of the results
                - @ref select_option_desc "desc": specifies descending order for results
                - @ref select_option_limit "limit": specifies the maximum number of results to be returned
                - @ref select_option_offset "offset": specifies the starting offset of the first record to be returned (starting with 0)
                - @ref select_option_join "join": specifies any SQL join operations to return results from multiple tables
                - @ref select_option_groupby "groupby": specifies grouping for aggregate column functions
                - @ref select_option_having "having": specifies filtering for results with aggregate column functions
                - @ref select_option_superquery "superquery": specifies that the rest of the argument belong to a subquery and the hash arguments under this key will select from the subquery
                - @ref select_option_forupdate "forupdate": allows for the rows selected to be locked for updating
             */
            const SelectOptions = (
                "comment": Type::String,
                "hint": Type::String,
                "columns": Type::NothingType,
                "where": "hash/list",
                "orderby": "softstringinthashlist",
                "desc": Type::Boolean,
                "limit": Type::Int,
                "offset": Type::Int,
                "join": Type::Hash,
                "groupby": "softstringinthashlist",
                "having": Type::Hash,
                "superquery": Type::Hash,
                "forupdate": Type::Boolean,
                );

            #! alignment omission options
            const TableOmissionOptions = (
                "indexes": True,
                "foreign_constraints": True,
                "triggers": True,
                );

            #! table creation options
            /** currently this option is a combination of @ref SqlUtil::AbstractTable::IndexOptions and @ref AbstractDatabase::CreationOptions plus the following:
                - \c omit: a list pf attributes to omit; possible values are: \c indexes, \c foreign_constraints, \c triggers (see @ref TableOmissionOptions)
            */
            const TableCreationOptions = IndexOptions + AbstractDatabase::CreationOptions + (
                "omit": "softstringlist",
                );

            #! table alignment options
            /** currently this option is a combination of @ref SqlUtil::AbstractTable::TableCreationOptions and the following options:
                - \c column_map: (@ref hash_type) a hash for automatically renaming columns; if the source name (key) exists and the target name (value) does not exist, then the source column is automatically renamed
                - \c index_map: (@ref hash_type) a hash for automatically renaming indexes; if the source name (key) exists and the target name (value) does not exist, then the source index is automatically renamed
                - \c constraint_map: (@ref hash_type) a hash for automatically renaming constraints; if the source name (key) exists and the target name (value) does not exist, then the source constraint is automatically renamed
                - \c trigger_map: (@ref hash_type) a hash for automatically renaming triggers; if the source name (key) exists and the target name (value) does not exist, then the source trigger is automatically renamed
                - \c db_table_cache: (@ref SqlUtil::Tables "Tables") an optional table cache for maintaining tables in the database and foreign key relationships between tables
                - \c force: (@ref bool_type) if @ref Qore::True "True" and supported by the driver and object, any objects dropped will be dropped with \c FORCE or \c CASCADE options
            */
            const AlignTableOptions = TableCreationOptions + (
                "column_map": Type::Hash,
                "index_map": Type::Hash,
                "constraint_map": Type::Hash,
                "trigger_map": Type::Hash,
                "db_table_cache": "Tables",
                "force": Type::Boolean,
                );

            #! Table description options
            /** this option is made up of the following keys:
                - \c columns: (@ref column_desc_hash "column description hash") a hash describing the column
                - \c primary_key : (@ref pk_desc_hash "primary key description hash") a hash describing the primary key for the table
                - \c indexes: (@ref index_desc_hash "index description hashes") a hash describing the indexes on the table
                - \c triggers: a hash of trigger information keyed by trigger name; the values are the trigger source code; since triggers are driver-dependent, a driver-independent table description would include trigger hashes under the \c drivers key and the driver key name under that
                - \c foreign_constraints: (@ref fk_desc_hash "foreign constraint hashes") a hash describing the foreign constraints on the table
                - \c unique_constraints: (@ref uk_desc_hash "unique constraint hashes") a hash describing the unique constraints on the table
                - \c table_cache: (@ref SqlUtil::Tables "Tables") an optional table cache for maintaining cached tables and foreign key relationships between tables

                @see @ref table_desc_hash
            */
            const TableDescriptionHashOptions = (
                "columns": Type::Hash,
                "primary_key": Type::Hash,
                "indexes": Type::Hash,
                "triggers": Type::Hash,
                "foreign_constraints": Type::Hash,
                "unique_constraints": Type::Hash,
                #"check_constraints": Type::Hash,
                "table_cache": "Tables",
                );

            #! Column description options
            /** this option is made up of the following keys:
                - \c qore_type: (@ref string_type) a qore type string that will be converted to a native DB type with some default conversion
                - \c native_type: (@ref string_type) the native database column type; if both \c native_type and \c qore_type are given then \c native_type is used
                - \c size: (@ref int_type) for data types requiring a size component, the size; for numeric columns this represents the precision for example
                - \c scale: (@ref int_type) for numeric data types, this value gives the scale
                - \c default_value: the default value for the column
                - \c default_value_native: a boolean flag to say if a \c default_value should be validated against table column type (False) or used as it is (True) to allow to use DBMS native functions or features. Defaults to False. It is strongly recommended to use \c default_value_native for \c default_value in \c driver specific sub-hash to avoid non-portable schema hashes
                - \c comment: (@ref string_type) an optional comment for the column
                - \c notnull: if the column should have a "not null" constraint on it; if missing the default value is @ref Qore::False "False"
                - \c driver: this key can optionally contain a hash keyed by driver name which contains a hash of values that will be added to the column description hash before processing; this way a column description hash can contain all the information required for the column including driver-specific options; any driver-specific options will overwrite values in the top level of the hash if there are duplicate hash keys, see below for an example
                @see @ref column_desc_hash
            */
            const ColumnDescOptions = (
                "qore_type": Type::String,
                "native_type": Type::String,
                "size": Type::Int,
                "scale": Type::Int,
                "default_value": Type::NothingType,
                "default_value_native" : Type::Boolean,
                "comment": Type::String,
                );

            #! additional column description keys valid when describing columns in a @ref table_desc_hash "table description hash"
            /** the following keys are valid in a @ref table_desc_hash "table description hash" in addition to any @ref SqlUtil::AbstractTable::ColumnDescOptions "AbstractTable::ColumnDescOptions":
                - \c notnull: (@ref bool_type) if @ref Qore::True "True" then the column will have a not null constraint
            */
            const AdditionalColumnDescOptions = (
                "notnull": Type::Boolean,
                );

            #! Column options; this is currently empty and can be extended in database-specific modules
            const ColumnOptions = {};

            #! generic SQL data operation callbacks
            /** The following keys can be set for this option:
                - \c sqlarg_callback: see @ref sqlarg_callback
                - \c tablecode: a closure taking a string table name and returning an AbstractTable object for late resolution of table names in joins
            */
            const SqlDataCallbackOptions = (
                "sqlarg_callback": "code",
                "tablecode": "code",
                );

            #! generic SQL insert options
            /** In addition to any @ref SqlDataCallbackOptions, the following keys can be set for this option:
                - \c returning: a list having elements of one of the two following types:
                  - @ref type_string "string": column names to return the value inserted
                  - @ref type_hash "hash": a hash having the following keys:
                    - \c "key": (required) the column name to return
                    - \c "type": (optional) the data type for the output placeholder buffer (ex: @ref Qore::Type::Number "Type::Number")

                @note using \c "returning" with a database that does not support this clause will cause an exception to be thrown; see @ref SqlUtil::AbstractTable::hasReturning()
            */
            const InsertOptions = SqlDataCallbackOptions + (
                "returning": "stringhashlist",
            );

            #! default upsert option keys
            /** The following keys can be set for this option:
                - \c commit_block: the number of changes made before an automatic commit is made for upsert methods that perform commits
                - \c delete_others: if this option is @ref Qore::True "True", then a hash of primary key values in the input data is built as the input data is iterated.  After iterating, if the row count of the table and the input data matches, then nothing more is done, otherwise, every row of the table is iterated and compared to the primary key hash; if a row does not match a primary key value, then it is deleted.  This operation allows tables to be completely synchronized by removing rows in the target table not present in the source table.  This operation is expensive for large data sets.
                - \c info_callback: see @ref upsert_info_callback
                - \c omit_update: allows for an asymmetrical upsert where a set of column values is inserted, but a smaller set is updated in case the unique key values are present in the target table; the value of this key should be set to the columns to omit in the update clause
            */
            const UpsertOptions = (
                "info_callback": "code",
                "commit_block": Type::Int,
                "delete_others": Type::Boolean,
                "omit_update": "softstringlist",
                );

            #! default insert option keys
            /** In addition to any @ref SqlDataCallbackOptions, the following keys can be set for this option:
                - \c info_callback: see @ref insert_info_callback
                - \c commit_block: the number of changes made before an automatic commit is made for insert methods that perform commits
            */
            const InsertFromIteratorOptions = SqlDataCallbackOptions + (
                "info_callback": "code",
                "commit_block": Type::Int,
                );

            /** @defgroup upsert_options Upsert Strategy Codes
                These options are used with:
                - @ref SqlUtil::AbstractTable::getBulkUpsertClosure()
                - @ref SqlUtil::AbstractTable::getUpsertClosure()
                - @ref SqlUtil::AbstractTable::getUpsertClosureWithValidation()
                - @ref SqlUtil::AbstractTable::upsert()
                - @ref SqlUtil::AbstractTable::upsertCommit()
                - @ref SqlUtil::AbstractTable::upsertFromIterator()
                - @ref SqlUtil::AbstractTable::upsertFromIteratorCommit()
                - @ref SqlUtil::AbstractTable::upsertFromSelect()
                - @ref SqlUtil::AbstractTable::upsertFromSelectCommit()

                to specify the upsert strategy when synchronizing table data
             */
            #@{
            #! Upsert option: insert first, if the insert fails, then update
            /** with this option an insert is attempted, and if it fails due to a duplicate row, then an update is made unconditionally; with
                this upsert strategy, the following row result codes are possible:
                - @ref UR_Inserted
                - @ref UR_Verified (the only value returned by certain drivers with optimized upsert implementations)
             */
            const UpsertInsertFirst = 1;

            #! Upsert option: update first, if the update fails, then insert
            /** with this option an update is attempted, and if it fails due to a missing row, then an insert is performed; with
                this upsert strategy, the following row result codes are possible:
                - @ref UR_Inserted
                - @ref UR_Verified (the only value returned by certain drivers with optimized upsert implementations)
             */
            const UpsertUpdateFirst = 2;

            #! Upsert option: select first, if the row is unchanged, do nothing, if it doesn't exist, insert, otherwise update
            /** with this option the row is selected, if it doesn't exist, an insert is made, and an update is made only if the
                values are different; with this upsert strategy, the following row result codes are possible:
                - @ref UR_Inserted
                - @ref UR_Updated
                - @ref UR_Unchanged
             */
            const UpsertSelectFirst = 3;

            #! Upsert option: if the target table is empty, use @ref UpsertInsertFirst, otherwise use @ref UpsertUpdateFirst
            /** With this upsert strategy, the following row result codes are possible:
                - @ref UR_Inserted
                - @ref UR_Verified (the only value returned by certain drivers with optimized upsert implementations)
             */
            const UpsertAuto = 4;

            #! Upsert option: insert if the row does not exist, otherwise ignore
            /** With this upsert strategy, the following row result codes are possible:
                - @ref UR_Inserted
                - @ref UR_Unchanged
             */
            const UpsertInsertOnly = 5;

            #! Upsert option: update if the row exists, otherwise ignore
            /** With this upsert strategy, the following row result codes are possible:
                - @ref UR_Verified
                - @ref UR_Unchanged
             */
            const UpsertUpdateOnly = 6;

            #! hash mapping upsert strategy codes to a text description
            /** @see @ref UpsertStrategyDescriptionMap for a reverse mapping
            */
            const UpsertStrategyMap = (
                UpsertInsertFirst: "UpsertInsertFirst",
                UpsertUpdateFirst: "UpsertUpdateFirst",
                UpsertSelectFirst: "UpsertSelectFirst",
                UpsertAuto: "UpsertAuto",
                UpsertInsertOnly: "UpsertInsertOnly",
                UpsertUpdateOnly: "UpsertUpdateOnly",
            );

            #! hash mapping upsert strategy descriptions to upsert strategy codes
            /** @see @ref UpsertStrategyMap for a reverse mapping
            */
            const UpsertStrategyDescriptionMap = (
                "UpsertInsertFirst": UpsertInsertFirst,
                "UpsertUpdateFirst": UpsertUpdateFirst,
                "UpsertSelectFirst": UpsertSelectFirst,
                "UpsertAuto": UpsertAuto,
                "UpsertInsertOnly": UpsertInsertOnly,
                "UpsertUpdateOnly": UpsertUpdateOnly,
            );
            #@}

            /** @defgroup upsert_results Upsert Result Codes
                @see @ref UpsertResultMap and @ref UpsertResultDescriptionMap
             */
            #@{
            #! row was inserted
            const UR_Inserted = 1;

            #! row was updated unconditionally (not returned with @ref UpsertSelectFirst)
            const UR_Verified = 2;

            #! row was updated because it was different (only possible with @ref UpsertSelectFirst)
            const UR_Updated = 3;

            #! row was unchanged (only possible with @ref UpsertSelectFirst, @ref UpsertInsertOnly, and @ref UpsertUpdateOnly)
            const UR_Unchanged = 4;

            #! row was deleted (only possible with batch upsert methods such as @ref AbstractTable::upsertFromIterator() where @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c delete_others is @ref Qore::True "True")
            const UR_Deleted = 5;
            #@}

            #! hash mapping upsert results to a description
            /** @see @ref UpsertResultDescriptionMap for a reverse mapping
            */
            const UpsertResultMap = (
                UR_Inserted: "inserted",
                UR_Verified: "verified",
                UR_Updated: "updated",
                UR_Unchanged: "unchanged",
                UR_Deleted: "deleted",
                );

            #! hash mapping upsert descriptions to codes
            /** @see @ref UpsertResultMap for a reverse mapping
            */
            const UpsertResultDescriptionMap = (
                "inserted": UR_Inserted,
                "verified": UR_Verified,
                "updated": UR_Updated,
                "unchanged": UR_Unchanged,
                "deleted": UR_Deleted,
                );

            #! maps upsert result codes to single letter symbols
            const UpsertResultLetterMap = (
                UR_Inserted: "I",
                UR_Verified: "V",
                UR_Updated: "U",
                UR_Unchanged: ".",
                UR_Deleted: "X",
                );
        }

        private {
            #! the table's name
            string name;
            #! column description object
            Columns columns;
            #! primary key description
            AbstractPrimaryKey primaryKey;
            #! index descriptions
            Indexes indexes;
            #! foreign constraints description
            ForeignConstraints foreignConstraints;
            #! constraint descriptions
            Constraints constraints;
            #! trigger descriptions
            Triggers triggers;
            #! native case option
            bool native_case = False;
            #! in database
            bool inDb = False;
            #! manual edits
            bool manual = False;
        }

        #! creates the object; private constructor
        /** @param nds the AbstractDatasource for the connection to the database
            @param nname the name of the table
            @param nopts an optional hash of options for the table creation string; see @ref SqlUtil::AbstractTable::TableOptions for common options; each driver can support additional driver-specific options

            @throw OPTION-ERROR invalid or unsupported option passed
         */
        private constructor(AbstractDatasource nds, string nname, *hash nopts) : AbstractSqlUtilBase(nds, nopts) {
            name = nname;
            doTableOptions(nopts);
        }

        #! copies the object
        copy(AbstractTable old) {
            # copy container objects
            if (columns)
                columns = columns.copy();
            if (primaryKey)
                primaryKey = primaryKey.copy();
            if (indexes)
                indexes = indexes.copy();
            if (foreignConstraints)
                foreignConstraints = foreignConstraints.copy();
            if (constraints)
                constraints = constraints.copy();
            if (triggers)
                triggers = triggers.copy();

            copyImpl(old);
        }

        #! changes the datasource for the table; if the \c inDb flag is @ref Qore::True "True", then it is set to @ref Qore::False "False" by calling this method
        /** @par Example:
            @code{.py}
Table table(ds1, "table_name");
table.setDatasource(ds2);
table.create();
            @endcode

            @param nds the new datasource for the table

            @note it's generally not safe to call this method while SQL operations are in progress in other threads on the same table
        */
        setDatasource(AbstractDatasource nds) {
            l.lock();
            on_exit l.unlock();

            ds = nds;
            inDb = False;
        }

        private doTableOptions(*hash nopts) {
            validateOptionsIntern("OPTION-ERROR", getTableOptions(), \nopts);
            if (nopts.native_case)
                native_case = True;
        }

        #! commits the current transaction on the underlying @ref Qore::SQL::AbstractDatasource
        commit() {
            ds.commit();
        }

        #! rolls back the current transaction on the underlying @ref Qore::SQL::AbstractDatasource
        rollback() {
            ds.rollback();
        }

        #! returns @ref Qore::True "True" if the table has been read from or created in the database, @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
bool b = table.inDb();
            @endcode

            @return @ref Qore::True "True" if the table has been read from or created in the database, @ref Qore::False "False" if not

            @note this method only returns a flag if the object contains configuration already retrieved from the database, @see AbstractTable::checkExistence() for a method that will check the database if the table exists in case the table is not already known to exist in the database
        */
        bool inDb() {
            return inDb;
        }

        #! returns an iterator for all unique constraints on the table (including the primary key if any)
        /** @par Example:
            @code{.py}
map printf("unique constraint %s\n", $1.getName()), table.getUniqueConstraintIterator();
            @endcode

            @return an iterator for all unique constraints on the table (including the primary key if any)
        */
        Qore::AbstractIterator getUniqueConstraintIterator() {
            bool lck = !l.lockOwner();
            if (lck)
                l.lock();
            on_exit if (lck)
                l.unlock();

            if (!manual) {
                getPrimaryKeyUnlocked();
                getConstraintsUnlocked();
            }

            list lst = ();
            if (!primaryKey.empty())
                lst += primaryKey;

            foreach AbstractConstraint c in (constraints.iterator()) {
                if (c instanceof AbstractUniqueConstraint)
                    lst += c;
            }

            return lst.iterator();
        }

        #! drops the table from the database; releases the transaction lock after dropping the table
        /** @par Example:
            @code{.py}
table.dropCommit();
            @endcode

            @param opt optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @throw OPTION-ERROR invalid or unknown callback option

            @note The transaction is committed if successful or rolled back if an exception occurs; use @ref drop() to execute without any transaction management
         */
        dropCommit(*hash opt) {
            on_exit ds.commit();
            on_error ds.rollback();
            drop(opt);
        }

        #! drops the table from the database without any transaction management
        /** @par Example:
            @code{.py}
table.drop();
            @endcode

            @param opt optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @throw OPTION-ERROR invalid or unknown callback option

            @note Transaction management is normally not performed when dropping tables, however this method uses the Qore::SQL::AbstractDatasource::exec() method, which normally participates in acquiring a transaction lock for the underlying datasource object; therefore after this method executes normally the transaction lock will be dedicated to the calling thread.
         */
        drop(*hash opt) {
            map ds.execRaw($1), getDropSql(opt);
            inDb = False;
        }

        #! A legacy wrapper for drop()
        deprecated dropNoCommit(*hash opt) {
            drop(opt);
        }

        #! executes some SQL with optional arguments so that if an error occurs the current transaction state is not lost
        /** @par Example:
            @code{.py}
t.tryExec("drop table tmp_table");
            @endcode

            Include any arguments in the parameter list after the \a sql argument

            @param sql the SQL to execute

            @return any return value from the SQL command executed
         */
        any tryExec(string sql) {
            return tryExecArgsImpl(sql, argv);
        }

        #! executes some SQL with optional arguments so that if an error occurs the current transaction state is not lost
        /** @par Example:
            @code{.py}
t.tryExec("delete from tmp_table where id = %v and name = %v", arglist);
            @endcode

            @param sql the SQL to execute
            @param args the bind / placeholder or other arguments corresponding to the SQL string

            @return any return value from the SQL command executed
         */
        any tryExecArgs(string sql, *softlist args) {
            return tryExecArgsImpl(sql, args);
        }

        #! executes some SQL so that if an error occurs the current transaction state is not lost
        /** @par Example:
            @code{.py}
t.tryExecRaw("drop table tmp_table");
            @endcode

            Include any arguments in the parameter list after the \a sql argument

            @param sql the SQL to execute

            @return any return value from the SQL command executed
         */
        any tryExecRaw(string sql) {
            return tryExecRawImpl(sql);
        }

        #! returns the sql required to drop the table; reimplement in subclasses if necessary
        /** @par Example:
            @code{.py}
list l = table.getDropSql();
            @endcode

            @param opt optional callback options; see @ref AbstractDatabase::CallbackOptions for more info

            @return a list of strings that can be used to drop the table and any other objects assocatied with the table (for example: PostgreSQL table trigger function(s))

            @throw OPTION-ERROR invalid or unknown callback option
        */
        softlist getDropSql(*hash opt) {
            cache();
            return AbstractDatabase::doCallback(opt, getDropSqlImpl(), AbstractDatabase::AC_Drop, "table", getSqlName());
        }

        #! truncates all the table data; releases the transaction lock after executing
        /** @par Example:
            @code{.py}
table.truncateCommit();
            @endcode

            @note The transaction is committed if successful or rolled back if an exception occurs; use @ref truncate() to execute without any transaction management
         */
        truncateCommit() {
            on_exit ds.commit();
            on_error ds.rollback();
            truncate();
        }

        #! truncates all the table data without any transaction management
        /** @par Example:
            @code{.py}
table.truncate();
            @endcode

            @note Transaction management may not be applied when truncating tables depending on the database driver (for example truncating tables in Oracle does not participate in transaction management), however this method uses the Qore::SQL::AbstractDatasource::exec() method, which normally participates in acquiring a transaction lock for the underlying datasource object; therefore after this method executes normally the transaction lock will be dedicated to the calling thread.
         */
        truncate() {
            ds.exec(getTruncateSqlImpl());
            inDb = False;
        }

        #! A legacy warpper for truncate()
        deprecated truncateNoCommit() {
            truncate();
        }

        #! gets the SQL that can be used to truncate the table
        /** @par Example:
            @code{.py}
string sql = table.getTruncateSql();
            @endcode

            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return the SQL that can be used to truncate the table

            @throw OPTION-ERROR invalid or unsupported option passed

            @see inDb() for a method that tells if the table is already in the database or not

            @note if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)
         */
        string getTruncateSql(*hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);
            return AbstractDatabase::doCallback(opt, getTruncateSqlImpl(), AbstractDatabase::AC_Truncate, "table", name);
        }

        #! creates the table in the database; releases the transaction lock after creating the table
        /** @par Example:
            @code{.py}
table.createCommit();
            @endcode

            @param opt a hash of options for the SQL creation strings

            @note The transaction is committed if successful or rolled back if an exception occurs; use @ref create() to execute without any transaction management
         */
        createCommit(*hash opt) {
            on_exit ds.commit();
            on_error ds.rollback();
            create(opt);
        }

        #! creates the table with all associated properties (indexes, constraints, etc) without any transaction management
        /** @par Example:
            @code{.py}
table.create();
            @endcode

            @param opt a hash of options for the SQL creation strings

            @note Transaction management is normally not performed when creating tables, however this method uses the Qore::SQL::AbstractDatasource::exec() method, which normally participates in acquiring a transaction lock for the underlying datasource object; therefore after this method executes normally the transaction lock will be dedicated to the calling thread.

            @throw CREATE-TABLE-ERROR table has already been read from or created in the database
         */
        create(*hash opt) {
            if (inDb)
                throw "CREATE-TABLE-ERROR", sprintf("%s has already been read from or created in the database", getDesc());

            # get and execute all the creation strings
            map ds.execRaw($1), getCreateSql(opt);
            inDb = True;
            manual = False;
        }

        #! A legacy wrapper for create()
        deprecated createNoCommit(*hash opt) {
            create(opt);
        }

        #! renames the table; if the table is already known to be in the database in the database, then the changes are effected in the database also immediately; otherwise it is only updated internally
        /** @par Example:
            @code{.py}
table.rename("new_name");
            @endcode

            @param new_name the new name for the table
            @param sql an optional reference to a string to retrieve the SQL used to modify the table (only executed if the table is already in the database)
            @param table_cache an optional @ref SqlUtil::Tables "Tables" argument to update the table name in any table cache

            @see getRenameSql()
        */
        rename(string new_name, *reference sql, *Tables table_cache) {
            l.lock();
            on_exit l.unlock();

            execSql(sql = getRenameSqlImpl(new_name));
            doRenameIntern(new_name, table_cache);
        }

        private doRenameIntern(string new_name, *Tables table_cache) {
            if (table_cache)
                table_cache.tableRenamed(name, new_name, getSqlName());
            name = new_name;
        }

        #! returns @ref Qore::True "True" if the table has no data rows, @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
bool b = table.emptyData();
            @endcode

            @return @ref Qore::True "True" if the table has no data rows, @ref Qore::False "False" if not

            @see
            - empty()
            - rowCount()
        */
        bool emptyData() {
            return findSingle() ? False : True;
        }

        #! returns @ref Qore::True "True" if the table has no definitions, @ref Qore::False "False" if not
        /** @par Example:
            @code{.py}
bool b = table.empty();
            @endcode

            @return @ref Qore::True "True" if the table has no definitions, @ref Qore::False "False" if not

            @see emptyData()
        */
        bool empty() {
            l.lock();
            on_exit l.unlock();
            return emptyUnlocked();
        }

        private bool emptyUnlocked() {
            if (!columns.empty()
                || !primaryKey.empty()
                || !indexes.empty()
                || !foreignConstraints.empty()
                || !constraints.empty()
                || !triggers.empty())
                return False;

            return emptyImpl();
        }

        #! creates the object from a table description hash
        /** @param desc a @ref table_desc_hash "table description hash" describing the table
            @param opt an optional hash of options for the table creation string; see @ref SqlUtil::AbstractTable::TableOptions for common options; each driver can support additional driver-specific options

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw DESCRIPTION-ERROR invalid or unsupported description hash value passed
        */
        setupTable(hash desc, *hash opt) {
            # check description
            validateOptionsIntern("DESCRIPTION-ERROR", getTableDescriptionHashOptions(), \desc);

            # check options
            hash copt = getColumnOptions();
            hash tcopt = getTableCreationOptions() + copt;
            opt = opt{tcopt.keys()};
            validateOptionsIntern("OPTION-ERROR", tcopt, \opt);

            # setup table cache
            if (!opt.table_cache)
                opt.table_cache = new Tables();

            l.lock();
            on_exit l.unlock();

            if (!emptyUnlocked())
                throw "DESCRIPTION-ERROR", sprintf("%s: cannot set up a non-empty table from a hash description", getDesc());

            # add current table to cache
            opt.table_cache.add(name, self);

            string drv = ds.getDriverName();

            # extract column options
            *hash oc = opt.(copt.keys());

            # process descriptions
            /* issue #1684: sqlutil: schema alignment skips column with name "driver"
               handle driver options with special logic: try to remove any driver
               configurations so that a column with the name "driver" can be
               supported
            */
            if (desc.columns.driver) {
                *hash h = remove desc.columns.driver{SqlUtilDrivers};
                if (h) {
                    # if we have driver-specific info, add it to the columns hash
                    if (h{drv})
                        desc.columns += h{drv};
                    # if there is no column information, then delete the "driver" key
                    if (!desc.columns.driver)
                        delete desc.columns.driver;
                }
            }

            AbstractDatabase::checkDriverOptions(\desc.indexes, drv);
            AbstractDatabase::checkDriverOptions(\desc.primary_key, drv);
            AbstractDatabase::checkDriverOptions(\desc.unique_constraints, drv);
            AbstractDatabase::checkDriverOptions(\desc.foreign_constraints, drv);
            AbstractDatabase::checkDriverOptions(\desc.triggers, drv);

            # process column definitions
            foreach string cn in (desc.columns.keyIterator()) {
                if (desc.columns{cn}.typeCode() != NT_HASH)
                    throw "DESCRIPTION-ERROR", sprintf("%s: value assigned to column key %y is not a hash, got type %y instead (%y)", getDesc(), cn, desc.columns{cn}.type(), desc.columns{cn});
                AbstractDatabase::checkDriverOptions(\desc.columns{cn}, drv);
            }

            preSetupTableImpl(\desc, opt);

            # add columns to table
            foreach string cn in (desc.columns.keyIterator()) {
                hash ch = desc.columns{cn};
                softbool nullable = !exists ch.notnull ? True : !(remove ch.notnull);
                # bug #1688: sqlutil: oracle: character_semantics can be
                # different for each column. So the global options (oc)
                # are overwriten with column options (ch).
                addColumnUnlocked(cn, oc + ch, nullable ? True : False);
            }

            # add indexes to table
            foreach string ixn in (desc.indexes.keyIterator()) {
                if (desc.indexes{ixn}.typeCode() != NT_HASH)
                    throw "DESCRIPTION-ERROR", sprintf("%s: value assigned to index key %y is not a hash, got type %y instead (%y)", getDesc(), ixn, desc.indexes{ixn}.type(), desc.indexes{ixn});

                hash ih = desc.indexes{ixn};

                if (!ih.columns || !inlist(ih.columns.typeCode(), (NT_LIST, NT_STRING)))
                    throw "DESCRIPTION-ERROR", sprintf("%s: value assigned to \"columns\" key for index %y is not a list or a string, got type %y instead (%y)", getDesc(), ixn, ih.columns.type(), ih.columns);

                addIndexUnlocked(ixn, ih.unique ? True : False, ih.columns, opt.(getIndexOptions().keys()));
            }

            # add primary key to table
            if (desc.primary_key) {
                if (desc.primary_key.typeCode() != NT_HASH)
                    throw "DESCRIPTION-ERROR", sprintf("%s: value assigned to primary_key is not a hash, got type %y instead (%y)", getDesc(), desc.primary_key.type(), desc.primary_key);
                if (!desc.primary_key.name || desc.primary_key.name.typeCode() != NT_STRING)
                    throw "DESCRIPTION-ERROR", sprintf("%s: missing string value for 'name' key in primary_key hash: %y", getDesc(), desc.primary_key);

                addPrimaryKeyUnlocked(desc.primary_key.name, desc.primary_key.columns, opt.(getConstraintOptions().keys()));
            }

            # add unique constraints to table
            foreach string un in (desc.unique_constraints.keyIterator()) {
                if (desc.unique_constraints{un}.typeCode() != NT_HASH)
                    throw "DESCRIPTION-ERROR", sprintf("%s: value assigned to index key %y is not a hash, got type %y instead (%y)", getDesc(), un, desc.unique_constraints{un}.type(), desc.unique_constraints{un});

                hash ch = desc.unique_constraints{un};

                if (!ch.columns || !inlist(ch.columns.typeCode(), (NT_LIST, NT_STRING)))
                    throw "DESCRIPTION-ERROR", sprintf("%s: value assigned to \"columns\" key for unique constraint %y is not a list or a string, got type %y instead (%y)", getDesc(), un, ch.columns.type(), ch.columns);

                if (constraints.hasKey(un))
                    continue;
                addUniqueConstraintUnlocked(un, ch.columns, opt.(getConstraintOptions().keys()));
            }

            # add foreign constraints to table
            foreach string cn in (desc.foreign_constraints.keyIterator()) {
                if (desc.foreign_constraints{cn}.typeCode() != NT_HASH)
                    throw "DESCRIPTION-ERROR", sprintf("%s: value assigned to foreign constraint key %y is not a hash, got type %y instead (%y)", getDesc(), cn, desc.foreign_constraints{cn}.type(), desc.foreign_constraints{cn});

                hash ch = desc.foreign_constraints{cn};

                if (!ch.columns || !inlist(ch.columns.typeCode(), (NT_LIST, NT_STRING)))
                    throw "DESCRIPTION-ERROR", sprintf("%s: value assigned to \"columns\" key for foreign constraint %y is not a list or a string, got type %y instead (%y)", getDesc(), cn, ch.columns.type(), ch.columns);

                if (!ch.table || ch.table.typeCode() != NT_STRING)
                    throw "DESCRIPTION-ERROR", sprintf("%s: missing string value for \"table\" key in foreign constraint %y description: %y", getDesc(), cn, ch);

                if (exists ch.target_columns && !inlist(ch.target_columns.typeCode(), (NT_LIST, NT_STRING)))
                    throw "DESCRIPTION-ERROR", sprintf("%s: value assigned to \"target_columns\" key for foreign constraint %y is not a list or a string, got type %y instead (%y)", getDesc(), cn, ch.target_columns.type(), ch.target_columns);

                if (!ch.target_columns)
                    ch.target_columns = ch.columns;

                try {
                    addForeignConstraintUnlocked(cn, ch.columns, ch.table, ch.target_columns, opt.(getForeignConstraintOptions().keys()));
                }
                catch (hash ex) {
                    throw ex.err, sprintf("error setting up %s: %s", getDesc(), ex.desc), ex.arg;
                }
            }

            # add triggers to table
            foreach string tn in (desc.triggers.keyIterator()) {
                if (desc.triggers{tn}.typeCode() != NT_STRING)
                    throw "DESCRIPTION-ERROR", sprintf("%s: value assigned to trigger key %y is not a string giving the trigger source code, got type %y instead (%y)", getDesc(), tn, desc.triggers{tn}.type(), desc.triggers{tn});
                string src = desc.triggers{tn};

                addTriggerUnlocked(tn, src, opt.(getTriggerOptions().keys()));
            }

            setupTableImpl(desc, opt);
        }

        #! adds a column to the table; if the table is already known to be in the database, then it is added in the database also immediately; otherwise it is only added internally and can be created when create() is called for example
        /** @par Example:
            @code{.py}
table.addColumn("name", ("qore_type": Type::String, "size": 50), False);
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param cname the name of the column
            @param opt a hash describing the column; the following keys are permitted (other column options may be supported depending on the underlying AbstractTable implementation):
            - \c qore_type: a qore type string that will be converted to a native DB type with some default conversion;
            - \c native_type: the native database column type; if both \c native_type and \c qore_type are given then \c native_type is used
            - \c size: for data types requiring a size component, the size; for numeric columns this represents the precision for example
            - \c scale: for numeric data types, this value gives the scale
            - \c default_value: the default value for the column
            - \c default_value_native: a boolean flag to say if a \c default_value should be validated against table column type (False) or used as it is (True) to allow to use DBMS native functions or features. Defaults to False. It is strongly recommended to use \c default_value_native for \c default_value in \c driver specific sub-hash to avoid non-portable schema hashes
            @param nullable if @ref Qore::True "True" then the column can hold NULL values; note that primary key columns cannot be nullable
            @param lsql an optional reference to a list of strings to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @throw COLUMN-ERROR no \c native_type or \c qore_type keys in column option hash, column already exists, invalid column data

            @note make sure and add a \c default_value value when adding a column with a \c "not null" constraint with existing data

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractColumn addColumn(string cname, hash opt, bool nullable = True, *reference lsql) {
            l.lock();
            on_exit l.unlock();

            return addColumnUnlocked(cname, opt, nullable, \lsql);
        }

        #! returns a list of SQL strings that can be use to add a column to the table
        /** @par Example:
            @code{.py}
list l = table.getAddColumnSql("name", ("qore_type": Type::String, "size": 50), False);
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param cname the name of the column
            @param copt a hash describing the column; the following keys are permitted (other column options may be supported depending on the underlying AbstractTable implementation):
            - \c qore_type: a qore type string that will be converted to a native DB type with some default conversion;
            - \c native_type: the native database column type; if both \c native_type and \c qore_type are given then \c native_type is used
            - \c size: for data types requiring a size component, the size; for numeric columns this represents the precision for example
            - \c scale: for numeric data types, this value gives the scale
            - \c default_value: the default value for the column
            - \c default_value_native: a boolean flag to say if a \c default_value should be validated against table column type (False) or used as it is (True) to allow to use DBMS native functions or features. Defaults to False. It is strongly recommended to use \c default_value_native for \c default_value in \c driver specific sub-hash to avoid non-portable schema hashes
            @param nullable if @ref Qore::True "True" then the column can hold NULL values; note that primary key columns cannot be nullable
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return a list of SQL strings that can be use to add a column to the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw COLUMN-ERROR no \c native_type or \c qore_type keys in column option hash, column already exists, invalid column data

            @note
            - make sure and add a \c default_value value when adding a column with a \c "not null" constraint with existing data
            - if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)

            @see inDb() for a method that tells if the table is already in the database or not
         */
        list getAddColumnSql(string cname, hash copt, bool nullable = True, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            list lsql;

            {
                l.lock();
                on_exit l.unlock();
                addColumnUnlocked(cname, copt, nullable, \lsql, False, opt.sql_callback_executed);
            }

            return AbstractDatabase::doCallback(opt, lsql, AbstractDatabase::AC_Add, "column", name, cname);
        }

        private AbstractColumn addColumnUnlocked(string cname, hash opt, bool nullable = True, *reference lsql, bool do_exec = True, bool modify_table = True) {
            if (!columns)
                columns = new Columns();
            else if (columns.hasKey(cname))
                throw "COLUMN-ERROR", sprintf("%s column %y: this column already exists", getDesc(), cname);

            if (!opt.native_type && !opt.qore_type)
                throw "COLUMN-ERROR", sprintf("%s column %y: no native_type or qore_type keys in column option hash: %y", getDesc(), cname, opt);

            validateColumnOptions(cname, \opt, nullable);

            # get the new column object
            AbstractColumn c = addColumnImpl(cname, opt, nullable);

            # add column in table if the table is already known to be in the database
            lsql = c.getAddColumnSql(self);
            if (do_exec)
                execSql(lsql);

            # add the column to the internal column representation
            if (modify_table)
                addColumnToTableUnlocked(c);

            return c;
        }

        private addColumnToTableUnlocked(AbstractColumn c) {
            columns.add(c.name, c);
        }

        #! modifies an existing column in the table; if the table is already known to be in the database, then the changes are effected in the database also immediately; otherwise it is only updated internally and the new column definition will be created when create() is called for example
        /** @par Example:
            @code{.py}
table.modifyColumn("name", ("qore_type": Type::String, "size": 240), False);
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param cname the name of the column
            @param opt a hash describing the column; the following keys are permitted (other column options may be supported depending on the underlying AbstractTable implementation):
            - \c qore_type: a qore type string that will be converted to a native DB type with some default conversion;
            - \c native_type: the native database column type; if both \c native_type and \c qore_type are given then \c native_type is used
            - \c size: for data types requiring a size component, the size; for numeric columns this represents the precision for example
            - \c scale: for numeric data types, this value gives the scale
            - \c default_value: the default value for the column
            - \c default_value_native: a boolean flag to say if a \c default_value should be validated against table column type (False) or used as it is (True) to allow to use DBMS native functions or features. Defaults to False. It is strongly recommended to use \c default_value_native for \c default_value in \c driver specific sub-hash to avoid non-portable schema hashes
            @param lsql an optional reference to a list of strings to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @param nullable if @ref Qore::True "True" then the column can hold NULL values; note that primary key columns cannot be nullable

            @throw COLUMN-ERROR no \c native_type or \c qore_type keys in column option hash, column does not exist, invalid column data

            @note make sure and add a \c default_value value when modifying a column to have a \c "not null" constraint with existing data

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractColumn modifyColumn(string cname, hash opt, bool nullable = True, *reference lsql) {
            l.lock();
            on_exit l.unlock();
            getColumnsUnlocked();
            if (!columns.hasKey(cname))
                throw "COLUMN-ERROR", sprintf("%s column %y: the given column does not exist", getDesc(), cname);

            if (!opt.native_type && !opt.qore_type)
                throw "COLUMN-ERROR", sprintf("%s column %y: no native_type or qore_type keys in column option hash: %y", getDesc(), cname, opt);

            validateColumnOptions(cname, \opt, nullable);

            # get the new column object
            AbstractColumn c = addColumnImpl(cname, opt, nullable);

            # add column in table if the table already exists in the DB
            execSql(lsql = columns{cname}.getModifySql(self, c));

            # replaces the column in the internal column representation with the new definition
            addColumnToTableUnlocked(c);
            return c;
        }

        #! gets a list of SQL strings that can be used to modify an existing column in the table
        /** @par Example:
            @code{.py}
list l = table.getModifyColumnSql("name", ("qore_type": Type::String, "size": 240), False);
            @endcode

            @param cname the name of the column
            @param copt a hash describing the column; the following keys are permitted (other column options may be supported depending on the underlying AbstractTable implementation):
            - \c qore_type: a qore type string that will be converted to a native DB type with some default conversion;
            - \c native_type: the native database column type; if both \c native_type and \c qore_type are given then \c native_type is used
            - \c size: for data types requiring a size component, the size; for numeric columns this represents the precision for example
            - \c scale: for numeric data types, this value gives the scale
            - \c default_value: the default value for the column
            - \c default_value_native: a boolean flag to say if a \c default_value should be validated against table column type (False) or used as it is (True) to allow to use DBMS native functions or features. Defaults to False. It is strongly recommended to use \c default_value_native for \c default_value in \c driver specific sub-hash to avoid non-portable schema hashes
            @param nullable if @ref Qore::True "True" then the column can hold NULL values; note that primary key columns cannot be nullable
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return a list of SQL strings that can be used to modify an existing column in the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw COLUMN-ERROR no \c native_type or \c qore_type keys in column option hash, column does not exist, invalid column data

            @note
            - make sure and add a \c default_value value when modifying a column to have a \c "not null" constraint with existing data
            - if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)

            @see inDb() for a method that tells if the table is already in the database or not
         */
        list getModifyColumnSql(string cname, hash copt, bool nullable = True, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();
            getColumnsUnlocked();
            if (!columns.hasKey(cname))
                throw "COLUMN-ERROR", sprintf("%s column %y: the given column does not exist", getDesc(), cname);

            if (!copt.native_type && !copt.qore_type)
                throw "COLUMN-ERROR", sprintf("%s column %y: no native_type or qore_type keys in column option hash: %y", getDesc(), cname, copt);

            validateColumnOptions(cname, \copt, nullable);

            # get the new column object
            AbstractColumn c = addColumnImpl(cname, copt, nullable);
            # replaces the column in the internal column representation with the new definition
            on_success if (opt.sql_callback_executed)
                addColumnToTableUnlocked(c);

            return AbstractDatabase::doCallback(opt, columns{cname}.getModifySql(self, c), AbstractDatabase::AC_Modify, "column", cname, name, NOTHING, c.getNativeTypeString());
        }

        #! renames an existing column; if the table is already known to be in the database, then the changes are effected in the database also immediately; otherwise it is only updated internally
        /** @par Example:
            @code{.py}
AbstractColumn col = table.renameColumn("name", "family_name");
            @endcode

            @param old_name the current name of the column
            @param new_name the new name of the column
            @param sql an optional reference to a string to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @return the column object that was updated

            @throw COLUMN-ERROR if the given column does not exist in the table

            @see modifyColumn() for a method that allows the column definition to be updated

            @note if any primary key or foreign table constraints refer to the column, the internal representations are also renamed, however check constraints are not updated and will be invalid if they refer to the renamed column
        */
        AbstractColumn renameColumn(string old_name, string new_name, reference sql) {
            l.lock();
            on_exit l.unlock();
            getColumnsUnlocked();
            if (!columns.hasKey(old_name))
                throw "COLUMN-ERROR", sprintf("%s column %y: the given column does not exist", getDesc(), old_name);

            AbstractColumn c = columns{old_name};

            # add column in table if the table already exists in the DB
            execSql(sql = c.getRenameSql(self, new_name));

            renameColumnIntern(c, new_name);

            return c;
        }

        #! gets an SQL string that can be used to rename an existing column in the table
        /** @par Example:
            @code{.py}
string sql = table.getRenameColumnSql("name", "family_name");
            @endcode

            @param old_name the current name of the column
            @param new_name the new name of the column
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return an SQL string that can be used to rename an existing column in the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw COLUMN-ERROR if the old column does not exist in the table or the new column already does

            @see getModifyColumnSql() for a method that allows the column definition to be updated

            @note if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)
        */
        string getRenameColumnSql(string old_name, string new_name, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();
            getColumnsUnlocked();

            if (!columns.hasKey(old_name))
                throw "COLUMN-ERROR", sprintf("rename %s column %y to %y: old column %y column does not exist", getDesc(), old_name, new_name, old_name);

            if (columns.hasKey(new_name))
                throw "COLUMN-ERROR", sprintf("rename %s column %y to %y: new column %y already exists in the table", getDesc(), old_name, new_name, new_name);

            on_success if (opt.sql_callback_executed)
                renameColumnIntern(columns{old_name}, new_name);

            return AbstractDatabase::doCallback(opt, columns{old_name}.getRenameSql(self, new_name), AbstractDatabase::AC_Rename, "column", old_name, name, new_name);
        }

        private AbstractColumn renameColumnIntern(AbstractColumn c, string new_name) {
            string old_name = c.name;

            # rename column after database is updated
            c.name = new_name;

            # replaces the column in the internal column representation with the new definition
            columns.renameKey(old_name, new_name);

            # now rename column in any constraints
            # check primary key
            if (!primaryKey.empty() && primaryKey.hasKey(old_name))
                primaryKey.renameKey(old_name, new_name);

            # check foreign constraints
            foreach AbstractForeignConstraint fk in (foreignConstraints.iterator()) {
                if (fk.hasKey(old_name))
                    fk.renameKey(old_name, new_name);
            }

            # check unique constraints
            foreach AbstractConstraint ac in (constraints.iterator()) {
                if (!(ac instanceof AbstractUniqueConstraint))
                    continue;
                AbstractUniqueConstraint uc = cast<AbstractUniqueConstraint>(ac);
                if (uc.hasKey(old_name))
                    uc.renameKey(old_name, new_name);
            }

            # FIXME: check constraints are not updated

            return c;
        }

        private validateOptionsIntern(string err, hash ropt, reference opt) {
            AbstractSqlUtilBase::validateOptionsIntern(err, ropt, \opt, name);
        }

        private validateOptionsIntern(string err, hash ropt, reference opt, string tag) {
            AbstractSqlUtilBase::validateOptionsIntern(err, ropt, \opt, tag);
        }

        private execSql(softlist lsql) {
            #map printf("%s;\n", $1), lsql;
            if (inDb) {
                on_success ds.commit();
                on_error ds.rollback();
                map ds.execRaw($1), lsql;
            }
            else
                manual = True;
        }

        #! adds a primary key to the table; if the table is already known to be in the database, then it is added in the database also immediately; otherwise it is only added internally and can be created when create() is called for example
        /** @par Example:
            @code{.py}
string sql;
AbstractPrimaryKey pk = table.addPrimaryKey("pk_mytable", "id", ("index_tablespace": "index1"), \sql);
printf("%s;\n", sql);
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param pkname the name of the new primary key constraint
            @param cols a single column name or a list of columns that make up the primary key
            @param opt a hash of options for the new primary key; each driver may implement its own options; for common options, see @ref SqlUtil::AbstractTable::ConstraintOptions
            @param sql an optional reference to a string to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @return an AbstractPrimaryKey object corresponding to the primary key created

            @throw PRIMARY-KEY-ERROR the table already has a primary key or invalid columns or options passed

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractPrimaryKey addPrimaryKey(string pkname, softlist cols, *hash opt, *reference sql) {
            l.lock();
            on_exit l.unlock();

            return addPrimaryKeyUnlocked(pkname, cols, opt, \sql);
        }

        #! returns the SQL that can be used to add a primary key to the table
        /** @par Example:
            @code{.py}
string sql = table.getAddPrimaryKeySql("pk_mytable", "id", ("index_tablespace": "index1"), opt);
printf("%s;\n", sql);
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param pkname the name of the new primary key constraint
            @param cols a single column name or a list of columns that make up the primary key
            @param pkopt a hash of options for the new primary key; each driver may implement its own options; for common options, see @ref SqlUtil::AbstractTable::ConstraintOptions
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return the SQL that can be used to add a primary key to the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw PRIMARY-KEY-ERROR the table already has a primary key or invalid columns or options passed

            @see inDb() for a method that tells if the table is already in the database or not

            @note if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)
         */
        string getAddPrimaryKeySql(string pkname, softlist cols, *hash pkopt, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);
            l.lock();
            on_exit l.unlock();

            string sql;
            AbstractPrimaryKey pk = addPrimaryKeyUnlockedIntern(pkname, cols, pkopt, \sql);
            on_success if (opt.sql_callback_executed) {
                setPrimaryKeyUnlocked(pk);
                manual = True;
            }
            return AbstractDatabase::doCallback(opt, sql, AbstractDatabase::AC_Add, "primary key", pkname, getSqlName());
        }

        private setPrimaryKeyUnlocked(AbstractPrimaryKey pk) {
            primaryKey = pk;
        }

        private AbstractPrimaryKey addPrimaryKeyUnlocked(string pkname, softlist cols, *hash opt, *reference sql) {
            AbstractPrimaryKey pk = addPrimaryKeyUnlockedIntern(pkname, cols, opt, \sql);
            execSql(sql);
            setPrimaryKeyUnlocked(pk);
            return pk;
        }

        private AbstractPrimaryKey addPrimaryKeyUnlockedIntern(string pkname, softlist cols, *hash opt, *reference sql) {
            getColumnsUnlocked();
            if (!manual)
                getIndexesUnlocked();

            # load constraints if needed, verify unique constraint name, validate/process options
            checkUniqueConstraintNameValidateOptions("PRIMARY-KEY-ERROR", pkname, getConstraintOptions(), \opt);

            if (!primaryKey.empty())
                throw "PRIMARY-KEY-ERROR", sprintf("%s add primary key %y: already has primary key %y with columns: %y", getDesc(), pkname, primaryKey.getName(), primaryKey.keys());

            if (!cols)
                throw "PRIMARY-KEY-ERROR", sprintf("%s add primary key %y: no column names passed to %s::addPrimaryKey()", getDesc(), pkname, self.className());

            hash ch;
            while (cols) {
                any v = shift cols;
                if (v.typeCode() != NT_STRING)
                    throw "PRIMARY-KEY-ERROR", sprintf("%s add primary key %y: %y argument passed as column name to %s::addPrimaryKey(); expecting \"string\" (value: %y)", getDesc(), pkname, v.type(), self.className(), v);
                if (!columns.hasKey(v))
                    throw "PRIMARY-KEY-ERROR", sprintf("%s add primary key %y: table has no column named %y", getDesc(), pkname, v);
                ch{v} = columns{v};
            }

            # get the new primary key object
            AbstractPrimaryKey pk = addPrimaryKeyImpl(pkname, ch, opt);

            # find matching index for unique constraint
            pk.findMatchingIndex(indexes);

            # add pk to table if the table already exists in the DB
            sql = pk.getCreateSql(getSqlName());

            return pk;
        }

        #! gets a list of SQL strings to drop all constraints and indexes with the given column name; if the column does not exist then an empty list is returned
        /** @par Example:
            @code{.py}
list l = table.getDropAllConstraintsAndIndexesOnColumnSql("status");
            @endcode

            @param cname the name of the column
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @throw OPTION-ERROR invalid or unsupported option passed

            @note
            - this method retrieves current primary key definition from the database if not already loaded or defined
            - if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)

            @see inDb() for a method that tells if the table is already in the database or not
        */
        list getDropAllConstraintsAndIndexesOnColumnSql(string cname, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            return getDropAllConstraintsAndIndexesOnColumnSqlUnlocked(cname, opt);
        }

        private list getDropAllConstraintsAndIndexesOnColumnSqlUnlocked(string cname, *hash opt) {
            getAllConstraintsUnlocked();
            getIndexesUnlocked();

            list lst = ();

            if (primaryKey && primaryKey.hasColumn(cname)) {
                on_success if (opt.sql_callback_executed)
                    remove primaryKey;

                lst += AbstractDatabase::doCallback(opt, primaryKey.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "primary key", primaryKey.getName(), getSqlName());
            }

            foreach AbstractConstraint c in (constraints.iterator()) {
                if (c.hasColumn(cname)) {
                    on_success if (opt.sql_callback_executed) {
                        constraints.take(c.getName());
                        # if it's a unique constraint, remove any index with the same name
                        if (c instanceof AbstractUniqueConstraint && constraintsLinkedToIndexesImpl())
                            indexes.take(c.getName());
                    }

                    lst += AbstractDatabase::doCallback(opt, c.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "constraint", c.getName(), getSqlName());
                }
            }

            foreach AbstractForeignConstraint fk in (foreignConstraints.iterator()) {
                if (fk.hasColumn(cname)) {
                    on_success if (opt.sql_callback_executed)
                        foreignConstraints.take(fk.getName());

                    lst += AbstractDatabase::doCallback(opt, fk.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "foreign constraint", fk.getName(), getSqlName());
                }
            }

            foreach AbstractIndex ix in (indexes.iterator()) {
                if (ix.hasColumn(cname)) {
                    on_success if (opt.sql_callback_executed)
                        indexes.take(ix.name);

                    lst += AbstractDatabase::doCallback(opt, ix.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "index", ix.name, getSqlName());
                }
            }

            return lst;
        }

        #! gets a list of SQL strings that can be used to drop the primary key from the table
        /** @par Example:
            @code{.py}
list l = table.getDropPrimaryKeySql();
            @endcode

            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return a list of SQL strings that can be used to drop the primary key from the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw PRIMARY-KEY-ERROR the table has no primary key

            @note
            - this method retrieves current primary key definition from the database if not already loaded or defined
            - if there are known foreign contraints on the primary key, SQL for dropping those constraints is also returned
            - if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)

            @see inDb() for a method that tells if the table is already in the database or not
         */
        list getDropPrimaryKeySql(*hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            getPrimaryKeyUnlocked();
            if (primaryKey.empty())
                throw "PRIMARY-KEY-ERROR", sprintf("%s: has no primary key", getDesc());

            on_success if (opt.sql_callback_executed)
                remove primaryKey;

            list l = ();
            map l += AbstractDatabase::doCallback(opt, $1.fk.getDropSql($1.table), AbstractDatabase::AC_Drop, "foreign constraint", $1.fk.getName(), $1.table), primaryKey.getSourceConstraintIterator();

            l += AbstractDatabase::doCallback(opt, primaryKey.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "primary key", primaryKey.getName(), name);
            return l;
        }

        #! drops the primary key from the table; if the table is known to be in the database already, then it is also dropped from the database immediately; otherwise it is only removed internally
        /** @par Example:
            @code{.py}
table.dropPrimaryKey();
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.
            @param lsql an optional reference to a string to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @return the primary key dropped from the table

            @throw PRIMARY-KEY-ERROR no primary key exists

            @note
            - this method retrieves the primary key definition from the database if none has already been defined
            - if there are known foreign contraints on the primary key, SQL for dropping those constraints is also included

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractPrimaryKey dropPrimaryKey(*reference lsql) {
            l.lock();
            on_exit l.unlock();
            getPrimaryKeyUnlocked();
            if (primaryKey.empty())
                throw "PRIMARY-KEY-ERROR", sprintf("%s: has no primary key", getDesc());

            lsql = ();
            map execSql(lsql += $1.fk.getDropSql($1.table)), primaryKey.getSourceConstraintIterator();

            # drop pk from table if the table already exists in the DB
            execSql(lsql += primaryKey.getDropSql(getSqlName()));

            return remove primaryKey;
        }

        #! adds a unique constraint to the table; if the table is known to be in the database already, then the constraint is added to the database also immediately; otherwise it is only added internally and can be created when create() is called for example
        /** @par Example:
            @code{.py}
string sql;
AbstractUniqueConstraint c = table.addUniqueConstraint("uk_mytable", "name", ("index_tablespace": "index1"), \sql);
printf("%s;\n", sql);
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param cname the name of the new unique constraint
            @param cols a single column name or a list of columns that make up the unique constraint
            @param opt a hash of options for the new unique constraint; each driver may implement its own options; for common options, see @ref SqlUtil::AbstractTable::ConstraintOptions
            @param sql an optional reference to a string to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @return an AbstractUniqueConstraint object corresponding to the unique constraint created

            @throw UNIQUE-CONSTRAINT-ERROR the table already has a constraint with the given name or invalid columns passed
            @throw OPTION-ERROR invalid or unsupported option passed

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractUniqueConstraint addUniqueConstraint(string cname, softlist cols, *hash opt, *reference sql) {
            l.lock();
            on_exit l.unlock();
            return addUniqueConstraintUnlocked(cname, cols, opt, \sql);
        }

        #! returns an SQL string that can be used to add a unique constraint to the table
        /** @par Example:
            @code{.py}
string sql = table.getAddUniqueConstraintSql("uk_mytable", "name", ("index_tablespace": "index1"));
printf("%s;\n", sql);
            @endcode

            @param cname the name of the new unique constraint
            @param cols a single column name or a list of columns that make up the unique constraint
            @param ukopt a hash of options for the new unique constraint; each driver may implement its own options; for common options, see @ref SqlUtil::AbstractTable::ConstraintOptions
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return an SQL string that can be used to add a unique constraint to the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw UNIQUE-CONSTRAINT-ERROR the table already has a constraint with the given name or invalid columns passed

            @see inDb() for a method that tells if the table is already in the database or not

            @note if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)
         */
        string getAddUniqueConstraintSql(string cname, softlist cols, *hash ukopt, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            string sql;
            AbstractUniqueConstraint uk = addUniqueConstraintUnlockedIntern(cname, cols, ukopt, \sql);
            on_success if (opt.sql_callback_executed) {
                # set as constraint for the table
                if (!constraints)
                    constraints = new Constraints();
                constraints.add(cname, uk);
                manual = True;
            }
            return AbstractDatabase::doCallback(opt, sql, AbstractDatabase::AC_Add, "unique constraint", cname, getSqlName());
        }

        private AbstractUniqueConstraint addUniqueConstraintUnlocked(string cname, softlist cols, *hash opt, *reference sql) {
            AbstractUniqueConstraint uk = addUniqueConstraintUnlockedIntern(cname, cols, opt, \sql);
            # add foreign constraint to table in the DB if the table already exists in the DB
            execSql(sql);
            # set as constraint for the table
            if (!constraints)
                constraints = new Constraints();
            constraints.add(cname, uk);
            return uk;
        }

        private AbstractUniqueConstraint addUniqueConstraintUnlockedIntern(string cname, softlist cols, *hash opt, *reference sql) {
            getColumnsUnlocked();
            if (!manual)
                getIndexesUnlocked();

            # load constraints if needed, verify unique constraint name, validate/process options
            checkUniqueConstraintNameValidateOptions("UNIQUE-CONSTRAINT-ERROR", cname, getConstraintOptions(), \opt);

            if (!cols)
                throw "UNIQUE-CONSTRAINT-ERROR", sprintf("%s add unique constraint %y: no column names passed to %s::addUniqueConstraint()", getDesc(), cname, self.className());

            hash ch;
            while (cols) {
                any v = shift cols;
                if (v.typeCode() != NT_STRING)
                    throw "PRIMARY-KEY-ERROR", sprintf("%s add unique constraint %y: %y argument passed as column name to %s::addUniqueConstraint(); expecting \"string\" (value: %y)", getDesc(), cname, v.type(), self.className(), v);
                if (!columns.hasKey(v))
                    throw "PRIMARY-KEY-ERROR", sprintf("%s add unique constraint %y: table has no column named %y", getDesc(), cname, v);
                ch{v} = columns{v};
            }

            # get the new constraint object
            AbstractUniqueConstraint c = addUniqueConstraintImpl(cname, ch, opt);

            # find matching index for unique constraint
            c.findMatchingIndex(indexes);

            # add foreign constraint to table in the DB if the table already exists in the DB
            sql = c.getCreateSql(getSqlName());

            # return the new constraint
            return c;
        }

        #! adds an index to the table; if the table is already known to be in the database, then it is added in the database also immediately; otherwise it is only added internally and can be created when create() is called for example
        /** @par Example:
            @code{.py}
string sql;
AbstractIndex ix = table.addIndex("uk_mytable_name", True, "name", ("index_tablespace": "index1"), \sql);
printf("%s;\n", sql);
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param iname the name of the new index
            @param unique a flag to tell if the new index should be unique or not
            @param cols a single column name or a list of columns that make up the index
            @param opt a hash of options for the new index; each driver may implement its own options; for common options, see @ref SqlUtil::AbstractTable::IndexOptions
            @param sql an optional reference to a string to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @return an AbstractIndex object corresponding to the index created

            @throw INDEX-ERROR the table already has an index with the given name or invalid columns or options were passed

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractIndex addIndex(string iname, bool unique, softlist cols, *hash opt, *reference sql) {
            l.lock();
            on_exit l.unlock();

            return addIndexUnlocked(iname, unique, cols, opt, \sql);
        }

        #! returns an SQL string that can be used to add an index to the table
        /** @par Example:
            @code{.py}
string sql = table.getAddIndexSql("uk_mytable_name", True, "name", ("index_tablespace": "index1"));
printf("%s;\n", sql);
            @endcode

            @param iname the name of the new index
            @param unique a flag to tell if the new index should be unique or not
            @param cols a single column name or a list of columns that make up the index
            @param ixopt a hash of options for the new index; each driver may implement its own options; for common options, see @ref SqlUtil::AbstractTable::IndexOptions
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return an SQL string that can be used to add an index to the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw INDEX-ERROR the table already has an index with the given name or invalid columns or options were passed

            @see inDb() for a method that tells if the table is already in the database or not

            @note if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)
         */
        string getAddIndexSql(string iname, bool unique, softlist cols, *hash ixopt, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            string sql;
            AbstractIndex ix = addIndexUnlockedIntern(iname, unique, cols, ixopt, \sql);
            on_success if (opt.sql_callback_executed) {
                # set as index for the table
                indexes.add(iname, ix);
                manual = True;
            }
            return AbstractDatabase::doCallback(opt, sql, AbstractDatabase::AC_Add, "index", iname, getSqlName());
        }

        private AbstractIndex addIndexUnlocked(string iname, bool unique, softlist cols, *hash opt, *reference sql) {
            AbstractIndex ix = addIndexUnlockedIntern(iname, unique, cols, opt, \sql);
            execSql(sql);
            # set as index for the table
            indexes.add(iname, ix);
            if (ix.unique && uniqueIndexCreatesConstraintImpl()) {
                getConstraintsUnlocked();
                if (!constraints.hasKey(ix.name)) {
                    string csql;
                    AbstractUniqueConstraint uk = addUniqueConstraintUnlockedIntern(iname, cols, opt, \csql);
                    uk.setIndexBase(ix.name);
                    if (!constraints)
                        constraints = new Constraints();
                    constraints.add(iname, uk);
                    ix.setSupportingConstraint(uk);
                }
            }
            return ix;
        }

        private AbstractIndex addIndexUnlockedIntern(string iname, bool unique, softlist cols, *hash opt, *reference sql) {
            getColumnsUnlocked();
            if (inDb)
                getIndexesUnlocked();

            if (!indexes)
                indexes = new Indexes();
            else if (indexes.hasKey(iname))
                throw "INDEX-ERROR", sprintf("%s: already has an index named %y with columns: %y", getDesc(), iname, indexes{iname}.columns.keys());

            if (!cols)
                throw "INDEX-ERROR", sprintf("%s: no column names passed to %s::addIndex()", getDesc(), self.className());

            hash ch;
            while (cols) {
                any v = shift cols;
                if (v.typeCode() != NT_STRING)
                    throw "INDEX-ERROR", sprintf("%s: %y argument passed as column name to %s::addIndex(); expecting \"string\" (value: %y)", getDesc(), v.type(), self.className(), v);
                # only check for column if the index is not a function index
                if (!columns.hasKey(v)) {
                    # actually the original check for /\(.*\)/ in the value is not correct.
                    # DBMS usually allow to specify special types of func. indexes. Like eg.
                    # Oracle's "foo desc", PGSQL "collate" etc. All these constructs are
                    # without () inside.
                    ch{v} = {};
                }
                else
                    ch{v} = columns{v};
            }

            # validate options
            hash ixopt = getIndexOptions();
            validateOptionsIntern("INDEX-ERROR", ixopt, \opt);

            # get the new index object
            AbstractIndex ix = addIndexImpl(iname, unique, ch, opt);

            # add index to table in the DB if the table already exists in the DB
            sql = ix.getCreateSql(getSqlName(), opt);

            # return the new index
            return ix;
        }

        #! renames an existing index; if the table is already known to be in the database, then the changes are effected in the database also immediately; otherwise it is only updated internally
        /** @par Example:
            @code{.py}
AbstractIndex ix = table.renameIndex("uk_table_name", "unique_table_name");
            @endcode

            @param old_name the current name of the index
            @param new_name the new name for the index
            @param sql an optional reference to a string to retrieve the SQL used to modify the table (only executed if the table is already in the database)
            @return the index object that was updated

            @throw INDEX-ERROR if the old index does not exist in the table or if the new index name already exists
        */
        AbstractIndex renameIndex(string old_name, string new_name, reference sql) {
            l.lock();
            on_exit l.unlock();
            getIndexesUnlocked();

            AbstractIndex ix;

            code rename;

            if (!indexes || !indexes.hasKey(old_name))
                throw "INDEX-ERROR", sprintf("%s: has no index %y; valid index: %y", getDesc(), old_name, indexes.keys());

            ix = indexes{old_name};
            rename = sub () { indexes.renameKey(old_name, new_name); };

            # rename index in table if the table already exists in the DB
            execSql(sql = ix.getRenameSql(getSqlName(), new_name));

            ix.name = new_name;

            # rename index in container structure
            rename();

            # return the updated index
            return ix;
        }

        #! drops the given index from the table; if the table is known to be in the database already, then it is also dropped from the database immediately; otherwise it is only removed internally
        /** @par Example:
            @code{.py}
table.dropIndex("uk_mytable_name");
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param iname the name of the index to drop
            @param sql an optional reference to a string to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @return the index dropped from the table

            @throw INDEX-ERROR the given index does not exist

            @note this method retrieves all current index definitions from the database if none have already been defined

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractIndex dropIndex(string iname, *reference sql) {
            l.lock();
            on_exit l.unlock();
            getIndexesUnlocked();
            if (!indexes.hasKey(iname))
                throw "INDEX-ERROR", sprintf("%s: has no index %y; valid indexes: %y", getDesc(), iname, indexes.keys());

            # drop index from table if the table already exists in the DB
            execSql(sql = indexes{iname}.getDropSql(getSqlName()));

            # remove from index structure and return removed index
            return indexes.take(iname);
        }

        #! gets the SQL that can be used to drop an index from the table
        /** @par Example:
            @code{.py}
string sql = table.getDropIndexSql("uk_mytable_name");
            @endcode

            @param iname the name of the index to drop
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return the SQL that can be used to drop an index from the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw INDEX-ERROR the given index does not exist in the table

            @note
            - this method retrieves current index definitions from the database if not already loaded or defined
            - if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)

            @see inDb() for a method that tells if the table is already in the database or not
         */
        string getDropIndexSql(string iname, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            getIndexesUnlocked();
            if (!indexes.hasKey(iname))
                throw "INDEX-ERROR", sprintf("%s: has no index %y; valid indexes: %y", getDesc(), iname, indexes.keys());

            on_success if (opt.sql_callback_executed)
                indexes.take(iname);

            return AbstractDatabase::doCallback(opt, indexes{iname}.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "index", iname, name);
        }

        #! adds a foreign constraint to the table; if the table is already known to be in the database, then it is added in the database also immediately; otherwise it is only added internally and can be created when create() is called for example
        /** @par Example:
            @code{.py}
string sql;
AbstractForeignConstraint fc = table.addForeignConstraint("fk_mytable_other_table", ("name", "version"), "other_table", NOTHING, \sql);
printf("%s;\n", sql);
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param cname the name of the new foreign constraint
            @param cols a single column name or a list of columns in the local table that make up the foreign constraint
            @param table the name of the other table that the constraint targets
            @param tcols a single column name or a list of columns in the foreign table or @ref nothing meaning that the column names are the same as in the local table; if column names are given the same number of columns must be given in the local and foreign tables
            @param opt a hash of options for the new foreign constraint; each driver may implement its own options; for common options, see @ref SqlUtil::AbstractTable::ForeignConstraintOptions
            @param sql an optional reference to a string to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @return an AbstractForeignConstraint object corresponding to the foreign constraint created

            @throw FOREIGN-CONSTRAINT-ERROR the table already has a constraint with the given name or invalid columns or options were passed

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractForeignConstraint addForeignConstraint(string cname, softlist cols, string table, *softlist tcols, *hash opt, *reference sql) {
            l.lock();
            on_exit l.unlock();

            return addForeignConstraintUnlocked(cname, cols, table, tcols, opt, \sql);
        }

        #! returns an SQL string that can be used to add a foreign constraint to the table
        /** @par Example:
            @code{.py}
string sql = table.getAddForeignConstraintSql("fk_mytable_other_table", ("name", "version"), "other_table");
printf("%s;\n", sql);
            @endcode

            @param cname the name of the new foreign constraint
            @param cols a single column name or a list of columns in the local table that make up the foreign constraint
            @param table the name of the other table that the constraint targets
            @param tcols a single column name or a list of columns in the foreign table or @ref nothing meaning that the column names are the same as in the local table; if column names are given the same number of columns must be given in the local and foreign tables
            @param fkopt a hash of options for the new foreign constraint; each driver may implement its own options; for common options, see @ref SqlUtil::AbstractTable::ForeignConstraintOptions
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return an SQL string that can be used to add a foreign constraint to the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw FOREIGN-CONSTRAINT-ERROR the table already has a constraint with the given name or invalid columns or options were passed

            @see inDb() for a method that tells if the table is already in the database or not

            @note if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)
         */
        string getAddForeignConstraintSql(string cname, softlist cols, string table, *softlist tcols, *hash fkopt, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            string sql;
            AbstractForeignConstraint fk = addForeignConstraintUnlockedIntern(cname, cols, table, tcols, fkopt, \sql);
            on_success if (opt.sql_callback_executed) {
                # update cached tables with foreign constraint source info
                if (opt.table_cache.hasKey(fk.target.table))
                    opt.table_cache{fk.target.table}.addSourceConstraint(getSqlName(), fk);

                # set as foreign constraint for the table
                if (!foreignConstraints)
                    foreignConstraints = new ForeignConstraints();
                foreignConstraints.add(cname, fk);
                manual = True;
            }
            return AbstractDatabase::doCallback(opt, sql, AbstractDatabase::AC_Add, "foreign constraint", cname, getSqlName());
        }

        private Columns getReferencedTableColumnsUnlocked(string table, *Tables cache, string err = "FOREIGN-CONSTRAINT-ERROR") {
            if (table == name) {
                getColumnsUnlocked();
                return columns;
            }
            if (cache.hasKey(table))
                return cache{table}.describe();
            Table t(ds, table);
            if (cache) {
                #printf("*** adding %s to cache\n", name);
                cache.add(table, t.getTable());
            }
            return t.describe();
        }

        private AbstractForeignConstraint addForeignConstraintUnlocked(string cname, softlist cols, string table, *softlist tcols, *hash opt, *reference sql) {
            AbstractForeignConstraint fk = addForeignConstraintUnlockedIntern(cname, cols, table, tcols, opt, \sql);
            execSql(sql);

            # update cached tables with foreign constraint source info
            if (opt.table_cache.hasKey(fk.target.table))
                opt.table_cache{fk.target.table}.addSourceConstraint(getSqlName(), fk);

            # set as foreign constraint for the table
            if (!foreignConstraints)
                foreignConstraints = new ForeignConstraints();
            foreignConstraints.add(cname, fk);
            return fk;
        }

        private AbstractForeignConstraint addForeignConstraintUnlockedIntern(string cname, softlist cols, string table, *softlist tcols, *hash opt, *reference sql) {
            getColumnsUnlocked();
            # load constraints if needed, verify unique constraint name, validate/process options
            checkUniqueConstraintNameValidateOptions("FOREIGN-CONSTRAINT-ERROR", cname, getForeignConstraintOptions(), \opt);

            if (!cols)
                throw "FOREIGN-CONSTRAINT-ERROR", sprintf("%y: no column names passed to %s::addForeignConstraint()", getDesc(), self.className());

            hash ch;
            while (cols) {
                any v = shift cols;
                if (v.typeCode() != NT_STRING)
                    throw "FOREIGN-CONSTRAINT-ERROR", sprintf("%y: %y argument passed as column name to %s::addForeignConstraint(); expecting \"string\" (value: %y)", getDesc(), v.type(), self.className(), v);
                if (!columns.hasKey(v))
                    throw "FOREIGN-CONSTRAINT-ERROR", sprintf("%y: has no column named %y", getDesc(), v);
                ch{v} = columns{v};
            }

            if (!tcols)
                tcols = ch.keys();

            # get target table description
            Columns targ_cols = getReferencedTableColumnsUnlocked(table, opt.table_cache);
            hash tch;
            while (tcols) {
                any v = shift tcols;
                if (v.typeCode() != NT_STRING)
                    throw "FOREIGN-CONSTRAINT-ERROR", sprintf("%y target %y: %y argument passed as target column name to %s::addForeignConstraint(); expecting \"string\" (value: %y)", getDesc(), table, v.type(), self.className(), v);
                if (!targ_cols.hasKey(v))
                    throw "FOREIGN-CONSTRAINT-ERROR", sprintf("%y target %y: target table has no column named %y", getDesc(), table, v);
                tch{v} = targ_cols{v};
            }

            # get the new foreign constraint object
            AbstractForeignConstraint fk = addForeignConstraintImpl(cname, ch, table, tch, opt);

            sql = fk.getCreateSql(getSqlName());

            # return the new foreign constraint
            return fk;
        }

        #! drops a foreign constraint from the table; if the table is known to be in the database already, then it is also dropped from the database immediately; otherwise it is only removed internally
        /** @par Example:
            @code{.py}
table.dropForeignConstraint("uk_mytable_name");
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param cname the name of the foreign constraint to drop
            @param sql an optional reference to a string to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @return the foreign constraint dropped from the table

            @throw FOREIGN-CONSTRAINT-ERROR the given constraint does not exist

            @note this method retrieves all current foreign constraint definitions from the database if none have already been defined

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractForeignConstraint dropForeignConstraint(string cname, *reference sql) {
            l.lock();
            on_exit l.unlock();
            getForeignConstraintsUnlocked();
            if (!foreignConstraints.hasKey(cname))
                throw "FOREIGN-CONSTRAINT-ERROR", sprintf("%s: has no foreign constraint %y; valid foreign constraints: %y", getDesc(), cname, foreignConstraints.keys());

            # drop foreign constraint from table if the table already exists in the DB
            execSql(sql = foreignConstraints{cname}.getDropSql(getSqlName()));

            # remove from foreign constraint structure and return removed foreign constraint
            return foreignConstraints.take(cname);
        }

        #! removes the named foreign constraint from the table; no SQL is executed in any case, only the named foreign constraint is removed from the table definition
        /** @par Example:
            @code{.py}
table.removeForeignConstraint("uk_mytable_name");
            @endcode

            @param cname the name of the foreign constraint to remove

            @return the foreign constraint removed from the table

            @throw FOREIGN-CONSTRAINT-ERROR the given constraint does not exist

            @note this method retrieves all current foreign constraint definitions from the database if none have already been defined

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractForeignConstraint removeForeignConstraint(string cname) {
            l.lock();
            on_exit l.unlock();
            getForeignConstraintsUnlocked();
            if (!foreignConstraints.hasKey(cname))
                throw "FOREIGN-CONSTRAINT-ERROR", sprintf("%s: has no foreign constraint %y; valid foreign constraints: %y", getDesc(), cname, foreignConstraints.keys());

            # remove from foreign constraint structure and return removed foreign constraint
            return foreignConstraints.take(cname);
        }

        #! adds a check constraint to the table; if the table is already known to be in the database, then it is added in the database also immediately; otherwise it is only added internally and can be created when create() is called for example
        /** @par Example:
            @code{.py}
string sql;
AbstractCheckConstraint cc = table.addCheckConstraint("check_mytable_id", "id > 10", NOTHING, \sql);
printf("%s;\n", sql);
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param cname the name of the new constraint
            @param src the source of the constraint clause
            @param opt a hash of options for the new constraint; each driver may implement its own options; for common options, see @ref SqlUtil::AbstractTable::ConstraintOptions
            @param sql an optional reference to a string to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @return an AbstractCheckConstraint object corresponding to the check constraint created

            @throw CHECK-CONSTRAINT-ERROR the table already has a constraint with the given name or invalid columns or options were passed

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractCheckConstraint addCheckConstraint(string cname, string src, *hash opt, *reference sql) {
            l.lock();
            on_exit l.unlock();
            return addCheckConstraintUnlocked(cname, src, opt, \sql);
        }

        #! returns an SQL string that can be used to add a check constraint to the table
        /** @par Example:
            @code{.py}
string sql = table.getAddCheckConstraintSql("check_mytable_id", "id > 10");
printf("%s;\n", sql);
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param cname the name of the new constraint
            @param src the source of the constraint clause
            @param copt a hash of options for the new constraint; each driver may implement its own options; for common options, see @ref SqlUtil::AbstractTable::ConstraintOptions
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return an SQL string that can be used to add a check constraint to the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw CHECK-CONSTRAINT-ERROR the table already has a constraint with the given name or invalid columns or options were passed

            @see inDb() for a method that tells if the table is already in the database or not

            @note if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)
         */
        string getAddCheckConstraintSql(string cname, string src, *hash copt, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            string sql;
            AbstractCheckConstraint cc = addCheckConstraintUnlocked(cname, src, copt, \sql);
            on_success if (opt.sql_callback_executed) {
                # set as constraint for the table
                if (!constraints)
                    constraints = new Constraints();
                constraints.add(cname, cc);
                manual = True;
            }
            return AbstractDatabase::doCallback(opt, sql, AbstractDatabase::AC_Add, "check constraint", cname, getSqlName());
        }

        private AbstractCheckConstraint addCheckConstraintUnlocked(string cname, string src, *hash opt, *reference sql) {
            AbstractCheckConstraint cc = addCheckConstraintUnlockedIntern(cname, src, opt, \sql);
            execSql(sql);

            # set as constraint for the table
            if (!constraints)
                constraints = new Constraints();
            constraints.add(cname, cc);

            # return the new constraint
            return cc;
        }

        private AbstractCheckConstraint addCheckConstraintUnlockedIntern(string cname, string src, *hash opt, *reference sql) {
            # load constraints if needed, verify unique constraint name, validate/process options
            checkUniqueConstraintNameValidateOptions("CHECK-CONSTRAINT-ERROR", cname, getConstraintOptions(), \opt);

            # get the new constraint object
            AbstractCheckConstraint cc = addCheckConstraintImpl(cname, src, opt);

            # get the sql creation string
            sql = cc.getCreateSql(getSqlName());

            # return the new constraint
            return cc;
        }

        #! renames an existing constraint; this can be any constraint on the table, a primary key, a foreign key constraint, or a generic constraint; if the table is already known to be in the database, then the changes are effected in the database also immediately; otherwise it is only updated internally
        /** @par Example:
            @code{.py}
AbstractConstraint con = table.renameConstraint("pk_table_name", "pkey_table_name");
            @endcode

            @param old_name the current name of the constraint
            @param new_name the new name for the constraint
            @param lsql an optional reference to a list of strings to retrieve the SQL used to modify the constraint (only executed if the table is already in the database)
            @return the constraint object that was updated

            @throw CONSTRAINT-ERROR if the old constraint does not exist in the table or if the new constraint name already exists
        */
        AbstractConstraint renameConstraint(string old_name, string new_name, reference lsql) {
            l.lock();
            on_exit l.unlock();
            getAllConstraintsUnlocked();

            AbstractConstraint c;

            code rename;

            if (constraints && constraints.hasKey(old_name)) {
                c = constraints{old_name};
                rename = sub () { constraints.renameKey(old_name, new_name); };
            }
            else {
                if (inDb) {
                    getPrimaryKeyUnlocked();
                    if (!primaryKey.empty() && primaryKey.getName() == old_name) {
                        c = primaryKey;
                        # no need to update any container if the primary key is being updated
                        rename = sub () { };
                    }
                    else {
                        getForeignConstraintsUnlocked();
                        if (foreignConstraints && foreignConstraints.hasKey(old_name)) {
                            c = foreignConstraints{old_name};
                            rename = sub () { foreignConstraints.take(old_name); };
                        }
                    }
                }
            }

            if (!c) {
                list cl = ();
                # get a list of all constraint names
                cl += constraints.keys();
                if (!primaryKey.empty())
                    cl += primaryKey.getName();
                if (foreignConstraints)
                    cl += foreignConstraints.keys();
                throw "CONSTRAINT-ERROR", sprintf("%s: has no constraint %y; valid constraints: %y", getDesc(), old_name, cl);
            }

            # rename foreign constraint in table if the table already exists in the DB
            execSql(lsql = c.getRenameSql(getSqlName(), new_name));

            c.rename(new_name);

            # rename constraint in container structure
            rename();

            # return the updated constraint
            return c;
        }

        #! gets the SQL that can be used to drop a constraint from the table; this can be any constraint on the table, a primary key, a foreign key constraint, or a generic constraint
        /** @par Example:
            @code{.py}
string sql = table.getDropConstraintSql("uk_mytable_name");
            @endcode

            @param cname the name of the constraint to drop
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return the SQL that can be used to drop a constraint from the table; this can be any constraint on the table, a primary key, a foreign key constraint, or a generic constraint

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw CONSTRAINT-ERROR the given constraint does not exist in the table

            @note
            - this method retrieves current constraint definitions from the database if not already loaded or defined
            - if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)

            @see inDb() for a method that tells if the table is already in the database or not
         */
        string getDropConstraintSql(string cname, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            code rmv;
            AbstractConstraint c = findDropConstraintUnlocked(cname, \rmv);

            on_success if (opt.sql_callback_executed)
                rmv();

            return AbstractDatabase::doCallback(opt, c.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "constraint", c.getName(), name);
        }

        #! gets the SQL that can be used to drop a constraint from the table if it exists, otherwise returns @ref nothing; this can be any constraint on the table, a primary key, a foreign key constraint, or a generic constraint
        /** @par Example:
            @code{.py}
*string sql = table.getDropConstraintIfExistsSql("uk_mytable_name");
            @endcode

            @param cname the name of the constraint to drop
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options
            @param cref an optional reference to an AbstractConstraint object that will return the constraint dropped

            @return the SQL that can be used to drop a constraint from the table if it exists, otherwise returns @ref nothing; this can be any constraint on the table, a primary key, a foreign key constraint, or a generic constraint

            @throw OPTION-ERROR invalid or unsupported option passed

            @note
            - this method retrieves current constraint definitions from the database if not already loaded or defined
            - if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)

            @see inDb() for a method that tells if the table is already in the database or not
         */
        *string getDropConstraintIfExistsSql(string cname, *hash opt, *reference cref) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            code rmv;
            AbstractConstraint c;
            try {
                c = findDropConstraintUnlocked(cname, \rmv);
                cref = c;
            }
            catch (hash ex) {
                if (ex.err == "CONSTRAINT-ERROR")
                    return;
                rethrow;
            }

            on_success if (opt.sql_callback_executed)
                rmv();

            return AbstractDatabase::doCallback(opt, c.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "constraint", c.getName(), name);
        }

        private AbstractConstraint findDropConstraintUnlocked(string cname, reference rmv) {
            getAllConstraintsUnlocked();

            AbstractConstraint c;

            if (constraints && constraints.hasKey(cname)) {
                c = constraints{cname};
                rmv = sub () {
                    constraints.take(cname);
                    # if it's a unique constraint, remove any index with the same name
                    if (c instanceof AbstractUniqueConstraint && constraintsLinkedToIndexesImpl())
                        indexes.take(cname);
                };
            }
            else if (!primaryKey.empty() && primaryKey.getName() == cname) {
                c = primaryKey;
                rmv = sub () {
                    remove self.primaryKey;
                    # remove any index with the same name
                    if (constraintsLinkedToIndexesImpl())
                        indexes.take(cname);
                };
            }
            else if (foreignConstraints && foreignConstraints.hasKey(cname)) {
                c = foreignConstraints{cname};
                rmv = sub () {
                    foreignConstraints.take(cname);
                };
            }

            if (!c) {
                list cl = ();
                # get a list of all constraint names
                cl += constraints.keys();
                if (!primaryKey.empty())
                    cl += primaryKey.getName();
                if (foreignConstraints)
                    cl += foreignConstraints.keys();
                throw "CONSTRAINT-ERROR", sprintf("%s: has no constraint %y; valid constraints: %y", getDesc(), cname, cl);
            }

            return c;
        }

        #! drops a constraint from the table; this can be any constraint on the table, a primary key, a foreign key constraint, or a generic constraint; if the table is known to be in the database already, then it is also dropped from the database immediately; otherwise it is only removed internally
        /** @par Example:
            @code{.py}
table.dropConstraint("uk_mytable_name");
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param cname the name of the constraint to drop
            @param sql an optional reference to a string to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @return the constraint dropped from the table

            @throw CONSTRAINT-ERROR the given constraint does not exist in the table

            @note this method retrieves current constraint definitions from the database if not already loaded or defined

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractConstraint dropConstraint(string cname, *reference sql) {
            l.lock();
            on_exit l.unlock();

            code rmv;
            AbstractConstraint c = findDropConstraintUnlocked(cname, \rmv);

            # drop foreign constraint from table if the table already exists in the DB
            execSql(sql = c.getDropSql(getSqlName()));

            # remove from foreign constraint structure and return removed foreign constraint
            rmv();
            return c;
        }

        #! adds a trigger to the table; if the table is already known to be in the database, then it is added in the database also immediately; otherwise it is only added internally and can be created when create() is called for example
        /** @par Example:
            @code{.py}
string sql;
AbstractTrigger trig = table.addTrigger("trig_mytable", "", NOTHING, \sql);
printf("%s;\n", sql);
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param tname the name of the new trigger
            @param src the source of the trigger
            @param opt a hash of options for the new trigger; each driver may implement its own options; for common options, see @ref SqlUtil::AbstractTable::TriggerOptions
            @param lsql an optional reference to a list of strings to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @return an AbstractTrigger object corresponding to the trigger created

            @throw TRIGGER-ERROR the table already has a trigger with the given name or invalid options were passed

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractTrigger addTrigger(string tname, string src, *hash opt, *reference lsql) {
            l.lock();
            on_exit l.unlock();

            return addTriggerUnlocked(tname, src, opt, \lsql);
        }

        #! returns a list of SQL strings that can be used to add a trigger to the table
        /** @par Example:
            @code{.py}
string sql = table.getAddTriggerSql("trig_mytable", trigger_src);
printf("%s;\n", sql);
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param tname the name of the new trigger
            @param src the source of the trigger
            @param topt a hash of options for the new trigger; each driver may implement its own options; for common options, see @ref SqlUtil::AbstractTable::TriggerOptions
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return a list of SQL strings that can be used to add a trigger to the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw TRIGGER-ERROR the table already has a trigger with the given name or invalid options were passed

            @see inDb() for a method that tells if the table is already in the database or not

            @note if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)
         */
        list getAddTriggerSql(string tname, string src, *hash topt, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            list lsql;
            AbstractTrigger trig = addTriggerUnlockedIntern(tname, src, topt, \lsql);
            on_success if (opt.sql_callback_executed) {
                # store trigger in trigger container
                if (!triggers)
                    triggers = new Triggers();

                triggers.add(tname, trig);
                manual = True;
            }
            return AbstractDatabase::doCallback(opt, lsql, AbstractDatabase::AC_Add, "trigger", tname, getSqlName());
        }

        private AbstractTrigger addTriggerUnlocked(string tname, string src, *hash opt, *reference lsql) {
            AbstractTrigger trig = addTriggerUnlockedIntern(tname, src, opt, \lsql);
            execSql(lsql);

            # store trigger in trigger container
            if (!triggers)
                triggers = new Triggers();

            triggers.add(tname, trig);

            # return the new trigger
            return trig;
       }

        private AbstractTrigger addTriggerUnlockedIntern(string tname, string src, *hash opt, *reference lsql) {
            # load triggers if needed
            if (!manual)
                getTriggersUnlocked();

            # verify unique trigger name
            if (triggers && triggers.hasKey(tname))
                throw "TRIGGER-ERROR", sprintf("%y: already has a trigger named %y, drop the trigger before adding it", getDesc(), tname);

            # validate/process options
            validateOptionsIntern("TRIGGER-ERROR", getTriggerOptions(), \opt);

            # get the new constraint object
            AbstractTrigger trig = addTriggerImpl(tname, src, opt);

            lsql = trig.getCreateSql(getSqlName());

            # return the new trigger
            return trig;
        }

        #! drops the given trigger from the table; if the table is known to be in the database already, then it is also dropped from the database immediately; otherwise it is only removed internally
        /** @par Example:
            @code{.py}
table.dropTrigger("trig_mytable");
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param tname the name of the trigger to drop
            @param sql an optional reference to a string to retrieve the SQL used to modify the database (only executed if the table is already in the database)

            @return the trigger dropped from the table

            @throw TRIGGER-ERROR the given trigger does not exist

            @note this method retrieves all current trigger definitions from the database if none have already been defined

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractTrigger dropTrigger(string tname, *reference sql) {
            l.lock();
            on_exit l.unlock();
            getTriggersUnlocked();
            if (!triggers.hasKey(tname))
                throw "TRIGGER-ERROR", sprintf("%s: has no trigger %y; valid triggers: %y", getDesc(), tname, triggers.keys());

            # drop trigger from table if the table already exists in the DB
            execSql(sql = triggers{tname}.getDropSql(getSqlName()));

            # remove from trigger structure and return removed trigger
            return triggers.take(tname);
        }

        #! returns SQL that can be used to drop the given trigger from the table
        /** @par Example:
            @code{.py}
string sql = table.getDropTriggerSql("trig_mytable");
            @endcode

            @param tname the name of the trigger to drop
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return the SQL that can be used to drop the given trigger from the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw TRIGGER-ERROR the given trigger does not exist

            @note
            - this method retrieves all current trigger definitions from the database if none have already been defined
            - if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)

            @see inDb() for a method that tells if the table is already in the database or not
         */
        list getDropTriggerSql(string tname, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            getTriggersUnlocked();
            if (!triggers.hasKey(tname))
                throw "TRIGGER-ERROR", sprintf("%s: has no trigger %y; valid triggers: %y", getDesc(), tname, triggers.keys());

            # remove from trigger structure if sql is executed
            on_success if (opt.sql_callback_executed)
                triggers.take(tname);

            # drop trigger from table if the table already exists in the DB
            return AbstractDatabase::doCallback(opt, triggers{tname}.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "trigger", tname, getSqlName());
        }

        private getAllConstraintsUnlocked(*hash opt) {
            if (manual)
                return;

            getPrimaryKeyUnlocked();
            getForeignConstraintsUnlocked(opt);
            getConstraintsUnlocked();
        }

        private checkUniqueConstraintName(string err, string cname) {
            getAllConstraintsUnlocked();
            if (foreignConstraints.hasKey(cname))
                throw err, printf("%y: already has a foreign constraint named %y with columns: %y", name, cname, foreignConstraints{cname}.columns.keys());

            if (constraints.hasKey(cname))
                throw err, printf("%y: already has a constraint named %y", name, cname);

            if (!primaryKey.empty() && primaryKey.getName() == cname)
                throw err, printf("%y: already has a primary key constraint named %y with columns: %y", name, cname, primaryKey.keys());
        }

        private checkUniqueConstraintNameValidateOptions(string err, string cname, hash ropt, reference opt) {
            checkUniqueConstraintName(err, cname);
            # validate options
            validateOptionsIntern(err, ropt, \opt);
        }

        #! validates column options
        private validateColumnOptions(string cname, reference opt, bool nullable) {
            hash copt = getColumnDescOptions();

            # check valid options and option value types
            validateOptionsIntern("COLUMN-ERROR", copt, \opt, sprintf("%s.%s", name, cname));

            hash otm = getTypeMapImpl();
            hash th;

            # first try to handle non-registered type (like eg. Oracle's objects)
            if (opt.native_type && !otm.hasKey(opt.native_type) && !opt.qore_type) {
                opt.qore_type = "any";
            }
            else {
                # get native_type from registered maps
                if (!opt.native_type) {
                    hash qtm = getQoreTypeMapImpl();
                    opt.native_type = qtm{opt.qore_type};
                    if (!opt.native_type)
                        throw "COLUMN-ERROR", sprintf("%s column %y: qore type %y cannot be mapped to a column type (valid values: %y)", getDesc(), cname, opt.qore_type, qtm.keys());
                }

                # get native type description
                th = otm{opt.native_type};

                # reset qore type from native type
                opt.qore_type = th.qore;
                opt.local_def = th;
            }

            if (exists opt.size) {
                if (!th.size)
                    throw "COLUMN-ERROR", sprintf("%s column %y: size %y given but type %y does not take a size attribute", getDesc(), cname, opt.size, opt.native_type);
                if (th.size_range && (opt.size < th.size_range[0] || opt.size > th.size_range[1]))
                    throw "COLUMN-ERROR", sprintf("%s column %y: size %y is not valid for type %y; must be between %d and %d inclusive", getDesc(), cname, opt.size, opt.native_type, th.size_range[0], th.size_range[1]);
            }
            else {
                if (th.size == SZ_MAND)
                    throw "COLUMN-ERROR", sprintf("%s column %y: type %y requires a size argument but none was supplied (options: %y)", getDesc(), cname, opt.native_type, opt);
                opt.size = 0;
            }

            if (th.size == SZ_NUM) {
                opt.precision = opt.size;
                opt.size = 0;
                if (opt.scale) {
                    if (th.scale_range && (opt.scale < th.scale_range[0] || opt.scale > th.scale_range[1]))
                        throw "COLUMN-ERROR", sprintf("%s column %y: scale %y is not valid for type %y; must be between %d and %d inclusive", getDesc(), cname, opt.scale, opt.native_type, th.scale_range[0], th.scale_range[1]);
                }
                else
                    opt.scale = 0;
            }
            else if (opt.scale)
                throw "COLUMN-ERROR", sprintf("%s column %y: scale %y is not valid for type %y; it is not a numeric type", getDesc(), cname, opt.scale, opt.native_type);
            else
                opt.precision = opt.scale = 0;

            if (!opt.size && th.default_size)
                opt.size = th.default_size;

            # check type of default value (must match column type)
            if (exists opt.default_value && !opt.default_value_native) {
                checkValue(cname, "default_value", \opt.default_value, opt.qore_type);
                opt.default_value = getSqlValue(opt.default_value);
            }
        }

        #! drops a column from the table
        /** @par Example:
            @code{.py}
table.dropColumn("notes_2");
            @endcode

            In case the table is already in the database, this method commits the transaction on success and rolls back the transaction if there's an error.

            @param cname the name of the column to drop
            @param lsql an optional reference to a list of strings to retrieve the SQL used to modify the table (only executed if the table is already in the database)

            @return the AbstractColumn object representing the column dropped from the table

            @throw COLUMN-ERROR the named column is not present in the table

            @note this method retrieves the table definition from the database if none has already been defined

            @see inDb() for a method that tells if the table is already in the database or not
         */
        AbstractColumn dropColumn(string cname, *reference lsql) {
            l.lock();
            on_exit l.unlock();
            getColumnsUnlocked();
            if (!columns.hasKey(cname))
                throw "COLUMN-ERROR", sprintf("%s column %y: no such column (valid columns: %y)", getDesc(), cname, columns.keys());

            code sc;
            if (inDb) {
                sc = sub (string str) {
                    ds.execRaw(str);
                };
            }

            # if there are any indexes or constraints on this column, remove them from the table first
            lsql = getDropAllConstraintsAndIndexesOnColumnSql(cname, ("sql_callback": sc, "sql_callback_executed": True));

            # drop column from table if the table already exists in the DB
            execSql(lsql += columns{cname}.getDropSql(getSqlName()));
            return columns.take(cname);
        }

        #! returns the SQL that can be used to drop a column from the table
        /** @par Example:
            @code{.py}
string sql = table.getDropColumnSql("notes_2");
            @endcode

            @param cname the name of the column to drop
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return the SQL that can be used to drop a column from the table

            @throw OPTION-ERROR invalid or unsupported option passed
            @throw COLUMN-ERROR the named column is not present in the table

            @note
            - this method retrieves the column definitions from the database if none have already been defined
            - if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)

            @see inDb() for a method that tells if the table is already in the database or not
         */
        list getDropColumnSql(string cname, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();
            getColumnsUnlocked();
            if (!columns.hasKey(cname))
                throw "COLUMN-ERROR", sprintf("%s column %y: no such column (valid columns: %y)", getDesc(), cname, columns.keys());

            on_success if (opt.sql_callback_executed)
                columns.take(cname);

            list lst = ();
            lst += getDropAllConstraintsAndIndexesOnColumnSqlUnlocked(cname, opt);
            lst += AbstractDatabase::doCallback(opt, columns{cname}.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "column", cname, name);
            return lst;
        }

        #! inserts a row into the table; the transaction is committed if successful, if an error occurs, it is rolled back
        /** @par Example:
            @code{.py}
table.insertCommit(row);
            @endcode

            @param row a hash representing the row to insert; hash values can also be set with @ref sql_iop_funcs to insert values based on SQL operations to be used directly in the insert statement
            @param sql an optional reference to a string to return the SQL generated for the insert statement
            @param opt optional insert options; see @ref AbstractTable::InsertOptions for more info

            @return in case the \c "returning" @ref AbstractTable::InsertOptions "insert option" is used, a hash of return values is returned, otherwise @ref nothing is returned

            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted
         */
        *hash insertCommit(hash row) {
            on_success ds.commit();
            on_error ds.rollback();

            return insertIntern(row);
        }

        #! @ref SqlUtil::AbstractTable::insertCommit() variant
        *hash insertCommit(hash row, reference sql) {
            on_success ds.commit();
            on_error ds.rollback();

            return insertIntern(row, \sql);
        }

        #! @ref SqlUtil::AbstractTable::insertCommit() variant
        *hash insertCommit(hash row, hash opt) {
            on_success ds.commit();
            on_error ds.rollback();

            return insertIntern(row, NOTHING, opt);
        }

        #! @ref SqlUtil::AbstractTable::insertCommit() variant
        *hash insertCommit(hash row, reference sql, hash opt) {
            on_success ds.commit();
            on_error ds.rollback();

            return insertIntern(row, \sql, opt);
        }

        #! inserts a row into the table without any transaction management; a transaction will be in progress after this method is successfully executed
        /** @ingroup inserts
            @par Example:
            @code{.py}
table.insert(row);
            @endcode

            @param row a hash representing the row to insert; hash values can also be set with @ref sql_iop_funcs to insert values based on SQL operations to be used directly in the insert statement
            @param sql an optional reference to a string to return the SQL generated for the insert statement
            @param opt optional insert options; see @ref AbstractTable::InsertOptions for more info

            @return in case the \c "returning" @ref AbstractTable::InsertOptions "insert option" is used, a hash of return values is returned, otherwise @ref nothing is returned

            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted
         */
        *hash insert(hash row) {
            return insertIntern(row);
        }

        #! @ref SqlUtil::AbstractTable::insert() variant
        *hash insert(hash row, reference sql) {
            return insertIntern(row, \sql);
        }

        #! @ref SqlUtil::AbstractTable::insert() variant
        *hash insert(hash row, hash opt) {
            return insertIntern(row, NOTHING, opt);
        }

        #! @ref SqlUtil::AbstractTable::insert() variant
        *hash insert(hash row, reference sql, hash opt) {
            return insertIntern(row, \sql, opt);
        }

        #! A legacy wrapper for @ref SqlUtil::AbstractTable::insert()
        deprecated *hash insertNoCommit(hash row, *reference sql, *hash opt) {
            return insertIntern(row, \sql, opt);
        }

        #! A legacy wrapper for @ref SqlUtil::AbstractTable::insert()
        deprecated *hash insertNoCommit(hash row, hash opt) {
            return insertIntern(row, NOTHING, opt);
        }

        private *hash insertIntern(hash row, *reference sql, *hash opt) {
            # check data callback options if any
            validateOptionsIntern("OPTION-ERROR", getInsertOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            getColumnsUnlocked();
            sql = sprintf("insert into %s (", getSqlName());
            foreach string k in (row.keyIterator()) {
                if (!columns.hasKey(k))
                    throw "COLUMN-ERROR", sprintf("%s column %y is not a valid column (valid columns: %y)", getDesc(), k, columns.keys());
            }
            hash vh = getPlaceholdersAndValues(row);

            sql += (foldl $1 + "," + $2, (map getColumnSqlName($1), row.keyIterator()));
            sql += ") values (";
            sql += (foldl $1 + "," + $2, vh.placeholders);
            sql += ")";

            softlist args = vh.values;

            # check for a bulk insert in case the driver does not support array binding
            if (!hasArrayBind()) {
                int size = 0;
                foreach any arg in (args) {
                    if (arg.typeCode() == NT_LIST) {
                        size = arg.size();
                        break;
                    }
                }
                if (size) {
                     hash rv;
                     for (int i = 0; i < size; ++i) {
                         # get arg list for row
                         list targs = map $1.typeCode() == NT_LIST ? $1[i] : $1, args;
                         if (opt.returning) {
                             if (!rv) {
                                 hash h = doReturningImpl(opt, \sql, targs);
                                 map rv{$1.key} += ($1.value,), h.pairIterator();
                             }
                             else {
                                 hash h = execData(opt, sql, targs);
                                 map rv{$1.key} += $1.value, h.pairIterator();
                             }
                         }
                         else
                             execData(opt, sql, targs);
                     }
                     return rv;
                }
            }

            if (opt.returning)
                return doReturningImpl(opt, \sql, args);

            execData(opt, sql, args);
        }

        private hash getPlaceholdersAndValues(hash row) {
            hash im;
            # placeholder list
            list phl = ();
            # value list
            list vl = ();

            HashIterator i(row);
            while (i.next()) {
                string key = i.getKey();
                any v = i.getValue();
                string ph = "%v";
                if (v.typeCode() == NT_LIST)
                    v = v[0];
                if (v."_iop".typeCode() == NT_STRING) {
                    string iop = v."_iop";
                    if (!exists im)
                        im = getInsertOperatorMap();
                    *hash cmd = im{iop};
                    if (!cmd)
                        throw "INSERT-OPERATOR-ERROR", sprintf("%s: unknown insert operator %y: expecting one of: %y", getDesc(), iop, im.keys());
                    v = v.arg;
                    if (cmd.arg) {
                        string at = v.type();
                        switch (cmd.arg) {
                            default: {
                                if (at != cmd.arg)
                                    throw "INSERT-OPERATOR-ERROR", sprintf("%s: invalid argument to insert operator %y; got type %y, expecting %y", getDesc(), iop, at, cmd.arg);
                            }
                        }
                    }

                    if (cmd.code)
                        v = cmd.code(key, v);
                    if (cmd.immediate) {
                        # make sure only identifiers are included here since the string is inserted directly into the query
                        if (v =~ /[^a-z\._0-9]/i)
                            throw "INSERT-OPERATOR-ERROR", sprintf("%s: immediate value to insert operator %y is %y which contains illegal characters", getDesc(), iop, v);
                        phl += v;
                        continue;
                    }
                    if (cmd.placeholder)
                        ph = cmd.placeholder;
                    phl += ph;
                    push vl, v;
                    continue;
                }
                phl += ph;
                push vl, i.getValue();
            }

            return (
                "placeholders": phl,
                "values": vl,
            );
        }

        #! returns @ref Qore::True "True" if the current database driver supports the \c "returning" clause in insert statements, @ref Qore::False "False" if not
        /** @return @ref Qore::True "True" if the current database driver supports the \c "returning" clause in insert statements, @ref Qore::False "False" if not
            @since %SqlUtil 1.3
        */
        bool hasReturning() {
            return hasReturningImpl();
        }

        #! inserts rows into a table based on a select statement from another table (which must be using the same datasource as the current table); the transaction is committed if successful, if an error occurs, it is rolled back
        /** @par Example:
            @code{.py}
int rows = table.insertFromSelectCommit(("id", "name", "created"), source_table, (("columns": ("id", "name", "created"), "where": ("type": "CUSTOMER"))));
            @endcode

            @param cols the list of column names to use to insert in the current table
            @param source the source table for the select statement
            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param sql an optional reference to a string to return the SQL generated for the select statement
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more inf

            @return the number of rows inserted

            @throw OPTION-ERROR invalid or unsupported option
            @throw COLUMN-ERROR unknown or invalid column in insert list
            @throw SELECT-ERROR \c 'offset' supplied without \c 'orderby' or \c 'limit', \c 'orderby' with \c 'limit' and \c 'offset' does not match any unique constraint

            @note this method does not take insert options because it is executed entirely in the database server; use insertFromIterator() or insertFromIteratorCommit() to insert arbitrary data with insert options
        */
        int insertFromSelectCommit(list cols, AbstractTable source, hash sh, reference sql, hash opt) {
            on_success ds.commit();
            on_error ds.rollback();

            return insertFromSelectIntern(cols, source, sh, \sql);
        }

        #! @ref SqlUtil::AbstractTable::insertFromSelectCommit() variant
        int insertFromSelectCommit(list cols, AbstractTable source) {
            on_success ds.commit();
            on_error ds.rollback();

            return insertFromSelectIntern(cols, source);
        }

        #! @ref SqlUtil::AbstractTable::insertFromSelectCommit() variant
        int insertFromSelectCommit(list cols, AbstractTable source, hash sh) {
            on_success ds.commit();
            on_error ds.rollback();

            return insertFromSelectIntern(cols, source, sh);
        }

        #! @ref SqlUtil::AbstractTable::insertFromSelectCommit() variant
        int insertFromSelectCommit(list cols, AbstractTable source, hash sh, reference sql) {
            on_success ds.commit();
            on_error ds.rollback();

            return insertFromSelectIntern(cols, source, sh, \sql);
        }

        #! @ref SqlUtil::AbstractTable::insertFromSelectCommit() variant
        int insertFromSelectCommit(list cols, AbstractTable source, hash sh, hash opt) {
            on_success ds.commit();
            on_error ds.rollback();

            return insertFromSelectIntern(cols, source, sh, NOTHING, opt);
        }

        #! inserts rows into a table based on a select statement from another table (which must be using the same datasource as the current table); a transaction will be in progress after this method is successfully executed
        /** @par Example:
            @code{.py}
int rows = table.insertFromSelect(("id", "name", "created"), source_table, (("columns": ("id", "name", "created"), "where": ("type": "CUSTOMER"))));
            @endcode

            @param cols the list of column names to use to insert in the current table
            @param source the source table for the select statement
            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param sql an optional reference to a string to return the SQL generated for the select statement
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more inf

            @return the number of rows inserted

            @throw OPTION-ERROR invalid or unsupported option
            @throw COLUMN-ERROR unknown or invalid column in insert list
            @throw SELECT-ERROR \c 'offset' supplied without \c 'orderby' or \c 'limit', \c 'orderby' with \c 'limit' and \c 'offset' does not match any unique constraint

            @note this method does not take insert options because it is executed entirely in the database server; use insertFromIterator() or insertFromIteratorCommit() to insert arbitrary data with insert options
        */
        int insertFromSelect(list cols, AbstractTable source, hash sh, reference sql, hash opt) {
            return insertFromSelectIntern(cols, source, sh, \sql, opt);
        }

        #! @ref SqlUtil::AbstractTable::insertFromSelectCommit() variant
        int insertFromSelect(list cols, AbstractTable source) {
            return insertFromSelectIntern(cols, source);
        }

        #! @ref SqlUtil::AbstractTable::insertFromSelectCommit() variant
        int insertFromSelect(list cols, AbstractTable source, hash sh) {
            return insertFromSelectIntern(cols, source, sh);
        }

        #! @ref SqlUtil::AbstractTable::insertFromSelectCommit() variant
        int insertFromSelect(list cols, AbstractTable source, hash sh, reference sql) {
            return insertFromSelectIntern(cols, source, sh, \sql);
        }

        #! @ref SqlUtil::AbstractTable::insertFromSelectCommit() variant
        int insertFromSelect(list cols, AbstractTable source, hash sh, hash opt) {
            return insertFromSelectIntern(cols, source, sh, NOTHING, opt);
        }

        #! A legacy @ref SqlUtil::AbstractTable::insertFromSelect() wrapper
        deprecated int insertFromSelectNoCommit(list cols, AbstractTable source, *hash sh, *reference sql, *hash opt) {
            return insertFromSelectIntern(cols, source, sh, \sql, opt);
        }

        private int insertFromSelectIntern(list cols, AbstractTable source, *hash sh, *reference sql, *hash opt) {
            list args;
            string ssql = source.getSelectSql(sh, \args);

            l.lock();
            on_exit l.unlock();
            getColumnsUnlocked();

            sql = sprintf("insert into %s (", getSqlName());

            foreach string k in (cols) {
                if (!columns.hasKey(k))
                    throw "COLUMN-ERROR", sprintf("%s column %y is not a valid column (valid columns: %y)", getDesc(), k, cols);
            }

            sql += (foldl $1 + "," + $2, getColumnSqlNames(cols));
            sql += ") " + ssql;

            return execData(opt, sql, args);
        }

        #! this method inserts data from the given iterator argument (whose @ref Qore::AbstractIterator::getValue() "getValue()" method must return a hash giving row values) into the current table; the transaction is committed if successful, if an error occurs then it is rolled back
        /** @par Example:
            @code{.py}
int rows = table.insertFromIteratorCommit(i);
            @endcode

            @param i the @ref Qore::AbstractIterator argument that will provide the data to insert into the current table; the @ref Qore::AbstractIterator::getValue() "getValue()" method of this object must return a hash giving row values (for example, the @ref Qore::SQL::SQLStatement class is well-suited to be used here); note that the iterator is assumed to return the same columns in every element
            @param opt a hash of options for the insert operation; see @ref SqlUtil::AbstractTable::InsertFromIteratorOptions for common options; each driver can support additional driver-specific options

            @return the number of rows inserted

            @throw OPTION-ERROR invalid or unsupported option
            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted

            @see
            - insertFromIterator()
            - upsertFromIterator()
            - upsertFromIteratorCommit()
        */
        int insertFromIteratorCommit(Qore::AbstractIterator i, *hash opt) {
            on_success ds.commit();
            on_error ds.rollback();

            return insertFromIteratorIntern(i, opt);
        }

        #! this method inserts data from the given iterator argument (whose @ref Qore::AbstractIterator::getValue() "getValue()" method must return a hash giving row values) into the current table; no transaction management is performed with this method
        /** @par Example:
            @code{.py}
int rows = table.insertFromIterator(i);
            @endcode

            @param i the @ref Qore::AbstractIterator argument that will provide the data to insert into the current table; the @ref Qore::AbstractIterator::getValue() "getValue()" method of this object must return a hash giving row values (for example, the @ref Qore::SQL::SQLStatement class is well-suited to be used here); note that the iterator is assumed to return the same columns in the same order in every element
            @param opt a hash of options for the insert operation; see @ref SqlUtil::AbstractTable::InsertFromIteratorOptions for common options; each driver can support additional driver-specific options; note that this method ignores any \c "commit_block" option

            @return the number of rows inserted

            @throw OPTION-ERROR invalid or unsupported option
            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted

            @see
            - insertFromIterator()
            - upsertFromIterator()
            - upsertFromIteratorCommit()
        */
        int insertFromIterator(Qore::AbstractIterator i, *hash opt) {
            return insertFromIteratorIntern(i, opt - "commit_block");
        }

        #! A legacy @ref SqlUtil::AbstractTable::insertFromIterator() wrapper
        deprecated int insertFromIteratorNoCommit(Qore::AbstractIterator i, *hash opt) {
            return insertFromIteratorIntern(i, opt - "commit_block");
        }

        private int insertFromIteratorIntern(Qore::AbstractIterator i, *hash opt) {
            # check upsert options
            validateOptionsIntern("OPTION-ERROR", getInsertFromIteratorOptions(), \opt);

            if (!i.next())
                return 0;

            # get initial row
            hash row = i.getValue();

            # make insert statement template
            string sql = sprintf("insert into %s (%s) values (%s)", getSqlName(), (foldl $1 + "," + $2, (map getColumnSqlName($1), row.keyIterator())),
                                 (foldl $1 + "," + $2, (map "%v", row.keyIterator())));
            SQLStatement stmt(ds);
            stmt.prepare(sql);

            opt.change_count = 0;
            int rc = 0;
            do {
                execData(stmt, opt, row.values());
                if (opt.info_callback)
                    opt.info_callback(getSqlName(), row, UR_Inserted);
                ++rc;

                if (opt.commit_block) {
                    if (++opt.change_count >= opt.commit_block) {
                        ds.commit();
                        opt.change_count = 0;
                    }
                }
            } while (i.next() && (row = i.getValue()));

            return rc;
        }

        #! update or insert the data in the table according to the hash argument; the table must have a unique key to do this; the transaction is committed if successful, if an error occurs then it is rolled back
        /** @par Example:
            @code{.py}
table.upsertCommit(row);
            @endcode

            @param row a hash representing the row to insert or update
            @param upsert_strategy see @ref upsert_options for possible values for the upsert strategy
            @param opt a hash of options for the upsert operation; see @ref SqlUtil::AbstractTable::UpsertOptions for common options; each driver can support additional driver-specific options

            @return an integer code giving the result of the update; see @ref upsert_results for more information

            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted
            @throw UPSERT-ERROR no primary key, unique constraint, or unique index for upsert; not all columns of the unique constraint/index are used in the upsert statement

            @note if upserting multiple rows; it's better to use getBulkUpsertClosure(), getUpsertClosure(), or getUpsertClosureWithValidation() and execute the closure on each row; when using this method, the overhead for setting up the upsert is made for each row which is very inefficient
         */
        int upsertCommit(hash row, int upsert_strategy = UpsertAuto, *hash opt) {
            on_success ds.commit();
            on_error ds.rollback();

            return upsert(row, upsert_strategy, opt);
        }

        #! update or insert the data in the table according to the hash argument; the table must have a unique key to do this
        /** @par Example:
            @code{.py}
table.upsert(row);
            @endcode

            @param row a hash representing the row to insert or update
            @param upsert_strategy see @ref upsert_options for possible values for the upsert strategy
            @param opt a hash of options for the upsert operation; see @ref SqlUtil::AbstractTable::UpsertOptions for common options; each driver can support additional driver-specific options

            @return an integer code giving the result of the update; see @ref upsert_results for more information

            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted
            @throw UPSERT-ERROR no primary key, unique constraint, or unique index for upsert; not all columns of the unique constraint/index are used in the upsert statement

            @note if upserting multiple rows; it's better to use getBulkUpsertClosure(), getUpsertClosure(), or getUpsertClosureWithValidation() and execute the closure on each row; when using this method, the overhead for setting up the upsert is made for each row which is very inefficient
         */
        int upsert(hash row, int upsert_strategy = UpsertAuto, *hash opt) {
            return getUpsertClosure(row, upsert_strategy, opt)(row);
        }

        #! A legacy @ref SqlUtil::AbstractTable::upsert() wrapper
        deprecated int upsertNoCommit(hash row, int upsert_strategy = UpsertAuto) {
            return getUpsertClosure(row, upsert_strategy)(row);
        }

        #! returns a closure that can be executed given a hash argument representing a single row that will be updated or inserted in the database with the given upsert strategy; the table must have a unique key to do this; the closure returned does not check the input hash for validity
        /** @par Example:
            @code{.py}
Datasource ds("pgsql:user/pass@db%localhost");
Table table(ds, "table_name");
code upsert = table.getUpsertClosure(row, AbstractTable::UpsertSelectFirst);
on_success ds.commit();
on_error ds.rollback();
map upsert($1), row_list;
            @endcode

            @param row a hash representing an example row to insert or update; every row passed to the upsert closure returned must have the same keys in the same order
            @param upsert_strategy see @ref upsert_options for possible values for the upsert strategy
            @param opt a hash of options for the upsert operation; see @ref SqlUtil::AbstractTable::UpsertOptions for common options; each driver can support additional driver-specific options

            @return a closure that can be executed given a hash argument representing a single row that will be updated or inserted in the database with the given upsert strategy; the closure returned does not check the input hash for validity; the closure has the following signature: @code{.py} int sub upsert(hash row) {} @endcode The return value of the closure is an integer code giving the result of the update; see @ref upsert_results for more information

            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted
            @throw UPSERT-ERROR no primary key, unique constraint, or unique index for upsert; not all columns of the unique constraint/index are used in the upsert statement

            @note the row values passed to the closure for upserting are not checked if they match the example row passed to the getUpsertClosure() method; passing non-conforming data will cause errors; see @ref SqlUtil::AbstractTable::getUpsertClosureWithValidation() for a similar method that returns a validating closure; the closure returned by this method is faster than the one returned by @ref SqlUtil::AbstractTable::getUpsertClosure() since there is no validation
         */
        code getUpsertClosure(hash row, int upsert_strategy = UpsertAuto, *hash opt) {
            # check upsert options
            validateOptionsIntern("OPTION-ERROR", getUpsertOptions(), \opt);

            if (upsert_strategy == UpsertAuto)
                upsert_strategy = emptyDataIntern() ? UpsertInsertFirst : UpsertUpdateFirst;

            # accommodate for bulk DML
            if (row.firstValue().typeCode() == NT_LIST)
                row = map {$1.key : $1.value[0]}, row.pairIterator();

            Columns cols = checkUpsertRow(row, \upsert_strategy);

            switch (upsert_strategy) {
                case UpsertInsertFirst:
                    return getUpsertInsertFirst(cols, row, opt);

                case UpsertUpdateFirst:
                    return getUpsertUpdateFirst(cols, row, opt);

                case UpsertSelectFirst:
                    return getUpsertSelectFirst(cols, row, opt);

                case UpsertInsertOnly:
                    return getUpsertInsertOnly(cols, row, opt);

                case UpsertUpdateOnly:
                    return getUpsertUpdateOnly(cols, row, opt);
            }

            throw "UPSERT-ERROR", sprintf("%s: invalid upsert code: %d", getDesc(), upsert_strategy);
        }

        #! returns a closure that can be executed given a hash argument representing either a single row or a set of rows (where each key value is a list of column values) that will be updated or inserted in the database with the given upsert strategy; the table must have a unique key to do this; the closure returned does not check the input hash for validity
        /** @par Example:
            @code{.py}
Datasource ds("pgsql:user/pass@db%localhost");
Table table(ds, "table_name");
code upsert = table.getBulkUpsertClosure(row, AbstractTable::UpsertSelectFirst);
on_success ds.commit();
on_error ds.rollback();
upsert(row_hash);
            @endcode

            @param example_row a hash representing an example row to insert or update; every hash passed to the upsert closure returned must have the same keys in the same order
            @param upsert_strategy see @ref upsert_options for possible values for the upsert strategy
            @param opt a hash of options for the upsert operation; see @ref SqlUtil::AbstractTable::UpsertOptions for common options; each driver can support additional driver-specific options

            @return a closure that can be executed given a hash argument representing either a single row or a set of rows (where each key value is a list of column values) that will be updated or inserted in the database with the given upsert strategy; the closure returned does not check the input hash for validity; the closure has the following signature: @code{.py} int sub upsert(hash row) {} @endcode The return value of the closure is always @ref SqlUtil::AbstractTable::UR_Verified; see @ref upsert_results for more information

            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted
            @throw UPSERT-ERROR no primary key, unique constraint, or unique index for upsert; not all columns of the unique constraint/index are used in the upsert statement

            @note the row values passed to the closure for upserting are not checked if they match the example row passed to the getUpsertClosure() method; passing non-conforming data will cause errors; see @ref SqlUtil::AbstractTable::getUpsertClosureWithValidation() for a similar method that returns a validating closure; the closure returned by this method is faster than the one returned by @ref SqlUtil::AbstractTable::getUpsertClosure() since there is no validation

            @see
            - upsert()
            - upsertCommit()
            - getUpsertClosure()
            - getUpsertClosureWithValidation()
         */
        code getBulkUpsertClosure(hash example_row, int upsert_strategy = AbstractTable::UpsertAuto, *hash opt) {
            code upsert = getUpsertClosure(example_row, upsert_strategy, opt);
            return int sub (hash h) {
                map upsert($1), h.contextIterator();
                return UR_Verified;
            };
        }

        #! returns a closure that can be executed given a hash argument representing a single row that will be updated or inserted in the database with the given upsert strategy; the table must have a unique key to do this; the closure returned checks the input hash for validity
        /** @par Example:
            @code{.py}
Datasource ds("pgsql:user/pass@db%localhost");
Table table(ds, "table_name");
code upsert = table.getUpsertClosureWithValidation(row, AbstractTable::UpsertSelectFirst);
on_success ds.commit();
on_error ds.rollback();
map upsert($1), row_list;
            @endcode

            @param example_row a hash representing an example row to insert or update; every row passed to the upsert closure returned must have the same keys in the same order or the closure returned will throw an \c UPSERT-ERROR exception
            @param upsert_strategy see @ref upsert_options for possible values for the upsert strategy
            @param opt a hash of options for the upsert operation; see @ref SqlUtil::AbstractTable::UpsertOptions for common options; each driver can support additional driver-specific options

            @return a closure that can be executed given a hash argument representing a single row that will be updated or inserted in the database with the given upsert strategy; the closure has the following signature: @code{.py} int sub upsert(hash row) {} @endcode The return value of the closure is an integer code giving the result of the update; see @ref upsert_results for more information

            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted
            @throw UPSERT-ERROR no primary key, unique constraint, or unique index for upsert; not all columns of the unique constraint/index are used in the upsert statement

            @note the row values passed to the closure for upserting are checked if they match the example row passed to the getUpsertClosure() method; passing non-conforming data to the closure will cause the closure to throw an \c UPSERT-ERROR exception; see @ref SqlUtil::AbstractTable::getUpsertClosure() for a similar method that returns a non-validating closure; the closure returned by this method is a little slower than the one returned by @ref SqlUtil::AbstractTable::getUpsertClosure() since each row is validated
         */
        code getUpsertClosureWithValidation(hash example_row, int upsert_strategy = UpsertAuto, *hash opt) {
            code upsert = getUpsertClosure(example_row, upsert_strategy, opt);

            return int sub (hash row) {
                if (!row.compareKeys(example_row))
                    throw "UPSERT-ERROR", sprintf("%s: the keys in the row passed to the upsert closure (%y) do not match the keys passed to AbstractTable::getUpsertClosureWithValidation() (%y)", getDesc(), row.keys, example_row.keys());
                return upsert(row);
            };
        }

        #! this method upserts or merges data from the given iterator argument (whose @ref Qore::AbstractIterator::getValue() "getValue()" method must return a hash giving row values) into the current table; the transaction is committed if successful, if an error occurs then it is rolled back
        /** @par Example:
            @code{.py}
hash h = table.upsertFromIterator(i, AbstractTable::UpsertUpdateFirst);
            @endcode

            @param i the @ref Qore::AbstractIterator argument that will provide the data to upsert or merge into the current table; the @ref Qore::AbstractIterator::getValue() "getValue()" method of this object must return a hash giving row values (for example, the @ref Qore::SQL::SQLStatement class is well-suited to be used here)
            @param upsert_strategy see @ref upsert_options for possible values for the upsert strategy
            @param opt a hash of options for the upsert operation; see @ref SqlUtil::AbstractTable::UpsertOptions for common options; each driver can support additional driver-specific options

            @return @ref nothing if no actions were taken or a hash with the following keys assigned to numeric values indicating the number of rows processed (keys correspond to @ref SqlUtil::AbstractTable::UpsertResultDescriptionMap keys):
            - \c "inserted": the number of rows inserted
            - \c "verified": the number of rows updated unconditionally; note that this key is returned with all upsert strategy codes other than @ref SqlUtil::AbstractTable::UpsertSelectFirst instead of \c "updated"
            - \c "updated": the number of rows updated; note that this key is only returned if \a upsert_strategy is @ref SqlUtil::AbstractTable::UpsertSelectFirst, otherwise updated rows are reported as \c "verified" since rows are updated unconditionally with other the upsert strategy codes
            - \c "unchanged": the number of rows unchanged; this key can only be returned if \a upsert_strategy is @ref SqlUtil::AbstractTable::UpsertSelectFirst, @ref SqlUtil::AbstractTable::UpsertInsertOnly, or @ref SqlUtil::AbstractTable::UpsertUpdateOnly
            - \c "deleted": the number of rows deleted; this can only be returned if @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c delete_others is @ref Qore::True "True"

            @throw OPTION-ERROR invalid or unsupported option
            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted
            @throw UPSERT-ERROR no primary key, unique constraint, or unique index for upsert; not all columns of the unique constraint/index are used in the upsert statement

            @note if @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c delete_others is @ref Qore::True "True", then a hash of primary key values in the input data is built as the input data is iterated.  After iterating, if the row count of the table and the input data matches, then nothing more is done, otherwise, every row of the table is iterated and compared to the primary key hash; if a row does not match a primary key value, then it is deleted.  This operation is only executed if \c delete_others is @ref Qore::True "True" and is expensive for large data sets.

            @see
            - upsertFromIteratorCommit()
            - upsertFromSelect()
            - upsertFromSelectCommit()
            - upsert()
            - upsertCommit()
            - getUpsertClosure()
            - insertFromIteratorCommit()
            - insertFromIterator()
        */
        *hash upsertFromIteratorCommit(Qore::AbstractIterator i, int upsert_strategy = AbstractTable::UpsertAuto, *hash opt) {
            on_success ds.commit();
            on_error ds.rollback();

            return upsertFromIteratorIntern(i, upsert_strategy, opt);
        }

        #! this method upserts or merges data from the given iterator argument (whose @ref Qore::AbstractIterator::getValue() "getValue()" method must return a hash giving row values) into the current table; no transaction management is performed with this method
        /** @par Example:
            @code{.py}
hash h = table.upsertFromIterator(i, AbstractTable::UpsertUpdateFirst);
            @endcode

            @param i the @ref Qore::AbstractIterator argument that will provide the data to upsert or merge into the current table; the @ref Qore::AbstractIterator::getValue() "getValue()" method of this object must return a hash giving row values (for example, the @ref Qore::SQL::SQLStatement class is well-suited to be used here)
            @param upsert_strategy see @ref upsert_options for possible values for the upsert strategy
            @param opt a hash of options for the upsert operation; see @ref SqlUtil::AbstractTable::UpsertOptions for common options; each driver can support additional driver-specific options; note that this method ignores any \c "commit_block" option

            @return @ref nothing if no actions were taken or a hash with the following keys assigned to numeric values indicating the number of rows processed (keys correspond to @ref SqlUtil::AbstractTable::UpsertResultDescriptionMap keys):
            - \c "inserted": the number of rows inserted
            - \c "verified": the number of rows updated unconditionally; note that this key is returned with all upsert strategy codes other than @ref SqlUtil::AbstractTable::UpsertSelectFirst instead of \c "updated"
            - \c "updated": the number of rows updated; note that this key is only returned if \a upsert_strategy is @ref SqlUtil::AbstractTable::UpsertSelectFirst, otherwise updated rows are reported as \c "verified" since rows are updated unconditionally with other the upsert strategy codes
            - \c "unchanged": the number of rows unchanged; this key can only be returned if \a upsert_strategy is @ref SqlUtil::AbstractTable::UpsertSelectFirst, @ref SqlUtil::AbstractTable::UpsertInsertOnly,  or @ref SqlUtil::AbstractTable::UpsertUpdateOnly
            - \c "deleted": the number of rows deleted; this can only be returned if @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c delete_others is @ref Qore::True "True"

            @throw OPTION-ERROR invalid or unsupported option
            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted
            @throw UPSERT-ERROR no primary key, unique constraint, or unique index for upsert; not all columns of the unique constraint/index are used in the upsert statement

            @note if @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c delete_others is @ref Qore::True "True", then a hash of primary key values in the input data is built as the input data is iterated.  After iterating, if the row count of the table and the input data matches, then nothing more is done, otherwise, every row of the table is iterated and compared to the primary key hash; if a row does not match a primary key value, then it is deleted.  This operation is only executed if \c delete_others is @ref Qore::True "True" and is expensive for large data sets.

            @see
            - upsertFromIteratorCommit()
            - upsertFromSelect()
            - upsertFromSelectCommit()
            - upsert()
            - upsertCommit()
            - getUpsertClosure()
            - insertFromIteratorCommit()
            - insertFromIterator()
        */
        *hash upsertFromIterator(Qore::AbstractIterator i, int upsert_strategy = AbstractTable::UpsertAuto, *hash opt) {
            return upsertFromIteratorIntern(i, upsert_strategy, opt - "commit_block");
        }

        #! A legacy SqlUtik::AbstractTable::upsertFromIterator() wrapper
        deprecated *hash upsertFromIteratorNoCommit(Qore::AbstractIterator i, int upsert_strategy = AbstractTable::UpsertAuto, *hash opt) {
            return upsertFromIteratorIntern(i, upsert_strategy, opt - "commit_block");
        }

        private *hash upsertFromIteratorIntern(Qore::AbstractIterator i, int upsert_strategy = AbstractTable::UpsertAuto, *hash opt) {
            # check upsert options
            validateOptionsIntern("OPTION-ERROR", getUpsertOptions(), \opt);

            # try to get initial row
            if (!i.next())
                return;

            # result hash
            hash rh;

            # primary key value hash for "delete_others"
            hash pkh;

            hash row = i.getValue();
            code upsert = getUpsertClosure(row, upsert_strategy, opt);

            opt.change_count = 0;
            do {
                int code = upsert(row);
                ++rh{AbstractTable::UpsertResultMap{code}};
                if (opt.info_callback)
                    opt.info_callback(getSqlName(), row, code);

                if (code != UR_Unchanged && opt.commit_block) {
                    if (++opt.change_count >= opt.commit_block) {
                        ds.commit();
                        opt.change_count = 0;
                    }
                }

                # save primary key value in primary key hash if "delete_others" is set
                if (opt.delete_others)
                    pkh{foldl $1 + "-" + $2, (map row.$1.toString(), primaryKey.keyIterator())} = True;
            } while (i.next() && (row = i.getValue()));

            if (opt.delete_others)
                rh += doDeleteOthersIntern(pkh, opt);

            return rh;
        }

        private *hash doDeleteOthersIntern(hash pkh, *hash opt) {
            hash rh;

            # iterate through the table to find rows that do not belong and delete them
            list pkl = primaryKey.keys();
            foreach hash row in (getRowIterator()) {
                string k = foldl $1 + "-" + $2, (map row.$1.toString(), pkl);
                if (pkh{k})
                    continue;

                hash h = row{pkl};
                self.del(h);

                ++rh.deleted;
                if (opt.info_callback)
                    opt.info_callback(getSqlName(), row, UR_Deleted);
                if (opt.commit_block) {
                    if (++opt.change_count >= opt.commit_block) {
                        ds.commit();
                        opt.change_count = 0;
                    }
                }
            }

            return rh;
        }

        #! this method upserts or merges data from the given foreign table and @ref select_option_hash "select option hash" into the current table; the transaction is committed if successful, if an error occurs then it is rolled back
        /** @par Example:
            @code{.py}
hash h = table.upsertFromSelectCommit(table2, ("where": ("account_type": "CUSTOMER")), AbstractTable::UpsertUpdateFirst);
            @endcode

            The table argument does not need to be in the same database as the current table; it can also
            be in a different database server or a database server of a different type (you can use this method to upsert or
            merge data to or from any database supported by SqlUtil).

            @param t the table for the source data; this does not need to be in the same database as the target (the current table), nor does it need to be the same database type
            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param upsert_strategy see @ref upsert_options for possible values for the upsert strategy
            @param opt a hash of options for the upsert operation; see @ref SqlUtil::AbstractTable::UpsertOptions for common options; each driver can support additional driver-specific options

            @return @ref nothing if no actions were taken or a hash with the following keys assigned to numeric values indicating the number of rows processed (keys correspond to @ref SqlUtil::AbstractTable::UpsertResultDescriptionMap keys):
            - \c "inserted": the number of rows inserted
            - \c "verified": the number of rows updated unconditionally; note that this key is returned with all upsert strategy codes other than @ref SqlUtil::AbstractTable::UpsertSelectFirst instead of \c "updated"
            - \c "updated": the number of rows updated; note that this key is only returned if \a upsert_strategy is @ref SqlUtil::AbstractTable::UpsertSelectFirst, otherwise updated rows are reported as \c "verified" since rows are updated unconditionally with other the upsert strategy codes
            - \c "unchanged": the number of rows unchanged; this key can only be returned if \a upsert_strategy is @ref SqlUtil::AbstractTable::UpsertSelectFirst, @ref SqlUtil::AbstractTable::UpsertInsertOnly, or @ref SqlUtil::AbstractTable::UpsertUpdateOnly
            - \c "deleted": the number of rows deleted; this can only be returned if @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c delete_others is @ref Qore::True "True"

            @throw OPTION-ERROR invalid or unsupported option
            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted
            @throw UPSERT-ERROR no primary key, unique constraint, or unique index for upsert; not all columns of the unique constraint/index are used in the upsert statement

            @note
            - if @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c delete_others is @ref Qore::True "True", then a hash of primary key values in the input data is built as the input data is iterated.  After iterating, if the row count of the table and the input data matches, then nothing more is done, otherwise, every row of the table is iterated and compared to the primary key hash; if a row does not match a primary key value, then it is deleted.  This operation is only executed if \c delete_others is @ref Qore::True "True" and is expensive for large data sets.
            - this method also calls commit() or rollback() on the source table to release the transaction lock on the underlying source table datasource as an @ref Qore::SQL::SQLStatement "SQLStatement" object is used to pipeline the data to the upsert code; use upsertFromSelect() to do manual transaction management instead
            - unlike insertFromSelect() and insertFromSelectCommit(), this method processes arbitrary input data and accepts @ref SqlUtil::AbstractTable::UpsertOptions "upsert options"

            @see
            - upsertFromSelect()
            - upsertFromIterator()
            - upsertFromIteratorCommit()
            - upsert()
            - upsertCommit()
            - getUpsertClosure()
        */
        *hash upsertFromSelectCommit(AbstractTable t, *hash sh, int upsert_strategy = AbstractTable::UpsertAuto, *hash opt) {
            on_success { ds.commit(); t.commit(); }
            on_error { ds.rollback(); t.rollback(); }

            return upsertFromSelect(t, sh, upsert_strategy, opt);
        }

        #! @ref SqlUtil::AbstractTable::upsertFromSelectCommit() variant
        *hash upsertFromSelectCommit(Table t, *hash sh, int upsert_strategy = AbstractTable::UpsertAuto, *hash opt) {
            on_success { ds.commit(); t.commit(); }
            on_error { ds.rollback(); t.rollback(); }

            return upsertFromIteratorIntern(t.getRowIterator(sh), upsert_strategy, opt);
        }

        #! this method upserts or merges data from the given foreign table and @ref select_option_hash "select option hash" into the current table; no transaction management is performed with this method
        /** @par Example:
            @code{.py}
on_success { table.commit(); table2.commit(); }
on_error { table.rollback(); table2.rollback(); }
hash h = table.upsertFromSelect(table2, ("where": ("account_type": "CUSTOMER")), AbstractTable::UpsertUpdateFirst);
            @endcode

            The table argument does not need to be in the same database as the current table; it can also
            be in a different database server or a database server of a different type (you can use this method to upsert or
            merge data to or from any database supported by SqlUtil).

            @param t the table for the source data; this does not need to be in the same database as the target (the current table), nor does it need to be the same database type
            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param upsert_strategy see @ref upsert_options for possible values for the upsert strategy
            @param opt a hash of options for the upsert operation; see @ref SqlUtil::AbstractTable::UpsertOptions for common options; each driver can support additional driver-specific options; note that this method ignores any \c "commit_block" option

            @return @ref nothing if no actions were taken or a hash with the following keys assigned to numeric values indicating the number of rows processed (keys correspond to @ref SqlUtil::AbstractTable::UpsertResultDescriptionMap keys):
            - \c "inserted": the number of rows inserted
            - \c "verified": the number of rows updated unconditionally; note that this key is returned with all upsert strategy codes other than @ref SqlUtil::AbstractTable::UpsertSelectFirst instead of \c "updated"
            - \c "updated": the number of rows updated; note that this key is only returned if \a upsert_strategy is @ref SqlUtil::AbstractTable::UpsertSelectFirst, otherwise updated rows are reported as \c "verified" since rows are updated unconditionally with other the upsert strategy codes
            - \c "unchanged": the number of rows unchanged; this key can only be returned if \a upsert_strategy is @ref SqlUtil::AbstractTable::UpsertSelectFirst, @ref SqlUtil::AbstractTable::UpsertInsertOnly, or @ref SqlUtil::AbstractTable::UpsertUpdateOnly
            - \c "deleted": the number of rows deleted; this can only be returned if @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c delete_others is @ref Qore::True "True"

            @throw OPTION-ERROR invalid or unsupported option
            @throw COLUMN-ERROR an unknown column was referenced in the hash to be inserted
            @throw UPSERT-ERROR no primary key, unique constraint, or unique index for upsert; not all columns of the unique constraint/index are used in the upsert statement

            @note
            - if @ref SqlUtil::AbstractTable::UpsertOptions "upsert option" \c delete_others is @ref Qore::True "True", then a hash of primary key values in the input data is built as the input data is iterated.  After iterating, if the row count of the table and the input data matches, then nothing more is done, otherwise, every row of the table is iterated and compared to the primary key hash; if a row does not match a primary key value, then it is deleted.  This operation is only executed if \c delete_others is @ref Qore::True "True" and is expensive for large data sets.
           - this method uses an @ref Qore::SQL::SQLStatement "SQLStatement" object to pipeline the select data to the upsert code; to release the transaction lock acquired by the @ref Qore::SQL::SQLStatement "SQLStatement" object, a commit() or rollback() action must be executed on the underlying datasource object as in the example above
            - unlike insertFromSelect() and insertFromSelectCommit(), this method processes arbitrary input data and accepts @ref SqlUtil::AbstractTable::UpsertOptions "upsert options"

            @see
            - upsertFromSelect()
            - upsertFromIterator()
            - upsertFromIteratorCommit()
            - upsert()
            - upsertCommit()
            - getUpsertClosure()
        */
        *hash upsertFromSelect(AbstractTable t, *hash sh, int upsert_strategy = AbstractTable::UpsertAuto, *hash opt) {
            return upsertFromIteratorIntern(t.getRowIterator(sh), upsert_strategy, opt - "commit_block");
        }

        #! A legacy @ref SqlUtil::AbstractTable::upsertFromSelect() wrapper
        deprecated *hash upsertFromSelectNoCommit(AbstractTable t, *hash sh, int upsert_strategy = AbstractTable::UpsertAuto, *hash opt) {
            return upsertFromIteratorIntern(t.getRowIterator(sh), upsert_strategy, opt - "commit_block");
        }

        #! @ref SqlUtil::AbstractTable::upsertFromSelect() variant
        deprecated *hash upsertFromSelect(Table t, *hash sh, int upsert_strategy = AbstractTable::UpsertAuto, *hash opt) {
            return upsertFromIteratorIntern(t.getRowIterator(sh), upsert_strategy, opt - "commit_block");
        }

        #! A legacy @ref SqlUtil::AbstractTable::upsertFromSelect() wrapper
        deprecated *hash upsertFromSelectNoCommit(Table t, *hash sh, int upsert_strategy = AbstractTable::UpsertAuto, *hash opt) {
            return upsertFromIteratorIntern(t.getRowIterator(sh), upsert_strategy, opt - "commit_block");
        }

        #! returns the number of rows in the table
        /** @par Example:
            @code{.py}
int cnt = table.rowCount();
            @endcode

            @return the number of rows in the table

            @note to see if the table is empty or not, use emptyData() as this is much faster than rowCount()

            @see emptyData()
         */
        softint rowCount() {
            return ds.selectRow("select count(1) cnt from %s", getSqlName()).cnt;
        }

        #! returns an @ref Qore::SQL::SQLStatement "SQLStatement" object that will iterate the results of a select statement matching the arguments
        /** @par Example:
            @code{.py}
SQLStatement i = table.getRowIterator(sh);
            @endcode

            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param sql an optional reference to a string to return the SQL generated for the select statement
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more info

            @return an @ref Qore::SQL::SQLStatement "SQLStatement" object that will iterate the results of a select statement matching the arguments

            @throw OPTION-ERROR invalid or unsupported select option
            @throw SELECT-ERROR \c 'offset' supplied without \c 'orderby' or \c 'limit', \c 'orderby' with \c 'limit' and \c 'offset' does not match any unique constraint

            @note
            - if \c "offset" is supplied and no \c "orderby" is supplied, then if any primary key exists, the primary key columns will be used for the \c "orderby" option automatically
            - the @ref Qore::SQL::SQLStatement "SQLStatement" object created by a successful call to this method acquires a thread resource for the underlying @ref Qore::SQL::AbstractDatasource "AbstractDatasource" object that must be released by calling @ref Qore::SQL::AbstractDatasource::commit() "commit()" or @ref Qore::SQL::AbstractDatasource::rollback() "rollback()", even if the statement does not acquire any database locks
         */
        Qore::SQL::SQLStatement getRowIterator(*hash sh, *reference sql, *hash opt) {
            return getRowIteratorIntern(sh, \sql, opt);
        }

        private Qore::SQL::SQLStatement getRowIteratorIntern(*hash sh, *reference sql, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getSelectOptions(), \sh);

            list args;
            sql = getSelectSqlIntern(sh, \args, opt);

            SQLStatement stmt(ds);
            stmt.prepare(sql);
            execData(stmt, opt, args);

            return stmt;
        }

        #! returns an @ref Qore::SQL::SQLStatement "SQLStatement" object that will iterate the results of a select statement matching the arguments
        /** @par Example:
            @code{.py}
SQLStatement i = table.getRowIterator();
            @endcode

            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more info

            @return an @ref Qore::SQL::SQLStatement "SQLStatement" object that will iterate the results of a select statement matching the arguments

            @throw OPTION-ERROR invalid or unsupported select option
            @throw SELECT-ERROR \c 'offset' supplied without \c 'orderby' or \c 'limit', \c 'orderby' with \c 'limit' and \c 'offset' does not match any unique constraint

            @note
            - if \c "offset" is supplied and no \c "orderby" is supplied, then if any primary key exists, the primary key columns will be used for the \c "orderby" option automatically
            - the @ref Qore::SQL::SQLStatement "SQLStatement" object created by a successful call to this method acquires a thread resource for the underlying @ref Qore::SQL::AbstractDatasource "AbstractDatasource" object that must be released by calling @ref Qore::SQL::AbstractDatasource::commit() "commit()" or @ref Qore::SQL::AbstractDatasource::rollback() "rollback()", even if the statement does not acquire any database locks
         */
        Qore::SQL::SQLStatement getRowIterator(*hash sh, *hash opt) {
            return getRowIteratorIntern(sh, NOTHING, opt);
        }

        #! returns a hash representing the row in the table that matches the argument hash; if more than one row would be returned an exception is raised
        /** @par Example:
            @code{.py}
*hash h = table.selectRow(sh);
            @endcode

            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param sql an optional reference to a string to return the SQL generated for the select statement
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more info

            @return a hash representing the row in the table that matches the argument hash; if more than one row would be returned an exception is raised

            @throw OPTION-ERROR invalid or unsupported select option
            @throw SELECT-ERROR \c 'offset' supplied without \c 'orderby' or \c 'limit', \c 'orderby' with \c 'limit' and \c 'offset' does not match any unique constraint
            @throw DBI-SELECT-ROW-ERROR more than 1 row retrieved from the server

            @note
            - if \c "offset" is supplied and no \c "orderby" is supplied, then if any primary key exists, the primary key columns will be used for the \c "orderby" option automatically
            - if the \c forupdate @ref select_option_hash "select option" is used, then after a successful select operation, the calling thread will own the thread transaction lock
         */
        *hash selectRow(*hash sh, *reference sql, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getSelectOptions(), \sh);

            list args;
            sql = getSelectSqlUnlocked(sh, \args, opt);

            bool rollback_on_error = False;
            if (sh.forupdate) {
                rollback_on_error = !ds.currentThreadInTransaction();
                if (rollback_on_error)
                    ds.beginTransaction();
            }
            on_error if (rollback_on_error)
                ds.rollback();
            return ds.vselectRow(sql, args);
        }

        #! returns a list of hashes representing the rows in the table that match the argument hash
        /** @par Example:
            @code{.py}
*list l = table.selectRows(sh);
            @endcode

            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param sql an optional reference to a string to return the SQL generated for the select statement
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more info

            @return a list of hashes representing the rows in the table that match the argument hash

            @throw OPTION-ERROR invalid or unsupported select option
            @throw SELECT-ERROR \c 'offset' supplied without \c 'orderby' or \c 'limit', \c 'orderby' with \c 'limit' and \c 'offset' does not match any unique constraint

            @note
            - if \c "offset" is supplied and no \c "orderby" is supplied, then if any primary key exists, the primary key columns will be used for the \c "orderby" option automatically
            - if the \c forupdate @ref select_option_hash "select option" is used, then after a successful select operation, the calling thread will own the thread transaction lock
         */
        *list selectRows(*hash sh, *reference sql, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getSelectOptions(), \sh);

            list args;
            sql = getSelectSqlUnlocked(sh, \args, opt);

            bool rollback_on_error = False;
            if (sh.forupdate) {
                rollback_on_error = !ds.currentThreadInTransaction();
                if (rollback_on_error)
                    ds.beginTransaction();
            }
            on_error if (rollback_on_error)
                ds.rollback();

            return ds.vselectRows(sql, args);
        }

        #! returns a hash of lists representing the columns and rows in the table that match the argument hahs
        /** @par Example:
            @code{.py}
*hash h = table.select(sh);
            @endcode

            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param sql an optional reference to a string to return the SQL generated for the select statement
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more info

            @return a hash of lists representing the columns and rows in the table that match the argument hash

            @throw OPTION-ERROR invalid or unsupported select option
            @throw SELECT-ERROR \c 'offset' supplied without \c 'orderby' or \c 'limit', \c 'orderby' with \c 'limit' and \c 'offset' does not match any unique constraint

            @note
            - if \c "offset" is supplied and no \c "orderby" is supplied, then if any primary key exists, the primary key columns will be used for the \c "orderby" option automatically
            - if the \c forupdate @ref select_option_hash "select option" is used, then after a successful select operation, the calling thread will own the thread transaction lock
         */
        *hash select(*hash sh, *reference sql, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getSelectOptions(), \sh);

            list args;
            sql = getSelectSqlUnlocked(sh, \args, opt);

            bool rollback_on_error = False;
            if (sh.forupdate) {
                rollback_on_error = !ds.currentThreadInTransaction();
                if (rollback_on_error)
                    ds.beginTransaction();
            }
            on_error if (rollback_on_error)
                ds.rollback();

            return ds.vselect(sql, args);
        }

        #! returns a hash representing the row in the table that matches the argument hash; if more than one row would be returned an exception is raised
        /** @par Example:
            @code{.py}
*hash h = table.selectRow(sh);
            @endcode

            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more info

            @return a hash representing the row in the table that matches the argument hash; if more than one row would be returned an exception is raised

            @throw OPTION-ERROR invalid or unsupported select option
            @throw SELECT-ERROR \c 'offset' supplied without \c 'orderby' or \c 'limit', \c 'orderby' with \c 'limit' and \c 'offset' does not match any unique constraint
            @throw DBI-SELECT-ROW-ERROR more than 1 row retrieved from the server

            @note
            - if \c "offset" is supplied and no \c "orderby" is supplied, then if any primary key exists, the primary key columns will be used for the \c "orderby" option automatically
            - if the \c forupdate @ref select_option_hash "select option" is used, then after a successful select operation, the calling thread will own the thread transaction lock
         */
        *hash selectRow(*hash sh, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getSelectOptions(), \sh);
            # check data callback options if any
            validateOptionsIntern("OPTION-ERROR", getSqlDataCallbackOptions(), \opt);

            list args;
            string sql = getSelectSqlUnlocked(sh, \args, opt);

            if (opt.sqlarg_callback)
                opt.sqlarg_callback(sql, args);

            bool rollback_on_error = False;
            if (sh.forupdate) {
                rollback_on_error = !ds.currentThreadInTransaction();
                if (rollback_on_error)
                    ds.beginTransaction();
            }
            on_error if (rollback_on_error)
                ds.rollback();

            return ds.vselectRow(sql, args);
        }

        #! returns a list of hashes representing the rows in the table that match the argument hash
        /** @par Example:
            @code{.py}
*list l = table.selectRows(sh);
            @endcode

            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more info

            @return a list of hashes representing the rows in the table that match the argument hash

            @throw OPTION-ERROR invalid or unsupported select option
            @throw SELECT-ERROR \c 'offset' supplied without \c 'orderby' or \c 'limit', \c 'orderby' with \c 'limit' and \c 'offset' does not match any unique constraint

            @note
            - if \c "offset" is supplied and no \c "orderby" is supplied, then if any primary key exists, the primary key columns will be used for the \c "orderby" option automatically
            - if the \c forupdate @ref select_option_hash "select option" is used, then after a successful select operation, the calling thread will own the thread transaction lock
         */
        *list selectRows(*hash sh, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getSelectOptions(), \sh);
            # check data callback options if any
            validateOptionsIntern("OPTION-ERROR", getSqlDataCallbackOptions(), \opt);

            list args;
            string sql = getSelectSqlUnlocked(sh, \args, opt);

            if (opt.sqlarg_callback)
                opt.sqlarg_callback(sql, args);

            bool rollback_on_error = False;
            if (sh.forupdate) {
                rollback_on_error = !ds.currentThreadInTransaction();
                if (rollback_on_error)
                    ds.beginTransaction();
            }
            on_error if (rollback_on_error)
                ds.rollback();

            return ds.vselectRows(sql, args);
        }

        #! returns a hash of lists representing the columns and rows in the table that match the argument hahs
        /** @par Example:
            @code{.py}
*hash h = table.select(sh);
            @endcode

            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more info

            @return a hash of lists representing the columns and rows in the table that match the argument hash

            @throw OPTION-ERROR invalid or unsupported select option
            @throw SELECT-ERROR \c 'offset' supplied without \c 'orderby' or \c 'limit', \c 'orderby' with \c 'limit' and \c 'offset' does not match any unique constraint

            @note
            - if \c "offset" is supplied and no \c "orderby" is supplied, then if any primary key exists, the primary key columns will be used for the \c "orderby" option automatically
            - if the \c forupdate @ref select_option_hash "select option" is used, then after a successful select operation, the calling thread will own the thread transaction lock
         */
        *hash select(*hash sh, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getSelectOptions(), \sh);
            # check data callback options if any
            validateOptionsIntern("OPTION-ERROR", getSqlDataCallbackOptions(), \opt);

            list args;
            string sql = getSelectSqlUnlocked(sh, \args, opt);

            if (opt.sqlarg_callback)
                opt.sqlarg_callback(sql, args);

            bool rollback_on_error = False;
            if (sh.forupdate) {
                rollback_on_error = !ds.currentThreadInTransaction();
                if (rollback_on_error)
                    ds.beginTransaction();
            }
            on_error if (rollback_on_error)
                ds.rollback();

            return ds.vselect(sql, args);
        }

        #! returns the SQL string to be executed corresponding to the argument hash with an output parameter for the select bind arguments
        /** @par Example:
            @code{.py}
list args;
string sql = table.getSelectSql(sh, \args);
            @endcode

            @param sh a hash of conditions for the select statement; see @ref select_option_hash "select option hash" for information about this argument
            @param args an optional reference to a list for any bind arguments

            @return the SQL that can be used to execute a select on the database corresponding to the arguments; the select bind arguments are returned in an output parameter

            @throw OPTION-ERROR invalid or unsupported select option
            @throw SELECT-ERROR \c 'offset' supplied without \c 'orderby' or \c 'limit', \c 'orderby' with \c 'limit' and \c 'offset' does not match any unique constraint

            @note
            - the select string is specific to the arguments passed (for example @ref null arguments will have clauses like 'is null')
            - if \c "offset" is supplied and no \c "orderby" is supplied, then if any primary key exists, the primary key columns will be used for the \c "orderby" option automatically
         */
        string getSelectSql(*hash sh, *reference args) {
            validateOptionsIntern("OPTION-ERROR", getSelectOptions(), \sh);

            args = ();
            return getSelectSqlIntern(sh, \args);
        }

        *AbstractUniqueConstraint matchAnyUnique(list cols) {
            if (!manual)
                getPrimaryKeyUnlocked();
            if (!primaryKey.empty() && primaryKey.partialMatchKeys(cols))
                return primaryKey;
            if (!manual)
                getConstraintsUnlocked();
            foreach AbstractConstraint c in (constraints.iterator()) {
                if (c instanceof AbstractUniqueConstraint) {
                    AbstractUniqueConstraint uk = c;
                    if (uk.partialMatchKeys(cols))
                        return uk;
                }
            }
        }

        string getSelectSqlIntern(*hash qh, reference args, *hash opt) {
            l.lock();
            l.unlock();

            return getSelectSqlUnlocked(qh, \args, opt);
        }

        string getSelectSqlUnlocked(*hash qh, reference args, *hash opt) {
            # we first get column & table info in case we are referencing a table through a synonym
            # so the getSelectSqlName() will refer to the proper schema owner
            getColumnsUnlocked();
            return getSelectSqlUnlockedIntern(qh, getSelectSqlName(qh), \args, NOTHING, opt);
        }

        # column & table information must be retrieved before calling this function
        string getSelectSqlUnlockedIntern(*hash qh, string from, reference args, *hash ch, *hash opt) {
            # get pseudo-column hash
            *hash psch = getPseudoColumnHash();

            if (qh.offset) {
                if (!qh.orderby) {
                    # use primary key by default
                    getPrimaryKeyUnlocked();
                    if (!primaryKey.empty())
                        qh.orderby = primaryKey.keys();
                    else
                        throw "SELECT-ERROR", sprintf("%s: select 'offset' supplied (%d) but no 'orderby' and the table has no primary key", getDesc(), qh.offset);
                }
                if (!qh.limit)
                    throw "SELECT-ERROR", sprintf("%s: select 'offset' supplied (%d) without limit", getDesc(), qh.offset);
            }
            # do not overwrite args; could be used in a subquery
            if (!exists args)
                args = ();

            # resolve any table names in joins first
            foreach string k in (qh.join.keyIterator()) {
                if (qh.join{k}.table.typeCode() == NT_STRING)
                        qh.join{k}.table = opt.tablecode ? opt.tablecode(ds, qh.join{k}.table) : new Table(ds, qh.join{k}.table).getTable();
            }

            # make a hash of aliases to tables when using joins
            hash jch;
            map jch{$1.alias ? $1.alias : $1.table.getName()} = $1.table, qh.join.iterator();

            # if we have a "superquery" argument, then we need to track which columns are in this subquery
            hash mch;

            string cstr;
            # make query
            list coll = ();
            if (qh.columns) {
                # column expressions
                list ce = ();
                foreach any cv in (qh.columns) {
                    switch (cv.typeCode()) {
                        case NT_STRING: {
                            string cn = getColumnNameIntern(cv, jch, boolean(qh.join), ch, psch);
                            if (qh.superquery)
                                mch{cv} = True;
                            ce += cn;
                            break;
                        }

                        case NT_HASH: {
                            if (!cv.cop)
                                throw "SELECT-ERROR", sprintf("%s: missing column operator in \"columns\" hash element %d/%d; the \"columns\" select option must be assigned to a list of strings giving the column names or a list of column operator hashes as returned from column operator functions; this element is missing hash key \"cop\" giving the column operator; keys provided: %y", getDesc(), $# + 1, qh.columns.lsize(), cv.keys());
                            try {
                                string val = doColumnOperatorIntern(cv, jch, boolean(qh.join), ch, psch, \psch);
                                ce += val;
                            }
                            catch (hash ex) {
                                # rethrow exception with additional contextual information
                                throw ex.err, sprintf("%s: %s: in \"columns\" hash element %d/%d: %s", get_ex_pos(ex), getName(), $# + 1, qh.columns.lsize(), ex.desc), ex.arg;
                            }
                            if (qh.superquery && cv.cop == COP_AS && cv.arg)
                                mch{cv.arg} = True;
                            break;
                        }

                        default:
                            throw "SELECT-ERROR", sprintf("%s: don't know how to parse argument type %y (value: %y) in column list, expecting \"string\" or \"hash\"", getDesc(), cv.type(), cv);
                    }
                }
                cstr = foldl $1 + ", " + $2, ce.iterator();
                coll = ce;
            }
            else
                cstr = "*";

            # comment handling
            string comment = '';
            if (qh.comment) {
                comment += sprintf("/* %s */ ", replace(qh.comment, "*/", ""));
            }
            if (qh.hint) {
                comment += sprintf("/*+ %s */ ", replace(qh.hint, "*/", ""));
            }

            string sql = sprintf("select %s%s from %s", comment, cstr, getFromIntern(from, qh));

            # process join specifications
            foreach any js in (qh.join.iterator()) {
                hash jh;
                if (js.typeCode() == NT_HASH)
                    jh = js;
                else if (js instanceof AbstractTable)
                    jh.table = js;
                else if (js instanceof Table)
                    jh.table = js.getTable();
                else
                    throw "SELECT-ERROR", sprintf("%s: don't know how to process join element of type %y; expecting a join hash, an AbstractTable object, or a Table object", getDesc(), js.type());

                AbstractTable t = jh.table;
                t.describe();

                string an = jh.alias ? jh.alias : t.getSqlName();

                AbstractTable jt;
                if (jh.ta) {
                    if (!jch{jh.ta})
                        throw "SELECT-ERROR", sprintf("%s: can't join with unknown table/alias %y; known tables/aliases: %y", getDesc(), jh.ta, list(getName()) + jch.keys());
                    jt = jch{jh.ta};
                }
                else
                    jt = self;

                # set join table name
                string jtn;

                if (jt != self) {
                    jt.getPrimaryKey();
                    jt.getForeignConstraints();
                    jtn = jh.ta;
                }
                else {
                    getPrimaryKeyUnlocked();
                    getForeignConstraintsUnlocked();
                    jtn = getSqlName();
                }

                # create "join on" clause
                # the join clause expression list
                list jel = ();
                if (jh.jcols) {
                    if (jh.jcols.typeCode() != NT_HASH)
                        throw "SELECT-ERROR", sprintf("%s: join clause error; expecting a hash assigned to the \"jcols\" key; got %y instead", getDesc(), jh.jcols.type());
                    HashIterator i(jh.jcols);
                    while (i.next()) {
                        string key = i.getKey();
                        string value = i.getValue();
                        if (!jt.columns.hasKey(key))
                            throw "SELECT-ERROR", sprintf("%s: cannot join on unknown column %y.%y; known columns: %y", getDesc(), jt.getSqlName(), key, jt.columns.keys());
                        if (!t.describe().hasKey(value))
                            throw "SELECT-ERROR", sprintf("%s: cannot join on unknown column %y.%y; known columns: %y", getDesc(), t.getSqlName(), value, t.columns.keys());
                        jel += sprintf("%s.%s = %s.%s", jtn, key, an, value);
                    }
                }
                else {
                    *hash fkh;
                    if (!jt.primaryKey.empty()) {
                        # see if there is a foreign constraint from table -> pk of self
                        fkh = t.getForeignConstraints().findConstraintOn(jt.name, jt.primaryKey.keys());
                        if (fkh) {
                            AbstractIterator pki = jt.primaryKey.keyIterator();
                            AbstractIterator fki = fkh.keyIterator();
                            while (pki.next() && fki.next())
                                jel += sprintf("%s.%s = %s.%s", jtn, pki.getValue(), an, fki.getValue());
                        }
                    }
                    if (!fkh) {
                        # see if there is a foreign constraint in self -> pk of table
                        if (!jt.foreignConstraints.empty()) {
                            AbstractPrimaryKey pk = t.getPrimaryKey();
                            if (!pk.empty()) {
                                fkh = jt.foreignConstraints.findConstraintOn(t.getName(), pk.keys());
                                if (fkh) {
                                    AbstractIterator fki = fkh.keyIterator();
                                    AbstractIterator pki = pk.keyIterator();
                                    while (pki.next() && fki.next())
                                        jel += sprintf("%s.%s = %s.%s", jtn, fki.getValue(), an, pki.getValue());
                                }
                            }
                        }

                        if (!fkh)
                            throw "SELECT-ERROR", sprintf("%s: cannot perform a %s join from %s on %s without a column specification since neither table has a foreign constraint on the primary key of the other table", getDesc(), jh.jop, jt.getSqlName(), t.getSqlName());
                    }
                }

                # process any additional join conditions
                if (jh.cond)
                    jel += jt.getWhereClauseIntern(jh.cond, \args, an, NOTHING, NOTHING, NOTHING, psch);

                sql += sprintf(" %s join %s%s on (%s)", JopMap{jh.jop}, t.getSelectSqlName(jh.opt), jh.alias ? (" " + jh.alias) : "", (foldl $1 + " and " + $2, jel));
            }

            getSelectWhereSqlUnlocked(\sql, \args, qh, jch, boolean(qh.join), ch, psch);

            if (qh.groupby) {
                list ce = getGroupByListUnlocked(qh, jch, ch, psch, coll);
                sql += " group by " + (foldl $1 + ", " + $2, ce.iterator());
            }

            if (qh.having) {
                getColumnsUnlocked();
                hash cm = getColumnOperatorMap();

                list hl = ();

                HashIterator i(qh.having);
                while (i.next()) {
                    string key = i.getKey();
                    any value = i.getValue();
                    if (value.typeCode() != NT_LIST)
                        throw "SELECT-ERROR", sprintf("%s: expecting a list argument to the \"having\" condition for column %y; got type %y instead (value: %y)", getDesc(), key, value.type(), value);
                    if (value[0].typeCode() != NT_STRING)
                        throw "SELECT-ERROR", sprintf("%s: expecting a string argument to the \"having\" condition in the first position giving the column operator code for column %y; got type %y instead (value: %y)", getDesc(), key, value[0].type(), value[0]);

                    *hash psch_copy = psch;
                    string hstr = doColumnOperatorIntern(value[0], NOTHING, getColumnExpressionIntern(key, jch, boolean(qh.join), ch, psch), cm, jch, boolean(qh.join), ch, psch);
                    if (psch_copy != psch)
                        throw "HAVING-ERROR", sprintf("%s: cannot use column operator functions in the having clause with column alias specifications", getDesc());
                    hl += doWhereExpressionIntern(hstr, value[1], \args, jch, boolean(qh.join), ch);
                }
                if (hl)
                    sql += " having " + (foldl $1 + " and " + $2, hl);
            }

            if (qh.orderby) {
                # process minuses to generate the "orderbydesc" keys
                foreach any k in (\qh.orderby) {
                    if (k.typeCode() == NT_STRING && k =~ /^-/) {
                        k = k.substr(1);
                        qh.orderbydesc{k} = True;
                    }
                }
                if (qh.offset || qh.limit) {
                    if (!matchAnyUnique(qh.orderby))
                        throw "SELECT-ERROR", sprintf("%s: 'orderby': %y with 'offset' = %d, 'limit' = %d does not match any unique constraint", getDesc(), qh.orderby, qh.offset, qh.limit);

                    doSelectOrderByWithOffsetSqlUnlockedImpl(\sql, \args, qh, jch, ch, psch, coll);
                }
                else
                    doSelectOrderBySqlUnlocked(\sql, \args, qh, jch, ch, psch, coll);
            }
            else if (qh.limit)
                doSelectLimitOnlyUnlockedImpl(\sql, \args, qh);

            #printf("SQL: %s\nargs: %y\n", sql, args);

            if (qh.forupdate && !qh.superquery)
                doForUpdate(\sql);

            if (qh.superquery) {
                validateOptionsIntern("OPTION-ERROR", getSelectOptions(), \qh.superquery);

                return getSelectSqlUnlockedIntern(qh.superquery, sprintf("(%s) subquery", sql), \args, mch);
            }

            return sql;
        }

        private string getFromIntern(string from, *hash qh) {
            return from;
        }

        private list getGroupByListUnlocked(hash qh, *hash jch, *hash ch, *hash psch, list coll) {
            return getGroupOrderByListUnlocked("groupby", qh, jch, ch, psch, coll);
        }

        private list getOrderByListUnlocked(hash qh, *hash jch, *hash ch, *hash psch, list coll) {
            return getGroupOrderByListUnlocked("orderby", qh, jch, ch, psch, coll);
        }

        private list getGroupOrderByListUnlocked(string key, hash qh, *hash jch, *hash ch, *hash psch, list coll) {
            list ce = ();

            code do_int = sub (int cv) {
                if (!coll) {
                    if (cv > columns.size())
                        throw "SELECT-ERROR", sprintf("%s %d: %s does not support numeric column offsets for the %s expression, and the table has %d column%s", key, cv, ds.getDriverName(), key, columns.size(), columns.size() == 1 ? "" : "s");
                    ce += columns.keys()[cv - 1];
                }
                else {
                    if (cv > coll.size())
                        throw "SELECT-ERROR", sprintf("%s %d: %s does not support numeric column offsets for the %s expression, and %d column%s %s given in the select argument", key, cv, ds.getDriverName(), key, coll.size(), coll.size() == 1 ? "" : "s", coll.size() == 1 ? "was" : "were");
                    string val = coll[cv - 1];
                    val =~ s/ as .*$//;
                    ce += val;
                }
            };

            foreach any cv in (qh{key}) {
                switch (cv.typeCode()) {
                    case NT_STRING: {
                        if (cv.toInt() == cv)
                            do_int(cv.toInt());
                        else
                            ce += getColumnNameIntern(cv, jch, boolean(qh.join), ch, psch);
                        break;
                    }

                    case NT_HASH: {
                        ce += doColumnOperatorIntern(cv, jch, boolean(qh.join), ch, psch);
                        break;
                    }

                    case NT_INT: {
                        do_int(cv);
                        break;
                    }

                    default:
                        throw "SELECT-ERROR", sprintf("%s: don't know how to parse argument type %y (value: %y) in %s list, expecting \"string\" or \"hash\"", getDesc(), cv.type(), cv, key);
                }
            }
            return ce;
        }

        private doForUpdate(reference sql) {
            sql += " for update";
        }

        private string getSelectSqlName(*hash qh) {
            return getSqlName();
        }

        private string getColumnExpressionIntern(any cvc, *hash jch, bool join, *hash ch, *hash psch) {
            switch (cvc.typeCode()) {
                case NT_HASH:
                    return doColumnOperatorIntern(cvc, jch, join, ch, psch);
                case NT_STRING:
                    return getColumnNameIntern(cvc, jch, join, ch, psch);
            }
            throw "SELECT-ERROR", sprintf("%s: column operator hash %y has an invalid \"column\" key; expecting a string column specification or a column operator description hash", getDesc(), cvc);
        }

        private string doColumnOperatorIntern(hash cvc, *hash jch, bool join, *hash ch, *hash psch, *reference psch_ref) {
            return doColumnOperatorIntern(cvc.cop, cvc.arg, (cvc.column ? getColumnExpressionIntern(cvc.column, jch, join, ch, psch) : NOTHING), getColumnOperatorMap(), jch, join, ch, psch, \psch_ref);
        }

        private string doColumnOperatorIntern(any cop, any arg, *string cve, hash cm, *hash jch, bool join, *hash ch, *hash psch, *reference psch_ref) {
            if (cop.typeCode() != NT_STRING)
                throw "SELECT-ERROR", sprintf("%s: invalid column operator code %y; use column operator functions to create valid column specifications with supported column operators", getDesc(), cop);

            *hash cmd = cm{cop};
            if (!cmd)
                throw "SELECT-ERROR", sprintf("%s: unknown column operator %y: expecting one of: %y", getDesc(), cop, cm.keys());

            if (cmd.arg) {
                string at = arg.type();
                switch (cmd.arg) {
                    default: {
                        if (at != cmd.arg)
                            throw "SELECT-ERROR", sprintf("%s: invalid argument to column operator %y; got type %y, expecting %y", getDesc(), cop, at, cmd.arg);
                    }
                }
            }

            if (cmd.sqlvalue)
                arg = getSqlValue(arg);
            else if (cmd.argcolumn) {
                if (!cmd.argoptional || arg) {
                    if (arg.typeCode() != NT_STRING)
                        throw "SELECT-ERROR", sprintf("%s: expecting a string column designation for column operator %y; got type %y instead", getDesc(), cop, arg.type());
                    arg = getColumnNameIntern(arg, jch, join, ch, psch);
                }
            }
            else if (cmd.columnargs) {
                if (arg.args.typeCode() != NT_LIST)
                    throw "SELECT-ERROR", sprintf("%s: expecting a list of column designators for column operator %y; got type %y instead", getDesc(), cop, arg.args.type());
                foreach any narg in (\arg.args) {
                    switch (narg.typeCode()) {
                        case NT_STRING: narg = getColumnNameIntern(narg, jch, join, ch, psch); break;
                        case NT_HASH: narg = doColumnOperatorIntern(narg, jch, join, ch, psch); break;
                        default: throw "COLUMN-ERROR", sprintf("%s: expecting a string column designator or a column operator hash; got %y in position %d/%d instead", getDesc(), narg, $#, arg.args.lsize());
                    }
                }
            }

            if (!exists cve && !cmd.nocolumn && !cmd.columnargs)
                throw "SELECT-ERROR", sprintf("%s: column operator %y requires a column argument name but none was provided", getDesc(), cop);

            return cmd.withalias ? cmd.code(cve, arg, \psch_ref) : cmd.code(cve, arg);
        }

        private string getColumnNameIntern(string cv, *hash jch, bool join, *hash ch, *hash psch) {
            # remove any disposable unique prefix
            cv =~ s/^[0-9]+://;
            # return the name if it's a "technical name" (starts with "_")
            if (cv =~ /^_/)
                return cv;

            bool use_prefix = join;

            # see if we have a table.column spec
            *string tp = (cv =~ x/(\w+)\..+/)[0];
            if (tp) {
                if (!jch{tp})
                    throw "SELECT-ERROR", sprintf("%s: query references unknown alias %y in %y; known aliases in this query: %y", getDesc(), tp, cv, jch.keys());
                # remove table alias from column name
                splice cv, 0, tp.size() + 1;

                if (cv != "*" && !jch{tp}.describe().hasKey(cv) && !psch{cv})
                    throw "SELECT-ERROR", sprintf("%s: query references unknown column \"%s.%s\" (%y is aliased to table %y); known columns: %y", getDesc(), tp, cv, tp, jch{tp}.getSqlName(), jch{tp}.describe().keys());
            }
            # substitute aliases with their actual definition
            else if (psch{cv} && psch{cv}.typeCode() == NT_STRING) {
                return psch{cv};
            }
            else if (ch) {
                if (!ch{cv} && !psch{cv})
                    throw "SELECT-ERROR", sprintf("%s: query references unknown column %y; known columns from subquery: %y", getDesc(), cv, ch.keys());
                return cv;
            }
            else {
                tp = getSqlName();
                if (cv == "*")
                   use_prefix = asteriskRequiresPrefix();
                else if (!columns.hasKey(cv) && !psch{cv})
                    throw "SELECT-ERROR", sprintf("%s: query references unknown column \"%s.%s\"; known columns: %y", getDesc(), tp, cv, columns.keys());
            }

            # ensure that reserved words are properly quoted
            cv = getColumnSqlName(cv);

            return use_prefix ? sprintf("%s.%s", tp, cv) : cv;
        }

        #! returns @ref True if the database requires a wildcard \c "*" to be prefixed with the table name when it appears with other column arguments in a select statement
        private bool asteriskRequiresPrefix() {
            return False;
        }

        private getSelectWhereSqlUnlocked(reference sql, reference args, *hash qh, *hash jch, bool join = False, *hash ch, *hash psch) {
            if (qh."where")
                sql += getWhereClauseUnlocked(qh."where", \args, NOTHING, jch, join, ch, psch);
        }

        private *string getWhereClause(*hash cond, reference args, *string cprefix, *hash jch, bool join = False) {
            l.lock();
            on_exit l.unlock();
            return getWhereClauseUnlocked(cond, \args, cprefix, jch, join, NOTHING, getPseudoColumnHash());
        }

        private *string getWhereClause(list cond, reference args, *string cprefix, *hash jch, bool join = False) {
            l.lock();
            on_exit l.unlock();
            return getWhereClauseUnlocked(cond, \args, cprefix, jch, join, NOTHING, getPseudoColumnHash());
        }

        private *string getWhereClauseUnlocked(list cond, reference args, *string cprefix, *hash jch, bool join = False, *hash pch, *hash psch) {
            getColumnsUnlocked();
            list al = ();
            if (!exists args)
                args = ();
            foreach hash ch in (cond) {
                *list wl = getWhereClauseIntern(ch, \args, cprefix, jch, join, pch, psch);
                if (wl)
                    al += "(" + (foldl $1 + " and " + $2, wl) + ")";
            }
            if (al)
                return " where " + (foldl $1 + " or " + $2, al);
        }

        private *string getWhereClauseUnlocked(*hash cond, reference args, *string cprefix, *hash jch, bool join = False, *hash pch, *hash psch) {
            getColumnsUnlocked();
            if (!exists args)
                args = ();
            *list wl = getWhereClauseIntern(cond, \args, cprefix, jch, join, pch, psch);
            if (wl)
                return " where " + (foldl $1 + " and " + $2, wl);
        }

        private *list getWhereClauseIntern(*hash cond, reference args, *string cprefix, *hash jch, bool join = False, *hash ch, *hash psch) {
            if (!cond)
                return;

            return map doWhereExpressionIntern(cprefix ? sprintf("%s.%s", cprefix, $1.key) : getColumnNameIntern($1.key, jch, join, ch, psch), $1.value, \args, jch, join, ch, psch), cond.pairIterator();
        }

        private string doWhereExpressionIntern(string cn, any we, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
            int wt = we.typeCode();
            if (wt == NT_HASH) {
                if (we.cop) {
                    *hash psch_copy = psch;
                    string val = doColumnOperatorIntern(we, jch, join, ch, psch, \psch_copy);
                    if (psch_copy != psch)
                        throw "WHERE-ERROR", sprintf("%s: cannot use column operator function %y in the where clause with column alias specifications", getDesc(), we.cop);
                    return sprintf("%s = %s", cn, val);
                }

                if (!we.op)
                    throw "WHERE-ERROR", sprintf("%s: argument to key %y is a hash without an \"op\" key: %y", getDesc(), cn, we);

                hash wm = getWhereOperatorMap();

                *hash cmd = wm{we.op};
                if (!cmd)
                    throw "WHERE-ERROR", sprintf("%s: operator hash for key %y has unknown operator %y: expecting one of: %y (operator hash: %y)", getDesc(), cn, we.op, wm.keys(), we);

                if (cmd.recursive) {
                    cn = doWhereExpressionIntern(cn, we.arg, \args, jch, join, ch, psch);
                    delete we.arg;
                }
                else if (cmd.argcolumn) {
                    if (!we.arg || we.arg.typeCode() != NT_STRING)
                        throw "WHERE-ERROR", sprintf("%s: operator hash for key %y expects a string column name; got type %y instead", getDesc(), cn, we.arg.type());
                    we.arg = getColumnNameIntern(we.arg, jch, join, ch, psch);
                }

                return cmd.code(self, cn, we.arg, \args, jch, join, ch, psch);
            }
            if (we === NULL || !exists we)
                return sprintf("%s is null", cn);
            args += we;
            return sprintf("%s = %v", cn);
        }

        string getOrClause(list arglist, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
            list l = ();
            foreach hash h in (arglist) {
                string str = getOrClause(h, \args, jch, join, ch, psch);
                if (str)
                    l += str;
            }
            return l ? ("(" + (foldl $1 + " or " + $2, l) + ")") : "";
        }

        string getOrClause(hash arg, reference args, *hash jch, bool join = False, *hash ch, *hash psch) {
            *list wl = getWhereClauseIntern(arg, \args, NOTHING, jch, join, ch, psch);
            return wl ? ("(" + (foldl $1 + " and " + $2, wl) + ")") : "";
        }

        private doSelectOrderBySqlUnlocked(reference sql, reference args, *hash qh, *hash jch, *hash ch, *hash psch, list coll) {
            list ce = getOrderByListUnlocked(qh, jch, ch, psch, coll);

            sql += " order by " + (foldl $1 + ", " + $2, (map $1 + (qh.desc || qh.orderbydesc.$1 ? " desc" : ""), ce));
        }

        #! deletes rows in the table matching the condition and returns the count of rows deleted; the transaction is committed if successful, if an error occurs then it is rolled back
        /** @par Example:
            @code{.py}
int dcnt = table.delCommit(("name": name));
            @endcode

            @param cond a hash of conditions for the where clause; see @ref where_clauses for more information
            @param sql an optional reference to a string to return the SQL generated for the select statement
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more info

            @return the count of rows deleted

            @throw WHERE-ERROR unknown operator or invalid arguments given in the cond hash for the where clause

            @see delete(*hash)
         */
        int delCommit(hash cond, reference sql, hash opt) {
            on_success ds.commit();
            on_error ds.rollback();
            return delIntern(cond, \sql, opt);
        }

        #! @ref SqlUtil::AbstractTable::delCommit() variant
        int delCommit(hash cond, hash opt) {
            on_success ds.commit();
            on_error ds.rollback();
            return delIntern(cond, NOTHING, opt);
        }

        #! @ref SqlUtil::AbstractTable::delCommit() variant
        int delCommit(hash cond, reference sql) {
            on_success ds.commit();
            on_error ds.rollback();
            return delIntern(cond, \sql);
        }

        #! @ref SqlUtil::AbstractTable::delCommit() variant
        int delCommit(hash cond) {
            on_success ds.commit();
            on_error ds.rollback();
            return delIntern(cond);
        }

        #! @ref SqlUtil::AbstractTable::delCommit() variant
        int delCommit() {
            on_success ds.commit();
            on_error ds.rollback();

            return delIntern();
        }

        #! deletes rows in the table matching the condition and returns the count of rows deleted; no transaction management is performed with this method
        /** @par Example:
            @code{.py}
int dcnt = table.del(("name": name));
            @endcode

            @param cond a hash of conditions for the where clause; see @ref where_clauses for more information
            @param sql an optional reference to a string to return the SQL generated for the select statement
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more info

            @return the count of rows deleted

            @throw WHERE-ERROR unknown operator or invalid arguments given in the cond hash for the where clause

            @see delCommit(*hash)
         */
        int del(hash cond, reference sql, hash opt) {
            return delIntern(cond, \sql, opt);
        }

        #! @ref SqlUtil::AbstractTable::del() variant
        int del(hash cond, hash opt) {
            return delIntern(cond, NOTHING, opt);
        }

        #! @ref SqlUtil::AbstractTable::del() variant
        int del(hash cond, reference sql) {
            return delIntern(cond, \sql);
        }

        #! @ref SqlUtil::AbstractTable::del() variant
        int del(hash cond) {
            return delIntern(cond);
        }

        #! @ref SqlUtil::AbstractTable::del() variant
        int del() {
            return delIntern();
        }

        #! A legacy @ref SqlUtil::AbstractTable::del() wrapper
        deprecated int delNoCommit(*hash cond, *reference sql) {
            return del(cond, \sql);
        }

        private int delIntern(*hash cond, *reference sql, *hash opt) {
            # make query
            sql = sprintf("delete from %s", getSqlName());
            list args;
            if (cond)
                sql += getWhereClause(cond, \args);

            return execData(opt, sql, args);
        }

        #! updates rows in the table matching an optional condition and returns the count of rows updated; the transaction is committed if successful, if an error occurs then it is rolled back
        /** @par Example:
            @code{.py}
int ucnt = table.updateCommit(("id": id), ("name": name));
            @endcode

            @param set the hash of values to set, key values are column names, hash values are the values to assign to those columns or update operators (see @ref sql_uop_funcs)
            @param cond a hash of conditions for the where clause; see @ref where_clauses for more information
            @param sql an optional reference to a string to return the SQL generated for the select statement
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more info

            @return the count of rows updated

            @throw UPDATE-ERROR the set hash is empty
            @throw WHERE-ERROR unknown operator or invalid arguments given in the cond hash for the where clause

            @see update()
         */
        int updateCommit(hash set, hash cond, reference sql, hash opt) {
            on_success ds.commit();
            on_error ds.rollback();
            return updateIntern(set, cond, \sql, opt);
        }

        #! A @ref SqlUtil::AbstractTable::updateCommit() variant
        int updateCommit(hash set, hash cond, reference sql) {
            on_success ds.commit();
            on_error ds.rollback();
            return updateIntern(set, cond, \sql);
        }

        #! A @ref SqlUtil::AbstractTable::updateCommit() variant
        int updateCommit(hash set, hash cond, hash opt) {
            on_success ds.commit();
            on_error ds.rollback();
            return updateIntern(set, cond, NOTHING, opt);
        }

        #! A @ref SqlUtil::AbstractTable::updateCommit() variant
        int updateCommit(hash set, hash cond) {
            on_success ds.commit();
            on_error ds.rollback();
            return updateIntern(set, cond);
        }

        #! A @ref SqlUtil::AbstractTable::updateCommit() variant
        int updateCommit(hash set) {
            on_success ds.commit();
            on_error ds.rollback();
            return updateIntern(set);
        }

        #! updates rows in the table matching an optional condition and returns the count of rows updated; no transaction management is performed with this method
        /** @par Example:
            @code{.py}
int ucnt = table.update(("id": id), ("name": name));
            @endcode

            @param set the hash of values to set, key values are column names, hash values are the values to assign to those columns or update operators (see @ref sql_uop_funcs)
            @param cond a hash of conditions for the where clause; see @ref where_clauses for more information
            @param sql an optional reference to a string to return the SQL generated for the select statement
            @param opt optional SQL data operation callback options; see @ref AbstractTable::SqlDataCallbackOptions for more info

            @return the count of rows updated

            @throw UPDATE-ERROR the set hash is empty
            @throw WHERE-ERROR unknown operator or invalid arguments given in the cond hash for the where clause

            @see updateCommit()
         */
        int update(hash set, hash cond, reference sql, hash opt) {
            return updateIntern(set, cond, \sql, opt);
        }

        #! A @ref SqlUtil::AbstractTable::update() variant
        int update(hash set, hash cond, reference sql) {
            return updateIntern(set, cond, \sql);
        }

        #! A @ref SqlUtil::AbstractTable::update() variant
        int update(hash set, hash cond, hash opt) {
            return updateIntern(set, cond, NOTHING, opt);
        }

        #! A @ref SqlUtil::AbstractTable::update() variant
        int update(hash set, hash cond) {
            return updateIntern(set, cond);
        }

        #! A @ref SqlUtil::AbstractTable::update() variant
        int update(hash set) {
            return updateIntern(set);
        }

        #! A legacy @ref SqlUtil::AbstractTable::update() wrapper
        deprecated int updateNoCommit(hash set, *hash cond, *reference sql) {
            return updateIntern(set, cond, \sql);
        }

        #! A legacy @ref SqlUtil::AbstractTable::update() wrapper
        deprecated int updateNoCommit(hash set, *hash cond, *hash opt) {
            return updateIntern(set, cond, NOTHING, opt);
        }

        private int updateIntern(hash set, *hash cond, *reference sql, *hash opt) {
            # make query
            sql = sprintf("update %s set ", getSqlName());

            if (!set)
                throw "UPDATE-ERROR", sprintf("%s: the set hash is empty", getDesc());

            list args;

            list sl = ();
            HashIterator i(set);
            while (i.next()) {
                string key = i.getKey();
                any value = i.getValue();
                string exp = sprintf("%s = ", getColumnSqlName(key));
                if (value.uop)
                    exp += getUpdateExpression(key, value);
                else {
                    exp += "%v";
                    args += value;
                }
                sl += exp;
            }

            sql += foldl $1 + ", " + $2, sl;

            if (cond)
                sql += getWhereClause(cond, \args);

            #printf("sql: %y\nargs: %y\n", sql, args);
            return execData(opt, sql, args);
        }

        private string getUpdateExpression(string col, hash uh) {
            hash um = getUpdateOperatorMap();

            *hash cmd = um{uh.uop};
            if (!cmd)
                throw "UPDATE-ERROR", sprintf("%s: unknown update operator %y in %y: expecting one of: %y", getDesc(), uh.uop, uh, um.keys());

            if (cmd.arg) {
                string at = uh.arg.type();
                switch (cmd.arg) {
                    default: {
                        if (at != cmd.arg)
                            throw "UPDATE-ERROR", sprintf("%s: invalid argument to update operator %y; got type %y, expecting %y", getDesc(), uh.uop, at, cmd.arg);
                    }
                }
            }

            if (cmd.sqlvalue)
                uh.arg = getSqlValue(uh.arg);
            else if (cmd.argcolumn) {
                if (!cmd.argoptional || uh.arg) {
                    if (uh.arg.typeCode() != NT_STRING)
                        throw "UPDATE-ERROR", sprintf("%s: expecting a string column designation for update operator %y; got type %y instead", getDesc(), uh.uop, uh.arg.type());
                    uh.arg = getColumnNameIntern(uh.arg, NOTHING, False);
                }
            }
            else if (cmd.columnargs) {
                if (uh.arg.typeCode() != NT_LIST)
                    throw "UPDATE-ERROR", sprintf("%s: expecting a list of column designators for update operator %y; got type %y instead", getDesc(), uh.uop, uh.arg.type());
                foreach any narg in (\uh.arg) {
                    switch (narg.typeCode()) {
                        case NT_STRING: narg = getColumnNameIntern(narg, NOTHING, False); break;
                        case NT_HASH: narg = doColumnOperatorIntern(narg, NOTHING, False); break;
                        default: throw "COLUMN-ERROR", sprintf("%s: expecting a string column designator or a update operator hash; got %y in position %d/%d instead", getDesc(), narg, $#, uh.arg.lsize());
                    }
                }
            }

            string arg = (uh.nest ? getUpdateExpression(col, uh.nest) : col);

            return cmd.code(arg, uh.arg);
        }

        private bool emptyDataIntern() {
            list args;
            string sql = getSelectSql(("limit": 1), \args);
            return boolean(ds.vselectRow(sql, args));
        }

        private Columns checkUpsertRow(hash row, reference upsert_strategy) {
            # unique column source
            string csrc;

            # get unique columns
            Columns cols = getUpsertColumns(\csrc);

            # column list: make sure all columns in the key are used and have a value
            list cl = ();
            foreach string k in (row.keyIterator()) {
                if (!columns.hasKey(k))
                    throw "COLUMN-ERROR", sprintf("%s column %y is not a valid column (valid columns: %y)", getDesc(), k, columns.keys());
                if (cols.hasKey(k))
                    cl += k;
            }

            # if not all columns of the unique constraint are used, then throw an exception
            if (cl.size() != cols.size())
                throw "UPSERT-ERROR", sprintf("%s: not all columns from %s are used in the upsert statement; required: %y; used: %y", getDesc(), csrc, columns.keys(), cl);

            # use UpsertSelectFirst if the row size == the key size (ie only key values are used)
            if (cols.size() == row.size())
                upsert_strategy = UpsertSelectFirst;

            return cols;
        }

        private code getUpsertInsertFirst(Columns cols, hash example_row, *hash opt) {
            string insert_sql = getUpsertInsertSql(example_row);
            list updc = ();
            string update_sql = getUpsertUpdateSql(example_row, cols, \updc, opt);

            return int sub (hash row) {
                if (tryInsertImpl(insert_sql, row))
                    return UR_Inserted;

                tryUpdate(update_sql, row, cols, updc);
                return UR_Verified;
            };
        }

        private code getUpsertUpdateFirst(Columns cols, hash example_row, *hash opt) {
            list updc = ();
            string update_sql = getUpsertUpdateSql(example_row, cols, \updc, opt);
            string insert_sql = getUpsertInsertSql(example_row);

            return int sub (hash row) {
                if (tryUpdate(update_sql, row, cols, updc))
                    return UR_Verified;

                ds.vexec(insert_sql, row.values());
                return UR_Inserted;
            };
        }

        private code getUpsertSelectFirst(Columns cols, hash example_row, *hash opt) {
            #printf("getUpsertSelectFirst() %s: %y\n", name, cols.keys());
            list updc = ();
            string select_sql = getUpsertSelectSql(example_row, cols, \updc);
            string insert_sql = getUpsertInsertSql(example_row);
            string update_sql = getUpsertUpdateSql(example_row, cols, \updc, opt);

            return int sub (hash row) {
                list args = row.(cols.keys()).values();
                #printf("select_sql: %s args: %y\n", select_sql, args);
                *hash dbrow = ds.vselectRow(select_sql, args);
                if (!dbrow) {
                    ds.vexec(insert_sql, row.values());
                    return UR_Inserted;
                }
                if (dbrow == row{updc})
                    return UR_Unchanged;

                #printf("dbrow: %y row{%y}: %y\n", dbrow, updc, row{updc});

                tryUpdate(update_sql, row, cols, updc);
                return UR_Updated;
            };
        }

        private code getUpsertInsertOnly(Columns cols, hash example_row, *hash opt) {
            string insert_sql = getUpsertInsertSql(example_row);

            return int sub (hash row) {
                return tryInsertImpl(insert_sql, row) ? UR_Inserted : UR_Unchanged;
            };
        }

        private code getUpsertUpdateOnly(Columns cols, hash example_row, *hash opt) {
            list updc = ();
            string update_sql = getUpsertUpdateSql(example_row, cols, \updc, opt);

            return int sub (hash row) {
                return tryUpdate(update_sql, row, cols, updc) ? UR_Verified : UR_Unchanged;
            };
        }

        private Columns getUpsertColumns(reference csrc) {
            l.lock();
            on_exit l.unlock();

            getColumnsUnlocked();
            getPrimaryKeyUnlocked();

            Columns cols;

            if (!primaryKey.empty()) {
                cols = primaryKey;
                csrc = sprintf("primary key %s", primaryKey.getName());
            }
            else {
                getConstraintsUnlocked();
                # find first unique constraint
                foreach AbstractConstraint c in (constraints.iterator()) {
                    if (c instanceof AbstractUniqueConstraint) {
                        cols = cast<AbstractUniqueConstraint>(c);
                        csrc = sprintf("unique constraint %s", c.getName());
                        break;
                    }
                }
                if (!cols) {
                    getIndexesUnlocked();
                    foreach AbstractIndex ix in (indexes.iterator()) {
                        if (ix.unique) {
                            cols = ix.columns;
                            csrc = sprintf("unique index %s", ix.name);
                            break;
                        }
                    }

                    if (!cols)
                        throw "UPSERT-ERROR", sprintf("%s: cannot perform upsert because there is no primary key, unique constraint, or unique index", getDesc());
                }
            }

            return cols;
        }

        private string getUpsertSelectSql(hash row, Columns cols, reference updc) {
            if (cols.size() != row.size()) {
                # make list of columns not in unique key
                map updc += $1, row.keyIterator(), !cols.hasKey($1);
            }
            else
                updc = cols.keys();

            return sprintf("select %s from %s where %s",
                           (foldl $1 + ", " + $2, getColumnSqlNames(updc)),
                           getSqlName(),
                           (foldl $1 + " and " + $2, (map sprintf("%s = %v", getColumnSqlName($1)), cols.keyIterator()))
                );
        }

        private string getUpsertInsertSql(hash row) {
            string sql = sprintf("insert into %s (", getSqlName());
            sql += (foldl $1 + "," + $2, (map getColumnSqlName($1), row.keyIterator()));
            sql += ") values (";
            sql += (foldl $1 + "," + $2, (map "%v", row.keyIterator()));
            sql += ")";
            return sql;
        }

        private string getUpsertUpdateSql(hash row, Columns cols, reference updc, *hash opt) {
            # make list of columns not in unique key if necessary
            if (!updc) {
                # hash of columns to skip on update if applicable
                *hash suh = map {$1: True}, opt.omit_update;
                map updc += $1, row.keyIterator(), !cols.hasKey($1) && !suh.$1;
            }

            # make sql
            string sql = sprintf("update %s set ", getSqlName());
            sql += foldl $1 + ", " + $2, (map sprintf("%s = %v", getColumnSqlName($1)), updc);
            sql += " where ";
            sql += foldl $1 + " and " + $2, (map sprintf("%s = %v", getColumnSqlName($1)), cols.keyIterator());

            return sql;
        }

        private softbool tryUpdate(string sql, hash row, Columns cols, list updc) {
            list args = row{updc}.values() + row{cols.keys()}.values();
            return ds.vexec(sql, args);
        }

        private checkValue(string cname, string argname, reference val, string type) {
            if (val === NULL) {
                delete val;
                return;
            }

            switch (type) {
                case Type::Int: {
                    if (!val.intp())
                        throw "COLUMN-ERROR", sprintf("%s column %y: %s has type %y, expecting %y (value: %y)", name, cname, argname, val.type(), type, val);
                    if (val.type() != type)
                        val = val.toInt();
                    break;
                }
                case Type::Float: {
                    if (!val.intp())
                        throw "COLUMN-ERROR", sprintf("%s column %y: %s has type %y, expecting %y (value: %y)", getDesc(), cname, argname, val.type(), type, val);
                    if (val.type() != type)
                        val = val.toFloat();
                    break;
                }
                case Type::Number: {
                    if (!val.intp())
                        throw "COLUMN-ERROR", sprintf("%s column %y: %s has type %y, expecting %y (value: %y)", getDesc(), cname, argname, val.type(), type, val);
                    if (val.type() != type)
                        val = number(val);
                    break;
                }
                case Type::Boolean: {
                    if (!val.intp())
                        throw "COLUMN-ERROR", sprintf("%s column %y: %s has type %y, expecting %y (value: %y)", getDesc(), cname, argname, val.type(), type, val);
                    if (val.type() != type)
                        val = val.toBool();
                    break;
                }
                case Type::String: {
                    if (!val.strp())
                        throw "COLUMN-ERROR", sprintf("%s column %y: %s has type %y, expecting %y (value: %y)", getDesc(), cname, argname, val.type(), type, val);
                    if (val.type() != type)
                        val = val.toString();
                    break;
                }
              default: {
                  if (val.type() != type)
                      throw "COLUMN-ERROR", sprintf("%s column %y: %s value for has type %y, expecting %y (value: %y)", getDesc(), cname, argname, val.type(), type, val);
                  break;
                }
            }
        }

        #! returns an SQL string corresponding to the list of commands in the argument
        /** @par Example:
            @code{.py}
string sql = t.getSqlFromList(list);
            @endcode

            @param l a list of SQL commands

            @return an SQL string corresponding to the list of commands in the argument
        */
        string getSqlFromList(list l) {
            return getCreateSqlImpl(l);
        }

        #! returns a string for use in SQL queries representing the DB-specific value of the argument
        /** @par Example:
            @code{.py}
string str = table.getSqlValue(date);
            @endcode

            @param v the value to convert to an SQL value

            @return a string representing the value usable in SQL strings for the given database driver

            @throw VALUE-ERROR the given value cannot be converted to an SQL string (missing rules for value)
         */
        string getSqlValue(any v) {
            *string str = getSqlValueImpl(v);
            if (!exists str)
                throw "VALUE-ERROR", sprintf("%s: class %s cannot convert type %y to an SQL string (value: %y)", getDesc(), self.className(), v.type(), v);

            return str;
        }

        #! returns the name of the table
        string getName() {
            return name;
        }

        #! reads in all attributes of the table from the database
        /** @par Example:
            @code{.py}
table.cache();
            @endcode

            @param opts cache options; see @ref SqlUtil::AbstractTable::CacheOptions for common options; each driver can support additional driver-specific options
        */
        cache(*hash opts) {
            l.lock();
            on_exit l.unlock();
            cacheUnlocked(opts);
        }

        #! purges the current table definition
        /** @par Example:
            @code{.py}
table.clear();
            @endcode
        */
        clear() {
            l.lock();
            on_exit l.unlock();

            delete columns;
            delete primaryKey;
            delete foreignConstraints;
            delete constraints;
            delete indexes;
            delete triggers;

            clearImpl();
        }

        #! returns an object of class Columns describing the table
        /** @par Example:
            @code{.py}
Columns cols = table.describe();
            @endcode

            @return an object of class Columns describing the table
        */
        Columns describe() {
            l.lock();
            on_exit l.unlock();
            getColumnsUnlocked();
            return columns;
        }

        #! returns an object of class AbstractPrimaryKey describing the primary key of the table
        /** If there is no primary key then the object returned will be empty (see @ref SqlUtil::AbstractPrimaryKey::empty())

            @par Example:
            @code{.py}
            @endcode

            @return an object of class AbstractPrimaryKey describing the primary key of the table
        */
        AbstractPrimaryKey getPrimaryKey() {
            l.lock();
            on_exit l.unlock();
            getPrimaryKeyUnlocked();
            return primaryKey;
        }

        #! returns the given AbstractUniqueConstraint object if defined for the table (also includes the primary key)
        /** @par Example:
            @code{.py}
my *AbstractUniqueConstraint uk = table.findUniqueConstraint("uk_table2");
            @endcode

            @param name the name of the unique constraint to find

            @return the given AbstractUniqueConstraint object if defined for the table (also includes the primary key)
        */
        *AbstractUniqueConstraint findUniqueConstraint(string name) {
            l.lock();
            on_exit l.unlock();

            return findUniqueConstraintUnlocked(name);
        }

        *AbstractUniqueConstraint findUniqueConstraintUnlocked(string name) {
            getPrimaryKeyUnlocked();
            getConstraintsUnlocked();
            if (!primaryKey.empty() && primaryKey.getName() == name)
                return primaryKey;

            return constraints.hasKey(name) && constraints{name} instanceof AbstractUniqueConstraint ? constraints{name} : NOTHING;
        }

        #! returns an object of class Indexes describing the indexes on the table
        /** If there are no indexes on the table then the object returned will be empty (see @ref SqlUtil::Indexes::empty())

            @par Example:
            @code{.py}
Indexes ix = table.getIndexes();
            @endcode

            @return an object of class Indexes describing the indexes on the table
        */
        Indexes getIndexes() {
            l.lock();
            on_exit l.unlock();
            getIndexesUnlocked();
            return indexes;
        }

        #! returns a ForeignConstraints object describing the foreign constraints that the table has on other tables
        ForeignConstraints getForeignConstraints(*hash opt) {
            l.lock();
            on_exit l.unlock();
            getForeignConstraintsUnlocked(opt);
            return foreignConstraints;
        }

        #! returns a Constraints object describing the non-foreign constraints on the table
        Constraints getConstraints() {
            l.lock();
            on_exit l.unlock();
            getConstraintsUnlocked();
            return constraints;
        }

        #! returns an object of class Triggers describing the triggers on the table
        /** If there are no triggers on the table then the object returned will be empty (see @ref SqlUtil::Triggers::empty())

            @par Example:
            @code{.py}
Triggers trig = table.getTriggers();
            @endcode

            @return an object of class Triggers describing the triggers on the table
        */
        Triggers getTriggers() {
            l.lock();
            on_exit l.unlock();
            getTriggersUnlocked();
            return triggers;
        }

        #! returns an SQL string that could be used to rename the table in the database
        /** @par Example:
            @code{.py}
string sql = table.getRenameSql("new_name");
            @endcode

            @param new_name the new name for the table
            @param opt a hash of options for the SQL string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return an SQL string that could be used to rename the table in the database

            @throw OPTION-ERROR invalid or unsupported option passed

            @note If the @ref sql_callback_executed "sql_callback_executed option key" is @ref Qore::True "True", this method also renames the object internally (see @ref sql_callback_executed for more information), additionally if the \c "db_table_cache" option key is assigned to a @ref SqlUtil::Tables "Tables" argument, then the table cache is also updated with the name change in this case

            @see
            - rename()
            - inDb() for a method that tells if the table is already in the database or not
         */
        string getRenameSql(string new_name, *hash opt) {
            validateOptionsIntern("OPTION-ERROR", getAlignTableOptions(), \opt);

            l.lock();
            on_exit l.unlock();

            on_success if (opt.sql_callback_executed)
                doRenameIntern(new_name, opt.db_table_cache);

            return AbstractDatabase::doCallback(opt, getRenameSqlImpl(new_name), AbstractDatabase::AC_Rename, "table", name, NOTHING, new_name);
        }

        #! returns an SQL string that could be used to create the table and all known properties of the table
        /** @par Example:
            @code{.py}
printf("%s\n", table.getCreateSql());
            @endcode

            @param opt a hash of options for the SQL creation string

            @return an SQL string that could be used to create the table and all known properties of the table
        */
        string getCreateSqlString(*hash opt) {
            # convert table alignment omission options to a hash and check
            if (opt.omit) opt.omit = getCheckOmissionOptions(opt.omit, "CREATE-TABLE-ERROR");

            l.lock();
            on_exit l.unlock();

            if (!manual)
                cacheUnlocked(opt);

            return getCreateSqlImpl(getCreateSqlUnlocked(opt));
        }

        #! returns a list of SQL strings that could be used to create the table and all known properties of the table
        /** @par Example:
            @code{.py}
map printf("$1;\n", $1), table.getCreateSql();
            @endcode

            @param opt a hash of options for the table, index, and constraint creation strings; see @ref TableCreationOptions for common options; each driver can support additional driver-specific options

            @return a list of SQL strings that could be used to create the table and all known properties of the table
        */
        list getCreateSql(*hash opt) {
            # convert table alignment omission options to a hash and check
            if (opt.omit) opt.omit = getCheckOmissionOptions(opt.omit, "CREATE-TABLE-ERROR");

            l.lock();
            on_exit l.unlock();
            if (!manual)
                cacheUnlocked(opt);
            return getCreateSqlUnlocked(opt, False);
        }

        #! returns an SQL string that could be used to create the basic table structure without indexes and constraints
        /** @par Example:
            @code{.py}
string sql = table.getCreateTableSql();
            @endcode

            @param opt a hash of options for the table creation string; see @ref TableCreationOptions for common options; each driver can support additional driver-specific options

            @return an SQL string that could be used to create the basic table structure without indexes and constraints

            @throw OPTION-ERROR invalid or unsupported option passed
        */
        string getCreateTableSql(*hash opt) {
            # convert table alignment omission options to a hash and check
            if (opt.omit) opt.omit = getCheckOmissionOptions(opt.omit, "CREATE-TABLE-ERROR");

            validateOptionsIntern("OPTION-ERROR", getTableCreationOptions(), \opt);
            l.lock();
            on_exit l.unlock();
            return getCreateTableSqlUnlocked(opt);
        }

        #! returns @ref Qore::True "True" if the table exists in the database, @ref Qore::False "False" if not
        /** this method will actively check the database if the table is defined there in case no configuration from the database has already been cached for the object

            @see AbstractTable::inDb()
        */
        bool checkExistence() {
            l.lock();
            on_exit l.unlock();
            if (inDb)
                return True;
            return checkExistenceImpl();
        }

        private *hash getCheckOmissionOptions(*softlist ol, string err) {
            # convert table alignment omission options to a hash and check
            if (!ol)
                return;

            hash oh;
            foreach string ostr in (ol) {
                if (!TableOmissionOptions{ostr})
                    throw err, sprintf("omission option %y is unknown; known omission options: %y", ostr, TableOmissionOptions.keys());
                oh{ostr} = True;
            }
            return oh;
        }

        #! accepts an AbstractTable argument and returns a list of SQL strings required to align the structure and configuration of the current table with that of the argument; if the tables are identical then an empty list is returned
        /** @par Example:
            @code{.py}
list l = table.getAlignSql(table2);
            @endcode

            @param t the template table that the current table will be compared to
            @param opt a hash of options for the SQL creation string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return a list of SQL strings required to align the structure and configuration of the current table with that of the AbstractTable argument; if the tables are identical then an empty list is returned

            @throw ALIGN-TABLE-ERROR the argument must be of the same class as the current object; template table has no columns
            @throw OPTION-ERROR invalid or unsupported option passed

            @note if the @ref sql_callback_executed "sql_callback_executed option" is @ref Qore::True "True" in \a opt, then the changes are also effected in the current object, if not, then they are not (see @ref sql_callback_executed for more information)
        */
        list getAlignSql(AbstractTable t, *hash opt) {
            if (self.className() != t.className())
                throw "ALIGN-TABLE-ERROR", sprintf("cannot align %s of class %y with %s of class %y", getDesc(), self.className(), t.getDesc(), t.className());

            hash ato = getAlignTableOptions();

            validateOptionsIntern("OPTION-ERROR", ato, \opt);

            # convert table alignment omission options to a hash and check
            opt.omit = getCheckOmissionOptions(opt.omit, "ALIGN-TABLE-ERROR");

            l.lock();
            on_exit l.unlock();
            # lock "template" table
            t.l.lock();
            on_exit t.l.unlock();

            # cache tables if not already cached
            if (!t.manual)
                t.cacheUnlocked(opt);

            if (!manual) {
                # if the table does not exist, then just return the create table strings for the template table
                if (!checkExistenceImpl())
                    return t.getCreateSqlUnlocked(opt, False);

                # use db_table_cache for table_cache when caching DB objects
                *hash db_opt = opt;
                db_opt.table_cache = remove db_opt.db_table_cache;
                cacheUnlocked(db_opt);
            }

            if (!t.columns || t.columns.empty())
                throw "ALIGN-TABLE-ERROR", sprintf("cannot align %s with %s: %y has no columns", getDesc(), t.getDesc(), t.name);

            if (!columns)
                columns = new Columns();

            AbstractTable this = opt.sql_callback_executed ? self : self.copy();

            return this.getAlignSqlUnlocked(t, opt);
        }

        private list getAlignSqlUnlocked(AbstractTable t, *hash opt) {
            list l = ();

            # check name
            if (name != t.name) {
                l += AbstractDatabase::doCallback(opt, getRenameSql(t.name), AbstractDatabase::AC_Rename, "table", name, NOTHING, t.name);
                name = t.name;
            }

            bool uixc = uniqueIndexCreatesConstraintImpl();

            # process column_map: rename columns
            HashIterator i(opt.column_map);
            while (i.next()) {
                string key = i.getKey();
                if (!columns.hasKey(key))
                    continue;
                string value = i.getValue();
                #printf("%s rename %s -> %s cols: %y\n", name, key, value, columns.keys());
                AbstractColumn c = columns{key};
                l += AbstractDatabase::doCallback(opt, c.getRenameSql(self, value), AbstractDatabase::AC_Rename, "column", c.name, name, value);
                columns.renameKey(key, value);
                c.name = value;

                # rename column in indexes
                foreach AbstractIndex ix in (indexes.iterator()) {
                    if (ix.columns.hasKey(key)) {
                        #printf("%s.%s: col %y %N\n", getName(), ix.name, key, ix.columns);
                        AbstractColumn rc = ix.columns{key};
                        rc.name = value;
                        ix.columns.renameKey(key, value);
                    }
                }
                # rename column in pk
                if (primaryKey.hasKey(key)) {
                    AbstractColumn rc = primaryKey{key};
                    rc.name = value;
                    primaryKey.renameKey(key, value);
                }
                # rename column in unique constraints
                foreach AbstractConstraint ct in (constraints.iterator()) {
                    if (ct instanceof AbstractUniqueConstraint) {
                        AbstractUniqueConstraint uk = ct;
                        if (uk.hasKey(key)) {
                            AbstractColumn rc = uk{key};
                            rc.name = value;
                            uk.renameKey(key, value);
                        }
                    }
                }
                # rename column in foreign constraints
                foreach AbstractForeignConstraint fk in (foreignConstraints.iterator()) {
                    if (fk.hasKey(key)) {
                        AbstractColumn rc = fk{key};
                        rc.name = value;
                        fk.renameKey(key, value);
                    }
                }
            }

            # before modifying columns, rename indexes constraints and drop any excess indexes and constraints
            # process index_map: rename indexes
            i = new HashIterator(opt.index_map);
            while (i.next()) {
                string key = i.getKey();
                if (indexes.hasKey(key)) {
                    AbstractIndex ix = indexes{key};
                    string value = i.getValue();
                    l += AbstractDatabase::doCallback(opt, ix.getRenameSql(getSqlName(), value), AbstractDatabase::AC_Rename, "index", ix.name, name, value);
                    renameIndexUnlocked(ix, value);
                }
            }

            if (!opt.omit.indexes) {
                # check for indexes with different names than in the template and rename them automatically
                foreach AbstractIndex ix in (indexes.iterator()) {
                    if (!t.indexes.hasKey(ix.name)
                        && t.indexes
                        && (*AbstractIndex other = t.indexes.findEqual(ix))
                        && !indexes.hasKey(other.name)) {
                        # rename index
                        l += AbstractDatabase::doCallback(opt, ix.getRenameSql(getSqlName(), other.name), AbstractDatabase::AC_Rename, "index", ix.name, name, other.name);
                        renameIndexUnlocked(ix, other.name);
                    }
                }

                # check for indexes not in the template and drop them
                foreach AbstractIndex ix in (indexes.iterator()) {
                    if (!t.indexes.hasKey(ix.name)) {
                        # make sure there is no unique constraint or primary key for the unique index if the "index" option is supported
                        # in which case update the constraint to be based on the index and do not drop the index
                        if (ix.unique && !t.primaryKey.empty() && t.primaryKey.matchKeys(ix.columns)) {
                            if (t.primaryKey.setIndexBase(ix.name)) {
                                ix.setSupportingConstraint(primaryKey);
                                continue;
                            }
                        }
                        else {
                            # see if a unique or fk constraint with the same name and columns exists
                            *AbstractColumnSupportingConstraint csc = getSupportingConstraint(ix.name);
                            if (csc && csc.matchKeys(ix.columns) && csc.setIndexBase(ix.name)) {
                                ix.setSupportingConstraint(csc);
                                continue;
                            }
                        }
                        # see if the index is supporting a constraint that has to be dropped first
                        *AbstractColumnSupportingConstraint csc = ix.getSupportingConstraint();
                        if (csc && constraintsLinkedToIndexesImpl()) {
                            code rmv;
                            AbstractConstraint c = findDropConstraintUnlocked(csc.getName(), \rmv);

                            l += AbstractDatabase::doCallback(opt, c.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "constraint", c.getName(), name);
                            rmv();
                        }
                        else
                            l += AbstractDatabase::doCallback(opt, ix.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "index", ix.name, name);
                        if (uixc) {
                            *AbstractUniqueConstraint c = ix.getSupportingConstraint();
                            if (c && constraints.hasKey(c.getName()))
                                constraints.take(c.getName());
                        }
                        indexes.tryTake(ix.name);
                    }
                }
            }

            # process constraint_map: rename constraints
            i = new HashIterator(opt.constraint_map);
            while (i.next()) {
                string key = i.getKey();
                string value = i.getValue();
                *AbstractConstraint c;
                if (foreignConstraints.hasKey(key)) {
                    c = foreignConstraints{key};
                    foreignConstraints.renameKey(key, value);
                }
                else if (constraints.hasKey(key)) {
                    c = constraints{key};
                    constraints.renameKey(key, value);
                }
                else if (!primaryKey.empty() && primaryKey.getName() == key)
                    c = primaryKey;

                if (c) {
                    l += AbstractDatabase::doCallback(opt, c.getRenameSql(getSqlName(), value), AbstractDatabase::AC_Rename, "constraint", c.getName(), name, value);
                    c.rename(value);
                }
            }

            # check for unique constraints with different names than in the template and rename them automatically
            foreach AbstractConstraint c in (constraints.iterator()) {
                if (!(c instanceof AbstractUniqueConstraint))
                    continue;
                AbstractUniqueConstraint uk = c;
                if (!t.constraints.hasKey(uk.getName())
                    && t.constraints
                    && (*AbstractUniqueConstraint other = t.constraints.findEqualUniqueConstraint(uk))
                    && !constraints.hasKey(other.getName())) {
                    # rename constraint
                    l += AbstractDatabase::doCallback(opt, uk.getRenameSql(getSqlName(), other.getName()), AbstractDatabase::AC_Rename, "constraint", uk.getName(), name, other.getName());
                    constraints.renameKey(uk.getName(), other.getName());
                    uk.rename(other.getName());
                }
            }

            if (!opt.omit.foreign_constraints) {
                # check for foreign constraints with different names than in the template and rename them automatically
                foreach AbstractForeignConstraint fk in (foreignConstraints.iterator()) {
                    if (!t.foreignConstraints.hasKey(fk.getName())
                        && t.foreignConstraints
                        && (*AbstractForeignConstraint other = t.foreignConstraints.findEqual(fk))
                        && !foreignConstraints.hasKey(other.getName())) {
                        # rename constraint
                        l += AbstractDatabase::doCallback(opt, fk.getRenameSql(getSqlName(), other.getName()), AbstractDatabase::AC_Rename, "constraint", fk.getName(), name, other.getName());
                        foreignConstraints.renameKey(fk.getName(), other.getName());
                        fk.rename(other.getName());
                    }
                }

                # check for foreign constraints not in the template and drop them
                foreach AbstractForeignConstraint c in (foreignConstraints.iterator()) {
                    if (!t.foreignConstraints.hasKey(c.getName())) {
                        l += AbstractDatabase::doCallback(opt, c.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "foreign constraint", c.getName(), name);
                        foreignConstraints.take(c.getName());
                    }
                }
            }

            # check for other constraints not in the template and drop them
            foreach AbstractConstraint c in (constraints.iterator()) {
                if (!t.constraints.hasKey(c.getName())) {
                    l += AbstractDatabase::doCallback(opt, c.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "constraint", c.getName(), name);
                    constraints.take(c.getName());
                }
            }

            # see if we need to drop the primary key
            if (!t.primaryKey.empty()) {
                if (!primaryKey.empty()) {
                    if (!primaryKey.equal(t.primaryKey)) {
                        # see if pk has the same definition but a different name; if so, it will be renamed later
                        if (primaryKey.getName() == t.primaryKey.getName() || primaryKey.matchKeys(t.primaryKey)) {
                            #printf("pk1: %N\nXXXXXXX\npk2: %N\n", primaryKey, t.primaryKey);
                            l += AbstractDatabase::doCallback(opt, primaryKey.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "primary key", t.primaryKey.getName(), name);
                            # clear supporting constraint from index in the template since we've dropped the PK constraint
                            *string pk_index = t.primaryKey.getIndex();
                            if (pk_index && t.indexes.hasKey(pk_index))
                                t.indexes{pk_index}.setSupportingConstraint();
                            if (constraintsLinkedToIndexesImpl() && indexes.hasKey(primaryKey.getName()))
                                t.primaryKey.clearIndex();

                            delete primaryKey;
                        }
                    }
                }
                else {
                    # there is no PK in the table, so we have to remove the link from the index to the PK in the template
                    *string pk_index = t.primaryKey.getIndex();
                    if (pk_index && t.indexes.hasKey(pk_index))
                        t.indexes{pk_index}.setSupportingConstraint();
                    if (constraintsLinkedToIndexesImpl() && indexes.hasKey(t.primaryKey.getName()))
                        t.primaryKey.clearIndex();
                }
            }
            else if (!primaryKey.empty()) {
                l += AbstractDatabase::doCallback(opt, primaryKey.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "primary key", primaryKey.getName(), name);
                delete primaryKey;
            }

            # check for columns in the template that are not present or different in the current table
            foreach AbstractColumn c in (t.columns.iterator()) {
                if (!columns.hasKey(c.name)) {
                    # add column to table
                    l += AbstractDatabase::doCallback(opt, c.getAddColumnSql(self), AbstractDatabase::AC_Add, "column", c.name, name);
                }
                else {
                    # get any delta SQL
                    list cl = columns{c.name}.getModifySql(self, c);
                    # add delta SQL to list if there is any to add
                    if (cl) {
                        l += AbstractDatabase::doCallback(opt, cl, AbstractDatabase::AC_Modify, "column", c.name, name, NOTHING, c.getNativeTypeString());
                    }
                    else
                        AbstractDatabase::doOkCallback(opt, AbstractDatabase::AC_Unchanged, "column", c.name, name);
                }
            }

            # check for columns not in the template and drop them
            foreach AbstractColumn c in (columns.iterator()) {
                if (!t.columns.hasKey(c.name)) {
                    l += getDropAllConstraintsAndIndexesOnColumnSqlUnlocked(c.name, opt);
                    l += AbstractDatabase::doCallback(opt, c.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "column", c.name, name);
                }
            }

            if (!opt.omit.indexes) {
                # check for indexes in the template that are not present or different in the current table
                foreach AbstractIndex ix in (t.indexes.iterator()) {
                    if (!indexes.hasKey(ix.name)) {
                        # add index to table
                        l += AbstractDatabase::doCallback(opt, ix.getCreateSql(getSqlName(), opt), AbstractDatabase::AC_Add, "index", ix.name, name);
                    }
                    else if (!ix.equal(indexes{ix.name})) {
                        l += AbstractDatabase::doCallback(opt, ix.getRecreateSql(ds, getSqlName(), opt), AbstractDatabase::AC_Recreate, "index", ix.name, name);
                    }
                    else
                        AbstractDatabase::doOkCallback(opt, AbstractDatabase::AC_Unchanged, "index", ix.name, name);
                }
            }

            # check for primary key differences
            if (!t.primaryKey.empty()) {
                if (!primaryKey.empty()) {
                    # we know the 2 primary key definitions have equal column definitions as it's checked above
                    # see if pk has a different name
                    if (primaryKey.getName() != t.primaryKey.getName()) {
                        l += AbstractDatabase::doCallback(opt, primaryKey.getRenameSql(getSqlName(), t.primaryKey.getName()), AbstractDatabase::AC_Rename, "constraint", primaryKey.getName(), name, t.primaryKey.getName());
                        primaryKey.rename(t.primaryKey.getName());
                    }
                }
                else {
                    l += AbstractDatabase::doCallback(opt, t.primaryKey.getCreateSql(getSqlName(), opt), AbstractDatabase::AC_Add, "primary key", t.primaryKey.getName(), name);
                }
            }

            if (!opt.omit.foreign_constraints) {
                # check for foreign constraints in the template that are not present or different in the current table
                foreach AbstractForeignConstraint c in (t.foreignConstraints.iterator()) {
                    if (!foreignConstraints.hasKey(c.getName())) {
                        # add constraint to table
                        l += AbstractDatabase::doCallback(opt, c.getCreateSql(getSqlName(), opt), AbstractDatabase::AC_Add, "foreign constraint", c.getName(), name);
                    }
                    else if (!c.equal(foreignConstraints{c.getName()})) {
                        # drop current constraint
                        l += AbstractDatabase::doCallback(opt, foreignConstraints{c.getName()}.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "foreign constraint", c.getName(), name);
                        # add new constraint to table
                        l += AbstractDatabase::doCallback(opt, c.getCreateSql(getSqlName(), opt), AbstractDatabase::AC_Add, "foreign constraint", c.getName(), name);
                    }
                    else
                        AbstractDatabase::doOkCallback(opt, AbstractDatabase::AC_Unchanged, "foreign constraint", c.getName(), name);
                }
            }

            # check for other constraints in the template that are not present or different in the current table
            foreach AbstractConstraint c in (t.constraints.iterator()) {
                if (!constraints.hasKey(c.getName())) {
                    # make sure the constraint wasn't implicitly added with a unique index
                    if (uixc && (c instanceof AbstractUniqueConstraint)) {
                        AbstractUniqueConstraint uk = c;
                        if (t.indexes.hasKey(uk.getName()) && uk.matchKeys(t.indexes{uk.getName()}.columns.getHash()))
                            continue;
                    }
                    # add constraint to table
                    l += AbstractDatabase::doCallback(opt, c.getCreateSql(getSqlName(), opt), AbstractDatabase::AC_Add, "constraint", c.getName(), name);
                }
                else if (!c.equal(constraints{c.getName()})) {
                     # drop current constraint
                    l += AbstractDatabase::doCallback(opt, constraints{c.getName()}.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "constraint", c.getName(), name);
                    if (constraints{c.getName()} instanceof AbstractUniqueConstraint && constraintsLinkedToIndexesImpl() && indexes.hasKey(c.getName()))
                        cast<AbstractUniqueConstraint>(c).clearIndex();

                    # add new constraint to table
                    l += AbstractDatabase::doCallback(opt, c.getCreateSql(getSqlName(), opt), AbstractDatabase::AC_Add, "constraint", c.getName(), name);
                }
                else
                    AbstractDatabase::doOkCallback(opt, AbstractDatabase::AC_Unchanged, "constraint", c.getName(), name);
            }

            # check for table-specific alignment actions before triggers
            {
                *list fl = getAlignSqlImpl(t, opt);
                if (fl)
                    l += fl;
            }

            if (!opt.omit.triggers) {
                # check for triggers not in the template and drop them
                # do this before adding for dbs like mysql that cannot support multiple triggers with the same action on the same table
                foreach AbstractTrigger trig in (triggers.iterator()) {
                    if (!t.triggers.hasKey(trig.name)) {
                        l += AbstractDatabase::doCallback(opt, trig.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "trigger", trig.name, name);
                    }
                }

                # check for triggers in the template that are not present or different in the current table
                foreach AbstractTrigger trig in (t.triggers.iterator()) {
                    if (!triggers.hasKey(trig.name)) {
                        # add trigger to table
                        l += AbstractDatabase::doCallback(opt, trig.getCreateSql(getSqlName(), opt), AbstractDatabase::AC_Add, "trigger", trig.name, name);
                    }
                    else if (!trig.equal(triggers{trig.name})) {
                        # drop current trigger
                        l += AbstractDatabase::doCallback(opt, triggers{trig.name}.getDropSql(getSqlName()), AbstractDatabase::AC_Drop, "trigger", trig.name, name);
                        # add new trigger to table
                        l += AbstractDatabase::doCallback(opt, trig.getCreateSql(getSqlName(), opt), AbstractDatabase::AC_Add, "trigger", trig.name, name);
                    }
                    else
                        AbstractDatabase::doOkCallback(opt, AbstractDatabase::AC_Unchanged, "trigger", trig.name, name);
                }
            }

            return l;
        }

        private *AbstractColumnSupportingConstraint getSupportingConstraint(string ixname) {
            if (constraints.hasKey(ixname) && constraints{ixname} instanceof AbstractColumnSupportingConstraint)
                return constraints{ixname};
            if (foreignConstraints.hasKey(ixname))
                return foreignConstraints{ixname};
        }

        private renameIndexUnlocked(AbstractIndex ix, string new_name) {
            string old_name = ix.name;
            indexes.renameKey(old_name, new_name);
            ix.name = new_name;

            # if constraints are linked to indexes, then rename any matching constraint as well
            if (constraintsLinkedToIndexesImpl()) {
                if (!primaryKey.empty() && primaryKey.getName() == old_name) {
                    primaryKey.rename(new_name);
                }
                else {
                    foreach AbstractConstraint uk in (constraints.iterator()) {
                        if (uk instanceof AbstractUniqueConstraint && uk.getName() == old_name) {
                            uk.rename(new_name);
                            constraints.renameKey(old_name, new_name);
                            break;
                        }
                    }
                }
            }
        }

        #! accepts an AbstractTable argument and returns an SQL string that could be executed to align the structure and configuration of the current table with that of the argument; if the tables are identical then an empty string is returned
        /** @par Example:
            @code{.py}
string sql = table.getAlignSqlString(table2);
            @endcode

            @param t the template table that the current table will be compared to
            @param opt a hash of options for the SQL creation string; see @ref SqlUtil::AbstractTable::AlignTableOptions for common options; each driver can support additional driver-specific options

            @return an SQL string that could be executed to align the structure and configuration of the current table with that of the argument; if the tables are identical then an empty string is returned

            @throw ALIGN-TABLE-ERROR the argument must be of the same class as the current object; template table has no columns
            @throw OPTION-ERROR invalid or unsupported option passed
        */
        string getAlignSqlString(AbstractTable t, *hash opt) {
            return getCreateSqlImpl(getAlignSql(t, opt));
        }

        #! returns a list of SQL strings that could be used to create indexes on the table or @ref nothing if there are no indexes on the table
        /** @par Example:
            @code{.py}
*list l = table.getCreateIndexesSql();
            @endcode

            @param opt a hash of options for the index creation string; see @ref SqlUtil::AbstractTable::IndexOptions for common options; each driver can support additional driver-specific options
            @param cache read in data from the database for uncached properties of the table

            @return a list of SQL strings that could be used to create indexes on the table or @ref nothing if there are no indexes on the table

            @throw OPTION-ERROR invalid or unsupported option passed
        */
        *list getCreateIndexesSql(*hash opt, bool cache = True) {
            validateOptionsIntern("OPTION-ERROR", getIndexOptions(), \opt);
            l.lock();
            on_exit l.unlock();
            return getCreateIndexesSqlUnlocked(opt, cache);
        }

        #! returns an SQL string that could be used to create the primary key on the table
        /** @par Example:
            @code{.py}
*string sql = table.getCreatePrimaryKeySql();
            @endcode

            @param opt a hash of options for the primary key creation string; see @ref SqlUtil::AbstractTable::ConstraintOptions for common options; each driver can support additional driver-specific options
            @param cache read in data from the database for uncached properties of the table

            @return an SQL string that could be used to create the primary key on the table or @ref nothing if there is no primary key on the table

            @throw OPTION-ERROR invalid or unsupported option passed
        */
        *string getCreatePrimaryKeySql(*hash opt, bool cache = True) {
            validateOptionsIntern("OPTION-ERROR", getConstraintOptions(), \opt);
            l.lock();
            on_exit l.unlock();
            return getCreatePrimaryKeySqlUnlocked(opt, cache);
        }

        #! returns a list of SQL strings that could be used to create foreign constraints on the table or @ref nothing if there are no foreign constraints on the table
        /** @par Example:
            @code{.py}
*list l = table.getCreateForeignConstraintsSql();
            @endcode

            @param opt a hash of options for the foreign constraint creation string; see @ref SqlUtil::AbstractTable::ForeignConstraintOptions for common options; each driver can support additional driver-specific options
            @param cache read in data from the database for uncached properties of the table

            @return a list of SQL strings that could be used to create foreign constraints on the table or @ref nothing if there are no foreign constraints on the table

            @throw OPTION-ERROR invalid or unsupported option passed
        */
        *list getCreateForeignConstraintsSql(*hash opt, bool cache = True) {
            validateOptionsIntern("OPTION-ERROR", getForeignConstraintOptions(), \opt);
            l.lock();
            on_exit l.unlock();
            return getCreateForeignConstraintsSqlUnlocked(opt, cache);
        }

        #! returns a list of SQL strings that could be used to create non-foreign constraints on the table or @ref nothing if there are no non-foreign constraints on the table
        /** @par Example:
            @code{.py}
*list l = table.getCreateConstraintsSql();
            @endcode

            @param opt a hash of options for the non-foreign constraint creation string; see @ref SqlUtil::AbstractTable::ConstraintOptions for common options; each driver can support additional driver-specific options
            @param cache read in data from the database for uncached properties of the table

            @return a list of SQL strings that could be used to create non-foreign constraints on the table or @ref nothing if there are no non-foreign constraints on the table

            @see getCreateForeignConstraintsSql()

            @throw OPTION-ERROR invalid or unsupported option passed
        */
        *list getCreateConstraintsSql(*hash opt, bool cache = True) {
            validateOptionsIntern("OPTION-ERROR", getConstraintOptions(), \opt);
            l.lock();
            on_exit l.unlock();
            return getCreateConstraintsSql(opt, cache);
        }

        #! returns a list of SQL strings that could be used to create other table attributes (such as comments, if supported and present) or @ref nothing if there are none
        /** @par Example:
            @code{.py}
*list l = table.getCreateMiscSql();
            @endcode

            @param opt a hash of options for the SQL creation string; see @ref TableCreationOptions for common options; each driver can support additional driver-specific options
            @param cache read in data from the database for uncached properties of the table

            @return a list of SQL strings that could be used to create other table attributes (such as comments, if supported and present) or @ref nothing if there are none

            @throw OPTION-ERROR invalid or unsupported option passed
        */
        *list getCreateMiscSql(*hash opt, bool cache = True) {
            validateOptionsIntern("OPTION-ERROR", getTableCreationOptions(), \opt);
            l.lock();
            on_exit l.unlock();
            return getCreateMiscSqlUnlocked(opt, cache);
        }

        #! returns a list of SQL strings that could be used to create triggers on the table or @ref nothing if there are no triggers on the table
        /** @par Example:
            @code{.py}
*list l = table.getCreateTriggersSql();
            @endcode

            @param opt a hash of options for the trigger creation strings; see @ref TableCreationOptions for common options; each driver can support additional driver-specific options
            @param cache read in data from the database for uncached properties of the table

            @return a list of SQL strings that could be used to create triggers on the table or @ref nothing if there are no triggers on the table

            @throw OPTION-ERROR invalid or unsupported option passed

            @see getCreateTriggersSql()
        */
        *list getCreateTriggersSql(*hash opt, bool cache = True) {
            validateOptionsIntern("OPTION-ERROR", getTableCreationOptions(), \opt);
            l.lock();
            on_exit l.unlock();
            return getCreateTriggersSqlUnlocked(opt, cache);
        }

        #! finds a row in the table with the given primary key value; if no row matches the primary key value passed then @ref nothing is returned
        /** @par Example:
            @code{.py}
*hash row = table.find(id);
            @endcode

            @throw PRIMARY-KEY-ERROR the table has no primary key or the primary key has more than one column
         */
        *hash find(any id) {
            string cname = getPrimaryKeyColumn();

            return ds.selectRow("select * from %s where %s = %v", getSqlName(), getColumnSqlName(cname), id);
        }

        #! finds rows in the table with the given primary key values; if no row matches any primary key value passed then @ref nothing is returned
        /** @par Example:
            @code{.py}
*list rows = table.find(list);
            @endcode

            @param ids the list of primary key IDs to find; if the list is empty then @ref nothing is returned

            @return a list of hashes of rows matching the primary key IDs passed or @ref nothing if no row matches any primary key value passed

            @throw PRIMARY-KEY-ERROR the table has no primary key or the primary key has more than one column
            */
        *list find(list ids) {
            string cname = getPrimaryKeyColumn();

            if (!ids)
                return;

            list args;
            hash cond{cname} = op_in(ids);
            string sql = getSelectSqlIntern(("where": cond), \args);

            return ds.vselectRows(sql, args);
        }

        private string getPrimaryKeyColumn() {
            l.lock();
            on_exit l.unlock();
            getPrimaryKeyUnlocked();
            if (primaryKey.empty())
                throw "PRIMARY-KEY-ERROR", sprintf("%s: has no primary key", getDesc());
            if (primaryKey.size() > 1)
                throw "PRIMARY-KEY-ERROR", sprintf("%s: has a primary key with %d columns (%y); this method can only be called on tables with a primary key with one column", getDesc(), primaryKey.size(), primaryKey.keys());
            return primaryKey.firstKey();
        }

        #! finds a row in the table with the given primary key value given as a hash; if no row matches the primary key value passed then @ref nothing is returned
        /** @par Example:
            @code{.py}
*hash row = table.find(("account_type": type, "name": name));
            @endcode

            @param row a hash giving the primary key value to find; other columns may also appear in the hash, however at least all columns of the primary key must be present

            @return a hash of the row value matching the primary key value passed or @ref nothing if no row matches the primary key value passed

            @throw PRIMARY-KEY-ERROR the table has no primary key or the the hash passed does not contain all columns of the primary key

            @note a table with a primary key with a single column can also be used with this method; just pass a hash with one key
         */
        *hash find(hash row) {
            Columns cols;
            {
                l.lock();
                on_exit l.unlock();
                getPrimaryKeyUnlocked();
                if (primaryKey.empty())
                    throw "PRIMARY-KEY-ERROR", sprintf("%s: has no primary key", getDesc());
                cols = primaryKey;
            }

            # make sure all columns of the primary key are included in the row
            foreach string k in (cols.keyIterator()) {
                if (!row.hasKey(k))
                    throw "PRIMARY-KEY-ERROR", sprintf("%s: row passed is missing column %y of primary key (%y)", getDesc(), k, cols.keys());
            }

            # make query
            string sql = sprintf("select * from %s where ", getSqlName());
            sql += foldl $1 + " and " + $2, (map sprintf("%s = %v", getColumnSqlName($1)), row.keyIterator());
            return ds.vselectRow(sql, row.values());
        }

        #! finds a single row in the table that match the row condition passed; multiple rows may match, but only one row will be returned from the database; if no row matches the condition hash passed then @ref nothing is returned
        /** @par Example:
            @code{.py}
*hash row = table.findSingle(h);
            @endcode

            @param cond a hash giving the column values to find; see @ref where_clauses for the format of this argument

            @return a hash representing a single row in the table with the given column values; multiple rows may match, but only one row will be returned from the database; if no row matches the condition hash passed then @ref nothing is returned

            @throw WHERE-ERROR unknown operator or invalid arguments given in the cond hash for the where clause

            @note this is equivalent to calling selectRow() with \c where = \c cond and \c limit = 1
         */
        *hash findSingle(*hash cond) {
            return selectRows(("where": cond, "limit": 1))[0];
        }

        #! finds all rows in the table with the given column values; a list of hashes is returned representing the rows returned
        /** @par Example:
            @code{.py}
*list rows = table.findAll(h);
            @endcode

            @param cond a hash giving the column values to find; see @ref where_clauses for the format of this argument

            @return a list of hashes is returned representing the rows returned

            @throw WHERE-ERROR unknown operator or invalid arguments given in the cond hash for the where clause

            @note this is equivalent to calling selectRows() with \c where = \c cond
         */
        *list findAll(*hash cond) {
            return selectRows(("where": cond));
        }

        #! returns a descriptive string of the datasource (without the password) and the table name (with a possible qualifier for schema, etc)
        /** Used in exception descriptions

            @since SqlUtil 1.3
        */
        string getDesc() {
            return sprintf("%s: %s %s", AbstractSqlUtilBase::makeDatasourceDesc(ds), getBaseType(), getSqlName());
        }

        #! returns the base type of the underlying object (normally \"table\", some DB-specific implementations may support others like \c "view")
        string getBaseType() {
            return "table";
        }

        #! returns the name of the table to be used in SQL (with a possible qualifier for schema, etc)
        string getSqlName() {
            return name;
        }

        #! returns the column name for use in SQL strings; subclasses can return a special string in case the column name is a reserved word
        string getColumnSqlName(string col) {
            return col;
        }

        #! returns a list of column names for use in SQL strings; subclasses can process the argument list in case a column name is a reserved word
        list getColumnSqlNames(softlist cols) {
            return cols;
        }

        #! returns @ref True if the DB treats empty strings as @ref NULL, @ref False if not; by default this method returns @ref False
        /** @since SqlUtil 1.3
        */
        bool bindEmptyStringsAsNull() {
            return False;
        }

        #! returns @ref Qore::True "True" if the underlying DB driver supports bulk DML operations
        abstract bool hasArrayBind();

        #! returns the table options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getTableOptions() {
            return TableOptions;
        }

        #! return the foreign constraint options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getForeignConstraintOptions() {
            return ForeignConstraintOptions;
        }

        #! returns the constraint options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getConstraintOptions() {
            return ConstraintOptions;
        }

        #! returns the cache options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getCacheOptions() {
            return CacheOptions;
        }

        #! returns the table creation options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getTableCreationOptions() {
            return TableCreationOptions;
        }

        #! returns the align table options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getAlignTableOptions() {
            return AlignTableOptions;
        }

        #! returns the table description hash options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getTableDescriptionHashOptions() {
            return TableDescriptionHashOptions;
        }

        #! returns the column options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getColumnOptions() {
            return ColumnOptions;
        }

        #! returns the column description options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getColumnDescOptions() {
            return ColumnDescOptions;
        }

        #! returns the table column description options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getTableColumnDescOptions() {
            return getColumnDescOptions() + AdditionalColumnDescOptions;
        }

        #! returns the index options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getIndexOptions() {
            return IndexOptions;
        }

        #! returns the trigger options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getTriggerOptions() {
            return TriggerOptions;
        }

        #! returns the select options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getSelectOptions() {
            return SelectOptions;
        }

        #! returns the upsert options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getUpsertOptions() {
            return UpsertOptions;
        }

        #! returns the insert options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getInsertOptions() {
            return InsertOptions;
        }

        #! returns the insert from iterator options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getInsertFromIteratorOptions() {
            return InsertFromIteratorOptions;
        }

        #! returns the sql data operation callback options for this driver
        /** override in subclasses to return driver-specific options
        */
        private hash getSqlDataCallbackOptions() {
            return SqlDataCallbackOptions;
        }

        #! returns the "where" operator map for this object
        /** override in subclasses to return driver-specific options
        */
        private hash getWhereOperatorMap() {
            return DefaultOpMap;
        }

        #! returns the column operator map for this object
        /** override in subclasses to return driver-specific options
        */
        private hash getColumnOperatorMap() {
            return DefaultCopMap;
        }

        #! returns the insert operator map for this object
        /** override in subclasses to return driver-specific options
        */
        private hash getInsertOperatorMap() {
            return DefaultIopMap;
        }

        #! returns the update operator map for this object
        /** override in subclasses to return driver-specific options
        */
        private hash getUpdateOperatorMap() {
            hash uom = getRawUpdateOperatorMap();
            return getColumnOperatorMap().(uom.keys())
                + (map {$1.key: $1.value}, uom.pairIterator(), $1.value.typeCode() == NT_HASH);
        }

        #! returns the raw (default) update operator map for this object
        /** override in subclasses to return driver-specific options
        */
        private hash getRawUpdateOperatorMap() {
            return DefaultUopMap;
        }

        #! returns a hash of valid pseudocolumns
        /** override in subclasses to return driver-specific pseudocolumns; by default this method returns @ref nothing
        */
        private *hash getPseudoColumnHash() {
        }

        private string getCreateTableSqlUnlocked(*hash opt) {
            getColumnsUnlocked();
            return AbstractDatabase::doCallback(opt, getCreateTableSqlImpl(opt), AbstractDatabase::AC_Create, "table", name);
        }

        private *list getCreateIndexesSqlUnlocked(*hash opt, bool cache = True) {
            if (cache)
                getIndexesUnlocked();
            if (!indexes)
                return;

            return map AbstractDatabase::doCallback(opt, $1.getCreateSql(name, opt), AbstractDatabase::AC_Add, "index", $1.name, name), indexes.iterator();
        }

        private *string getCreatePrimaryKeySqlUnlocked(*hash opt, bool cache = True) {
            if (cache)
                getPrimaryKeyUnlocked();
            if (primaryKey.empty())
                return;
            return AbstractDatabase::doCallback(opt, primaryKey.getCreateSql(name, opt), AbstractDatabase::AC_Add, "primary key", primaryKey.getName(), name);
        }

        private *list getCreateConstraintsSqlUnlocked(*hash opt, bool cache = True) {
            if (cache)
                getConstraintsUnlocked();
            if (constraints.empty())
                return;
            bool ix = uniqueIndexCreatesConstraintImpl();
            return map AbstractDatabase::doCallback(opt, $1.getCreateSql(name, opt), AbstractDatabase::AC_Add, "constraint", $1.getName(), name), constraints.iterator(), !ix || !$1.getIndex();
        }

        private *list getCreateForeignConstraintsSqlUnlocked(*hash opt, bool cache = True) {
            if (cache)
                getForeignConstraintsUnlocked(opt);
            if (foreignConstraints.empty())
                return;
            return map AbstractDatabase::doCallback(opt, $1.getCreateSql(name, opt), AbstractDatabase::AC_Add, "foreign constraint", $1.getName(), name), foreignConstraints.iterator();
        }

        private *list getCreateMiscSqlUnlocked(*hash opt, bool cache = True) {
            return getCreateMiscSqlImpl(opt, cache);
        }

        private *list getCreateTriggersSqlUnlocked(*hash opt, bool cache = True) {
            if (cache)
                getTriggersUnlocked();
            if (triggers.empty())
                return;

            list l = ();
            map l += AbstractDatabase::doCallback(opt, $1.getCreateSql(name, opt), AbstractDatabase::AC_Add, "trigger", $1.name, name), triggers.iterator();
            return l;
        }

        private list getCreateSqlUnlocked(*hash opt, bool cache = True) {
            # make sure driver options take precedence over generic options
            if (opt) {
                string dn = getDriverName();
                AbstractDatabase::checkDriverOptions(\opt, dn);
            }

            # mark in options that the entire table is being created
            opt.create_table_all = True;
            list l += getCreateTableSqlUnlocked(opt);
            *list tl;
            if (!opt.omit.indexes) {
                tl = getCreateIndexesSqlUnlocked(opt, cache);
                if (tl) l += tl;
            }
            *string sql = getCreatePrimaryKeySqlUnlocked(opt, cache); if (sql) l += sql;

            if (!opt.omit.foreign_constraints) {
                tl = getCreateForeignConstraintsSqlUnlocked(opt, cache);
                if (tl) l += tl;
            }
            tl = getCreateConstraintsSqlUnlocked(opt, cache); if (tl) l += tl;
            tl = getCreateMiscSqlUnlocked(opt, cache); if (tl) l += tl;
            if (!opt.omit.triggers) {
                tl = getCreateTriggersSqlUnlocked(opt, cache);
                if (tl) l += tl;
            }

            return l;
        }

        private cacheUnlocked(*hash opt) {
            getColumnsUnlocked();
            getPrimaryKeyUnlocked();
            getIndexesUnlocked();
            getForeignConstraintsUnlocked(opt);
            getConstraintsUnlocked();
            getTriggersUnlocked();
        }

        private any execData(*hash opt, string sql, *list args) {
            if (opt.sqlarg_callback)
                opt.sqlarg_callback(sql, args);
            return ds.vexec(sql, args);
        }

        private execData(SQLStatement stmt, *hash opt, *list args) {
            if (opt.sqlarg_callback)
                opt.sqlarg_callback(stmt.getSQL(), args);
            stmt.execArgs(args);
        }

        static AbstractTable getTable(AbstractDatasource nds, string nname, *hash opts) {
            string drv = nds.getDriverName();

            # generate module and module namespace name
            string mn = drv[0].upr() + drv.substr(1) + "SqlUtil";

            # try to load module
            try {
                load_module(mn);
            }
            catch (hash ex) {
                throw "TABLE-DRIVER-ERROR", sprintf("%s: no support for driver %y yet: %s: %s", get_ex_pos(ex), drv, ex.err, ex.desc);
            }

            # try to get AbstractTable object
            try {
                return call_function(mn + "::" + "get_table", nds, nname, opts);
            }
            catch (hash ex) {
                throw "TABLE-DRIVER-ERROR", sprintf("%s: error retrieving AbstractTable object for driver %y: %s: %s", get_ex_pos(ex), drv, ex.err, ex.desc);
            }
        }

        static AbstractTable getTable(string dsstr, string nname, *hash opts) {
            Datasource nds(dsstr);
            return AbstractTable::getTable(nds, nname, opts);
        }

        static AbstractTable getTable(hash dsh, string nname, *hash opts) {
            Datasource nds(dsh);
            return AbstractTable::getTable(nds, nname, opts);
        }

        private getColumnsUnlocked() {
            if (columns)
                return;
            if (manual) {
                columns = new Columns();
                return;
            }
            columns = describeImpl();
            inDb = True;
        }

        private getPrimaryKeyUnlocked() {
            if (primaryKey || manual)
                return;
            getColumnsUnlocked();
            primaryKey = getPrimaryKeyImpl();

            # get indexes
            if (!manual && !indexes)
                indexes = getIndexesImpl();

            if (!primaryKey.empty())
                primaryKey.findMatchingIndex(indexes);

            inDb = True;
        }

        # also loads primary key and constraints (for unique constraints)
        private getIndexesUnlocked() {
            if (indexes)
                return;
            if (manual) {
                indexes = new Indexes();
                return;
            }
            # make sure columns are loaded
            getColumnsUnlocked();
            if (!manual) {
                # make sure any primary key is loaded
                getPrimaryKeyUnlocked();
                # make sure constraints are also loaded
                getConstraintsUnlocked();
            }
            indexes = getIndexesImpl();
            inDb = True;
        }

        private getForeignConstraintsUnlocked(*hash opt) {
            if (foreignConstraints)
                return;
            if (manual) {
                foreignConstraints = new ForeignConstraints();
                return;
            }

            getColumnsUnlocked();
            foreignConstraints = getForeignConstraintsImpl(opt);

            # update cached tables with foreign constraint source info
            if (opt.table_cache) {
                foreach AbstractForeignConstraint fk in (foreignConstraints.iterator()) {
                    if (!opt.table_cache.hasKey(fk.target.table)) {
                        Table t(ds, fk.target.table);
                        opt.table_cache.add(fk.target.table, t.getTable());
                        #printf("ERROR: missing %y in table cache (adding fk %y to %y)\n", fk.target.table, fk.getName(), getName());
                    }
                    opt.table_cache{fk.target.table}.addSourceConstraint(getSqlName(), fk);
                }
            }
            inDb = True;
        }

        private addSourceConstraint(string table_name, AbstractForeignConstraint fk) {
            bool lock = !l.lockOwner();
            if (lock) l.lock();
            on_exit if (lock) l.unlock();

            getAllConstraintsUnlocked();

            # find unique constraint with the given columns
            if (!primaryKey.empty() && primaryKey.matchKeys(fk.target.columns)) {
                primaryKey.addSourceConstraint(table_name, fk);
                return;
            }
            foreach AbstractConstraint c in (constraints.iterator()) {
                if (!(c instanceof AbstractUniqueConstraint))
                    continue;
                AbstractUniqueConstraint uk = cast<AbstractUniqueConstraint>(c);
                if (uk.matchKeys(fk.target.columns)) {
                    uk.addSourceConstraint(table_name, fk);
                    return;
                }
            }
        }

        private getConstraintsUnlocked() {
            if (constraints)
                return;
            if (manual) {
                constraints = new Constraints();
                return;
            }
            getColumnsUnlocked();
            constraints = getConstraintsImpl();
            # get indexes
            if (!manual && !indexes)
                indexes = getIndexesImpl();

            foreach AbstractConstraint c in (constraints.iterator()) {
                if (!(c instanceof AbstractUniqueConstraint))
                    continue;
                AbstractUniqueConstraint uk = cast<AbstractUniqueConstraint>(c);
                uk.findMatchingIndex(indexes);
            }

            inDb = True;
        }

        private getTriggersUnlocked() {
            if (triggers)
                return;
            if (manual) {
                triggers = new Triggers();
                return;
            }
            triggers = getTriggersImpl();
            inDb = True;
        }

        #! returns @ref Qore::True "True" if the current database driver supports the \c "returning" clause in insert statements, @ref Qore::False "False" if not
        private bool hasReturningImpl() {
            return True;
        }

        private softlist getDropSqlImpl() {
            return sprintf("drop table %s", getSqlName());
        }

        private string getTruncateSqlImpl() {
            return sprintf("truncate table %s", getSqlName());
        }

        #! tries to execute a command so that if an error occurs the current transaction status is not lost
        private any tryExecArgsImpl(string sql, *softlist args) {
            return ds.vexec(sql, args);
        }

        #! tries to execute a command so that if an error occurs the current transaction status is not lost
        private any tryExecRawImpl(string sql) {
            return ds.execRaw(sql);
        }

        #! clears any driver-specific table information
        private clearImpl() {
        }

        private preSetupTableImpl(reference desc, *hash opt) {
        }

        private abstract *hash doReturningImpl(hash opt, reference sql, list args);

        private abstract bool emptyImpl();

        #! returns a string for use in SQL queries representing the DB-specific value of the argument; returns NOTHING if the type cannot be converted to an SQL string
        private abstract *string getSqlValueImpl(any v);

        #! returns @ref Qore::True "True" if the table exists in the DB, @ref Qore::False "False" if not
        /** this method will only be called if inDb is @ref Qore::False "False" and
            should set inDb = True if it returns @ref Qore::True "True"
         */
        private abstract bool checkExistenceImpl();

        #! returns @ref Qore::True "True" if the database support tablespaces
        private abstract bool supportsTablespacesImpl();

        #! returns @ref Qore::True "True" if the database links constraints to indexes (ie dropping the constraint drops the index, etc)
        private abstract bool constraintsLinkedToIndexesImpl();

        #! returns @ref Qore::True "True" if the database automatically creates a unique constraint when a unique index is created (ex: mysql)
        private abstract bool uniqueIndexCreatesConstraintImpl();

        private abstract setupTableImpl(hash desc, *hash opt);

        private abstract Columns describeImpl();
        private abstract AbstractPrimaryKey getPrimaryKeyImpl();
        private abstract Indexes getIndexesImpl();
        private abstract ForeignConstraints getForeignConstraintsImpl(*hash opt);
        private abstract Constraints getConstraintsImpl();
        private abstract Triggers getTriggersImpl();

        private abstract string getCreateTableSqlImpl(*hash opt);
        private abstract *list getCreateMiscSqlImpl(*hash opt, bool cache);
        private abstract string getCreateSqlImpl(list l);
        private abstract string getRenameSqlImpl(string new_name);
        private abstract *list getAlignSqlImpl(AbstractTable t, *hash opt);

        private abstract AbstractColumn addColumnImpl(string cname, hash opt, bool nullable = True);
        private abstract AbstractPrimaryKey addPrimaryKeyImpl(string cname, hash ch, *hash opt);
        private abstract AbstractIndex addIndexImpl(string iname, bool enabled, hash ch, *hash opt);
        private abstract AbstractForeignConstraint addForeignConstraintImpl(string cname, hash ch, string table, hash tch, *hash opt);
        private abstract AbstractCheckConstraint addCheckConstraintImpl(string cname, string src, *hash opt);
        private abstract AbstractUniqueConstraint addUniqueConstraintImpl(string cname, hash ch, *hash opt);

        private abstract AbstractTrigger addTriggerImpl(string tname, string src, *hash opt);

        #! tries to insert a row, if there is a duplicate key, then it returns @ref Qore::False "False", if successful, returns @ref Qore::True "True"
        private abstract bool tryInsertImpl(string sql, hash row);

        #! returns the qore type -> column type map
        private abstract hash getQoreTypeMapImpl();

        #! returns the type name -> type description hash
        private abstract hash getTypeMapImpl();

        #! processes a string for use in SQL select statements when there is an "order by" and "offset" argument
        private abstract doSelectOrderByWithOffsetSqlUnlockedImpl(reference sql, reference args, *hash qh, *hash jch, *hash ch, *hash psch, list coll);

        #! processes a string for use in SQL select statements when there is a "limit" argument, but no "orderby" or "offset" arguments
        private abstract doSelectLimitOnlyUnlockedImpl(reference sql, reference args, *hash qh);

        #! db-specific copy actions
        private abstract copyImpl(AbstractTable old);
    }
}
