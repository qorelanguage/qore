# -*- mode: qore; indent-tabs-mode: nil -*-
# @file Qorize.qm Qorize module definition

/*  Qorize.qm Copyright 2013 - 2017 Qore Technologies s r.o.

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

# minimum qore version
%requires qore >= 0.8.13

# assume local var scope, do not use "$" for vars, members, and method calls
%new-style

module Qorize {
    version = "0.1.2";
    desc = "Create Qore source code from data structures";
    author = "Petr Vanek";
    url = "http://qore.org";
    license = "MIT";
}

/*  Version History
    * 2016-28-11 v 0.1.2 Tomas Mandys
      + open parenthesis at the same line
      + more compact output
      + indentation size
      + cutly or round parenthesis for hash
      + qorize() formatting improvement when a list contains list or hash
      + object support
      + qorize_named() supports hash, object, list, any

    * 2016-06-29 v 0.1.1: Petr Vanek <petr@yarpen.cz>
      + qorize_val() implemented
      + new style is used as default for arguments
      + qorizeNamed() is deprecated now. Use qorize_named() instead

    * 2015-11-11 v 0.1.0: Petr Vanek <petr@yarpen.cz>
      + improvements and bugfixes

    * 2015-11-11 v 0.0.10: David Nichols <david@qore.org>
      + converted to new-style

    * 2013-08-15 v 0.0.9: Petr Vanek <petr@yarpen.cz>
      + initial version
*/


/** @mainpage Qorize Module

Qorize module provides functions to create Qore source code from Qore variables (structures).

Usage of this module can be for example:

  - generate code from XML(JSON, YAML, ...) files
  - create unit tests from currently running script
  - and more

To use this module, use "%requires Qorize" in your code.

All the public symbols in the module are defined in the @ref Qorize namespace.

Currently it can handle only simple types:

  - integer, numeric, float
  - string
  - binary
  - date including relative arithmetics
  - NULL, NOTHING
  - list of simple types (recursive parsing supported)
  - hash of simple types (recursive parsing supported)
  - object

@note There is no support for private object members and overloaded constructors.  References are not considered

*/


#! public Qorize namespace contains all relevant stuff for this module
public namespace Qorize {


/** @defgroup QorizeFunctions Qorize Functions
 */
#@{

#! create code from the boolean value
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @retval string a generated code

@code{.py}
    string s = qorize(True);
    # "bool var = True;"
    string s = qorize(True, "foo", True);
    # "bool foo = True;"
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize(bool val, string name = "var", bool newstyle = True) {
    return sprintf("%s = %s;", QorizeHelper::checkVariableName(name, val, newstyle),
                               QorizeHelper::escape(val));
}

#! create code from the string value
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @retval string a generated code

@code{.py}
    string s = qorize("lorem ipsum");
    # "string var = \"lorem ipsum\";"
    string s = qorize("lorem ipsum", "foo", True);
    # "string foo = \"lorem ipsum\";"
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize(string val, string name = "var", bool newstyle = True) {
    return sprintf("%s = %s;", QorizeHelper::checkVariableName(name, val, newstyle),
                                QorizeHelper::escape(val));
}

#! create code from the integer value
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @retval string a generated code

@code{.py}
    string s = qorize(1);
    # "int var = 1;"
    string s = qorize(1, "foo", True);
    # "int foo = 1;"
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize(int val, string name = "var", bool newstyle = True) {
    return sprintf("%s = %s;", QorizeHelper::checkVariableName(name, val, newstyle),
                                QorizeHelper::escape(val));
}

#! create code from the float value
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @retval string a generated code

@code{.py}
    string s = qorize(1.2);
    # "float var = 1;"
    string s = qorize(1.2, "foo", True);
    # "float foo = 1;"
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize(float val, string name = "var", bool newstyle = True) {
    return sprintf("%s = %s;", QorizeHelper::checkVariableName(name, val, newstyle),
                                QorizeHelper::escape(val));
}

#! create code from the number value
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @retval string a generated code

@code{.py}
    string s = qorize(123n);
    # "number var = 123n;"
    string s = qorize(123n, "foo", True);
    # "number foo = 123n;"
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize(number val, string name = "var", bool newstyle = True) {
    return sprintf("%s = %s;", QorizeHelper::checkVariableName(name, val, newstyle),
                               QorizeHelper::escape(val));
}

#! create code from the date value
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @retval string a generated code

@code{.py}
    string s = qorize(now());
    # "date var = 2013-08-16T14:37:05.000;"
    string s = qorize(now(), "foo", True);
    # "date foo = 2013-08-16T14:37:05.000;"
@endcode

    Relative date values (1D, etc.) are converted to @ref short_reldates

@code{.py}
    string s = qorize(3h);
    # date d = PT3H;
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize(date val, string name = "var", bool newstyle = True) {
    return sprintf("%s = %s;", QorizeHelper::checkVariableName(name, val, newstyle),
                               QorizeHelper::escape(val));
}

#! create code from the binary value
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @retval string a generated code

    Binary values are encoded in hexadecimal strings.

@code{.py}
    binary bin = ....; # binary value from file for example
    string s = qorize(bin);
    # "binary var = parseHexString(\"666f6f\");"
    string s = qorize(bin, "foo", True);
    # "binary foo = parseHexString(\"666f6f\");"
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize(binary val, string name = "var", bool newstyle = True) {
    return sprintf("%s = %s;", QorizeHelper::checkVariableName(name, val, newstyle),
                               QorizeHelper::escape(val));
}

#! create code from the list value
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @param indentation number of characters to indent inner hashes
    @param curlyHash use new style curly parentesis to surround hashes or old style round ones
    @retval string a generated code

@code{.py}
    list l =  (True, False, now(), 12, 12.1, ( 'a', 'b'), );
    string s = qorize(l);
    # "list var =
    #    (True,False,2013-08-16T15:06:29.000,12,12.1,
    #        ("a","b",),);"
    string s = qorize(l, "foo", True);
    # "list foo =
    #    (True,False,2013-08-16T15:06:29.000,12,12.1,
    #        ("a","b",),);"
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize(list val, string name = "var", bool newstyle = True, int indentation = 2, bool curlyHash = True) {
    QorizeHelper qh(val, newstyle, indentation, curlyHash);
    return sprintf("%s = %s;", QorizeHelper::checkVariableName(name, val, newstyle),
                               qh.list());
}

#! create code from the hash value
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @param indentation number of characters to indent inner hashes
    @param curlyHash use new style curly parentesis to surround hashes or old style round ones
    @retval string a generated code

@code{.py}
    hash h = (
        "foo" : "bar",
        "key1" : now(),
        "key2" : 12,
        "key3" : ( 1, 2, 3 ),
        "key4" : ( "subkey1" : 1, "subkey2" : 2, ),
    );
    string s = qorize(h);
    # "hash var = (
    #      "foo" : "bar",
    #      "key1" : 2013-08-16T15:10:30.000,
    #      "key2" : 12,
    #      "key3" : (1,2,3,),
    #      "key4" : (
    #          "subkey1" : 1,
    #          "subkey2" : 2,
    #      ),
    #  );"
    string s = qorize(h, "foo", True);
    # "hash foo = (
    #      "foo" : "bar",
    #      "key1" : 2013-08-16T15:10:30.000,
    #      "key2" : 12,
    #      "key3" : (1,2,3,),
    #      "key4" : (
    #          "subkey1" : 1,
    #          "subkey2" : 2,
    #      ),
    #  );"
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize(hash val, string name = "var", bool newstyle = True, int indentation = 2, bool curlyHash = True) {
    QorizeHelper qh(val, newstyle, indentation, curlyHash);
    return sprintf("%s = %s;", QorizeHelper::checkVariableName(name, val, newstyle),
                               qh.hash());
}

#! create code from the object value. Just initializes object with empty constructor. To initilize members use @ref qorize_named() function
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @param indentation number of characters to indent inner hashes
    @param curlyHash use new style curly parentesis to surround hashes or old style round ones
    @retval string a generated code

@code{.py}
    MyClass o();
    string s = qorize(o);
    # "MyClass var = new MyClass();"
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize(object val, string name = "var", bool newstyle = True, int indentation = 2, bool curlyHash = True) {
    QorizeHelper qh(val, newstyle, indentation, curlyHash);
    return sprintf("%s = %s;", QorizeHelper::checkVariableName(name, val, newstyle), QorizeHelper::escape(val));
}


#! create code from the hash value - with one value per one line
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @retval string a generated code

@code{.py}
    hash h = (
        "foo" : "bar",
        "key1" : now(),
        "key2" : 12,
        "key3" : ( 1, 2, 3 ),
        "key4" : ( "subkey1" : 1, "subkey2" : 2, ),
    );
    string s = qorize_named(h);
    # "hash name;
    # var."foo" = "bar";
    # var."key1" = 2013-08-16T15:14:40.000;
    # var."key2" = 12;
    # var."key3"[0] = 1;
    # var."key3"[1] = 2;
    # var."key3"[2] = 3;
    # var."key4"."subkey1" = 1;
    # var."key4"."subkey2" = 2;
    string s = qorize_named(h, "foo", True);
    # "hash foo;
    # foo."foo" = "bar";
    # foo."key1" = 2013-08-16T15:14:40.000;
    # foo."key2" = 12;
    # foo."key3"[0] = 1;
    # foo."key3"[1] = 2;
    # foo."key3"[2] = 3;
    # foo."key4"."subkey1" = 1;
    # foo."key4"."subkey2" = 2;"
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize_named(hash val, string name = "var", bool newstyle = True) {
    QorizeHelper qh(val, newstyle);
    return QorizeHelper::checkVariableName(name, val, newstyle) + ";\n" + qh.named(name);
}

#! create code from the object value, constructor with empty parameters is called and members are created with one member per line. It may not generate valid code as because of private members and overloaded constructor. Circular references are handled to avoid endless loop.
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @retval string a generated code

@code{.py}
    class MyClass {
    private {
        int m1;
        int m2;
    }

    public {
        int m3;
        string s1;
        list l;
        any a;
    }

        public constructor(int n_m1, int n_m2, int n_m3) {m1=n_m1; m2=n_m2; m3=n_m3;}
    }

    MyClass o(1, 2, 3);
    o.s1 = "xyz";
    o.a = o;
    string s = qorize_named(o);
    # "MyClass var;
    # var = new MyClass();
    # var."m3" = 3;
    # var."s1" = "xyz";
    # var."l" = NOTHING;
    # var."a" = var;"
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize_named(object val, string name = "var", bool newstyle = True) {
    QorizeHelper qh(val, newstyle);
    return QorizeHelper::checkVariableName(name, val, newstyle) + ";\n" + qh.named(name);
}

#! create code from the list value.
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @retval string a generated code

@code{.py}
    list l = (1,2,3);
    string s = qorize_named(l);
    # "list var;
    # var[0] = 1;
    # var[1] = 2;
    # var[2] = 3;
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize_named(list val, string name = "var", bool newstyle = True) {
    string rv = QorizeHelper::checkVariableName(name, val, newstyle) + ";\n";
    if (val) {
        QorizeHelper qh(val, newstyle);
        rv += qh.named(name);
    }
    return rv;
}

#! Deprecated, use qorize_named() instead
public deprecated string sub qorizeNamed(hash val, string name = "var", bool newstyle = True) {
    return qorize_named(val, name, newstyle);
}

#! Process other types normal way using @ref qorize().
public string sub qorize_named(any val, string name = "var", bool newstyle = True) {
    return qorize(val, name, newstyle);
}


#! create code from the any value - fallback for NULL and NOTHINGS
/**
    @param val an input value
    @param name an optional name of the resulting variable
    @param newstyle an optional argument. @ref True when to use @ref Qore::PO_NEW_STYLE syntax, @ref False otherwise
    @retval string a generated code

@code{.py}
    any s = qorize(NULL);
    # "any var = NULL;"
    string s = qorize(NULL, "foo", True);
    # "any foo = NULL;"
@endcode

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize(any val, string name = "var", bool newstyle = True) {
    return sprintf("%s = %s;", QorizeHelper::checkVariableName(name, val, newstyle),
                                QorizeHelper::escape(val));
}

#! create code without any variable assignment (lvalue)
/**
    @param val an input value
    @param indentation number of characters to indent inner hashes
    @param curlyHash use new style curly parentesis to surround hashes or old style round ones
    @retval string a generated code

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize_val(list val, int indentation = 2, bool curlyHash = True) {
    QorizeHelper qh(val, True, indentation, curlyHash);
    return sprintf("%s", qh.list());
}

#! create code without any variable assignment (lvalue)
/**
    @param val an input value
    @param indentation number of characters to indent inner hashes
    @param curlyHash use new style curly parentesis to surround hashes or old style round ones
    @retval string a generated code

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize_val(hash val, int indentation = 2, bool curlyHash = True) {
    QorizeHelper qh(val, True, indentation, curlyHash);
    return sprintf("%s", qh.hash());
}

#! create code without any variable assignment (lvalue)
/**
    @param val an input value
    @retval string a generated code

    @throw QORIZE-ERROR in case of error in code generator
*/
public string sub qorize_val(any val) {
    return sprintf("%s", QorizeHelper::escape(val));
}
#@}


# a helper class holding all logic
class QorizeHelper {

    private {
        any container;
        string code;
        int level = 0;
        bool newstyle;
        int indentation;
        bool curlyHash;
        hash objectSignatures;
    }

    static string checkVariableName(string name, any val, bool newstyle) {
        string ret;
        if (!newstyle)
            ret += "my ";

        switch (val.typeCode()) {
            case NT_BOOLEAN:   ret += "bool"; break;
            case NT_STRING:    ret += "string"; break;
            case NT_INT:       ret += "int"; break;
            case NT_FLOAT:     ret += "float"; break;
            case NT_NUMBER:    ret += "number"; break;
            case NT_DATE:      ret += "date"; break;
            case NT_BINARY:    ret += "binary"; break;
            case NT_LIST:      ret += "list"; break;
            case NT_HASH:      ret += "hash"; break;
            case NT_NULL:
            case NT_NOTHING:   ret += "any"; break;
            case NT_OBJECT:    ret += val.className(); break;
            default:
                throw "QORIZE-ERROR", sprintf("Unsupported type for checkVariableName %s", val.type());
        } # switch

        ret += " ";

        if (!newstyle)
            ret += "$";

        ret += name;

        return ret;
    }

    static string escape(bool val) {
        return val ? 'True' : 'False';
    }

    static string escape(string val) {
        string escaped = replace(val, '"', '\"');
        return sprintf("\"%s\"", escaped);
    }

    static string escape(int val) {
        return sprintf("%d", val);
    }

    static string escape(float val) {
        return sprintf("%s", string(val));
    }

    static string escape(number val) {
        return sprintf("%sn", string(val));
    }

    static string escape(date val) {
        hash info = date_info(val);
        if (!info.relative) {
            string escaped = format_date("YYYY-MM-DDTHH:mm:SS.ms", val);
            return sprintf("%s", escaped);
        }
        else {
            #PnYnMnDTnHnMnSnu
            string ret = "P";
            if (info.year) ret += sprintf("%dY", info.year);
            if (info.month) ret += sprintf("%dM", info.month);
            if (info.day) ret += sprintf("%dD", info.day);
            if (info.hour || info.minute || info.second || info.microsecond) ret += "T";
            if (info.hour) ret += sprintf("%dH", info.hour);
            if (info.minute) ret += sprintf("%dM", info.minute);
            if (info.second) ret += sprintf("%dS", info.second);
            if (info.microsecond) ret += sprintf("%du", info.microsecond);
            return ret;
        }
    }

    static string escape(binary val) {
        return sprintf("parseHexString(\"%s\")", makeHexString(val));
    }

    static string escape(object val) {
        return sprintf("new %s()", val.className());
    }

    static string escape(any val) {
        switch (val.typeCode()) {
            case NT_NULL:
                return "NULL";
            case NT_NOTHING:
                return "NOTHING";
            default:
                throw "QORIZE-ERROR", sprintf("No method to process type %s", val.type());
        }
    }

    constructor(any n_container, bool n_newstyle = True, int n_indentation = 2, bool n_curlyHash = True) {
        container = n_container;
        indentation = n_indentation;
        curlyHash = n_curlyHash;
        newstyle = n_newstyle;
        if (!inlist(container.typeCode(), (NT_LIST, NT_HASH, NT_OBJECT)))
            throw "QORIZE-ERROR", sprintf("Unsupported type %y (expecting \"object\", \"list\", \"hash\")", container.type());
    }

    string list() {
        if (container.typeCode() != NT_LIST)
            throw "QORIZE-ERROR", sprintf("Cannot call %s::list() for type \"hash\"", self.className());

        process(container);

        return code;
    }

    string hash() {
        if (container.typeCode() != NT_HASH)
            throw "QORIZE-ERROR", sprintf("Cannot call %s::hash() for type \"list\"", self.className());

        process(container);

        return code;
    }

    *string named(string name) {
        if (!newstyle) {
            name = "$" + name;
        }
        processNamed(container, name);
        return code;
    }

    private processNamed(hash val, string prefixName) {
        HashIterator it = val.iterator();
        while (it.next()) {
            processNamed(it.getValue(), prefixName+".\""+it.getKey()+"\"");
        }
    }

    private processNamed(object val, string prefixName) {
        if (objectSignatures{val.uniqueHash()}) {
            code += sprintf("%s = %s;\n", prefixName, objectSignatures{val.uniqueHash()});
            return;
        }
        objectSignatures{val.uniqueHash()} = prefixName;
        code += sprintf("%s = %s;\n", prefixName, QorizeHelper::escape(val));
        AbstractIterator it2 = val.iterator();
        if (it2 instanceof HashIterator) {
            HashIterator it = cast<HashIterator>(it2);
            while (it.next()) {
                any value = it.getValue();
                string key = it.getKey();
                processNamed(it.getValue(), prefixName+".\""+it.getKey()+"\"");
            }
        } else {
            # when a class overrides iterator() then process it as well
            while (it2.next()) {
                processNamed(it2.getValue(), prefixName);
            }
        }
    }

    private processNamed(list val, string prefixName) {
        if (!val) {
            code += prefixName + " = ();\n";
        } else {
            ListIterator it = val.iterator();
            while (it.next()) {
                any value = it.getValue();
                if (exists value) {
                    processNamed(value, prefixName + sprintf("[%d]", it.index()));
                }
            }
        }
    }

    private processNamed(any val, string prefixName) {
        code += prefixName + " = " + escape(val) + ";\n";
    }

    private process(list val) {
        if (!val.size()) {
            code += "()";
        } else {
            code += sprintf("(");
            ListIterator it(val);
            bool substruct = False;
            while (it.next()) {
                if (inlist(it.getValue().typeCode(), (NT_HASH, NT_LIST))) {
                    substruct = True;
                    break;
                }
            }
            level++;
            it.reset();
            if (substruct) {
                code += sprintf("\n%s", strmul(" ", level*indentation));
            }
            bool pending = False;
            while (it.next()) {
                if (!it.first())
                    code += ", ";
                any v = it.getValue();
                if (inlist(v.typeCode(), (NT_HASH, NT_LIST))) {
                    #pending = True;
                } else {
                    if (pending) {
                        code += sprintf("\n%s", strmul(" ", level*indentation));
                        pending = False;
                    }
                }
                process(v);
            }
            level--;
            if (substruct) {
                code += sprintf("\n%s", strmul(" ", level*indentation));
            }
            code += ")";
        }
    }

    private process(hash val) {
        if (!val.size()) {
            if (curlyHash) {
                code += "{}";
            } else {
                code += "hash()";
            }
        } else {
            code += curlyHash ? "{" : "(";
            level++;
            HashIterator it(val);
            while (it.next()) {
                 code += sprintf("\n%s\"%s\": ", strmul(" ", level*indentation), it.getKey());
                 process(it.getValue());
                 code += ",";
            }
            level--;
            code += sprintf("\n%s%s", strmul(" ", level*indentation), curlyHash ? "}" : ")");
        }
    }

    private process(any val) {
        code += escape(val);
    }

} # class QorizeHelper

} # namespace Qorize
