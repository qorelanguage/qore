# -*- mode: qore; indent-tabs-mode: nil -*-
#! @file ServiceNowRestClient.qm Qore user module for calling ServiceNow REST services

/*  ServiceNowRestClient.qm Copyright (C) 2020 - 2023 Qore Technologies, s.r.o.

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

# minimum qore version
%requires qore >= 2.0

# require type definitions everywhere
%require-types

# enable all warnings
%enable-all-warnings

# don't use "$" for vars, members, and methods, assume local variable scope
%new-style

# do not ignore argument errors
%strict-args

%requires(reexport) Mime >= 1.3
%requires(reexport) RestClient >= 1.3.1
%requires(reexport) ConnectionProvider >= 1.4

module ServiceNowRestClient {
    version = "1.2";
    desc = "user module for calling ServiceNow REST services";
    author = "David Nichols <david@qore.org>";
    url = "https://qore.org";
    license = "MIT";
    init = sub () {
        ConnectionSchemeCache::registerScheme("snrests", ServiceNowRestConnection::ConnectionScheme);
    };
}

/** @mainpage ServiceNowRestClient Module

    @tableofcontents

    @section serviceNowrestclientintro ServiceNowRestClient Introduction

    The %ServiceNowRestClient module provides an API for calling ServiceNow REST services.

    To use this module, use \c "%requires ServiceNowRestClient" in your code.

    All the public symbols in the module are defined in the ServiceNowRestClient namespace.

    The main classes are:
    - @ref ServiceNowRestClient::ServiceNowRestClient "ServiceNowRestClient":
      this class provides the REST client API for communuication with the ServiceNow REST
      API; it also automates authentication and authorization to the target
    - @ref ServiceNowRestClient::ServiceNowRestConnection "ServiceNowRestConnection":
      provides a REST connection object to a ServiceNow server (based on the
      @ref connectionproviderintro "ConnectionProvider" module)

    @par Example:
    @code{.py}
#!/usr/bin/env qore

%new-style
%strict-args
%require-types
%enable-all-warnings

%requires ServiceNowRestClient

hash<auto> opts = {
    "url": "https://dev493922.service-now.com/",
    "username": "admin",
    "password": "mypassword",
};

ServiceNowRestClient rest(opts);
hash<auto> ans = rest.get("...");
printf("%N\n", ans.body);
    @endcode

    @section ServiceNowrestclientrelnotes Release Notes

    @subsection ServiceNowrestclientv1_2 ServiceNowRestClient v1.2
    - use RestClient functionality for OAuth2 authorization
      (<a href="https://github.com/qorelanguage/qore/issues/4766">issue 4766</a>)

    @subsection ServiceNowrestclientv1_1_2 ServiceNowRestClient v1.1.2
    - fixed to create URI request paths properly; restored functionality with recent versions of ServiceNow
      (<a href="https://github.com/qorelanguage/qore/issues/4758">issue 4758</a>)

    @subsection ServiceNowrestclientv1_1_1 ServiceNowRestClient v1.1.1
    - support full ping operations, also with polling, using an authenticated request
      (<a href="https://github.com/qorelanguage/qore/issues/4677">issue 4677</a>)

    @subsection ServiceNowrestclientv1_1 ServiceNowRestClient v1.1
    - updated to no longer require OAuth2 login params
      (<a href="https://github.com/qorelanguage/qore/issues/4295">issue 4295</a>)

    @subsection ServiceNowrestclientv1_0 ServiceNowRestClient v1.0
    - the initial version of the %ServiceNowRestClient module
*/

#! the ServiceNowRestClient namespace contains all the objects in the ServiceNowRestClient module
public namespace ServiceNowRestClient {
#! this class provides the REST client API for communication with ServiceNow servers
/** This class requires the following options for ServiceNow authentication and
    authorization to the target server:
    - \c username: the ServiceNow username
    - \c password: the ServiceNow password

    To use an OAuth2 login, include the following options:
   - \c client_id: the OAuth2 client ID
   - \c client_secret: the OAuth2 client secret
*/
public class ServiceNowRestClient inherits RestClient::RestClient {
    public {
        #! OAuth2 default grant type
        const OAuth2DefaultGrantType = "authorization_code";

        #! OAuth2 auth URL
        const OAuth2AuthUriPathSuffix = "/oauth_auth.do";

        #! OAuth2 token URI path suffix
        const OAuth2TokenUriPathSuffix = "/oauth_token.do";

        #! Default API version
        const DefaultApiVersion = "now";

        #! Request URI path prefix
        const RequestUriPathPrefix = "/api/";
    }

    private:internal {
        *string api;
        string uri_path_pfx;
    }

    #! creates the object with the given options
    /**
        @par Example:
        @code{.py}
ServiceNowRestClient rest({
    "url": "https://dev493922.service-now.com",
    "username": "admin",
    "password": "mypassword",
});
        @endcode

        @param opts valid options are:
        - \c additional_methods: Optional hash with more but not-HTTP-standardized methods to handle. It allows to
            create various HTTP extensions like e.g. WebDAV. The hash takes the method name as a key, and the value
            is a boolean @ref True "True" or @ref False "False": indicating if the method requires a message body as
            well. Example:
            @code{.py}
# add new HTTP methods for WebDAV. Both of them require body posting to the server
{"additional_methods": {"PROPFIND": True, "MKCOL": True}}
            @endcode
        - \c api: the API version for ServiceNow
        - \c client_id: (required) the OAuth2 client ID
        - \c client_secret: (required) the OAuth2 client secret
        - \c content_encoding: for possible values, see @ref EncodingSupport; this sets the send encoding (if the
            \c "send_encoding" option is not set) and the requested response encoding (note that the
            @ref RestClient::RestClient "RestClient" class will only compress outgoing message bodies over
            @ref RestClient::RestClient::CompressionThreshold "CompressionThreshold" bytes in size)
        - \c data: a @ref DataSerializationOptions "data serialization option"; if not present defaults to \c "json"
        - \c default_path: The default path to use for new connections if a path is not otherwise specified in the
            connection URL
        - \c default_port: The default port number to connect to if none is given in the URL
        - \c error_passthru: if @ref True "True" then HTTP status codes indicating errors will not cause a
            \c REST-RESPONSE-ERROR exception to be raised, rather such responses will be passed through to the caller
            like any other response
        - \c headers: an optional hash of headers to send with every request, these can also be overridden in
            request method calls
        - \c http_version: Either '1.0' or '1.1' (default) for the claimed HTTP protocol version compliancy in
            outgoing message headers
        - \c max_redirects: The maximum number of redirects before throwing an exception (the default is 5)
        - \c password: (required) The ServiceNow password
        - \c proxy: The proxy URL for connecting through a proxy
        - \c redirect_passthru: if @ref True "True" then redirect responses will be passed to the caller instead of
            processed
        - \c send_encoding: a @ref EncodingSupport "send data encoding option" or the value \c "auto" which means
            to use automatic encoding; if not present defaults to \c "gzip" content encoding on sent message bodies
            (note that the @ref RestClient::RestClient "RestClient" class will only compress outgoing message bodies
            over @ref RestClient::RestClient::CompressionThreshold "CompressionThreshold" bytes in size)
        - \c timeout: The timeout value in milliseconds (also can be a relative date-time value for clarity, ex:
          \c 30s)
        - \c username: (required) The ServiceNow username
        @param do_not_connect if \c False (the default), then a connection will be immediately established to the
        remote server

        @throw RESTCLIENT-ERROR invalid option passed to constructor, unsupported data serialization, etc
        @throw ServiceNowRESTCLIENT-ERROR missing or invalid required option for REST
        authentication or communication
    */
    constructor(hash<auto> opts, *softbool do_not_connect)
            : RestClient(ServiceNowRestClient::getOptions(opts), True) {
        if (opts.api.val()) {
            api = getString(opts, "api");
            uri_path_pfx = RequestUriPathPrefix + api;
        } else {
            # default: use latest API
            uri_path_pfx = RequestUriPathPrefix + DefaultApiVersion;
        }
        setConnectionPath(uri_path_pfx);

        if (!do_not_connect) {
            if (oauth2_grant_type && opts.oauth2_grant_type != "none" && !token) {
                loginIntern();
            } else {
                connect();
            }
        }
    }

    #! returns the ServiceNow REST API version currently in use
    /** @return the ServiceNow REST API version currently in use
    */
    string getApi() {
        return api ?? DefaultApiVersion;
    }

    #! sends the outgoing HTTP message and recodes the response to data
    private hash<auto> sendAndDecodeResponse(*data body, string m, string path, hash<auto> hdr,
            *reference<hash<auto>> info, *softbool decode_errors) {
        try {
            return RestClient::sendAndDecodeResponse(body, m, path, hdr, \info, decode_errors);
        } catch (hash<ExceptionInfo> ex) {
            # throw an exception for the error
            if (info."response-body".response.error.message.typeCode() == NT_STRING) {
                hash<auto> error = info."response-body".response.error;
                throw "SERVICENOW-REST-RESPONSE-ERROR", remove error.message, error + ex.arg
                    + {"decoded_body": info."response-body"};
            }
            if (info."response-body".error_description.typeCode() == NT_STRING
                && info."response-body".error.typeCode() == NT_STRING) {
                hash<auto> error = info."response-body";
                string errcode = remove error.error;
                string msg = remove error.error_description;
                throw errcode, msg, error + ex.arg;
            }
            rethrow;
        }
    }

    #! gets a string option
    static string getString(hash<auto> opts, string key) {
        auto v = opts{key};
        if (!exists v || v == "") {
            ServiceNowRestClient::raiseError("missing required option %y in option argument", key);
        }
        if (v.typeCode() != NT_STRING) {
            ServiceNowRestClient::raiseError("required option %y was passed as a %s (%y); expecting \"string\"", key,
                v.type(), v);
        }
        return v;
    }

    #! throws an exception due to an error
    static raiseError(string fmt, ...) {
        throw "SERVICENOWRESTCLIENT-ERROR", vsprintf(fmt, argv);
    }

    #! returns options for @ref ServiceNowRestClient::ServiceNowRestClient::constructor()
    static hash<auto> getOptions(hash<auto> opts) {
        return opts + {
            "oauth2_auth_url": opts.oauth2_auth_url ?? OAuth2AuthUriPathSuffix,
            "oauth2_token_url": opts.oauth2_token_url ?? OAuth2TokenUriPathSuffix,
            "oauth2_grant_type": opts.oauth2_grant_type ?? OAuth2DefaultGrantType,
            "oauth2_client_id": opts.client_id,
            "oauth2_client_secret": opts.client_secret,
        };
    }
}

#! Class for ServiceNow REST connections; returns @ref ServiceNowRestClient objects
/** In addition to all options supported by @ref RestClient::RestClientConnection "RestClientConnection", this
    connection class supports the following options:
    - \c "api": the API version for ServiceNow
    - \c "client_id": (optional) the ServiceNow OAuth2 client ID (mapped to the \c oauth2_client_id option)
    - \c "client_secret": (optional) the ServiceNow OAuth2 client secret (mapped to the \c oauth2_client_secret option)

    @see @ref ServiceNowRestClient::ServiceNowRestClient::constructor() for more information on the above options
*/
public class ServiceNowRestConnection inherits RestClient::RestConnection {
    public {
        #! Connection entry info
        const ConnectionScheme = <ConnectionSchemeInfo>{
            "display_name": "ServiceNow REST Connection",
            "short_desc": "A connection to a ServiceNow instance using the REST API",
            "desc": "A connection to a ServiceNow instance using the REST API",
            "cls": Class::forName("ServiceNowRestConnection"),
            "schemes": {
                "snrests": True,
            },
            "base_scheme_map": {
                "https": "snrests",
            },
            "options": RestConnection::ConnectionScheme.options - (keys OAuth2DifferentOptionMap) + {
                "api": <ConnectionOptionInfo>{
                    "display_name": "API Version",
                    "short_desc": "The API version to use",
                    "type": "string",
                    "desc": "the API version for ServiceNow; if not set the latest version is used",
                    "default_value": ServiceNowRestClient::DefaultApiVersion,
                },
                "client_id": RestConnection::ConnectionScheme.options.oauth2_client_id + <ConnectionOptionInfo>{
                    "desc": "the ServiceNow OAuth2 client ID",
                },
                "client_secret": RestConnection::ConnectionScheme.options.oauth2_client_secret + <ConnectionOptionInfo>{
                    "desc": "the ServiceNow OAuth2 client secret",
                    "sensitive": True,
                },
                "oauth2_auth_url": RestConnection::ConnectionScheme.options.oauth2_auth_url + <ConnectionOptionInfo>{
                    "default_value": ServiceNowRestClient::OAuth2AuthUriPathSuffix,
                },
                "oauth2_grant_type": RestConnection::ConnectionScheme.options.oauth2_grant_type + <ConnectionOptionInfo>{
                    "allowed_values": RestConnection::ConnectionScheme.options.oauth2_grant_type.allowed_values + (
                        <AllowedValueInfo>{
                            "value": "none",
                            "desc": "no OAuth2 grant flow used",
                        },
                    ),
                    "default_value": ServiceNowRestClient::OAuth2DefaultGrantType,
                },
                "oauth2_token_url": RestConnection::ConnectionScheme.options.oauth2_token_url + <ConnectionOptionInfo>{
                    "default_value": ServiceNowRestClient::OAuth2TokenUriPathSuffix,
                },
                "password": RestConnection::ConnectionScheme.options.password + <ConnectionOptionInfo>{
                    "desc": "the ServiceNow password",
                    "sensitive": True,
                },
                "ping_headers": RestConnection::ConnectionScheme.options."ping_headers" + <ConnectionOptionInfo>{
                    "default_value": DefaultServiceNowPingHeaders,
                },
                "ping_method": RestConnection::ConnectionScheme.options."ping_method" + <ConnectionOptionInfo>{
                    "default_value": DefaultServiceNowPingMethod,
                },
                "ping_path": RestConnection::ConnectionScheme.options."ping_path" + <ConnectionOptionInfo>{
                    "default_value": DefaultServiceNowPingPath,
                },
                "username": RestConnection::ConnectionScheme.options.username + <ConnectionOptionInfo>{
                    "desc": "the ServiceNow username",
                },
            },
            "required_options": "username,password|token|client_id,client_secret",
        };

        #! Default ServiceNow ping method
        const DefaultServiceNowPingMethod= "GET";

        #! Default ServiceNow ping path (from the API root)
        const DefaultServiceNowPingPath = "table/sys_properties?sysparm_exclude_reference_link=true"
                        "&sysparm_suppress_pagination_header=true&sysparm_fields=value"
                        "&sysparm_query=name=mid.version";

        #! Default ServiceNow ping headers
        const DefaultServiceNowPingHeaders = {
            "Accept": MimeTypeJson,
        };

        #! options required to support the OAuth2 authorization code grant flow
        const OAuth2AuthRequestOptions = (
            "client_id",
            "client_secret",
            "oauth2_auth_url",
            "oauth2_token_url",
            "oauth2_redirect_url",
        );

        #! Maps ServiceNow REST options to standard options
        const OAuth2Options = {
            "oauth2_auto_refresh": "oauth2_auto_refresh",
            "oauth2_auth_args": "oauth2_auth_args",
            "oauth2_auth_url": "oauth2_auth_url",
            "client_id": "oauth2_client_id",
            "client_secret": "oauth2_client_secret",
            "oauth2_redirect_url": "oauth2_redirect_url",
            "oauth2_refresh_token": "oauth2_refresh_token",
            "oauth2_scopes": "oauth2_scopes",
            "oauth2_token_url": "oauth2_token_url",
            "password": "password",
            "username": "username",
        };

        #! Maps standard option names to ServiceNow REST option names
        const OAuth2StandardToServiceNowOptionMap = map {$1.value: $1.key}, OAuth2Options.pairIterator();

        #! Different OAuth2 option map; standard name => our name
        const OAuth2DifferentOptionMap = map {$1.value: $1.key}, OAuth2Options.pairIterator(), $1.key != $1.value;
    }

    #! creates the ServiceNowRestConnection object
    /** @param name the name of the connection
        @param description connection description
        @param url connection URL (potentially with password info)
        @param attributes various attributes. See below
        @param options connection options

        See @ref ConnectionProvider::AbstractConnection::constructor() for \c attributes and \c options reference.

        @throw CONNECTION-OPTION-ERROR missing or invalid connection option
    */
    constructor(string name, string description, string url, hash<auto> attributes = {}, hash<auto> options = {})
            : RestConnection(name, description, url, attributes, options) {
    }

    #! creates the ServiceNowRestConnection object
    /** @param config with the following keys:
        - name (required string): the connection name
        - display_name (optional string): the display name
        - short_desc (optional string): a short description in plain text
        - desc (optional string): a long description with markdown formatting
        - url (required string): the connection URL
        - opts (optional hash): connection options
        - logger (optional LoggerInterface object): logger for the connection
        @param attr optional connection attributes
        - monitor (optional bool): should the connection be monitored? Default: True
        - enabled (optional bool): is the connection enabled? Default: True
        - locked (optional bool): is the connection locked? Default: False
        - debug_data (optional bool): debug data? Default: False
        - tags (optional hash): tags for the connection; no default value

        @throw CONNECTION-OPTION-ERROR missing or invalid connection option or attribute
    */
    constructor(hash<auto> config, *hash<auto> attr) : RestConnection(config, attr) {
    }

    #! returns \c "snrest"
    string getType() {
        return "snrest";
    }

    #! returns @ref True as this connection returns a data provider with the @ref getDataProvider() method
    /** @return @ref True as this connection returns a data provider with the @ref getDataProvider() method

        @see @ref getDataProvider()
    */
    bool hasDataProvider() {
        return True;
    }

    #! returns a data provider object for this connection
    /** @param constructor_options any additional constructor options for the data provider

        @return a data provider object for this connection
    */
    DataProvider::AbstractDataProvider getDataProvider(*hash<auto> constructor_options) {
        # to avoid circular dependencies, this object loads the ServiceNowRestDataProvider and creates the data provider
        # object dynamically
        load_module("ServiceNowRestDataProvider");
        return create_object("ServiceNowRestDataProvider", get());
    }

    #! Returns the OAuth2 option name for this connection
    /** @since %ServiceNowRestClient 2.0
    */
    string getOAuth2OptionName(string opt) {
        return OAuth2StandardToServiceNowOptionMap{opt} ?? opt;
    }

    #! Returns OAuth2 options in a standard format
    /** @since %ServiceNowRestClient 2.0
    */
    hash<auto> getOAuth2Options() {
        hash<auto> rv;
        foreach hash<auto> i in (OAuth2Options.pairIterator()) {
            rv{i.value} = real_opts{i.key};
        }
        return rv;
    }

    #! Called to start a non-blocking polling ping operation on the ServiceNow REST server
    /**
        @return a socket poll operation object that will allow the connection goal to be reached with polling

        @see supportsPollingApi()
    */
    Qore::AbstractPollOperation startPollConnect() {
        # start the polling operation manually using the full request path
        return new RestPingPollOperation(getPollImpl(), real_opts);
    }

    #! Sets the auth code feature if supported
    private checkAuthCodeFeature() {
        if (opts{OAuth2AuthRequestOptions}.size() == OAuth2AuthRequestOptions.size()) {
            features{RCF_OAUTH2_AUTH_CODE} = True;
        }
    }

    #! Sets child data provider capabilities
    private setChildCapabilities() {
        children_can_support_records = True;
    }

    #! returns a @ref ServiceNowRestClient object
    /** @param connect if @ref True "True", then the connection is returned already connected
        @param rtopts this connection type does not accept any runtime options, so this parameter is ignored

        @return a @ref ServiceNowRestClient object
    */
    private ServiceNowRestClient getImpl(bool connect = True, *hash<auto> rtopts) {
        return new ServiceNowRestClient(getConnectionOptions(), !connect);
    }

    #! Returns the ConnectionSchemeInfo hash for this object
    private hash<ConnectionSchemeInfo> getConnectionSchemeInfoImpl() {
        return ConnectionScheme;
    }
}
}
