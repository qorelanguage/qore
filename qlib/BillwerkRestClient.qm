# -*- mode: qore; indent-tabs-mode: nil -*-
#! @file BillwerkRestClient.qm Qore user module for calling billwerk.com REST services

/*  BillwerkRestClient.qm Copyright (C) 2017 - 2023 Qore Technologies, s.r.o.

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

%requires qore >= 0.9.5

%new-style
%strict-args
%require-types
%enable-all-warnings

%requires(reexport) RestClient >= 1.3.1
%requires(reexport) ConnectionProvider >= 1.4

module BillwerkRestClient {
    version = "1.1";
    desc = "user module for calling billwerk.com REST services";
    author = "David Nichols <david.nichols@qoretechnologies.com>";
    url = "http://qore.org";
    license = "MIT";
    init = sub () {
        ConnectionSchemeCache::registerScheme("billwerkrest", BillwerkRestConnection::ConnectionScheme);
        ConnectionSchemeCache::registerScheme("billwerkrests", BillwerkRestConnection::ConnectionScheme);
    };
}

/** @mainpage BillwerkRestClient Module

    @tableofcontents

    @section billwerkrestclientintro BillwerkRestClient Introduction

    The %BillwerkRestClient module provides an API for calling REST services with the
    <a href="http://www.billwerk.com">billwerk.com</a> billing / payment platform system.

    To use this module, use \c "%requires BillwerkRestClient" in your code.

    All the public symbols in the module are defined in the BillwerkRestClient namespace.

    The main classes are:
    - @ref BillwerkRestClient::BillwerkRestClient "BillwerkRestClient": this class provides the REST client API for
      communuication with the <a href="http://www.billwerk.com">billwerk.com</a> REST API; it also automates
      authentication and authorization
    - @ref BillwerkRestClient::BillwerkRestConnection "BillwerkRestConnection": provides a REST connection object to the
      <a href="http://www.billwerk.com">billwerk.com</a> server (based on the @ref connectionproviderintro "ConnectionProvider" module)

    @par Example:
    @code{.py}
#!/usr/bin/env qore

%new-style
%strict-args
%require-types
%enable-all-warnings

%requires BillwerkRestClient

BillwerkRestClient billwerk(
    {
        "url": "https://name:pass@app.billwerk.com",
        "client_secret": "8df3cc8be92d300edb9ff9b9a680d22eacbf2ee5",
        "swagger": swagger_path,
    });

hash<auto> action_data = {
    "name": "Action name 1",
    "description": "description",
    "effort": 48,
    "actionnum": 7771,
    "status": 0,
};

hash<auto> response = billwerk.put("/api/v1/actionsteps", action_data, NOTHING, NOTHING);
printf("Response body: %N\n", response.body);

    @endcode

    @section Billwerkrestclientrelnotes Release Notes

    @subsection billwerkrestclientv1_1 BillwerkRestClient v1.1
    - implemented support for a data provider scheme cache and rich option information for connections
      (<a href="https://github.com/qorelanguage/qore/issues/4025">issue 4025</a>)

    @subsection billwerkrestclientv1_0 BillwerkRestClient v1.0
    - the initial version of the %BillwerkRestClient module
*/

#! the BillwerkRestClient namespace contains all the objects in the BillwerkRestClient module
public namespace BillwerkRestClient {
#! this class provides the REST client API for communication with the <a href="http://www.billwerk.com">billwerk.com</a> platform
/** This class requires one of the following options for <a href="http://www.billwerk.com">billwerk.com</a>'s
    authentication and authorization:
    - \c client_secret: the <a href="http://www.billwerk.com">billwerk.com</a>'s client_secret
    - \c token: the <a href="http://www.billwerk.com">billwerk.com</a>'s token, if provided then the HTTP username, password,
        and client_secret are not needed and will be ignored.

    After connecting, the BillwerkRestClient class will request a session token that will be used in further communication.
*/
public class BillwerkRestClient inherits RestClient::RestClient {
    public {
        const RequiredOptions = (
            "client_id",
        );

        const AuthPath = "/oauth/token";
    }

    private:internal {
        string client_id;
        string client_secret;
    }

    #! creates the object with the given options
    /**
        @par Example:
        @code{.py}
BillwerkRestClient billwerk(
{
    "url": "https://cloud.billwerk.com/test",
    "client_id": "id",
    "client_secret": "8df3cc8be92d300edb9ff9b9a680d22eacbf2ee5"
});
        @endcode

        @param options valid options are:
        - \c client_secret (optional): the <a href="http://www.billwerk.com">billwerk.com</a> client_secret
        - \c client_id (optional): the <a href="http://www.billwerk.com">billwerk.com</a> client_id
        - \c additional_methods: Optional hash with more but not-HTTP-standardized methods to handle. It allows to create various HTTP extensions like e.g. WebDAV. The hash takes the method name as a key, and the value is a boolean @ref True "True" or @ref False "False": indicating if the method requires a message body as well. Example:
            @code{.py}
# add new HTTP methods for WebDAV; both of which require a message body
{"additional_methods": {"PROPFIND": True, "MKCOL": True}};
            @endcode
        - \c connect_timeout: The timeout value in milliseconds for establishing a new socket connection (also can be a relative date-time value for clarity, ex: \c 20s)
        - \c content_encoding: for possible values, see @ref EncodingSupport; this sets the send encoding (if the \c "send_encoding" option is not set) and the requested response encoding (note that the @ref RestClient::RestClient "RestClient" class will only compress outgoing message bodies over @ref RestClient::RestClient::CompressionThreshold "CompressionThreshold" bytes in size)
        - \c data: a @ref DataSerializationOptions "data serialization option"; if not present defaults to \c "auto"
        - \c default_path: The default path to use for new connections if a path is not otherwise specified in the connection URL
        - \c default_port: The default port number to connect to if none is given in the URL
        - \c error_passthru: if @ref True "True" then HTTP status codes indicating errors will not cause an
            \c HTTP-CLIENT-RECEIVE-ERROR exception to be raised, rather such responses will be passed through to the caller
            like any other response
        - \c headers: an optional hash of headers to send with every request, these can also be overridden in request method calls
        - \c http_version: Either '1.0' or '1.1' for the claimed HTTP protocol version compliancy in outgoing message headers
        - \c max_redirects: The maximum number of redirects before throwing an exception (the default is 5)
        - \c proxy: The proxy URL for connecting through a proxy
        - \c send_encoding: a @ref EncodingSupport "send data encoding option" or the value \c "auto" which means to use automatic encoding; if not present defaults to no content-encoding on sent message bodies (note that the @ref RestClient::RestClient "RestClient" class will only compress outgoing message bodies over @ref RestClient::RestClient::CompressionThreshold "CompressionThreshold" bytes in size)
        - \c swagger: the path to a <a href="https://swagger.io/">Swagger 2.0</a> REST schema file for API validation; only used if \a validator not provided (see the @ref swaggerintro "Swagger" module)
        - \c timeout: The timeout value in milliseconds (also can be a relative date-time value for clarity, ex: \c 30s)
        - \c url: A string giving the URL to connect to; if not given then the target URL will be taken from any \c validator option, if given by calling @ref RestSchemaValidator::AbstractRestSchemaValidator::getTargetUrl() "AbstractRestSchemaValidator::getTargetUrl()"
        - \c validator: an @ref RestSchemaValidator::AbstractRestSchemaValidator "AbstractRestSchemaValidator" object to validate request and response messages; overrides \a swagger
        - \c no_charset: if True no charset will be added to the Content-Type header (should be set to True if data are sent in the body). Default is True if not passed.
        @param do_not_connect if \c False (the default), then a connection will be immediately established to the remote server

        @throw RESTCLIENT-ERROR invalid option passed to constructor, unsupported data serialization, etc
        @throw BILLWERKRESTCLIENT-ERROR missing or invalid required option for <a href="http://www.billwerk.com">Billwerk.com</a> REST authentication or communication
    */
    constructor(hash<auto> options, *softbool do_not_connect) : RestClient(BillwerkRestClient::getOptions(options), True) {
        # the Billwerk server does not return a charset=UTF-8 with its JSON responses, but they are in UTF-8 encoding
        setAssumedEncoding("UTF-8");

        if (options.token) {
            setToken("Bearer", options.token);
        } else {
            # check required options
            foreach string key in (RequiredOptions) {
                auto value = options{key};
                if (!value.val()) {
                    throw "BILLWERKRESTCLIENT-ERROR", sprintf("missing required option %y", key);
                }
                if (value.typeCode() != NT_STRING) {
                    throw "BILLWERKRESTCLIENT-ERROR",
                            sprintf("required option %y was passed as a %s (%y); expecting \"string\"",
                                    key, value.type(), value);
                }
                self{key} = value;
            }
            # see if we have the client_secret
            if (options.client_secret) {
                client_secret = options.client_secret;
            } else {
                # if no token is set, then the URL must have a username & password
                if (!exists username || !exists password) {
                    throw "BILLWERKRESTCLIENT-ERROR", sprintf("missing username and password; cannot login "
                        "without both a username and password or the client_secret");
                }
            }
        }

        # set Accept-Charset header
        if (!headers."Accept-Charset") {
            headers."Accept-Charset" = "utf-8";
        }

        if (!do_not_connect) {
            if (!token) {
                loginIntern();
            } else {
                connect();
            }
        }
    }

    #! returns options for the @ref RestClient::RestClient::constructor() "RestClient::constructor()"
    static hash<auto> getOptions(hash<auto> options) {
        # the "{}" is to ensure that "new_options" stays "hash<auto>"
        hash<auto> new_options = {} + options;

        # remove username and password from URL
        hash<UrlInfo> url_info = parse_url(options.url);
        url_info -= ("username", "password");
        # strip leading slash from path if present
        url_info.path =~ s/^\///;
        new_options.url = sprintf("%s://%s%s%s",
            url_info.protocol ?? "https",
            url_info.host,
            (url_info.port ? ":" + url_info.port : ""),
            (url_info.path.val() ? "/" + url_info.path : ""));

        if (!exists options.no_charset) {
            new_options.no_charset = True;
        }
        new_options.oauth2_token_url = AuthPath;
        if (options.client_id && options.client_secret) {
            if ((url_info.username && url_info.password)
                || (options.username && options.password)) {
                    new_options.oauth2_grant_type = "password";
            } else {
                new_options.oauth2_grant_type = "client_credentials";
            }
            new_options += {
                "oauth2_client_id": options.client_id,
                "oauth2_client_secret": options.client_secret,
            };
        }
        return new_options;
    }
}

#! class for <a href="http://www.billwerk.com">Billwerk.com</a> REST connections; returns @ref BillwerkRestClient::BillwerkRestClient objects
/** In addition to all options supported by @ref RestClient::RestClientConnection "RestClientConnection", this
    connection class supports the following options:
    - \c "client_id" (optional): the <a href="http://www.billwerk.com">billwerk.com</a> client_id
    - \c "client_secret" (mandatory): the <a href="http://www.billwerk.com">billwerk.com</a> client_secret

    @note additionally supports the following runtime option in getImpl():
    - \c "validator": an @ref RestSchemaValidator::AbstractRestSchemaValidator "AbstractRestSchemaValidator" object for REST message validation (if present, overrides any REST schema validation option provided as a connection option)

    @see @ref BillwerkRestClient::BillwerkRestClient::constructor() "BillwerkRestClient::constructor()" for more information on the above options
*/
public class BillwerkRestConnection inherits RestClient::RestConnection {
    public {
        #! Connection entry info
        const ConnectionScheme = <ConnectionSchemeInfo>{
            "cls": Class::forName("BillwerkRestConnection"),
            "options": RestConnection::ConnectionScheme.options + {
                "client_id": <ConnectionOptionInfo>{
                    "type": "string",
                    "desc": "the client ID",
                },
                "client_secret": <ConnectionOptionInfo>{
                    "type": "string",
                    "desc": "the client secret",
                    "sensitive": True,
                },
            },
            "required_options": foldl $1 + "," + $2, BillwerkRestClient::RequiredOptions,
        };
    }

    #! creates the BillwerkRestConnection object
    /** @param name the name of the connection
        @param description connection description
        @param url connection URL (potentially with password info)
        @param attributes various attributes. See below
        @param options connection options

        See @ref AbstractConnection::constructor() for \c attributes and \c options reference.

        @throw CONNECTION-OPTION-ERROR missing or invalid connection option
    */
    constructor(string name, string description, string url, hash<auto> attributes = {}, hash<auto> options = {})
        : RestConnection(name, description, url, attributes, BillwerkRestConnection::getConstructorOptions(options)) {
    }

    #! returns \c "billwerkrest"
    string getType() {
        return "billwerkrest";
    }

    #! gets options for the constructor
    hash<auto> getConstructorOptions(hash<auto> options) {
        return options + (options.client_id
            ? {}
            : {"client_id": self.uniqueHash()});
    }

    #! returns a @ref BillwerkRestClient::BillwerkRestClient object
    /** @param connect if @ref True "True", then the connection is returned already connected
        @param rtopts this connection type does not accept any runtime options, so this parameter is ignored

        @return a @ref BillwerkRestClient::BillwerkRestClient "BillwerkRestClient" object
    */
    private BillwerkRestClient getImpl(bool connect = True, *hash<auto> rtopts) {
        return new BillwerkRestClient(real_opts + {
            "logger": logger,
            "update_options": update_options,
        }, !connect);
    }

    #! Returns the ConnectionSchemeInfo hash for this object
    private hash<ConnectionSchemeInfo> getConnectionSchemeInfoImpl() {
        return ConnectionScheme;
    }
}
}
