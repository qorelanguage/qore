# -*- mode: qore; indent-tabs-mode: nil -*-
# @file Util.qm Qore user module for generally useful routines

/*  Util.qm Copyright 2014 - 2015 Qore Technologies, sro

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

# this module requires Qore 0.8.12 or better
%requires qore >= 0.8.12

# do not use $ for vars, assume local var scope
%new-style

# require type definitions everywhere
%require-types

# enable all warnings
%enable-all-warnings

module Util {
    version = "1.2";
    desc    = "user module for generally useful routines";
    author  = "David Nichols <david@qore.org>";
    url     = "http://qore.org";
    license = "MIT";
    init    = sub () { init(); };
}

/** @mainpage Util Module

    @tableofcontents

    @section utilintro Util Module Introduction

    The %Util module provides some generally-useful routines that are more suitable for
    writing in Qore than in C++.

    To use this module, use \c "%requires Util" in your code.

    All the public symbols in the module are defined in the Util namespace

    Functions:
    - @ref Util::compare_version()
    - @ref Util::get_byte_size()
    - @ref Util::get_exception_string()
    - @ref Util::get_marketing_byte_size()
    - @ref Util::get_random_string()
    - @ref Util::is_int()
    - @ref Util::is_float()
    - @ref Util::is_number()
    - @ref Util::normalize_dir()
      - @ref Util::normalize_dir_unix()
      - @ref Util::normalize_dir_windows()
    - @ref Util::parse_to_qore_value()
    - @ref Util::slice()
    - @ref Util::same()
    - @ref Util::tmp_location()

    @section utilrelnotes Release Notes

    @subsection util_1_2 Util 1.2
    - fixed Util::get_random_string() on Windows
    - fixed Util::absolute_path_windows()
    - fixed Util::parse_to_qore_value() with hashes with a comma in the first key name
    - the following functions are now actually implemented in the %Qore library and only reexported here for backwards-compatibility:
      - @ref Qore::absolute_path()
      - @ref Qore::absolute_path_windows()
      - @ref Qore::absolute_path_unix()
    - added Util::is_int()
    - added Util::is_float()
    - added Util::is_number()
    - added Util::slice()
    - added Util::same()
    - added Util::tmp_location()

    @subsection util_1_1 Util 1.1
    - added the Util::get_byte_size() and Util::get_marketing_byte_size() functions

    @subsection util_1_0 Util 1.0
    - the initial version of the Util module
*/

namespace Init {
    sub init() {
        our hash storage_sizes;
        map storage_sizes.$1 = pow(1024, $# + 1), StorageNames;

        our hash marketing_storage_sizes;
        map marketing_storage_sizes.$1 = pow(1000, $# + 1), StorageNames;
    }
}

#! the Util namespace contains all the objects in the Util module
public namespace Util {
    #! compares complex versions by breaking down strings into component parts
    /** @par Example:
        @code
int result = compare_version("2.11.1", "2.2");
        @endcode

        Each element is compared separately, so that compare_version("2.11.1", "2.2") returns 1
        (a regular string comparison would give the opposite result)

        @param lv left-hand version
        @param rv right-hand version

        @return -1 if lv < rv, 0 if equal, and 1 if lv > rv
    */
    public int sub compare_version(string lv, string rv) {
        list ll = lv.split(".");
        list rl = rv.split(".");

        int le = ll.size();
        int re = rl.size();

        int i = 0;
        while (True) {
            if (le == i) {
                if (re == i)
                    return 0;
                # see if all remaining elements in rl are 0
                splice rl, 0, i;
                return (select rl, $1) ? -1 : 0;
            }
            if (re == i) {
                # see if all remaining elements in ll are 0
                splice ll, 0, i;
                return (select ll, $1) ? 1 : 0;
            }
            any l = ll[i];
            any r = rl[i];
            if (l =~ /^[0-9]/)
                l = int(l);
            if (r =~ /^[0-9]/)
                r = int(r);

            int rc = l <=> r;
            if (rc)
                return rc;

            ++i;
        }
    }

    #! returns a platform-specific normalized path name (starting from /, eliminates \c ".", \c "..", and \c "//")
    /** @par Example:
        @code
string dir = normalize_dir(dir);
        @endcode

        Currently only Windows and UNIX-style directories are supported; this function calls either:
        - @ref Util::normalize_dir_unix() or
        - @ref Util::normalize_dir_windows()

        @param dir the directory name to normalize
        @param cwd the current working directory for normalizing relative paths

        @return a normalized path name for the current platform

        @see
        - @ref Util::normalize_dir_unix() or
        - @ref Util::normalize_dir_windows()
     */
    public string sub normalize_dir(string dir, string cwd = getcwd()) {
        return PlatformOS != "Windows" ? normalize_dir_unix(dir, cwd) : normalize_dir_windows(dir, cwd);
    }

    #! returns a normalized UNIX path name (starting from /, eliminates \c ".", \c "..", and \c "//")
    /** @par Example:
        @code
string dir = normalize_dir_unix(dir, "/var/tmp");
        @endcode

        For example, calling:
        @code normalize_dir("../../test/.//dir"); @endcode
        from current directory \c "/opt/qorus/sub/dir" returns:
        @code "/opt/qorus/test/dir" @endcode

        @param dir the directory name to normalize
        @param cwd the current working directory

        @return a normalized path name (starting from /, eliminates \c ".", \c "..", and \c "//")
     */
    public string sub normalize_dir_unix(string dir, string cwd = getcwd()) {
        # if the directory begins with a ".", prefix the cwd to the string
        if (!absolute_path_unix(dir))
            dir = cwd + "/" + dir;

        # substitute "/./" with "/" until all occurrences are gone
        while (True) {
            string old = dir;
            dir =~ s/\/\.\//\//g;
            if (old == dir)
                break;
        }

        dir =~ s/^\.\///;
        dir =~ s/\/\.$//;

        # change // -> /
        dir =~ s/\/\/+/\//g;

        # separate into a list for eliminating occurences of ".."
        list l = dir.split("/");

        for (int i = 1; i < l.size(); ++i) {
            if (l[i] == "..") {
                splice l, i - 1, 2;
                i -= 2;
            }
        }

        dir = l.join("/");
        return dir ? dir : "/";
    }

    #! returns a normalized Windows path name (eliminates \c ".", \c "..", and \c "//")
    /** @par Example:
        @code
string dir = normalize_dir_windows(dir, "c:\\qore"));
        @endcode

        For example, calling:
        @code normalize_dir("..\..\test\.\\dir"); @endcode
        from current directory \c "c:\\lang\\qore\\prog1" returns:
        @code "c:\\lang\\test\\dir" @endcode

        @param dir the directory name to normalize
        @param cwd the current working directory

        @return a normalized path name (eliminates \c ".", \c "..", and \c "\\")
     */
    public string sub normalize_dir_windows(string dir, string cwd = getcwd()) {
        # if the directory begins with a ".", prefix the cwd to the string
        if (!absolute_path_windows(dir))
            dir = cwd + "\\" + dir;

        # substitute "\.\" with "\" until all occurrences are gone
        while (True) {
            string old = dir;
            dir =~ s/\\\.\\/\\/g;
            if (old == dir)
                break;
        }

        dir =~ s/^\.\\//;
        dir =~ s/\\\.$//;

        # change \\ -> \
        dir =~ s/\\\\+/\\/g;

        # separate into a list for eliminating occurences of ".."
        list l = dir.split("\\");

        for (int i = 1; i < l.size(); ++i) {
            if (l[i] == "..") {
                splice l, i - 1, 2;
                i -= 2;
            }
        }

        dir = l.join("\\");
        return dir ? dir : "\\";
    }

    #! returns @ref Qore::True "True" if the argument is an absolute path, @ref Qore::False "False" if not
    /** @par Example:
        @code
bool abs = absolute_path(path);
        @endcode

        @param path the path to check

        @return @ref Qore::True "True" if the argument is an absolute path, @ref Qore::False "False" if not

        @see @ref Qore::absolute_path()

        @note this function has been moved to the %Qore library and is reexported here for backwards-compatibility
     */
    public bool sub absolute_path(string path) {
        return Qore::absolute_path(path);
    }

    #! returns @ref Qore::True "True" if the argument is a UNIX absolute path, @ref Qore::False "False" if not
    /** @par Example:
        @code
bool abs = absolute_path_unix(path);
        @endcode

        @param path the path to check

        @return @ref Qore::True "True" if the argument is a UNIX absolute path, @ref Qore::False "False" if not

        @see @ref Qore::absolute_path_unix()

        @note this function has been moved to the %Qore library and is reexported here for backwards-compatibility
     */
    public bool sub absolute_path_unix(string path) {
        return Qore::absolute_path_unix(path);
    }

    #! returns @ref Qore::True "True" if the argument is a Windows absolute path, @ref Qore::False "False" if not
    /** @par Example:
        @code
bool abs = absolute_path_windows(path);
        @endcode

        @param path the path to check

        @return @ref Qore::True "True" if the argument is a Windows absolute path, @ref Qore::False "False" if not

        @see @ref Qore::absolute_path_windows()

        @note this function has been moved to the %Qore library and is reexported here for backwards-compatibility
     */
    public bool sub absolute_path_windows(string path) {
        return Qore::absolute_path_windows(path);
    }

    # private, non-exported function: tries to split a string into a list, respects parentheses and quotes
    any sub try_split(string arg) {
        list l = ();

        hash bc;
        *string quote;
        int len = arg.length();
        string str;
        for (int i = 0; i < len; ++i) {
            string c = arg[i];
            if (c == ",") {
                if (!quote && !bc) {
                    l += parse_to_qore_value(str);
                    str = "";
                    continue;
                }
            }
            else if (c == '"' || c == "'") {
                if (quote) {
                    if (c == quote)
                        delete quote;
                }
                else
                    quote = c;
            }
            else if (!quote) {
                if (inlist(c, ("{","(","["))) {
                    ++bc{c};
                }
                else if (inlist(c, ("}",")","]"))) {
                    if (bc{c} && !--bc{c})
                        delete bc{c};
                }
            }
            str += c;
        }
        #printf("trySplit() str=%y l=%y\n", str, l);
        if (!l)
            return arg;

        l += str;
        return l;
    }

    # private, non-exported function: tries to evaluate the argument as a Qore expression/value
    hash sub eval_text(string arg, bool err = False) {
        # try to evaluate as a Qore expression
        string str = sprintf("any sub get() { return %s; }", arg);
        # use a sandboxed Program object
        Program prog(PO_LOCKDOWN);
        try {
            prog.parse(str, "main");
            any rv = prog.callFunction("get");
            #printf("DEBUG: no exception, rv=%y\nstr=%s\n", rv, rv, str);
            # if it's a float, then return a string to preseve formatting
            if (rv.typeCode() != NT_FLOAT && rv.val())
                return ("valid": True, "value": rv);
        }
        catch (hash ex) {
            if (err)
                throw "EVAL-ERROR", sprintf("error in EVAL cmd: %s: %s (arg: %s)\n", ex.err, ex.desc, arg);
        }

        return ("valid": False);
    }

    #! tries to convert a string (such as an argument given from the command-line) to a Qore value
    /** This method recognizes two special prefixes in the string:
        - \c %STR= this prefix indicates that the rest of the string should be passed literally and not parsed
        - \c %EVAL= this prefix indicates that the rest of the string should be taken as a Qore expression and executed and the value returned

        @param arg a string to convert to a Qore value
        @return the Qore value corresponding to the input string
    */
    public any sub parse_to_qore_value(string arg) {
        if (arg.empty())
            return;

        # see if there is aprocessing cmd
        if (*list pc = (arg =~ x/^\%(STR|EVAL)=(.*)$/s)) {
            if (pc[0] == "STR")
                return pc[1];
            else # must be EVAL
                return eval_text(pc[1], True).value;
        }

        # see if it's an int
        if (int(arg) == arg) {
            if (int(arg) >= 2147483648)
                return arg;
            return int(arg);
        }

        if (arg == "True")
            return True;
        if (arg == "False")
            return False;

        # see if it's a date/time value
        if (arg =~ /^[0-9]{4}-[0-9]{2}-[0-9]{2}([-T ][0-5][0-9]:[0-5][0-9](:[0-5][0-9](\.[0-9]+)))?/)
            return date(arg);

        # see if it's a potential string value
        if (arg =~ /^[-_\.[:alnum:]]+$/)
            return arg;

        # see if it's a Qore expression
        hash h = eval_text(arg);
        if (h.valid)
            return h.value;

        # see if it's a string like "key=val,key=val[,...]", if so, return a hash
        if (arg =~ /^([^ ]+)[ \t]*=([^,]*)(,([^ ]+)[ \t]*=([^,]*))?/s) {
            list l = split(",", arg);

            # coalesce each element without a "=" into the previous element
            for (int i = 1; i < l.size(); ++i) {
                if (l[i] !~ /.+=.+/) {
                    # copy element
                    string x = "," + l[i];
                    # remove from list
                    splice l, i, 1;
                    # append to previous string
                    l[i - 1] += x;
                    --i;
                }
            }

            # coalesce the first element into the second if there is no "="
            if (l[0] !~ /=/) {
                # copy element
                string x = l[0] + ",";
                # remove from list
                splice l, 0, 1;
                # append to new initial element
                l[0] = x + l[0];
            }

            hash rh;
            foreach string a in (l) {
                trim a;
                my (string key, any val) = (a =~ x/^([^ =]+)[ \t]*=(.*)/gm);
                rh{key} = parse_to_qore_value(val);
            }

            return rh;
        }

        # see if it's a list
        if (arg =~ /,/)
            return try_split(arg);

        return arg;
    }

    #! function that returns a random string
    /** @par Example:
        @code
string pass = get_random_string();
        @endcode

        @param len the length fo the string returned
        @param level a value from 1 - 4 inclusive giving the range of printable ASCII characters included in the output (values out of the range result in the nearest value in the range being used):
        - 1: only upper case
        - 2: +lower case
        - 3: +numbers
        - 4: +symbols but not including any quote characters

        @return a random string of the given length containing only printable ASCII characters according to the \a level argument
    */
    public string sub get_random_string(softint len = 15, int level = 3) {
        string str;
        if (level < 1)
            level = 1;
        else if (level > 4)
            level = 4;
        --level;
        level = (26, 52, 62, 88)[level];  # 15(77), 7(84), 4
        # do not seed the random number generator and use rand() as this requires PO_PROCESS_CONTROL permissions
        if (PlatformOS == "Windows") {
            # on windows, just use clock_getmicros() to generate a pseudo-random number
            int start = (clock_getmicros() * 7621 + 1);
            for (int i = 0; i < len; ++i) {
                int v = start;
                if (i)
                    v += rand();
                v = v.abs() % level;
                str += (v < 26 ? chr(v + ord("A")) :
                         (v < 52 ? chr(v - 26 + ord("a")) :
                          (v < 62 ? chr(v - 4) :
                           (v < 77 ? chr(v - 29) :
                            (v < 84 ? chr(v - 19) :
                             chr(v + 39))))));
            }
        }
        else {
            ReadOnlyFile f("/dev/urandom");
            for (int i = 0; i < len; ++i) {
                int v = f.readi2().abs() % level;
                str += (v < 26 ? chr(v + ord("A")) :
                         (v < 52 ? chr(v - 26 + ord("a")) :
                          (v < 62 ? chr(v - 4) :
                           (v < 77 ? chr(v - 29) :
                            (v < 84 ? chr(v - 19) :
                             chr(v + 39))))));
            }
        }
        return str;
    }

    #! returns a multi-line string from the exception hash argument suitable for logging or output on the console
    /** @param ex the exception hash to process

        @return a multi-line string from the exception hash argument suitable for logging or output on the console
     */
    public string sub get_exception_string(hash ex) {
        string str;

        while (True) {
            str += sprintf("%s exception: %s: %s: %s%s\ncall stack:",
                            ex.type, get_ex_pos(ex), ex.err, ex.desc,
                            ex.arg ? sprintf(" (arg: %y)", ex.arg) : "");

            if (ex.callstack) {
                foreach hash l in (ex.callstack) {
                    if (l.type == "new-thread")
                        str += sprintf("\n  *** thread started by background operator ***");
                    else if (l.type == "rethrow")
                        str += sprintf("\n  *** RETHROW at %s", get_ex_pos(l));
                    else
                        str += sprintf("\n  %s() called at %s (%s function)", l.function, get_ex_pos(l), l.type);
                }
            }

            if (!ex.next)
                break;
            ex = ex.next;
            str += "\nchained exception:\n";
        }
        return str;
    }

    #! storage names (Geop, which would normally follow Bronto, is excluded as the abbreviation would be GB/GiB)
    public const StorageNames = ("Kilo", "Mega", "Giga", "Tera", "Peta", "Exa", "Zetta", "Yotta", "Bronto");

    #! returns a string giving a user-friendly real storage size (ie based on 1KiB = 1024 bytes) in KiB, MiB, GiB, TiB, etc for the given byte size
    /** @par Example:
        @code
string str = get_byte_size(n);
        @endcode

        @param n the number to display
        @param decimals the number of digits after the decimal point
    */
    public string sub get_byte_size(softnumber n, int decimals = 2) {
        string k;

        HashPairIterator i(storage_sizes);
        while (i.next()) {
            if (n >= i.getValue().value)
                k = i.getValue().key;
            else
                break;
        }

        string fmt = sprintf("%%.%df ", decimals);

        if (!k)
            return sprintf(fmt + "byte" + (n == 1 ? "" : "s"), n);

        n /= storage_sizes{k};
        string v = sprintf(fmt, n);
        v =~ s/(\.)?0*$//;

        return v + k[0] + "iB";
    }

    #! returns a string giving a user-friendly "marketing storage" size (ie based on 1KB = 1000 bytes) in KB, MB, GB, TB, etc for the given byte size
    /** @par Example:
        @code
string str = get_marketing_byte_size(n);
        @endcode

        @param n the number to display
        @param decimals the number of digits after the decimal point
    */
    public string sub get_marketing_byte_size(softnumber n, int decimals = 2) {
        string k;

        HashPairIterator i(marketing_storage_sizes);
        while (i.next()) {
            if (n >= i.getValue().value)
                k = i.getValue().key;
            else
                break;
        }

        string fmt = sprintf("%%.%df ", decimals);

        if (!k)
            return sprintf(fmt + "byte" + (n == 1 ? "" : "s"), n);

        n /= marketing_storage_sizes{k};
        string v = sprintf(fmt, n);
        # remove trailing zeros
        v =~ s/(\.)?0*$//;

        return v + k[0] + "B";
    }

    #! checks whether given list has only uniq element
    /** @par Example:
        @code
        if (same((1, 1, 1)) {
            printf("only uniq element");
        };
        @endcode

        @param l some list

        @since Util 1.2
    */
    public bool sub same(list l) {
        if (l.size() < 2) {
            return True;
        }
        any random_value = l[0];
        for (int i = 1; i < l.size(); ++i) {
            if (l[i] != random_value) {
                return False;
            }
        }
        return True;
    }

    #! list slice implementation
    /** @par Example:
        @code
        list_slice(("a", "b", "c", "d"), (1, 3)); # returns ("b", "d")
        @endcode

        @param l some list
        @param indices indices that should be sliced from the original list

        @since Util 1.2
    */
    public list sub slice(list l, softlist indices) {
        return map l[$1], indices, ($1 >= 0 && $1 < l.size());
    }

    #! Returns a path for temporary files
    /** @par Example:
        @code
string testfile = tmp_location() + '/test.txt';
# some processing
unlink(testfile);
        @endcode

        Environment variables are searched in the following order (the first with a value is used):
        - \c TMPDIR
        - \c TMP
        - \c TEMP
        - \c TEMPDIR

        If none of these are present, then \c "/tmp" (UNIX) or \c "c:\temp" (Windows) is checked.

        @throw TMP-ERROR unable to find the location of the temporary directory; temporary directory is not writable

        @since Util 1.2
    */
    public string sub tmp_location() {
        # TODO: add support for other platforms
        *string dir = ENV.TMPDIR ?* ENV.TMP ?* ENV.TEMP ?* ENV.TEMPDIR;
        if (!dir)
            dir = PlatformOS == "Windows" ? "c::\\temp" : "/tmp";

        if (!is_writable(dir))
            throw "TMP-ERROR", sprintf("unable to find the location of the temporary directory; %y is not writable", dir);

        return realpath(dir);
    }

    #! Checks whether string represents an integer value.
    /**
        @param  text a string to check
        @return True if input represents an integer value, False otherwise.

        @par Example:
        @code
bool match = is_int("123");
# returns True, while any of the following returns False:
match = is_int("1.0");
match = is_int("12x");
        @endcode

        @note The function only checks whether a string represents an integer value, but it does not validate size restrictions (that is whether it would fit %Qore's native int type) etc.

        @since Util 1.2
    */
    public bool sub is_int (string text) {
        if (text =~ /^\s*[-+]?\d+\s*$/)
            return True;
        return False;
    }

    const RE_FLOAT = '^\s*[-+]?('
        '\d+\.\d*(e[-+]?\d+)?' # floating point number, with dot, optional exponent
        '|\.\d+(e[-+]?\d+)?' # floating point number, starting with a dot, optional exponent
        '|\d+e[-+]?\d+' # floating point number, without dot, with exponent
        ')\s*$';

    #! Checks whether string represents a true float value.
    /**
        @param  text a string to check
        @return True if input represents a float value, False otherwise.

        @par Example:
        @code
bool match = is_float("1.2");
# returns True, while any of the following returns False:
match = is_float("12");
match = is_float("1.2x");
        @endcode

        @note The function only checks whether a string represents a float value, but it does not validate size / precision restrictions (whether it would fit %Qore's native float type) nor does it match integers.

        @since Util 1.2
    */
    public bool sub is_float (string text) {
        if (text.regex(RE_FLOAT))
            return True;
        return False;
    }

    const RE_NUMBER = '^\s*[-+]?('
        '\d+\.\d*(e[-+]?\d+)?' # floating point number, with dot, optional exponent
        '|\.\d+(e[-+]?\d+)?' # floating point number, starting with a dot, optional exponent
        '|\d+e[-+]?\d+' # floating point number, without dot, with exponent
        '|\d+)n\s*$'; # integer

    #! Checks whether string represents a true number value.
    /**
        @param  text a string to check
        @return True if input represents a number value, False otherwise.

        @par Example:
        @code
bool match = is_number("1.2n");
# returns True, while any of the following returns False:
match = is_number("12");
match = is_number("1.2");
match = is_number("1.2x");
        @endcode

        @note The function only checks whether a string represents an arbitrary precision number value, but it does not validate any restrictions (whether it would fit %Qore's native number type) nor does it match subtypes (integers and floats).

        @since Util 1.2
    */
    public bool sub is_number (string text) {
        if (text.regex(RE_NUMBER))
            return True;
        return False;
    }
}
