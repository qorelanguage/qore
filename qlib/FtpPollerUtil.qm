# -*- mode: qore; indent-tabs-mode: nil -*-
#! @file FtpPollerUtil.qm Qore user module providing definitions for the FtpPoller module

/*  FtpPollerUtil.qm Copyright 2023 Qore Technologies, s.r.o.

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

%new-style
%strict-args
%require-types
%enable-all-warnings

# make sure we have the required qore version
%requires qore >= 1.0
%requires DataProvider

module FtpPollerUtil {
    version = "1.0";
    desc = "user module providing definitions for the FtpPoller module";
    author = "Alzhan Turlybekov <alzhan.turlybekov@qoretechnologies.com>";
    url = "http://qore.org";
    license = "MIT";
    init = sub () {
        # register the event data type
        DataProvider::registerType("qore/ftp/event", new FtpPollerFileEventInfoDataType());
    };
}

/*  Version History
    * 2019-10-16 v1.0: Alzhan Turlybekov <alzhan.turlybekov@qoretechnologies.com>
*/

/** @mainpage FtpPollerUtil Module

    @section ftppollerutilintro Introduction to the FtpPollerUtil Module

    The FtpPollerUtil module defines types for the \c FtpPoller module.

    @section ftppollerutilrelnotes FtpPollerUtil Module Release Notes

    @section ftppollerutilv1_0 Version 1.0
    - initial release
*/

#! main FtpPoller namespace
public namespace FtpPoller {
#! Single file event data description
public class FtpPollerFileEventInfoDataType inherits HashDataType {
    public {
        #! Markdown descriptions for hashdecl members
        const FieldDescriptions = {
            "name": "The name of the file, link, or directory",
            "size": "The size of the file in bytes",
            "mtime": "The last modified date/time of the file",
            "data": "The file's data if `local_dir` is not set; this will be a string unless the `binary` option is "
                "set to `True`, in which case this key is assigned to the file's binary data; when `local_dir` is "
                "set, file data is not included in the event but is rather transferred to the local directory",
            "filepath": "The remote filepath relative to SFTP root directory",
            "transfer_time": "The relative date/time value giving the time taken to transfer the file",
            "local_path": "Only included when `local_dir` is set; the target path for the file where the file will "
                "be moved to in the post event action",
        };
    }

    #! Creates the type
    constructor() {
        # add members for base hashdecl
        map addField(new QoreDataField($1.getName(), FieldDescriptions{$1.getName()}, $1.getType(),
            $1.getDefaultValue())), TypedHash::forName("FtpPoller::FtpPollerFileEventInfo").getMembers();
    }

    #! Returns the description
    string getDesc() {
        return "FTP polling event data type";
    }
}

#! FtpPoller file event hash
public hashdecl FtpPollerFileEventInfo {
    #! The name of the file, link, or directory
    string name;

    #! The size of the file in bytes
    int size;

    #! The last modified date/time of the file
    date mtime;

    #! The file's data
    /** this will be a string unless the \a "binary" option is set to @ref Qore::True "True", in which case this
        key is assigned to the file's binary data
    */
    data data;

    #! The remote filepath relative to the FTP root directory
    string filepath;

    #! The relative date/time value giving the time taken to transfer the file
    date transfer_time;

    #! Only included when `local_dir` is set; the target path for the file
    *string local_path;
}
}
