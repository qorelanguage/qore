# -*- mode: qore; indent-tabs-mode: nil -*-
# @file RestClient.qm Qore user module for calling REST services

/*  RestClient.qm Copyright (C) 2013 - 2016 Qore Technologies, sro

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

# this module requires Qore 0.8.12 or better
%requires qore >= 0.8.12

# require type definitions everywhere
%require-types

# enable all warnings
%enable-all-warnings

# don't use "$" for vars, members, and methods, assume local variable scope
%new-style

%requires Mime >= 1.3

%try-module yaml >= 0.5
%define NoYaml
%endtry

%try-module xml >= 1.3
%define NoXml
%endtry

%try-module json >= 1.5
%define NoJson
%endtry

module RestClient {
    version = "1.3";
    desc = "user module for calling REST services";
    author = "David Nichols <david@qore.org>";
    url = "http://qore.org";
    license = "MIT";

%ifndef QORE_QDX_RUN
    init = sub () {
        # if no modules for data de/serialization can be loaded, then throw an exception
        if (!RestClient::Accept)
            throw "RESTCLIENT-ERROR", sprintf("none of the 'yaml', 'xml', or 'json' modules can be loaded; at least one of which is required to support data de/serialization for REST requests and responses");
    };
%endif
}

/*  Version History - see below in docs
*/

/** @mainpage RestClient Module

    @tableofcontents

    @section restclientintro RestClient Introduction

    The %RestClient module provides an API for calling REST services.

    To use this module, use \c "%requires RestClient" in your code.

    All the public symbols in the module are defined in the RestClient namespace.

    The main classes are:
    - @ref RestClient::RestClient "RestClient": this class provides the REST client API

    The @ref RestClient::RestClient "RestClient" class can support three methods of data serialization:
    - \c "json": using the \c "json" module; messages bodies are encoded using JSON
    - \c "yaml": using the \c "yaml" module; message bodies are encoded using YAML
    - \c "xml": using the \c "xml" module; message bodies are encoded using XML-RPC value encoding

    If none of the above modules can be loaded by the RestClient module, then the RestClient module will fail to initialize.

    For maximum compatibility with other REST solutions, the \c "json" encoding method is the default when no other encoding method
    is chosen and the \c "json" module is available.

    @par Example:
    @code{.py}
#!/usr/bin/env qore

%requires RestClient

RestClient rest(("url": "http://localhost:8001/rest"));
hash ans = rest.get("orders/1?option=full");
printf("%N\n", ans.body);
    @endcode

    See example file \c "rest" in the examples directory for a more detailed example using this module

    @section restclientrelnotes Release Notes

    @subsection restclientv1_3 RestClient v1.3
    - added:
      - @ref RestClient::RestClient::addDefaultHeaders() "RestClient::addDefaultHeaders()"
      - @ref RestClient::RestClient::getDefaultHeaders() "RestClient::getDefaultHeaders()"
      - @ref RestClient::RestClient::getSendEncoding() "RestClient::getSendEncoding()"
      - @ref RestClient::RestClient::setContentEncoding() "RestClient::setContentEncoding()"
    - implemented the \c "content_encoding" option for the RestClient constructor
    - made \c "gzip" the default content encoding
    - added a compression threshold giving a minimum size for for applying content encoding on message bodies; small messages will be sent uncompressed
    - when possible, REST bodies are decoded and stored in the \a info output argument when the HTTP server returns a status code < 100 or >= 300 to allow for error-handling in the client
    - fixed issues where multiple leading \c "/" chars were sometimes present in the request URI path
    - doc updates

    @subsection restclientv1_2 RestClient v1.2
    - allow the Content-Type header to be overriden in REST requests
    - added \a hdr args to REST methods
    - fixed a bug using the module when the yaml module is not available
    - ensure URI paths are absolute

    @subsection restclientv1_1 RestClient v1.1
    - updated to use encode_url() before sending HTTP messages
    - add charset=xxx to \c "Content-Type" header in requests
    - fixed \c "Accept" header in requests

    @subsection restclientv1_0 RestClient v1.0
    - the initial version of the %RestClient module
*/

#! the RestClient namespace contains all the objects in the RestClient module
public namespace RestClient {
    #! this class provides the REST client API
    public class RestClient inherits Qore::HTTPClient {
        public {
            #! Data serialization support mapping codes to MIME types and de/serialization functions
            const DataSerializationSupport = {
%ifndef NoJson
                "json": (
                    "ct": MimeTypeJson,
                    "out": \make_json(),
                ),
%endif
%ifndef NoYaml
                "yaml": (
                    "ct": MimeTypeYaml,
                    "out": \make_yaml(),
                ),
%endif
%ifndef NoXml
                "xml": (
                    "ct": MimeTypeXml,
                    "out": \make_xmlrpc_value(),
                ),
%endif
                "text": (
                    "ct": MimeTypeText,
                    "out": string sub (string s) { return s; },
                ),
                };


%ifndef NoYaml
            const DeserializeYaml = (
                    "code": "yaml",
                    "in": \parse_yaml(),
                );
%endif
%ifndef NoXml
            const DeserializeXmlRpc = (
                    "code": "xml",
                    "in": \parse_xmlrpc_value(),
                );
%endif

            #! Data deserialization support MIME types to codes and de/serialization functions
            const DataDeserializationSupport = {
%ifndef NoJson
                MimeTypeJson: (
                    "code": "json",
                    "in": \parse_json(),
                ),
%endif
%ifndef NoYaml
                MimeTypeYamlRpc: DeserializeYaml,
                MimeTypeYaml: DeserializeYaml,
%endif
%ifndef NoXml
                MimeTypeXml: DeserializeXmlRpc,
                MimeTypeXmlApp: DeserializeXmlRpc,
%endif
                MimeTypeText: (
                    "code": "text",
                    "in": string sub (string s) { return s; },
                ),

                };

            #! Accept header list
            const AcceptList = (
%ifndef NoYaml
                MimeTypeYaml, MimeTypeYamlRpc,
%endif
%ifndef NoXml
                MimeTypeXml, MimeTypeXmlApp,
%endif
%ifndef NoJson
                MimeTypeJsonRpc,
%endif
                MimeTypeText
                );

            #! Accept header value
            const Accept = AcceptList.join(",");

            #! RestClient Version
            const Version = "1.3";

            #! RestClient Version String
            const VersionString = sprintf("Qore-RestClient/%s", RestClient::Version);

            #! default HTTP headers (Content-Type is added before sending)
            const DefaultHeaders = (
                "Accept": Accept,
                "User-Agent": RestClient::VersionString,
                );

            #! Data serialization options; this is a hash to simulate a set of strings
            /** Data serialization options are as follows:
                - \c "auto": prefers in this order: json, yaml, xml-rpc
                - \c "xml": use only XML-RPC serialization
                - \c "json": use only JSON serialization
                - \c "yaml": use only YAML serialization
                - \c "text": use only plain text. No serialization is used.
             */
            const DataSerializationOptions = (
                "auto": True,
%ifndef NoXml
                "xml": True,
%endif
%ifndef NoJson
                "json": True,
%endif
%ifndef NoYaml
                "yaml": True,
%endif
                "text": True,
                );

            #! Send content encoding options
            /** Send content encoding options are as follows:
                - \c "bzip": use bzip2 compression
                - \c "gzip": use gzip compression
                - \c "deflate": use deflate compression
                - \c "identity": use no content encoding
             */
            const EncodingSupport = (
                "gzip": (
                    "ce": "gzip",
                    "func": \gzip(),
                    ),
                "bzip2": (
                    "ce": "bzip2",
                    "func": \bzip2(),
                    ),
                "deflate": (
                    "ce": "deflate",
                    "func": \compress(),
                    ),
                "identity": (
                    "ce": NOTHING,
                    ),
                );

            #! default threadhold for data compressions; transfers smaller than this size will not be compressed
            const CompressionThreshold = 1024;
        }

        private {
            # headers to send with every request
            hash headers;
            # data serialization code
            string ds;
            # serialization info hash
            hash sih;
            # send content encoding hash
            *hash seh;
        }

        #! calls the base class HTTPClient constructor and optionally connects to the REST server
        /** @par Example:
            @code{.py}
RestClient rest(("url": "http://localhost:8001/rest"));
            @endcode

            @param opts valid options are:
            - \c connect_timeout: The timeout value in milliseconds for establishing a new socket connection (also can be a relative date-time value for clarity, ex: \c 20s)
            - \c data: a @ref DataSerializationOptions "data serialization option"; if not present defaults to \c "auto"
            - \c default_path: The default path to use for new connections if a path is not otherwise specified in the connection URL
            - \c default_port: The default port number to connect to if none is given in the URL
            - \c headers: an optional hash of headers to send with every request, these can also be overridden in request method calls
            - \c http_version: Either '1.0' or '1.1' for the claimed HTTP protocol version compliancy in outgoing message headers
            - \c max_redirects: The maximum number of redirects before throwing an exception (the default is 5)
            - \c proxy: The proxy URL for connecting through a proxy
            - \c send_encoding: a @ref EncodingSupport "send data encoding option" or the value \c "auto" which means to use automatic encoding; if not present defaults to no content-encoding on sent message bodies
            - \c content_encoding: for possible values, see @ref EncodingSupport; this sets the send encoding (if the \c "send_encoding" option is not set) and the requested response encoding
            - \c timeout: The timeout value in milliseconds (also can be a relative date-time value for clarity, ex: \c 30s)
            - \c url: A string giving the URL to connect to
            @param do_not_connect if \c False (the default), then a connection will be immediately established to the remote server

            @throw RESTCLIENT-ERROR invalid option passed to constructor, unsupported data serialization, etc

            @since %RestClient 1.2 the \a send_encoding option was added
        */
        constructor(*hash opts, *softbool do_not_connect) : HTTPClient(opts) {
            setSerialization(opts.data);
            if (opts.send_encoding)
                setSendEncoding(opts.send_encoding);

            if (opts.content_encoding) {
                if (!opts.send_encoding)
                    setSendEncoding(opts.content_encoding);
                else if (!EncodingSupport.(opts.content_encoding))
                    throw "RESTCLIENT-ERROR", sprintf("content encoding option %y is unknown; valid options: %y", opts.content_encoding, EncodingSupport.keys());
                opts.headers."Accept-Encoding" = opts.content_encoding;
            }

            # unconditionally set the encoding to utf-8
            setEncoding("utf-8");

            if (!do_not_connect)
                connect();

            headers = DefaultHeaders + opts.headers;
        }

        #! change the serialization option for the object; see @ref DataSerializationOptions for valid options
        /** @par Example:
            @code{.py}
rest.setSerialization("yaml");
            @endcode

            @param data the serialization option for the object; see @ref DataSerializationOptions for valid options

            @throw RESTCLIENT-ERROR invalid or unsupported serialization option

            @see @ref RestClient::RestClient::getSerialization() "RestClient::getSerialization()"
        */
        setSerialization(string data = "auto") {
            if (!DataSerializationOptions{data})
                throw "RESTCLIENT-ERROR", sprintf("data serialization option %y is unknown; valid options: %y", data, DataSerializationOptions.keys());

            if (data == "auto" && DataSerializationSupport)
                data = DataSerializationSupport.firstKey();
            else if (!DataSerializationSupport{data})
                throw "RESTCLIENT-ERROR", sprintf("data serialization option %y is not supported because the required module could not be loaded; currently supported options: %y", data, DataSerializationSupport.keys());

            ds = data;
            sih = DataSerializationSupport{data};
        }

        #! change the data content encoding (compression) option for the object; see @ref EncodingSupport for valid options
        /** @par Example:
            @code{.py}
rest.setSendEncoding("gzip");
            @endcode

            The default is to send requests unencoded/uncompressed.

            @param enc the data content encoding (compression) option for the object; see @ref EncodingSupport for valid options; if the value \c "auto" is passed then \c "gzip" encoding is used

            @throw RESTCLIENT-ERROR invalid or unsupported data content encoding / compression option

            @see
            - @ref RestClient::RestClient::setContentEncoding() "RestClient::setContentEncoding()"
            - @ref RestClient::RestClient::getSendEncoding() "RestClient::getSendEncoding()"
        */
        setSendEncoding(string enc = "auto") {
            if (enc == "auto")
                seh = EncodingSupport.firstValue();
            else {
                if (!EncodingSupport{enc})
                    throw "RESTCLIENT-ERROR", sprintf("send content encoding option %y is unknown; valid options: %y", enc, EncodingSupport.keys());
                seh = EncodingSupport{enc};
            }
        }

        #! sets the request and desired response encoding for the object; see @ref EncodingSupport for valid options
        /** @par Example:
            @code{.py}
rest.setContentEncoding("gzip");
            @endcode

            @param enc the data content encoding (compression) option for requests and the desired response content encoding for the object; see @ref EncodingSupport for valid options; if the value \c "auto" is passed then \c "gzip" encoding is used for outgoing requests and requested for responses

            @throw RESTCLIENT-ERROR invalid or unsupported data content encoding / compression option

            @see
            - @ref RestClient::RestClient::getSendEncoding() "RestClient::getSendEncoding()"
            - @ref RestClient::RestClient::setSendEncoding() "RestClient::setSendEncoding()"

            @since %RestClient 1.3
        */
        setContentEncoding(string enc = "auto") {
            if (enc == "auto")
                seh = EncodingSupport.firstValue();
            else
                setSendEncoding(enc);

            headers."Accept-Encoding" = seh.ce ? seh.ce : "identity";
        }

        #! adds default headers to each request; these headers will be sent in all requests but can be overridden in requests as well
        /** @par Example:
            @code{.py}
# disable gzip and bzip encoding in responses
rest.addDefaultHeaders(("Accept-Encoding": "compress"));
            @endcode

            @param h a hash of headers to add to the default headers to send on each request

            @note default headers can also be set in the constructor

            @see @ref RestClient::RestClient::getDefaultHeaders() "RestClient::getDefaultHeaders()"

            @since %RestClient 1.3
        */
        addDefaultHeaders(hash h) {
            headers += h;
        }

        #! returns the hash of default headers to sent in all requests
        /** @par Example:
            @code{.py}
hash h = rest.getDefaultHeaders();
            @endcode

            @return the hash of default headers to sent in all requests

            @note default headers can be set in the constructor and in addDefaultHeaders()

            @see @ref RestClient::RestClient::addDefaultHeaders() "RestClient::addDefaultHeaders()"

            @since %RestClient 1.3
        */
        hash getDefaultHeaders() {
            return headers;
        }

        #! returns the current data content encoding (compression) object or @ref nothing if no encoding option is set; see @ref EncodingSupport for valid options
        /** @par Example:
            @code{.py}
*string ce = rest.getSendEncoding();
            @endcode

            @return the current data content encoding (compression) object or @ref nothing if no encoding option is set; see @ref EncodingSupport for valid options

            @see
            - @ref RestClient::RestClient::setContentEncoding() "RestClient::setContentEncoding()"
            - @ref RestClient::RestClient::setSendEncoding() "RestClient::setSendEncoding()"

            @since %RestClient 1.3
        */
        *string getSendEncoding() {
            return seh.ce;
        }

        #! returns the current data serialization format currently in effect for the object (see @ref DataSerializationOptions for possible values)
        /** @par Example:
            @code{.py}
string ser = rest.getSerialization();
            @endcode

            @return the current data serialization format currently in effect for the object (see @ref DataSerializationOptions for possible values)

            @see @ref RestClient::RestClient::setSerialization() "RestClient::setSerialization()"
         */
        string getSerialization() {
            return ds;
        }

        #! sends an HTTP GET request to the REST server and returns the response
        /** @par Example:
            @code{.py}
hash ans = rest.get("/orders/1?info=verbose");
            @endcode

            @param path the URI path to add (will be appended to any root path given in the constructor)
            @param body an optional message body to be included in the request; if a value for this parameter is passed to the method, then the body will be serialized according to the serialization rules set in @ref RestClient::RestClient::constructor() "RestClient::constructor()"
            @param info an optional reference to a hash that will be used as an output variable giving a hash of request headers and other information about the HTTP request
            @param hdr any headers to be sent with the request; headers here will override default headers for the object as well

            @return A hash of headers received from the HTTP server with all key names converted to lower-case; if any message body is included in the response, it will be assigned to the value of the \c "body" key

            @throw DESERIALIZATION-ERROR the response body could not be deserialized (unknown \c Content-Type or invalid serialization)
            @throw HTTP-CLIENT-RECEIVE-ERROR if this exception is thrown by the @ref Qore::HTTPClient::send() call in case of an HTTP response code < 100 or >= 300, the message body is still deserialized if possible and the response information can be retrieved in the \a info hash output keys as follows:
            - \c "response-code": the HTTP response code given
            - \c "response-headers": a hash of response headers
            - \c "response-body": the decoded response body

            Other exceptions can be thrown by the @ref Qore::HTTPClient::send() call used to make the HTTP request.

            @see
            - @ref RestClient::RestClient::getSerialization() "RestClient::getSerialization()"
            - @ref RestClient::RestClient::setSerialization() "RestClient::setSerialization()"
         */
        hash get(string path, any body, *reference info, *hash hdr) {
            return doRequest("GET", path, body, \info, NOTHING, hdr);
        }

        #! sends an HTTP PUT request to the REST server and returns the response
        /** @par Example:
            @code{.py}
hash ans = rest.put("/orders/1", ("action": "cancel"));
            @endcode

            @param path the URI path to add (will be appended to any root path given in the constructor)
            @param body an optional message body to be included in the request; if a value for this parameter is passed to the method, then the body will be serialized according to the serialization rules set in @ref RestClient::RestClient::constructor() "RestClient::constructor()"
            @param info an optional reference to a hash that will be used as an output variable giving a hash of request headers and other information about the HTTP request
            @param hdr any headers to be sent with the request; headers here will override default headers for the object as well

            @return A hash of headers received from the HTTP server with all key names converted to lower-case; if any message body is included in the response, it will be assigned to the value of the \c "body" key

            @throw DESERIALIZATION-ERROR the response body could not be deserialized (unknown \c Content-Type or invalid serialization)
            @throw HTTP-CLIENT-RECEIVE-ERROR if this exception is thrown by the @ref Qore::HTTPClient::send() call in case of an HTTP response code < 100 or >= 300, the message body is still deserialized if possible and the response information can be retrieved in the \a info hash output keys as follows:
            - \c "response-code": the HTTP response code given
            - \c "response-headers": a hash of response headers
            - \c "response-body": the decoded response body

            Other exceptions can be thrown by the @ref Qore::HTTPClient::send() call used to make the HTTP request.

            @see
            - @ref RestClient::RestClient::getSerialization() "RestClient::getSerialization()"
            - @ref RestClient::RestClient::setSerialization() "RestClient::setSerialization()"
         */
        hash put(string path, any body, *reference info, *hash hdr) {
            return doRequest("PUT", path, body, \info, NOTHING, hdr);
        }

        #! sends an HTTP POST request to the REST server and returns the response
        /** @par Example:
            @code{.py}
hash ans = rest.post("/orders", ("product": "xyz123", "options": 500));
            @endcode

            @param path the URI path to add (will be appended to any root path given in the constructor)
            @param body an optional message body to be included in the request; if a value for this parameter is passed to the method, then the body will be serialized according to the serialization rules set in @ref RestClient::RestClient::constructor() "RestClient::constructor()"
            @param info an optional reference to a hash that will be used as an output variable giving a hash of request headers and other information about the HTTP request
            @param hdr any headers to be sent with the request; headers here will override default headers for the object as well

            @return A hash of headers received from the HTTP server with all key names converted to lower-case; if any message body is included in the response, it will be assigned to the value of the \c "body" key

            @throw DESERIALIZATION-ERROR the response body could not be deserialized (unknown \c Content-Type or invalid serialization)
            @throw HTTP-CLIENT-RECEIVE-ERROR if this exception is thrown by the @ref Qore::HTTPClient::send() call in case of an HTTP response code < 100 or >= 300, the message body is still deserialized if possible and the response information can be retrieved in the \a info hash output keys as follows:
            - \c "response-code": the HTTP response code given
            - \c "response-headers": a hash of response headers
            - \c "response-body": the decoded response body

            Other exceptions can be thrown by the @ref Qore::HTTPClient::send() call used to make the HTTP request.

            @see
            - @ref RestClient::RestClient::getSerialization() "RestClient::getSerialization()"
            - @ref RestClient::RestClient::setSerialization() "RestClient::setSerialization()"
         */
        hash post(string path, any body, *reference info, *hash hdr) {
            return doRequest("POST", path, body, \info, NOTHING, hdr);
        }

        #! sends an HTTP DELETE request to the REST server and returns the response
        /** @par Example:
            @code{.py}
hash ans = rest.post("/orders/1");
            @endcode

            @param path the URI path to add (will be appended to any root path given in the constructor)
            @param body an optional message body to be included in the request; if a value for this parameter is passed to the method, then the body will be serialized according to the serialization rules set in @ref RestClient::RestClient::constructor() "RestClient::constructor()"
            @param info an optional reference to a hash that will be used as an output variable giving a hash of request headers and other information about the HTTP request
            @param hdr any headers to be sent with the request; headers here will override default headers for the object as well

            @return A hash of headers received from the HTTP server with all key names converted to lower-case; if any message body is included in the response, it will be assigned to the value of the \c "body" key

            @throw DESERIALIZATION-ERROR the response body could not be deserialized (unknown \c Content-Type or invalid serialization)
            @throw HTTP-CLIENT-RECEIVE-ERROR if this exception is thrown by the @ref Qore::HTTPClient::send() call in case of an HTTP response code < 100 or >= 300, the message body is still deserialized if possible and the response information can be retrieved in the \a info hash output keys as follows:
            - \c "response-code": the HTTP response code given
            - \c "response-headers": a hash of response headers
            - \c "response-body": the decoded response body

            Other exceptions can be thrown by the @ref Qore::HTTPClient::send() call used to make the HTTP request.

            @see
            - @ref RestClient::RestClient::getSerialization() "RestClient::getSerialization()"
            - @ref RestClient::RestClient::setSerialization() "RestClient::setSerialization()"
         */
        hash del(string path, any body, *reference info, *hash hdr) {
            return doRequest("DELETE", path, body, \info, NOTHING, hdr);
        }

        #! sets up the Content-Type header and encodes any body for sending
        private nothing prepareMsg(reference body, reference hdr, string ct = "Content-Type") {
            hdr = headers + hdr;

            # serialize body if necessary
            if (exists body) {
                body = sih.out(body);
                if (!hdr{ct})
                    hdr{ct} = sih.ct;
                if (body.typeCode() == NT_STRING && hdr{ct} !~ /;charset=/)
                    hdr{ct} += ";charset=" + body.encoding();

                if (seh.ce && body.size() > CompressionThreshold) {
                    hdr."Content-Encoding" = seh.ce;
                    body = seh.func(body);
                }
            }
        }

        #! sets up the path for the HTTP request URI
        private nothing preparePath(reference path) {
            # prepare path
            *string p = getConnectionPath();

            # strip trailing "/" off the connection path
            p =~ s/\/+$//;
            # strip leading "/" off the given path
            path =~ s/^\/+//;
            if (p.val())
                path = p + (path.val() ? ("/" + path) : "");
            # ensure path is absolute
            if (path !~ /^\//)
                splice path, 0, 0, "/";

            path = encode_uri_request(path);
        }

        #! sends an HTTP request to the REST server and returns the response
        /** @par Example:
            @code{.py}
hash ans = rest.doRequest("DELETE", "/orders/1");
            @endcode

            @param m the HTTP method to be used; case is ignored (if not a valid method an \c HTTP-CLIENT-METHOD-ERROR exception is raised)
            @param path the URI path to add (will be appended to any root path given in the constructor)
            @param body an optional message body to be included in the request; if a value for this parameter is passed to the method, then the body will be serialized according to the serialization rules set in @ref RestClient::RestClient::constructor() "RestClient::constructor()"
            @param info an optional reference to a hash that will be used as an output variable giving a hash of request headers and other information about the HTTP request
            @param decode_errors decode the message body with HTTP error responses and throw an exception based on the message body
            @param hdr any headers to be sent with the request; headers here will override default headers for the object as well

            @return A hash of headers received from the HTTP server with all key names converted to lower-case; if any message body is included in the response, it will be assigned to the value of the \c "body" key

            @throw DESERIALIZATION-ERROR the response body could not be deserialized (unknown \c Content-Type or invalid serialization)
            @throw HTTP-CLIENT-METHOD-ERROR invalid HTTP method argument passed
            @throw HTTP-CLIENT-RECEIVE-ERROR if this exception is thrown by the @ref Qore::HTTPClient::send() call in case of an HTTP response code < 100 or >= 300, the message body is still deserialized if possible and the response information can be retrieved in the \a info hash output keys as follows:
            - \c "response-code": the HTTP response code given
            - \c "response-headers": a hash of response headers
            - \c "response-body": the decoded response body

            Other exceptions can be thrown by the @ref Qore::HTTPClient::send() call used to make the HTTP request.

            @see
            - @ref RestClient::RestClient::getSerialization() "RestClient::getSerialization()"
            - @ref RestClient::RestClient::setSerialization() "RestClient::setSerialization()"
         */
        hash doRequest(string m, string path, any body, *reference info, softbool decode_errors = True, *hash hdr) {
            prepareMsg(\body, \hdr);

            on_exit if (exists body) {
                info += (
                    "request-body": body,
                    "request-serialization": ds,
                    );
            }

            # prepare path
            preparePath(\path);

            return sendAndDecodeResponse(body, m, path, hdr, \info, decode_errors);
        }

        #! sends the outgoing HTTP message and recodes the response to data
        hash sendAndDecodeResponse(*data body, string m, string path, hash hdr, *reference info, *softbool decode_errors) {
            hash h;
            try {
                h = send(body, m, path, hdr, False, \info);
                info."response-code" = h.status_code;
            }
            catch (hash ex) {
                if (ex.arg) {
                    info."response-code" = ex.arg.status_code;
                    if (decode_errors && ex.arg.body)
                        decodeError(ex.arg, \info);
                }

                rethrow;
            }

            # decode any response body
            if (exists h.body)
                decodeResponse(\h, \info);

            return h;
        }

        #! decode any REST errors returned if possible
        private decodeError(hash h, *reference info) {
            try {
                decodeResponse(\h, \info);
                info."response-body" = h.body;
            }
            catch () {
                # ignore decoding exceptions
            }
            if (h.body.err) {
                if (h.body.desc.typeCode() == NT_STRING)
                    trim h.body.desc;
                throw h.body.err, h.body.desc, h.body.arg;
            }
            if (h.body && h.body.typeCode() == NT_STRING) {
                trim h.body;
                throw "REST-RESPONSE-ERROR", h.body;
            }
        }

        #! decodes the response
        decodeResponse(reference h, *reference info) {
            info."response-body" = h.body;

            *hash dh = DataDeserializationSupport.(h."content-type");
            if (!dh)
                throw "DESERIALIZATION-ERROR", sprintf("cannot deserialize request body; content-type is: %y; types supported: %y; response body: %y", h."content-type", DataDeserializationSupport.keys(), h.body);

            if (h.body.typeCode() != NT_STRING)
                throw "DESERIALIZATION-ERROR", sprintf("cannot deserialize request body; content-type is: %y but body type is %y", h."content-type", h.body.type());

            h.body = dh.in(h.body);

            info."response-serialization" = dh.code;
        }
    }
}
