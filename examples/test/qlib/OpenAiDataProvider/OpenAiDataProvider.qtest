#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%require-types
%enable-all-warnings
%new-style
%strict-args
%allow-injection

%requires ../../../../qlib/Util.qm
%requires ../../../../qlib/QUnit.qm
%requires ../../../../qlib/Logger.qm
%requires ../../../../qlib/DataProvider
%requires ../../../../qlib/OpenAiDataProvider

%exec-class OpenAiDataProviderTest

public class OpenAiDataProviderTest inherits QUnit::Test {
    public {
        OpenAiRestConnection rc;
        Logger logger;

        # command-line options
        const MyOpts = Opts + {
            "conn": "c,connection=s",
        };

        const OptionColumn = 22;
    }

    constructor() : Test("OpenAiDataProvider Test", "1.0", \ARGV, MyOpts) {
        addTestCase("sanity test", \sanityTest());
        addTestCase("test", \test());

        try {
            setupConnection();
        } catch (hash<ExceptionInfo> ex) {
            if (m_options.verbose > 2) {
                printf("%s\n", get_exception_string(ex));
            } else if (m_options.verbose) {
                printf("%s: %s\n", ex.err, ex.desc);
            }
        }

        # Return for compatibility with test harness that checks return value.
        set_return_value(main());
    }

    checkModule() {
%ifdef NoJson
        testSkip("no json module");
%endif
    }

    sanityTest() {
        checkModule();
        hash<auto> fake_opts = {
            "token": "x",
        };
        OpenAiRestConnection rc("test", "test", NOTHING, NOTHING, fake_opts);
        OpenAiDataProvider prov(rc.get(True));
        assertEq("openai", prov.getName());
    }

    test() {
        if (!rc) {
            testSkip("no connection");
        }

        AbstractDataProvider prov = new OpenAiDataProvider(rc.get());
        prov = prov.getChildProvider("models");
        assertGt(0, prov.getChildProviderNames().size());
    }

    private usageIntern() {
        TestReporter::usageIntern(OptionColumn);
        printOption("-c,--connection=ARG", "set connection name", OptionColumn);
    }

    private error(string fmt) {
        throw "OPTION-ERROR", sprintf("%s: ERROR: %s", get_script_name(), vsprintf(fmt, argv));
    }

    private setupConnection() {
        *string conn_name = m_options.conn ?? ENV.OPENAI_CONNECTION;
        if (!conn_name.val()) {
            error("missing -c,--connection option or OPENAI_CONNECTION environment variable");
        }

        AbstractConnection conn = get_connection(conn_name);
        if (!(conn instanceof OpenAiRestConnection)) {
            error("connection %y is type %y; expecting \"OpenAiRestConnection\"", conn_name, conn.className());
        }
        rc = conn;
    }
}
