#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%new-style
%enable-all-warnings
%require-types
%strict-args

%requires ../../../../qlib/Util.qm
%requires ../../../../qlib/QUnit.qm
%requires ../../../../qlib/DataProvider
%requires ../../../../qlib/ConnectionProvider
%requires ../../../../qlib/Logger.qm
%requires ../../../../qlib/HttpServerUtil.qm
%requires ../../../../qlib/HttpServer.qm
%requires ../../../../qlib/WebSocketUtil.qm
%requires ../../../../qlib/WebSocketHandler.qm
%requires ../../../../qlib/WebSocketClient.qm

%exec-class WebSocketClientTest

class MyWsConnection inherits WebSocketConnection {
    constructor(WebSocketHandler handler) : WebSocketConnection(handler) {
    }

    sendData() {
        map send($1), WebSocketClientTest::Data;
    }

    gotMessage(string msg) {
        send("RESPONSE:" + msg);
        #printf("GOT MSG: %y\n", msg);
    }
}

class MyWsHandler inherits WebSocketHandler {
    public {
        bool debug = False;
    }

    WebSocketConnection getConnectionImpl(hash<auto> cx, hash<auto> hdr, string cid) {
        MyWsConnection conn(self);
        if (hdr."send-data") {
            conn.sendData();
        }
        return conn;
    }

    logError(string fmt) {
        if (debug) {
            vprintf("ERROR " + fmt + "\n", argv);
        }
    }

    logInfo(string fmt) {
        if (debug) {
            vprintf("INFO " + fmt + "\n", argv);
        }
    }

    logDebug(string fmt) {
        if (debug) {
            vprintf("DEBUG " + fmt + "\n", argv);
        }
    }
}

class WebSocketClientTest inherits QUnit::Test {
    public {
        HttpServer mServer;
        MyWsHandler wsHandler();
        int responseSize;
        # listener info
        hash<auto> linfo;

        Logger logger;

        const Data = (
            "test1",
            <beeffeed>,
            "test3",
        );
    }

    constructor() : Test("WebSocketClientTest", "1.0") {
        addTestCase("reconnect test", \reconnectTest());
        addTestCase("data provider test", \dataProviderTest());
        addTestCase("WebSocketClient tests", \webSocketClientTests());
        addTestCase("connection tests", \connectionTests());

        logger = new Logger("test", LoggerLevel::getLevelInfo());
        if (m_options.verbose > 2) {
            logger.addAppender(new StdoutAppender());
        }
        hash<HttpServerOptionInfo> http_opts = <HttpServerOptionInfo>{
            "logger": logger,
            "debug": True,
        };
        mServer = new HttpServer(http_opts);
        mServer.setHandler("ws-handler", "", NOTHING, wsHandler);
        mServer.setDefaultHandler("ws-handler", wsHandler);
        linfo = mServer.addListener(<HttpListenerOptionInfo>{"service": 0});

        set_return_value(main());

        mServer.stop();
    }

    reconnectTest() {
        MyObserver observer();
        WebSocketClientDelayedDataProvider prov({
            "url": "ws://localhost:" + linfo.port,
            "data": "none",
            "logger": logger,
            "reconnect": True,
        });
        prov.registerObserver(observer);
        prov.observersReady();

        prov.sendMessage(MESSAGE_WS_PING, "hb");

        observer.wait(EVENT_WS_PONG);
        assertEq(2, observer.l.size());
        assertEq(EVENT_WS_CONNECTED, observer.getFirstMessageId());
        assertEq(EVENT_WS_PONG, observer.getLastMessageId());

        assertTrue(prov.isConnected());
        hash<DataProviderConnectionInfo> cinfo = prov.getConnectionInfo();
        assertTrue(cinfo.connected);
        assertFalse(cinfo.reconnecting);
        assertGe(0, cinfo.info.cid);
        assertEq(1, cinfo.info.num_connects);
        assertEq(linfo.port, cinfo.info.peer.port);

        # stop the listener
        mServer.stopListenerID(linfo.id);

        # check reconnecting status
        assertFalse(prov.isConnected());

        cinfo = prov.getConnectionInfo();
        assertFalse(cinfo.connected);
        assertTrue(cinfo.reconnecting);
        assertNothing(cinfo.info.peer);
        assertNothing(cinfo.info.socket);

        # clear the observer's event list
        observer.clear();

        # restart listener on the same port
        linfo = mServer.addListener(<HttpListenerOptionInfo>{"service": linfo.port});

        # wait for reconnect
        observer.wait(EVENT_WS_CONNECTED);

        cinfo = prov.getConnectionInfo();
        assertTrue(cinfo.connected);
        assertFalse(cinfo.reconnecting);
        assertGt(0, cinfo.info.cid);
        assertEq(2, cinfo.info.num_connects);
        assertEq(linfo.port, cinfo.info.peer.port);

        # redo ping test
        prov.sendMessage(MESSAGE_WS_PING, "hb");

        observer.wait(EVENT_WS_PONG);
        assertEq(2, observer.l.size());
        assertEq(EVENT_WS_PONG, observer.getLastMessageId());
    }

    dataProviderTest() {
        MyObserver observer();
        WebSocketClientDelayedDataProvider prov({
            "url": "ws://localhost:" + linfo.port,
            "data": "none",
            "logger": logger,
        });
        prov.registerObserver(observer);
        prov.observersReady();

        prov.sendMessage(MESSAGE_WS_PING, "hb");

        observer.wait(EVENT_WS_PONG);
        assertEq(2, observer.l.size());
        assertEq(EVENT_WS_CONNECTED, observer.getFirstMessageId());
        assertEq(EVENT_WS_PONG, observer.getLastMessageId());

        AbstractDataProvider child = prov.getChildProvider("wait-for-message");
        prov.sendMessage(MESSAGE_WS_DATA, "hi");

        auto response = child.doRequest();
        assertEq("RESPONSE:hi", response);

        # negative test
        assertThrows("WEBSOCKET-RECV-TIMEOUT", \child.doRequest(), {"timeout": 1});
    }

    webSocketClientTests() {
        list<auto> l = ();
        Counter c(1);
        code cb = sub (*data msg) {
            #printf("RECEIVED: %y\n", msg);
            if (exists msg) {
                l += msg;
                if (l.size() == responseSize)
                    c.dec();
            }
        };
        responseSize = Data.size();
        hash<auto> opts = {
            "url": "ws://localhost:" + linfo.port,
            "logger": logger,
        };
        WebSocketClient wsc(cb, opts);
        wsc.connect({"hdr": {"Send-Data": 1}});
        c.waitForZero();
        assertEq(Data, l);
        c.inc();
        l = ();
        responseSize = 1;
        # issue #2566: send a unidirectional PONG to make sure that the WebSocketHandler can handle it
        # https://tools.ietf.org/html/rfc6455#section-5.5.3: unsolicited PONG messages must be ignored
        wsc.pong("heartbeat");
        wsc.send("REQUEST");
        c.waitForZero();
        assertEq(list("RESPONSE:REQUEST"), l);
        wsc.disconnect();
        delete wsc;
    }

    connectionTests() {
        string url = "ws://localhost:8099";
        WebSocketConnectionObject conn("test", "test", url);
        WebSocketClient client = conn.get(False);
        assertEq(url, client.getUrl());

        conn = new WebSocketConnectionObject("test", "test", url, {"monitor": False});
        client = conn.get(False);
        assertEq(url, client.getUrl());
    }
}

class MyObserver inherits Observer {
    public {
        list<hash<auto>> l();
    }

    private {
        Mutex m();
        Condition cond();
        int waiting;
    }

    update(string event_id, hash<auto> data_) {
        m.lock();
        on_exit m.unlock();

        l += {
            "id": event_id,
            "data": data_,
        };

        if (waiting) {
            cond.broadcast();
        }
    }

    wait(*string msg) {
        date start = now_us();

        m.lock();
        on_exit m.unlock();

        bool found;
        while (!found) {
            cond.wait(m, 1s);
            if ((now_us() - start) > 5s) {
                throw "ERROR", sprintf("no matching data received in timeout; msg: %y l: %y", msg, l);
            }
            if (!msg.val()) {
                if (l) {
                    found = True;
                }
            } else if (l.last().id == msg) {
                found = True;
            }
        }
    }

    *string getFirstMessageId() {
        return l[0].id;
    }

    *string getLastMessageId() {
        return l.last().id;
    }

    clear() {
        m.lock();
        on_exit m.unlock();

        l = ();
    }
}
