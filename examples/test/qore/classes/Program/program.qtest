#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%new-style
%enable-all-warnings
%require-types
%strict-args
%no-child-restrictions

%requires QUnit

%exec-class ProgramTest

int sub t(any a) {
    return a + 1;
}

class Test inherits Socket {
    private {
        int a;
        int b;
    }
    public {
        list data;
        hash t;
    }

    constructor(any a, any b, any c) {
        a = 1;
        b = 2;
        data = (a, b, c);
    }
    any getData(int elem) {
	return data[elem];
    }
    string methodGate(string m) {
        return m;
    }
    string memberGate(string m) {
        return "memberGate-" + m;
    }
    memberNotification(string m) {
        t.m = self.m;
    }
    code closure(any x) {
        int a = 1;
        # return a closure encapsulating the state of the object
        return string sub (any y) {
            return sprintf("%s-%n-%n-%n", data[1], x, y, ++a);
        };
    }
    any argTest() {
        return argv;
    }
}

class ProgramTest inherits QUnit::Test {
    public {
    }

    private {
	const CombineSrc = "class B1 {private { x; }} class B2 {public {x;}} class D inherits B1, B2 {}";

        # global variables provided by the Qore language
        const QoreGlobalVars = ("Qore::ENV", "Qore::QORE_ARGV", "Qore::ARGV");
    }

    constructor() : QUnit::Test("ProgramTest", "1.0", \ARGV) {
	addTestCase("Program test", \programTest());
	addTestCase("Combine test", \combineTest());
        addTestCase("Int assignments test", \intAssignmentsTest());
        addTestCase("constant exp test", \constantExpressionTest());
        addTestCase("type error test", \typeErrorTest());
        addTestCase("broken-operators test", \brokenOperatorsTest());
        addTestCase("class test", \classTest());
        addTestCase("setThreadInit test", \setThreadInitTest());
        addTestCase("var test", \varTest());
	set_return_value(main());
    }

    programTest() {
	our Test to();
	our Test ro();

	string func = "namespace ITest { const val = 1.0; class X; } our int gv2 = 123; int sub t2(int a) { return a + 2; } int sub et(int a) { return t(a); } string sub tot() { return get_class_name(to); } Queue sub getObject() { return new Queue(); } X sub get_x() { return new X(); } sub deleteException() { ro.getData(0); delete ro; } bool sub check_ro() { return exists ro; }";

	string pf = "newfunc();";
	string nf = "bool sub newfunc() { return True; }";

	Program a(PO_NEW_STYLE);
	a.parsePending(pf, "pending test part1", 0);
	a.parsePending(nf, "pending test part2", 0);
	a.parseCommit();
	a.importFunction("t");
	a.importGlobalVariable("to");
	a.importGlobalVariable("ro", True);
	a.parse(func, "test");

	assertEq(True, a.callFunction("newfunc"));
	assertEq(3, a.callFunction("t2", 1));
	assertEq(4, a.callFunctionArgs("t2", list(int(2))));
	assertEq(2, a.callFunction("et", 1));
	assertEq("Test", a.callFunction("tot"));
	assertEq("memberGate-member", to.member);
	assertEq("method", to.method());

	try {
	    a.callFunction("deleteException");
	}
	catch (hash ex) {
	    assertEq("ACCESS-ERROR", ex.err);
	}

	assertEq(True, a.callFunction("check_ro"));

	Queue o = a.callFunction("getObject");
	object ox = a.callFunction("get_x");
	delete a;
	assertEq("Queue", get_class_name(o));
	assertEq("X", get_class_name(ox));

	# test for incorrect parse location when processing constants after a commit
	a = new Program(PO_NEW_STYLE);
	a.parse("sub x() {}", "lib");
	*hash h = a.parse("const X1 = 'a'; const X2 = 'a'; const h = (X1: 1, X2: 2);", "warn", WARN_ALL);
	assertEq("<run-time-loaded: warn>", h.file);

	# make sure recursive constant definitions are handled
	try {
	    a.parse("const A = B; const B = A; any a = A;", "rec");
	}
	catch (hash ex) {
	    assertEq("PARSE-EXCEPTION", ex.err);
	}

	string pstr = "class T { private { int i = 1; static *int j = 4; const X = 2; } int get() { return i; } static other (int x) {} } T sub getT() { return new T(); } int sub checkT(T t) { return t.get(); }";

	Program p1(PO_NEW_STYLE);
	Program p2(PO_NEW_STYLE);

	p1.parse(pstr, "p");
	p2.parse(pstr, "p");

	object o2 = p1.callFunction("getT");
	assertEq(1, p1.callFunction("checkT", o2));
	assertEq(1, p2.callFunction("checkT", o2));

	Program p4(PO_NEW_STYLE);
	try {
	    p4.parse("error", "error", 0, "source", 10);
	    assertEq(True, False);
	}
	catch (hash ex) {
	    assertEq("source", ex.source);
	    assertEq(10, ex.offset);
	}
    }

    combineTest() {
	Program p(PO_NEW_STYLE);
	testAssertion("combine", \p.parse(), (CombineSrc, "combine-test"), new TestResultExceptionRegexp("PARSE-ERROR", "B2"));
    }

    intAssignmentsTest() {
        {
            Program p(PO_NEW_STYLE);
            p.parse("any h = {}; int i = h;", "");

            assertThrows("RUNTIME-TYPE-ERROR", \p.run());
        }

        {
            Program p(PO_NEW_STYLE|PO_BROKEN_INT_ASSIGNMENTS);
            p.parse("any h = {}; int i = h;", "");

            assertEq(NOTHING, p.run());
        }

        {
            Program p(PO_NEW_STYLE|PO_BROKEN_INT_ASSIGNMENTS);
            p.parse("%correct-int-assignments\nany h = {}; int i = h;", "");
            assertThrows("RUNTIME-TYPE-ERROR", \p.run());
        }
    }

    constantExpressionTest() {
        {
            Program p(PO_NEW_STYLE);
            assertThrows("ILLEGAL-MEMBER-REFERENCE", \p.parse(), ("class T {public {int i;} t() {while (True) {switch (1) {case 1+i: break;}}}}", ""));
        }
    }

    typeErrorTest() {
        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS);
            assertThrows("PARSE-TYPE-ERROR", \p.parse(), ("sub f() {} f(1);", ""));
        }
        {
            Program p(PO_NEW_STYLE|PO_NO_CHILD_PO_RESTRICTIONS|PO_STRICT_ARGS);
            p.parse("Program p(PO_NEW_STYLE); p.parse('sub f() {}', ''); p.callFunction('f', 1);", "");
            assertEq(NOTHING, p.run());
        }
        {
            Program p(PO_NEW_STYLE|PO_NO_CHILD_PO_RESTRICTIONS|PO_STRICT_ARGS);
            p.parse("Program p(PO_NEW_STYLE|PO_STRICT_ARGS); p.parse('sub f() {}', ''); p.callFunction('f', 1);", "");
            assertThrows("RUNTIME-OVERLOAD-ERROR", \p.run());
        }
        {
            Program p(PO_NEW_STYLE|PO_NO_CHILD_PO_RESTRICTIONS|PO_STRICT_ARGS);
            p.parse("Program p(PO_NEW_STYLE|PO_REQUIRE_TYPES); p.parse('sub f() {}', ''); p.callFunction('f', 1);", "");
            assertThrows("RUNTIME-OVERLOAD-ERROR", \p.run());
        }
    }

    brokenOperatorsTest() {
        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertThrows("PARSE-EXCEPTION", \p.parse(), ("int i; i+ =1;", ""));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertThrows("PARSE-EXCEPTION", \p.parse(), ("hours()", ""));
        }

        {
            Program p(PO_NEW_STYLE|PO_REQUIRE_TYPES);
            assertThrows("PARSE-EXCEPTION", \p.parse(), ("hours()", ""));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS);
            assertThrows("PARSE-EXCEPTION", \p.parse(), ("hours()", ""));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS);
            assertEq(NOTHING, p.parse("%loose-args\nhours();", ""));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES|PO_BROKEN_OPERATORS);
            assertEq(NOTHING, p.parse("int i; i+ =1;", ""));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES|PO_BROKEN_OPERATORS);
            assertThrows("PARSE-EXCEPTION", \p.parse(), ("%correct-operators\nint i; int i+ =1;", ""));
        }
    }

    classTest() {
        # test constructor methods
        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            # it is illegal to declare a static contructor() method
            assertThrows("ILLEGAL-STATIC-METHOD", \p.parse(), ("class X { static constructor(int i) {}}", ""));
        }

        # test destructor methods
        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertThrows("PARSE-EXCEPTION", \p.parse(), ("class X { destructor(int i) {}}", ""));
            assertThrows("PARSE-EXCEPTION", \p.parse(), ("class X { int destructor() {}}", ""));
            assertThrows("PARSE-EXCEPTION", \p.parse(), ("class X { private destructor() {}}", ""));
            # it is illegal to declare a static destructor() method
            assertThrows("ILLEGAL-STATIC-METHOD", \p.parse(), ("class X { static destructor() {}}", ""));
            assertThrows("PARSE-EXCEPTION", \p.parse(), ("class X { abstract destructor();}", ""));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertEq(NOTHING, p.parse("class X { public destructor() {}}", ""));
        }

        # test copy methods
        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertThrows("PARSE-TYPE-ERROR", \p.parse(), ("class X { copy(int i) {}}", ""));
            assertThrows("PARSE-EXCEPTION", \p.parse(), ("class X { abstract copy() {}}", ""));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertEq(NOTHING, p.parse("class X { private copy() {}}", ""));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertEq(NOTHING, p.parse("class X { private copy(object obj) {}}", ""));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertEq(NOTHING, p.parse("class X { private copy(*object obj) {}}", ""));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertEq(NOTHING, p.parse("class X { private copy(*X obj) {}}", ""));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertEq(NOTHING, p.parse("class X { private copy(X obj) {}}", ""));
        }

        # it is legal to declare a static copy() method
        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertEq(NOTHING, p.parse("class X { static copy(X obj) {}}", ""));
        }
    }

    setThreadInitTest() {
        our int i = 0;
        our Counter cnt(1);
        Program p(PO_NEW_STYLE);
        p.importGlobalVariable("i");
        p.importGlobalVariable("cnt");
        bool b = p.setThreadInit(sub () { i = 1;});
        assertEq(False, b);
        p.parse("background cnt.dec();", "");
        p.run();
        cnt.waitForZero();
        assertEq(1, i);
        b = p.setThreadInit();
        assertEq(True, b);
        i = 0;
        cnt.inc();
        p.run();
        assertEq(0, i);
    }

    varTest() {
        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            p.parse("our int i = 1; namespace T { our string str; } T::str = 'hi';", "");
            p.run();
            assertEq(("i": 1, "T::str": "hi"), p.getGlobalVars() - QoreGlobalVars);
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            p.parse("our int i = 1; namespace T { our string str; } T::str = 'hi';", "");
            p.run();
            p.setGlobalVarValue("i", 2);
            assertEq(("i": 2, "T::str": "hi"), p.getGlobalVars() - QoreGlobalVars);
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            p.parse("our int i = 1; namespace T { our string str; } T::str = 'hi';", "");
            p.run();
            assertThrows("RUNTIME-TYPE-ERROR", \p.setGlobalVarValue(), ("i", "hi"));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            p.parse("our int i = 1; namespace T { our string str; } T::str = 'hi';", "");
            p.run();
            assertThrows("UNKNOWN-VARIABLE", \p.setGlobalVarValue(), ("x", "hi"));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            p.parse("our int i = 1; namespace T { our string str; } T::str = 'hi'; set_global_var_value('i', 'hi');", "");
            assertThrows("RUNTIME-TYPE-ERROR", \p.run());
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            p.parse("our int i = 1; namespace T { our string str; } T::str = 'hi'; set_global_var_value('x', 'hi');", "");
            assertThrows("UNKNOWN-VARIABLE", \p.run());
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES|PO_ALLOW_DEBUGGING);
            p.parse("hash sub test(int frame) { int i = 1; string str = 'hi'; return get_local_vars(frame); }", "");

            hash vh = (
                "frame": ("type": "local", "value": 0),
                "i": ("type": "local", "value": 1),
                "str": ("type": "local", "value": "hi"),
                "argv": ("type": "local", "value": NOTHING),
                );

            assertEq(vh, p.callFunction("test", 0));
            assertEq({}, p.callFunction("test", 1));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES|PO_ALLOW_DEBUGGING);
            p.parse("hash sub test(int frame) { int i = 1; string str = 'hi'; set_local_var_value('i', 2); set_local_var_value('str', 'bye'); sub () { i = 3; }; return get_local_vars(frame); }", "");

            hash vh = (
                "frame": ("type": "local", "value": 0),
                "i": ("type": "closure", "value": 2),
                "str": ("type": "local", "value": "bye"),
                "argv": ("type": "local", "value": NOTHING),
                );

            assertEq(vh, p.callFunction("test", 0));
            assertEq({}, p.callFunction("test", 1));
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES|PO_ALLOW_DEBUGGING);
            p.parse("sub test() { int i = 1; set_local_var_value('x', 2); }", "");
            assertThrows("UNKNOWN-VARIABLE", \p.callFunction(), "test");
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES|PO_ALLOW_DEBUGGING);
            p.parse("sub test() { int i = 1; set_local_var_value('x', 2); sub () { i = 3; };}", "");
            assertThrows("UNKNOWN-VARIABLE", \p.callFunction(), "test");
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES|PO_ALLOW_DEBUGGING);
            p.parse("sub test() { int i = 1; set_local_var_value('i', 'hi'); sub () { i = 3; };}", "");
            assertThrows("RUNTIME-TYPE-ERROR", \p.callFunction(), "test");
        }

        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES|PO_ALLOW_DEBUGGING);
            p.parse("hash sub test1(int frame) { code c = sub () { if (frame > 10) throw 1; }; c(); return get_local_vars(frame); } hash sub test2(int frame) { int i = 1; string str = 'hi'; return test1(frame); }", "");

            hash vh = (
                "str": ("type": "local", "value": "hi"),
                "i": ("type": "local", "value": 1),
                "argv": ("type": "local", "value": NOTHING),
                "frame": ("type": "local", "value": 1),
                );

            assertEq(vh, p.callFunction("test2", 1));

            vh = (
                "c": ("type": "local", "value": NOTHING),
                "frame": ("type": "closure", "value": 0),
                "argv": ("type": "local", "value": NOTHING),
                );

            hash pvh = p.callFunction("test2", 0);
            pvh.c.value = NOTHING;
            assertEq(vh, pvh);
            assertEq({}, p.callFunction("test2", -1));
            assertEq({}, p.callFunction("test2", 2));
        }
        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertThrows("PARSE-EXCEPTION", "do not allow access", \p.parse(), ("get_local_vars(0);", ""));
        }
        {
            Program p(PO_NEW_STYLE|PO_STRICT_ARGS|PO_REQUIRE_TYPES);
            assertThrows("PARSE-ERROR", "local variable.*already declared", \p.parse(), ("hash h; hash h;", ""));
        }
    }
}
