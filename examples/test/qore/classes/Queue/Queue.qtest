#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%new-style
%enable-all-warnings
%require-types
%strict-args

%requires ../../../../../qlib/QUnit.qm

%exec-class Test

class Test inherits QUnit::Test {
    constructor() : QUnit::Test("Queue", "1.0") {
        addTestCase("simple tests", \simpleTests());
        addTestCase("leak test", \leakTest());
        set_return_value(main());
    }

    leakTest() {
        Queue q();
        File f();
        f.setEventQueue(q);
        # this creates a memory leak visible in valgrind for example
        assertEq(Type::Object, q.copy().type());
    }

    simpleTests() {
        Queue q();

        q.push(1);
        assertEq(1, q.get());
        assertEq(0, q.size());
        q.push(1);
        q.setError("ERR", "desc");
        assertThrows("ERR", "desc", \q.push(), 1);
        q.clearError();
        assertEq(0, q.size());
        q.push(1);
        assertEq(1, q.get());
        assertEq(0, q.size());

        Counter c(1);
        background wait(q, c);
        # wait for background thread to start waiting
        while (!q.getReadWaiting())
            usleep(1ms);

        # set queue to error
        q.setError("ERR", "desc");
        c.waitForZero();
    }

    wait(Queue q, Counter c) {
        on_exit c.dec();
        assertThrows("ERR", \q.get());
    }
}
