#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%new-style
%enable-all-warnings
%require-types
%strict-args

%requires ../../../../../qlib/QUnit.qm

%exec-class ModulesTest

class ModulesTest inherits QUnit::Test {
    constructor() : QUnit::Test("Modules test", "1.0") {
        addTestCase("warning test", \warningTest());
        addTestCase("Test modules", \testModules());
        addTestCase("Side effect test", \sideEffectTest());
        set_return_value(main());
    }

    warningTest() {
        Program p();
        assertThrows("DEPRECATED", \p.loadModule(), ("./Issue3212.qm", WARN_MODULES | WARN_DEPRECATED));
        p.loadModule("./Issue3212.qm");
        p.parse("int sub test0() { return test(); } int sub test1() { return deprecated_test(); }", "");
        assertEq(1, p.callFunction("test0"));
        assertEq(1, p.callFunction("test1"));
    }

    testModules() {
        our ModulesTest mt = self;

        Program p(PO_NEW_STYLE);
        p.disableParseOptions(PO_NO_TOP_LEVEL_STATEMENTS);
        p.setScriptPath(get_script_path());
        p.importGlobalVariable("mt");
        p.parse("
%requires ./C.qm
mt.assertEq(1, AC, \"AC\");
mt.assertEq(1, BC, \"BC\");
", "p");
        p.run();
    }

    sideEffectTest() {
        Program p(PO_NEW_STYLE);
        p.setScriptPath(get_script_path());
        assertThrows("PARSE-EXCEPTION", "parse options do not allow access", \p.parse(), ("%requires ./SideEffect.qm", ""));
    }
}
