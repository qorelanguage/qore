#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%new-style
%enable-all-warnings
%require-types
%strict-args

%requires ../../../../qlib/QUnit.qm

%exec-class OverloadTest

string sub f1_test(string x) {
    return type(x);
}

string sub f1_test(float x) {
    return type(x);
}

string sub f_test(int x) {
    return type(x);
}

string sub f_test(float x) {
    return type(x);
}

int sub f2_test(*int i) {
    return -1;
}

int sub f2_test() {
    return 0;
}

int sub f3_test(*int i, *int j) {
    return -1;
}

int sub f3_test() {
    return 0;
}

int sub f4_test() {
    delete argv;
    return -1;
}

int sub f4_test(list l) {
    return 0;
}

int sub f5_test(softlist l, *object o) {
    return -1;
}

int sub f5_test(string str) {
    return 0;
}

int sub f6(int i, *string s1, *int i1, *hash h1) {
    return 4;
}

int sub f6(int i, *string s1, *int i1) {
    return 3;
}

int sub f6(int i, *string s1) {
    return 2;
}

int sub f6(int i) {
    return 1;
}

public class OverloadTest inherits QUnit::Test {
    constructor() : Test("Overload test", "1.0") {
        addTestCase("Values test", \testValues());
        addTestCase("Variables test", \testVariables());
        addTestCase("Overload test", \testOverload());

        # Return for compatibility with test harness that checks return value.
        set_return_value(main());
    }

    testValues() {
        assertEq("integer", f_test(1));
        assertEq("float", f_test(1.1));
        assertEq("float", f1_test(1));
        assertEq("float", f1_test(1.1));
        assertEq("string", f1_test("str"));
        assertEq("string", f1_test("1.11"));

        testAssertion("test not-existing overload", \f1_test(), (123.456n,), new TestResultExceptionType("RUNTIME-OVERLOAD-ERROR"));
    }

    testVariables() {
        int i = 1;
        float fi = 1.1;
        assertEq("integer", f_test(i));
        assertEq("float", f1_test(i));
        assertEq("float", f_test(fi));
        assertEq("float", f1_test(fi));
    }

    testOverload() {
        # test parse-time matching
        assertEq(0, f2_test());
        assertEq(0, f3_test());
        assertEq(0, f4_test(()));
        assertEq(0, f5_test("hi"));
        assertEq(1, f6(1));

        # test runtime matching
        assertEq(0, call_function(\f2_test()));
        assertEq(0, call_function(\f3_test()));
        assertEq(0, call_function(\f4_test(), ()));

        assertEq(1, call_function(\f6(), 1));
    }
}
